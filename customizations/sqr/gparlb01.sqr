!***********************************************************************
!  GPARLB01:  Legal Book Report                                        *
!***********************************************************************
!***********************************************************************
!                                                                      *
!                                                                      *
!                                                                      *
!                                                                      *
! This software and related documentation are provided under a         *
! license agreement containing restrictions on use and                 *
! disclosure and are protected by intellectual property                *
! laws. Except as expressly permitted in your license agreement        *
! or allowed by law, you may not use, copy, reproduce,                 *
! translate, broadcast, modify, license, transmit, distribute,         *
! exhibit, perform, publish or display any part, in any form or        *
! by any means. Reverse engineering, disassembly, or                   *
! decompilation of this software, unless required by law for           *
! interoperability, is prohibited.                                     *
! The information contained herein is subject to change without        *
! notice and is not warranted to be error-free. If you find any        *
! errors, please report them to us in writing.                         *
!                                                                      *
!                                                                      *
! Copyright (C) 1988, 2019, Oracle and/or its affiliates.              *
! All Rights Reserved.                                                 *
!***********************************************************************
!                                                                      *
!       $Release:  HR92                                                *
!           $Bug:  29711424                                            *
!                                                                      *
!***********************************************************************
#include 'setenv.sqc'    !Set environment
#include 'setup32.sqc'   !printer and page-size initialization

#define HDR_col_1  1
#define HDR_col_1b 10
#define HDR_col_2  22
#define HDR_col_3  70
#define HDR_col_3b 80
#define HDR_col_4  105
#define HDR_col_4b 115
#define HDR_col_5  130
#define HDR_col_5b 140
#define HDR_col_6  155
#define HDR_col_6b 165

#define LIN_lin_1  1

#define DET_col_1  1
#define DET_col_1b 10

#define DET_col_3  25
#define DET_col_4  50
#define DET_col_5  55

#define DET_col_6  70
#define DET_col_7  95
#define DET_col_8  100

#define DET_col_9  115
#define DET_col_10 155
#define DET_col_11 165

!************************************
begin-report
!************************************
   move '1' to $ReportDateType
   move '1' to $ReportYear4
   do Init-DateTime
   do Init-Number
   do Stdapi-Init
   do Get-Current-DateTime
   do Init-Report
   do Stdapi-Term
end-report

!************************************
!Procedure Init-Report
!************************************
begin-procedure Init-Report
    move 'GPARLB01'         To $ReportID
    move 'Legal Book ARG'   To $ReportTitle
    move 'Ver. PUM31.01'  to $ReportVersion 
    show $ReportID
    show $ReportTitle
    Show $ReportVersion 
   date-time () hh:mi:ss &timeBegan
   display 'Report Began: ' noline
   display &timeBegan
   DO Process-Main
   date-time () hh:mi:ss &timeEnded
   SHOW ' '
   display 'Report Ended: ' noline
   display &timeEnded
end-procedure Init-Report
   
Begin-heading 6
  Print $ReportTitle  (2,1) center Underline
  Print $CompName     (3,3,80) bold
  Print $CompCuit_lbl (,145) bold
  Print $CompCuit     (,155, 30)
  Print $CompAddr     (4,3)  
  Print $CompActv_lbl (,145) bold
  Print $CompActv     (,155, 30)
  Graphic (1,1,180) box 5
End-heading

Begin-footing 2
  Alter-printer font=5 point-size=7
  Graphic (1,1,180) horz-line
  Let #Last_pg_num = #Last_pg_num + 1
  Move #Last_pg_num To $PageNumberId 999999  
  Let $PageNumberPrint = $PageNumber_lbl || ' ' || $PageNumberId
  Print $PageNumberPrint (2,165) bold
End-footing

!************************************
!Procedure Select-Parameters
!************************************
begin-procedure Select-Parameters
begin-SELECT
X.COMPANY
X.CAL_RUN_ID
X.LAST_PGNBR_ARG
X.GPAR_LGLBK_FST
X.GPAR_FILENAME
X.GPAR_DGTL_BOOK
X.GPAR_FILENAME2
X.GPAR_PRNT_BOOK
X.GPAR_PRCS_TYPE
X.GPAR_SENDING_DT



  let $Company         = &X.COMPANY
  let $Cal_run_id      = &X.CAL_RUN_ID
  let #Last_pg_num     = &X.LAST_PGNBR_ARG
  let $Gpar_Lglbk_Fst  = &X.GPAR_LGLBK_FST
  let $Gpar_Dgtl_Book  = &X.GPAR_DGTL_BOOK
  let $Gpar_Prnt_Book  = &X.GPAR_PRNT_BOOK
  let $Gpar_Prcs_Type  = &X.GPAR_PRCS_TYPE
  let $Gpar_Sending_Dt = &X.GPAR_SENDING_DT
  let $FileNameFrstLd  = &X.GPAR_FILENAME
  let $FileNameDgtlBk  = &X.GPAR_FILENAME2

  LET $Ter_Nbr         = EDIT(TO_CHAR(#Last_pg_num),'00000')
  IF $Gpar_Sending_Dt = ''
    LET $Dtu_SendDate = $Current-Date
  ELSE
    DO Convert-To-DTU-Date($Gpar_Sending_Dt, $Dtu_SendDate)
  END-IF


FROM PS_GPAR_RC_LGLBOOK X
WHERE X.OPRID     = $PRCS_OPRID
AND X.RUN_CNTL_ID = $PRCS_RUN_CNTL_ID
End-Select
SHOW ' '
SHOW 'PARAMETERS'
SHOW 'Company                 : ' $Company
SHOW 'Cal Run Id              : ' $Cal_run_id
SHOW 'Last Page Number        : ' #Last_pg_num EDIT 888888
SHOW 'Digital Book First Load : ' $Gpar_Lglbk_Fst
SHOW 'Digital Book Interface  : ' $Gpar_Dgtl_Book
SHOW 'Printable Book          : ' $Gpar_Prnt_Book
SHOW 'Process Type            : ' $Gpar_Prcs_Type
SHOW ' '
end-procedure Select-Parameters

!************************************
Begin-Procedure Process-Main
!************************************
   do Select-Parameters
   DO Cuit
   IF $Gpar_Prnt_Book = 'Y'  !Printable Book
      #Debug display '  Printable Book'
      do Report-Translation
      do PrintableBook
      do UpdateNumberPage
   END-IF
   IF $Gpar_Lglbk_Fst = 'Y'  !Digital Book First Load
      #Debug display '  Digital Book First Load'
      LET #File_FstLoad = 1
      LET $FileNmFstLoad = '{FILEPREFIX}' ||$FileNameFrstLd
      DO Open-File($FileNmFstLoad, #File_FstLoad, #Filestat)
      DO ReadFstLoad
      DO Close_file (#File_FstLoad)
      #ifdef UNIX
        Do ConvertUnixToDOSWin($FileNmFstLoad)
      #endif
   END-IF
   IF $Gpar_Dgtl_Book = 'Y'  !Digital Book Interface
      #Debug display '  Digital Book Interface'
      LET #File_DigBook = 2
      LET $FileNmDigBook = '{FILEPREFIX}' ||$FileNameDgtlBk
      DO Open-File($FileNmDigBook, #File_DigBook, #FilestatDigBook)
      DO InsertTemp
      DO ReadDigBook
      DO DelTempTable
      DO Close_file (#File_DigBook)
      #ifdef UNIX
        Do ConvertUnixToDOSWin($FileNmDigBook)
      #endif
   END-IF
End-Procedure ! Process-Main


!************************************
Begin-Procedure PrintableBook
!************************************
Begin-Select 
A.DESCR
A.DEFAULT_SETID
A.ADDRESS1
A.ADDRESS2
A.ADDRESS3
A.ADDRESS4
A.CITY
A.STATE
A.EFFDT

  Let $CompName  = &A.DESCR
  Let $CompAddr  = &A.ADDRESS1 || ' ' || &A.ADDRESS2 || ' ' || &A.ADDRESS3 || ' ' || &A.ADDRESS4 || ' ' || &A.CITY || ' ' || &A.STATE
  Let $CompEffDt = &A.EFFDT
  
FROM PS_COMPANY_TBL A
WHERE A.EFFDT = (SELECT MAX(AA.EFFDT) FROM PS_COMPANY_TBL AA 
WHERE AA.COMPANY = A.COMPANY AND AA.EFFDT <= $AsOfToday)
AND A.COMPANY = $Company
End-Select


Begin-Select
B.EMPLID            () ON-BREAK LEVEL=1 PRINT=NEVER BEFORE=PrintEmplHdr Save=$CurrentEmplid
B.EMPL_RCD          () ON-BREAK LEVEL=2 PRINT=NEVER AFTER=PrintEmplFoot
A.RSLT_SEG_NUM
C.PIN_TYPE
C.PIN_NUM
C.DESCR
B.EFFDT
B.DEPTID
B.JOBCODE
B.COMPRATE
B.ACTION
B.ACTION_REASON
B.CONTRACT_NUM
B.SETID_DEPT
B.SETID_JOBCODE
D.SERVICE_DT
B.TERMINATION_DT
A.CALC_RSLT_VAL
A.CALC_ADJ_VAL
A.UNIT_RSLT_VAL
H.PRD_BGN_DT
H.PRD_END_DT

  Let $PinType         = &C.PIN_TYPE
  Let #PinNum          = &C.PIN_NUM
  Let $PinNumDescr     = &C.DESCR
  Do GetPinDescrLang
  Let #UnitRslt        = &A.UNIT_RSLT_VAL
  Let #CalcRslt        = &A.CALC_RSLT_VAL
  Let #CalcAdj         = &A.CALC_ADJ_VAL
  Let #Calc_Rslt_Val   = Round(#CalcRslt + #CalcAdj, 2)

  If $PinType = 'ER'
    If $IsFirstPinER = 'N'
      Print $PinNumDescr                 (+1,{DET_col_3})
    Else
      Print $PinNumDescr                 (,{DET_col_3})
    End-If
    Let $IsFirstPinER = 'N'
    Print #UnitRslt                      (,{DET_col_4}) Edit B999
    Print #Calc_Rslt_Val                 (,{DET_col_5}) Edit 99999999.99
    
    Let #PrintLine_ER = #PrintLine_ER + 1
    Let #Total_ER = #Total_ER + #Calc_Rslt_Val
  Else
    If $IsFirstPinDD <> 'N'
      Do RevPrint(#PrintLine_ER)
    End-If
    Print $PinNumDescr                   (+1,{DET_col_6})
    Let $IsFirstPinDD = 'N'
    Print #UnitRslt                      (,{DET_col_7}) Edit B999
    Print #Calc_Rslt_Val                 (,{DET_col_8}) Edit 99999999.99
    Let #PrintLine_DD = #PrintLine_DD + 1
    Let #Total_DD = #Total_DD + #Calc_Rslt_Val
  End-If

FROM PS_GP_RSLT_ERN_DED A, PS_JOB B, PS_GP_PIN C, PS_PER_ORG_ASGN D, 
     PS_GP_PYE_SEG_STAT F, PS_GP_CALENDAR G, PS_GP_CAL_PRD H 
WHERE B.EMPLID = A.EMPLID 
AND B.EMPL_RCD = A.EMPL_RCD 
AND D.EMPLID   = A.EMPLID 
AND D.EMPL_RCD = A.EMPL_RCD 
AND B.EFFDT    = ( SELECT MAX(C.EFFDT)   FROM PS_JOB C WHERE C.EMPLID = B.EMPLID AND C.EMPL_RCD = B.EMPL_RCD AND C.EFFDT <= H.PRD_END_DT) 
AND B.EFFSEQ   = ( SELECT MAX(D.EFFSEQ)  FROM PS_JOB D WHERE D.EMPLID = B.EMPLID AND D.EMPL_RCD = B.EMPL_RCD AND D.EFFDT = B.EFFDT) 
AND C.PIN_NUM  = A.PIN_NUM
AND C.PIN_TYPE IN ('ER','DD')
AND F.EMPLID   = A.EMPLID 
AND F.CAL_RUN_ID = A.CAL_RUN_ID
AND F.EMPL_RCD   = A.EMPL_RCD 
AND F.GP_PAYGROUP = A.GP_PAYGROUP
AND F.CAL_ID      = A.CAL_ID
AND F.ORIG_CAL_RUN_ID = A.ORIG_CAL_RUN_ID
AND F.RSLT_SEG_NUM    = A.RSLT_SEG_NUM
AND F.PYE_CALC_STAT IN ('70','75')
AND A.CAL_ID      = G.CAL_ID 
AND G.CAL_PRD_ID  = H.CAL_PRD_ID
AND G.GP_PAYGROUP = F.GP_PAYGROUP
AND B.COMPANY     = $Company
AND A.CAL_RUN_ID  = $Cal_run_id
AND A.CAL_RUN_ID  = A.ORIG_CAL_RUN_ID
ORDER BY B.EMPLID, B.EMPL_RCD, A.RSLT_SEG_NUM, C.PIN_TYPE DESC, C.PIN_NUM
End-Select
End-Procedure ! PrintableBook


!**********************************
! Procedure - GetPinDescrLang
!**********************************
Begin-Procedure GetPinDescrLang
Begin-Select
CPINLANG.DESCR

  Let $PinNumDescr = &CPINLANG.DESCR
  
FROM PS_GP_PIN_LANG CPINLANG
WHERE CPINLANG.PIN_NUM = #PinNum
AND CPINLANG.LANGUAGE_CD = $language_cd
End-Select
End-Procedure ! GetPinDescrLang

!**********************************
! Procedure - RevPrint
!**********************************
Begin-Procedure RevPrint(#NumToRev)
  While #count < #NumToRev
    Print '' (-1,1)
    add 1 to #count
  End-while
  Let #count = 0
End-Procedure ! RevPrint

!**********************************
! Procedure - PrintEmplHdr
!**********************************
Begin-Procedure PrintEmplHdr

  Alter-printer font=5 point-size=6 
  
  Let #Total_ER = 0
  Let #Total_DD = 0
  Let #PrintLine_ER = 0
  Let #PrintLine_DD = 0
  Let $IsFirstPinER = 'Y'
  Let $IsFirstPinDD = 'Y'

  Do NameAndDescriptions

  If $IsFirstPage = 'N'
    Print $Emplid_lbl     (+2,{HDR_col_1}) bold
    Print &B.EMPLID       (,{HDR_col_1b})
  Else
    Print $Emplid_lbl     ({LIN_lin_1},{HDR_col_1}) bold
    Print &B.EMPLID       ({LIN_lin_1},+1)
  End-If
  Let $IsFirstPage = 'N'

  Print $EmplName         (,{HDR_col_2}, 45) bold

  Print $Jobcode_lbl      (,{HDR_col_3}) bold
  Print $JobCodeDescr     (,{HDR_col_3b})

  Print $Deptid_lbl       (,{HDR_col_4}) bold
  Print $DeptDescr        (,{HDR_col_4b})
    
  Print $Nationalid_lbl   (+1,{HDR_col_1}) bold
  Print $NationalId       (,{HDR_col_1b})

  Print $Hiredt_lbl       (,{HDR_col_3}) bold
  Do Format-DateTime(&D.SERVICE_DT, $ServiceDtf, {DEFDMY}, '', '')
  Print $ServiceDtf       (,{HDR_col_3b})  
  
  If rtrim(&B.TERMINATION_DT,' ') <> ''
    Do Format-DateTime(&B.TERMINATION_DT, $TermDtt, {DEFDMY}, '', '')
    Print $Terminationdt_lbl (,{HDR_col_4}) bold
    Print $TermDtt           (,{HDR_col_4b})
    Print $Terminationrs_lbl (,{HDR_col_5}) bold
    Print $ActRsnDescr       (,{HDR_col_5b})
  End-If

  Let $FieldName = 'MAR_STATUS'
  Let $FieldValue = $MarStatus
  Do Read-Translate-Table
  Print $Marital_lbl         (,{HDR_col_6}) bold
  Print $XlatShortName       (,{HDR_col_6b})
  
  Do Format-DateTime(&H.PRD_BGN_DT, $PrdBgnDtf, {DEFDMY}, '', '')
  Do Format-DateTime(&H.PRD_END_DT, $PrdEndDtf, {DEFDMY}, '', '')
  Let $CalrunPeriod = $PrdBgnDtf || ' - ' || $PrdEndDtf
  Print $Calrunid_lbl        (+1,{HDR_col_1}) bold
  Print $CalrunPeriod        (,{HDR_col_1b})

  Print $Contract_lbl        (,{HDR_col_3}) bold
  Let $ContractNum = &B.CONTRACT_NUM
  Print $ContractNum         (,{HDR_col_3b})
  
  Let $HDR_er_descr_lbl = substr($HDR_er_descr_lbl || '                                        ',1,40)
  Print $HDR_er_descr_lbl (+1,{DET_col_3}) Underline
  Print $HDR_er_qty_lbl (,{DET_col_4}) bold Underline
  Let $HDR_er_amy_lbl = substr($HDR_er_amy_lbl || '            ',1,12)
  Print $HDR_er_amy_lbl (,{DET_col_5}) bold Underline
    
  Let $HDR_dd_descr_lbl = substr($HDR_dd_descr_lbl || '                                        ',1,40)
  Print $HDR_dd_descr_lbl (,{DET_col_6}) Underline
  Print $HDR_dd_qty_lbl (,{DET_col_7}) Underline
  Let $HDR_dd_amy_lbl = substr($HDR_dd_amy_lbl || '            ',1,12)
  Print $HDR_dd_amy_lbl (,{DET_col_8}) Underline

  Let $HDR_chl_name_lbl = substr($HDR_chl_name_lbl || '                              ',1,30)
  Print $HDR_chl_name_lbl (,{DET_col_9}) Underline
  Let $HDR_chl_dt_lbl = substr($HDR_chl_dt_lbl || '          ',1,10)
  Print $HDR_chl_dt_lbl (,{DET_col_10}) Underline
  Let $HDR_chl_ntlid_lbl = substr($HDR_chl_ntlid_lbl || '          ',1,12)
  Print $HDR_chl_ntlid_lbl (,{DET_col_11}) Underline
  
  Print $Comprate_lbl (+1,{DET_col_1})
  Let #Comprate = Round(&B.COMPRATE,2) 
  Print #Comprate (,{DET_col_1b}) Edit 99999999.99
  
End-Procedure !PrintEmplHdr

!**********************************
Begin-Procedure PrintEmplFoot
!**********************************
  Do DependentBenef
  Graphic (,{DET_col_5},7) horz-line
  Graphic (,{DET_col_8},7) horz-line
  Print $FOO_totals_lbl (+1,{DET_col_3}) Bold
  Print #Total_ER (,{DET_col_5})  Edit 99999999.99 Bold
  Print #Total_DD (,{DET_col_8})  Edit 99999999.99 Bold
End-Procedure !PrintEmplFoot

!**********************************
Begin-Procedure NameAndDescriptions
!**********************************
Begin-Select 
Y.NAME 
Y.MAR_STATUS

   Let $EmplName = &Y.NAME
   Let $MarStatus = &Y.MAR_STATUS
   
FROM PS_PERSONAL_DATA Y
WHERE Y.EMPLID = &B.EMPLID
End-Select

Let $NationalId = 'NA'
Begin-Select 
Y.NATIONAL_ID

  Let $NationalId = &Y.NATIONAL_ID
  Let $NationalId = substr($NationalId, 1, 2)||'-'||rtrim(substr($NationalId, 3, 8),' ') ||'-'||substr($NationalId,11, 1)
  
FROM PS_PERS_NID Y
WHERE Y.EMPLID = &B.EMPLID
AND Y.COUNTRY = 'ARG' 
AND Y.NATIONAL_ID_TYPE = 'CUIL'
End-Select

Begin-Select
DEPT.DESCR
  
  Let $DeptDescr   = &DEPT.DESCR
  
FROM PS_DEPT_TBL DEPT
WHERE DEPT.EFFDT = (SELECT MAX(YY.EFFDT) FROM PS_DEPT_TBL YY WHERE YY.DEPTID = DEPT.DEPTID AND YY.SETID = DEPT.SETID AND YY.EFFDT <= &B.EFFDT)
AND DEPT.EFF_STATUS = 'A'
AND DEPT.SETID =  &B.SETID_DEPT
AND DEPT.DEPTID = &B.DEPTID
End-Select

Begin-Select
DEPTLNG.DESCR

  Let $DeptDescr = &DEPTLNG.DESCR
  
FROM PS_DEPT_TBL_LANG DEPTLNG
WHERE DEPTLNG.EFFDT = (SELECT MAX(YY.EFFDT) FROM PS_DEPT_TBL_LANG YY WHERE YY.DEPTID = DEPTLNG.DEPTID AND YY.SETID = DEPTLNG.SETID AND YY.EFFDT <= &B.EFFDT)
AND DEPTLNG.SETID = &B.SETID_DEPT
AND DEPTLNG.DEPTID = &B.DEPTID
AND DEPTLNG.LANGUAGE_CD = $language_cd
End-Select

Begin-Select
JOBCD.DESCR

  Let $JobCodeDescr = &JOBCD.DESCR
  
FROM PS_JOBCODE_TBL JOBCD
WHERE JOBCD.EFFDT = (SELECT MAX(YY.EFFDT) FROM PS_JOBCODE_TBL YY WHERE YY.JOBCODE = JOBCD.JOBCODE AND YY.EFFDT <= &B.EFFDT)
AND JOBCD.EFF_STATUS = 'A'
AND JOBCD.SETID = &B.SETID_JOBCODE
AND JOBCD.JOBCODE = &B.JOBCODE
End-Select

Begin-Select
JOBCDLNG.DESCR

  Let $JobCodeDescr = &JOBCDLNG.DESCR
  
FROM PS_JOBCODE_LANG JOBCDLNG
WHERE JOBCDLNG.EFFDT = (SELECT MAX(YY.EFFDT) FROM PS_JOBCODE_TBL YY WHERE YY.JOBCODE = JOBCDLNG.JOBCODE AND YY.EFFDT <= &B.EFFDT)
AND JOBCDLNG.SETID = &B.SETID_JOBCODE
AND JOBCDLNG.JOBCODE = &B.JOBCODE
AND JOBCDLNG.LANGUAGE_CD = $language_cd
End-Select

Begin-Select
ACTNR.DESCR

  Let $ActRsnDescr = &ACTNR.DESCR
  
FROM PS_ACTN_REASON_TBL ACTNR
WHERE ACTNR.EFFDT = (SELECT MAX(YY.EFFDT) FROM PS_ACTN_REASON_TBL YY WHERE YY.ACTION = ACTNR.ACTION AND YY.ACTION_REASON = ACTNR.ACTION_REASON AND YY.EFFDT <= &B.EFFDT)
AND ACTNR.EFF_STATUS = 'A'
AND ACTNR.ACTION = &B.ACTION
AND ACTNR.ACTION_REASON = &B.ACTION_REASON
End-Select

Begin-Select
ACTNRLNG.DESCR

  Let $ActRsnDescr = &ACTNRLNG.DESCR
  
FROM PS_ACTN_RSN_LANG ACTNRLNG
WHERE ACTNRLNG.EFFDT = (SELECT MAX(YY.EFFDT) FROM PS_ACTN_RSN_LANG YY WHERE YY.ACTION = ACTNRLNG.ACTION AND YY.ACTION_REASON = ACTNRLNG.ACTION_REASON AND YY.EFFDT <= &B.EFFDT)
AND ACTNRLNG.ACTION = &B.ACTION
AND ACTNRLNG.ACTION_REASON = &B.ACTION_REASON
AND ACTNRLNG.LANGUAGE_CD = $language_cd
End-Select

End-Procedure !NameAndDescriptions

!**********************************
Begin-Procedure DependentBenef
!**********************************
Do RevPrint(#PrintLine_DD)
Begin-Select
BN.EMPLID
BN.DEPENDENT_BENEF
BN.BIRTHDATE
NM.NAME_DISPLAY

  Let $DepBenef = &BN.DEPENDENT_BENEF
  Do GetDepBenefName
  Let $DepName = &NM.NAME_DISPLAY
  Let $DepBirthDate = &BN.BIRTHDATE

  Do Format-DateTime($DepBirthDate, $DepBirthDate, {DEFDMY}, '', '')
  Print $DepName (+1,{DET_col_9})
  Print $DepBirthDate (,{DET_col_10})
  Let $NationalIdChl = substr($DepNatID, 1, 2)||'-'||rtrim(substr($DepNatID, 3, 8),' ') ||'-'||substr($DepNatID,11, 1)
  Print $NationalIdChl (,{DET_col_11},13)
  Add 1 to #CountDepBenef
  
FROM PS_DEP_BEN_NAME NM, PS_DEP_BEN BN, PS_DEP_BEN_EFF RLSH
WHERE BN.EMPLID = $CurrentEmplid
AND BN.EMPLID = NM.EMPLID
AND BN.DEPENDENT_BENEF = NM.DEPENDENT_BENEF
AND NM.EFFDT = (SELECT MAX(NM2.EFFDT) FROM PS_DEP_BEN_NAME NM2 WHERE NM2.EMPLID = NM.EMPLID AND NM2.DEPENDENT_BENEF = NM.DEPENDENT_BENEF AND NM2.EFFDT <= $AsOfToday)
AND RLSH.EMPLID = NM.EMPLID
AND RLSH.DEPENDENT_BENEF = NM.DEPENDENT_BENEF
AND RLSH.EFFDT = NM.EFFDT
AND RLSH.RELATIONSHIP IN ('C','FC')
End-Select

If #PrintLine_ER > #PrintLine_DD
  Let #CountRows = #PrintLine_ER
Else
  Let #CountRows = #PrintLine_DD
End-If

If #CountRows > #CountDepBenef
  Let #CountDepBenef = #CountRows - #CountDepBenef
  Print '' (+#CountDepBenef,1)
End-If
Let #CountDepBenef = 0
End-Procedure


!**********************************
!Procedure Report-Translation
!**********************************
begin-Procedure Report-Translation
let $language_cd = $prcs_language_cd
do Init_Report_Translation ('GPARLB01', $language_cd)
do Append_Report_Translation ('GPARLB01')
do Get_Field_Information ('GPARLB01', 'TITLE',     $ReportTitle,    #DW)
do Get_Field_Information ('GPARLB01', 'CUIT',      $CompCuit_lbl,   #DW)
do Get_Field_Information ('GPARLB01', 'ACTIVITY',  $CompActv_lbl,   #DW)
do Get_Field_Information ('GPARLB01', 'PAGENUMBER',$PageNumber_lbl, #DW)
do Get_Field_Information ('GPARLB01', 'COMPRATE',  $Comprate_lbl,   #DW)
do Get_Field_Information ('GPARLB01', 'EMPLID',    $Emplid_lbl,     #DW)
do Get_Field_Information ('GPARLB01', 'JOBCODE',   $Jobcode_lbl,    #DW)
do Get_Field_Information ('GPARLB01', 'DEPTID',    $Deptid_lbl,     #DW)
do Get_Field_Information ('GPARLB01', 'MARITAL',   $Marital_lbl,    #DW)
do Get_Field_Information ('GPARLB01', 'HIREDT',    $Hiredt_lbl,     #DW)
do Get_Field_Information ('GPARLB01', 'TERMINATIONDT',  $Terminationdt_lbl,   #DW)
do Get_Field_Information ('GPARLB01', 'TERMINATIONRS',  $Terminationrs_lbl,   #DW)
do Get_Field_Information ('GPARLB01', 'CUIL',      $Nationalid_lbl, #DW)
do Get_Field_Information ('GPARLB01', 'CALRUNID',  $Calrunid_lbl,   #DW)
do Get_Field_Information ('GPARLB01', 'CONTRACT',  $Contract_lbl,   #DW)
do Get_Field_Information ('GPARLB01', 'HDR_COMPRATE', $HDR_comprate_lbl, #DW)
do Get_Field_Information ('GPARLB01', 'HDR_ER_DESCR', $HDR_er_descr_lbl, #DW)
do Get_Field_Information ('GPARLB01', 'HDR_ER_QTY',   $HDR_er_qty_lbl,   #DW)
do Get_Field_Information ('GPARLB01', 'HDR_ER_AMT',   $HDR_er_amy_lbl,   #DW)
do Get_Field_Information ('GPARLB01', 'HDR_DD_DESCR', $HDR_dd_descr_lbl, #DW)
do Get_Field_Information ('GPARLB01', 'HDR_DD_QTY',   $HDR_dd_qty_lbl,   #DW)
do Get_Field_Information ('GPARLB01', 'HDR_DD_AMT',   $HDR_dd_amy_lbl,   #DW)
do Get_Field_Information ('GPARLB01', 'HDR_CHL_NAME', $HDR_chl_name_lbl, #DW)
do Get_Field_Information ('GPARLB01', 'HDR_CHL_DT',   $HDR_chl_dt_lbl,   #DW)
do Get_Field_Information ('GPARLB01', 'HDR_CHL_NTLID',$HDR_chl_ntlid_lbl,#DW)
do Get_Field_Information ('GPARLB01', 'FOO_TOTALS',   $FOO_totals_lbl,   #DW)
End-Procedure Report-Translation

!**********************************
Begin-Procedure GetDepBenefName
!**********************************
Begin-Select 
NI.NATIONAL_ID

  Let $DepNatID = &NI.NATIONAL_ID
  
FROM PS_DEP_BENEF_NID NI
WHERE NI.EMPLID = &BN.EMPLID
AND NI.DEPENDENT_BENEF = $DepBenef
AND NI.COUNTRY = 'ARG'
AND NI.NATIONAL_ID_TYPE='CUIL'
End-Select
End-Procedure !GetDepBenefName

!**********************************
! Procedure - UpdateNumberPage
!**********************************
Begin-Procedure UpdateNumberPage
Begin-SQL
UPDATE PS_COMPANY_TBL_ARG SET LAST_PGNBR_ARG = #Last_pg_num + 1
WHERE COMPANY = $Company
AND EFFDT = $CompEffDt
End-SQL
End-Procedure ! UpdateNumberPage



!**********************************
! Procedure - GetAddl_SIJP
!**********************************
Begin-Procedure GetAddl_SIJP($Emplid, #EmplRcd, $Effdt, :$HireModCd, :$EmpSit_Cd, :$Condition_Cd, :$AccidentCd, :$GeoZone, :$ActivityCd, $WorkedTime)
LET $HireModCd    = ' '
LET $EmpSit_Cd    = ' '
LET $Condition_Cd = ' '
LET $AccidentCd   = ' '
LET $GeoZone      = ' '
LET $ActivityCd   = ' '
LET $WorkedTime   = ' '
Begin-Select
A.HIREMOD_CD_ARG          
A.EMPSIT_CD_ARG           
A.CONDITION_CD_ARG        
A.ACCIDENT_CD_ARG         
A.GEOZONE_CD_ARG          
A.ACTIVITY_CD_ARG         
A.WORKED_TIME_ARG         


 LET $HireModCd    = &A.HIREMOD_CD_ARG  
 LET $EmpSit_Cd    = &A.EMPSIT_CD_ARG   
 LET $Condition_Cd = &A.CONDITION_CD_ARG
 LET $AccidentCd   = &A.ACCIDENT_CD_ARG 
 LET $GeoZone      = &A.GEOZONE_CD_ARG  
 LET $ActivityCd   = &A.ACTIVITY_CD_ARG 
 LET $WorkedTime   = &A.WORKED_TIME_ARG 
 
   
  
 FROM PS_ADDL_SIJP_ARG A 
WHERE A.EMPLID   = $Emplid
  AND A.EMPL_RCD = #EmplRcd
  AND A.EFFDT    = (SELECT MAX(AA.EFFDT) 
                      FROM PS_ADDL_SIJP_ARG AA 
                     WHERE AA.EMPLID   = A.EMPLID 
                       AND AA.EMPL_RCD = A.EMPL_RCD 
                       AND AA.EFFDT   <= $Effdt) 
  AND EFF_STATUS = 'A'
End-Select

End-Procedure GetAddl_SIJP

!**********************************
! Procedure - GetNid
!**********************************
Begin-Procedure GetNid($Emplid, $Country, $IdType, :$NatID)
Let $NatID = ' '
Begin-Select
NID.NATIONAL_ID         &NID.NATIONAL_ID

  LET $NatID = LTRIM(RTRIM(&NID.NATIONAL_ID ,' ') ,' ')

  FROM PS_PERS_NID NID 
 WHERE NID.EMPLID           = $Emplid
   AND NID.NATIONAL_ID_TYPE = $IdType
   AND NID.COUNTRY          = $Country
End-Select
End-Procedure GetNid

!**********************************
! Procedure - DepBenARG
!**********************************
Begin-Procedure DepBenARG($Emplid, $Effdt, :#NbrBenARG, :#NbrChild, :#Spouse)
LET #NbrBenARG = 0
LET #NbrChild  = 0
LET #Spouse    = 0
Begin-Select
COUNT(*)           &NbrBen
 
  LET #NbrBenARG = &NbrBen

  FROM PS_DEP_BENEF_ARG ADB 
 WHERE (($Effdt BETWEEN ADB.SOC_SEC_BGNDT_ARG AND ADB.SOC_SEC_ENDDT_ARG) 
    OR (ADB.SOC_SEC_BGNDT_ARG <= $Effdt 
   AND ADB.SOC_SEC_ENDDT_ARG IS NULL)) 
   AND ADB.SOC_SEC_CON_ARG    = 'A'
   AND ADB.EMPLID             =  $Emplid
End-Select  

Begin-Select
COUNT(*)            &NbrChild
 
  LET #NbrChild = &NbrChild
 
  FROM PS_DEP_BEN_EFF A 
WHERE A.EMPLID          = $Emplid
   AND A.RELATIONSHIP IN ('C','FC','RC') 
   AND A.EFFDT        = (SELECT MAX(AA.EFFDT) 
                           FROM PS_DEP_BEN_EFF AA 
                          WHERE AA.EMPLID          = A.EMPLID 
                            AND AA.DEPENDENT_BENEF = A.DEPENDENT_BENEF 
                            AND A.EFFDT           <= $Effdt) 
END-SELECT

Begin-Select
COUNT(*)            &Spouse

  LET #Spouse = &Spouse

  FROM PS_DEP_BEN_EFF A 
 WHERE A.EMPLID       = $Emplid
  AND A.RELATIONSHIP    = 'SP' 
  AND A.MAR_STATUS      = 'M' 
  AND A.EFFDT           = (SELECT MAX(AA.EFFDT) 
                             FROM PS_DEP_BEN_EFF AA 
                            WHERE AA.EMPLID          = A.EMPLID 
                              AND AA.DEPENDENT_BENEF = A.DEPENDENT_BENEF 
                              AND A.EFFDT           <= $Effdt)
 END-SELECT
End-Procedure DepBenARG

!**********************************
! Procedure - Cuit
!**********************************
Begin-Procedure Cuit
Begin-Select 
A.CUIT_ARG
A.DGI_ARG
A.ACTIVITY_ARG

  Let $CompCuit = &A.CUIT_ARG
  Let $CompDgi  = &A.DGI_ARG
  Let $CompActv = &A.ACTIVITY_ARG
  
FROM PS_COMPANY_TBL_ARG A
WHERE A.EFFDT = (SELECT MAX(AA.EFFDT) FROM PS_COMPANY_TBL_ARG AA 
WHERE AA.COMPANY = A.COMPANY AND AA.EFFDT <= $AsOfToday)
AND A.COMPANY = $Company
End-Select
End-Procedure ! Cuit


!**********************************
! Procedure - Soc_Sec_Cd_Arg
!**********************************
Begin-Procedure Soc_Sec_Cd_Arg($Emplid, #Empl_Rcd, $Effdt, :$Soc_Sec_Cd, :#Count)
LET $Soc_Sec_Cd = ''
LET #Count =0
Begin-Select
COUNT(A.EMPLID)     &SOC_SEC_CD_ARGCOUNT
A.SOC_SEC_CD_ARG     &SOC_SEC_CD_ARG

  LET $Soc_Sec_Cd = LTRIM(RTRIM(&SOC_SEC_CD_ARG,' '),' ')
  LET #COUNT      = &SOC_SEC_CD_ARGCOUNT

  FROM PS_ADDL_HB_ARG A 
 WHERE A.EMPLID     = $Emplid 
   AND A.EMPL_RCD   = #Empl_Rcd
   AND A.EFFDT      = (SELECT MAX(AA.EFFDT) 
                         FROM PS_ADDL_HB_ARG AA 
                        WHERE AA.EMPLID   = A.EMPLID 
                          AND AA.EMPL_RCD = A.EMPL_RCD 
                          AND AA.EFFDT   <= $Effdt) 
   AND EFF_STATUS   = 'A'
GROUP BY A.SOC_SEC_CD_ARG
End-Select
End-Procedure Soc_Sec_Cd_Arg

!**********************************
Begin-Procedure ReadFstLoad
!**********************************
Begin-Select 
A.GPAR_LGL_CODE                     &BAS.GPAR_LGL_CODE   
A.PIN_NUM                           &BAS.PIN_NUM         
A.GPAR_CONTRIB_TP                   &BAS.GPAR_CONTRIB_TP 
A.GPAR_AFIP_BASES                   &BAS.GPAR_AFIP_BASES 
A.GPAR_REPEAT_MARK                  &BAS.GPAR_REPEAT_MARK
B.PIN_TYPE                          &BAS.PIN_TYPE
B.DESCR                             &BAS.DESCR

  LET $AFIP          = LTRIM(RTRIM(&BAS.GPAR_LGL_CODE, ' '),' ')
  LET $ConcEmpleador = EDIT(TO_CHAR(&BAS.PIN_NUM),'0000000000')
  LET $DescrConcEmpl = LTRIM(RTRIM(&BAS.DESCR, ' '),' ')
  LET $RepeatMark    = LTRIM(RTRIM(&BAS.GPAR_REPEAT_MARK, ' '),' ')
  LET $AFIP_Base     = LTRIM(RTRIM(&BAS.GPAR_AFIP_BASES, ' '),' ')
  LET $Contrib_Tp    = LTRIM(RTRIM(&BAS.GPAR_CONTRIB_TP, ' '),' ')
  LET $Pin_Tp        = LTRIM(RTRIM(&BAS.PIN_TYPE, ' '),' ')

  LET $SIPA_Aport         = '0'
  LET $SIPA_Contrib       = '0'
  LET $INSSJyP_Aport      = '0'
  LET $INSSJyP_Contrib    = '0'
  LET $ObraSocial_Aport   = '0'
  LET $ObraSocial_Contrib = '0'
  LET $ANSSAL_Aport       = '0'
  LET $ANSSAL_Contrib     = '0'
  LET $RENATEA_Aport      = '0'
  LET $RENATEA_Contrib    = '0'
  LET $Family_Contrib     = '0'
  LET $NatEmp_Contrib     = '0'
  LET $WorkRisk_Contrib   = '0'
  LET $DifReg_Aport       = '0'
  LET $SpecialReg_Aport   = '0'
  LET $RepMark            = '0'

  IF $RepeatMark = 'Y'
    LET $RepMark  = '1'
  END-IF


  EVALUATE $AFIP_Base
    WHEN = '001'              !SIPA
      DO AportContrib($Contrib_Tp, $Pin_Tp, $SIPA_Contrib, $SIPA_Aport)
    WHEN = '002'              !INSSJyP
      DO AportContrib($Contrib_Tp, $Pin_Tp, $INSSJyP_Contrib, $INSSJyP_Aport)
    WHEN = '003'              !Social Benefits
      DO AportContrib($Contrib_Tp, $Pin_Tp, $ObraSocial_Contrib, $ObraSocial_Aport)
    WHEN = '004'              !exAnSSal
      DO AportContrib($Contrib_Tp, $Pin_Tp, $ANSSAL_Contrib, $ANSSAL_Aport)
    WHEN = '005'              !Family Assignments
      DO AportContrib($Contrib_Tp, $Pin_Tp, $Family_Contrib, $Family_Aport)
    WHEN = '006'              !National Employment Fund
      DO AportContrib($Contrib_Tp, $Pin_Tp, $NatEmp_Contrib, $NatEmp_Aport)
    WHEN = '007'              !Work Risk Law
      DO AportContrib($Contrib_Tp, $Pin_Tp, $WorkRisk_Contrib, $WorkRisk_Aport)
    WHEN = '008'              !Mandatory Collective Life Insurance
      DO AportContrib($Contrib_Tp, $Pin_Tp, $LifeInsu_Contrib, $LifeInsu_Aport)
    WHEN = '009'              !Diferetial Regimes
      DO AportContrib($Contrib_Tp, $Pin_Tp, $DifReg_Contrib, $DifReg_Aport)
    WHEN = '010'              !Special Regimes
      DO AportContrib($Contrib_Tp, $Pin_Tp, $SpecialReg_Contrib, $SpecialReg_Aport)
    WHEN = '011'              !RENATEA (ex RENATRE)
      DO AportContrib($Contrib_Tp, $Pin_Tp, $RENATEA_Contrib, $RENATEA_Aport)
  END-EVALUATE

  #Debug show '   AFIP: ' $AFIP ', $Pin_Tp: ' $Pin_Tp  ', Conc Empleador:' $ConcEmpleador ', Descr Conc Empl: ' $DescrConcEmpl ', Rep Mark: ' $RepMark ', AFIP Base: ' $AFIP_Base ', Contrib Tp: ' $Contrib_Tp
  
  DO FistLoadLayout

  FROM PS_GPAR_PRC_TP_DTL A, PS_GP_PIN B
  WHERE A.EFFDT           = (SELECT MAX(A_ED.EFFDT) FROM PS_GPAR_PRC_TP_DTL A_ED 
                              WHERE A.GPAR_PRCS_TYPE = A_ED.GPAR_PRCS_TYPE 
                                AND A_ED.EFF_STATUS  = 'A'
                                AND A_ED.EFFDT      <= $Current-Date) 
     AND A.EFF_STATUS     = 'A'
     AND A.PIN_NUM        = B.PIN_NUM
     AND A.GPAR_PRCS_TYPE = $Gpar_Prcs_Type
End-Select
End-Procedure !ReadFstLoad

!**********************************
Begin-Procedure ReadDigBooK
!**********************************
LET $Days_Per = '000'
Begin-Select 
COUNT(B.EMPLID)             &AFIPH.COUNT
A.PAY_ENTITY                &AFIPH.PAY_ENTITY       
A.GP_PAYGROUP               &AFIPH.GP_PAYGROUP      
A.CAL_ID                    &AFIPH.CAL_ID           
A.RUN_TYPE                  &AFIPH.RUN_TYPE         
A.CALC_TYPE                 &AFIPH.CALC_TYPE        
A.CAL_PRD_ID                &AFIPH.CAL_PRD_ID       
A.PRD_BGN_DT                &AFIPH.PRD_BGN_DT       
A.PRD_END_DT                &AFIPH.PRD_END_DT       
A.PYMT_DT                   &AFIPH.PYMT_DT          
A.PROCESS_RETRO_IND         &AFIPH.PROCESS_RETRO_IND
A.SEL_CRIT_OPTN             &AFIPH.SEL_CRIT_OPTN    
A.FREQUENCY_ID              &AFIPH.FREQUENCY_ID     
A.CURRENCY_CD               &AFIPH.CURRENCY_CD      
A.COUNTRY                   &AFIPH.COUNTRY          

  DO DigBookVar1
  LET $CountEmpl    = EDIT(TO_CHAR(&AFIPH.COUNT),'000000')

  LET $Prd_Bgn_Dt   = LTRIM(RTRIM(&AFIPH.PRD_BGN_DT   , ' '),' ')
  LET $RunType      = LTRIM(RTRIM(&AFIPH.RUN_TYPE   , ' '),' ')
  LET $Prd_End_Dt   = LTRIM(RTRIM(&AFIPH.PRD_END_DT   , ' '),' ')
  LET $Afip_Cal_Id  = LTRIM(RTRIM(&AFIPH.CAL_ID   , ' '),' ')
  LET $Cuit         = REPLACE(REPLACE($CompCuit, '-',''), '/','')
  LET $Frecuency    = LTRIM(RTRIM(&AFIPH.FREQUENCY_ID   , ' '),' ')
  DO Convert-To-DTU-Date($Prd_Bgn_Dt, $Dtu_Prd_Bgn_Dt)
  Do dtu-parse-date($Dtu_Prd_Bgn_Dt, #dtu_yr, #dtu_mo, #dtu_da)
  LET $Period       = EDIT(TO_CHAR(#dtu_yr),'0000')||EDIT(TO_CHAR(#dtu_mo),'00')
  LET $Pymt_Dt      = LTRIM(RTRIM(&AFIPH.PYMT_DT   , ' '),' ')
  DO Convert-To-DTU-Date($Pymt_Dt, $Dtu_Pymt_Dt)
  Do dtu-parse-date($Dtu_Pymt_Dt, #dtu_yr, #dtu_mo, #dtu_da)
  LET $Pymt_Dt = EDIT(TO_CHAR(#dtu_yr),'0000')||EDIT(TO_CHAR(#dtu_mo),'00')||EDIT(TO_CHAR(#dtu_da),'00')
  Do dtu-parse-date($Dtu_SendDate, #dtu_yr, #dtu_mo, #dtu_da)
  LET $SendDate = EDIT(TO_CHAR(#dtu_yr),'0000')||EDIT(TO_CHAR(#dtu_mo),'00')||EDIT(TO_CHAR(#dtu_da),'00')
  DO Convert-To-DTU-Date($Prd_End_Dt, $Dtu_Prd_End_Dt)
  DO dtu-diff-days($Dtu_Prd_Bgn_Dt, $Dtu_Prd_End_Dt, #dtu_days_per)
  LET $Days_Per = EDIT(TO_CHAR(#dtu_days_per),'000')
  #Debug SHOW 'DIAS PER: ' $Days_Per ', $Dtu_Prd_Bgn_Dt: ' $Dtu_Prd_Bgn_Dt ', $Dtu_Prd_End_Dt: ' $Dtu_Prd_End_Dt
  DO DigBookLayout1
  DO ReadDigBookDtl

 FROM PS_GP_CAL_RUN_DTL A
     ,PS_GPAR_AFIP_TMP  B
WHERE A.CAL_RUN_ID       = B.CAL_RUN_ID
  AND A.CAL_ID           = B.CAL_ID
  AND A.CAL_RUN_ID       = $Cal_Run_Id
  AND B.PROCESS_INSTANCE = #prcs_process_instance
 GROUP BY A.PAY_ENTITY, A.GP_PAYGROUP, A.CAL_ID,A.RUN_TYPE, A.CALC_TYPE, A.CAL_PRD_ID, A.PRD_BGN_DT, A.PRD_END_DT, A.PYMT_DT, A.PROCESS_RETRO_IND, A.SEL_CRIT_OPTN, A.FREQUENCY_ID, A.CURRENCY_CD, A.COUNTRY
End-Select
End-Procedure ReadDigBookHdr


!**********************************
Begin-Procedure ReadDigBookDtl
!**********************************
Begin-Select 
A.EMPLID                     &AFIP.EMPLID        
A.EMPL_RCD                   &AFIP.EMPL_RCD      
A.CAL_RUN_ID                 &AFIP.CAL_RUN_ID    
A.CAL_ID                     &AFIP.CAL_ID        
A.GP_PAYGROUP                &AFIP.GP_PAYGROUP   
A.SEG_BGN_DT                 &AFIP.SEG_BGN_DT    
A.SEG_END_DT                 &AFIP.SEG_END_DT    
A.UNION_CD                   &AFIP.UNION_CD      
A.JOBCODE                    &AFIP.JOBCODE       
A.HIRE_DT                    &AFIP.HIRE_DT       
A.TERMINATION_DT             &AFIP.TERMINATION_DT
A.EMPL_CTG                   &AFIP.EMPL_CTG      
A.COMPRATE                   &AFIP.COMPRATE      
A.LAST_HIRE_DT               &AFIP.LAST_HIRE_DT
A.REG_TEMP                   &AFIP.REG_TEMP

  DO DigBookVar2
  DO DigBookVar4
  DO DigBookVar5
  LET #Empl_Rcd     = &AFIP.EMPL_RCD
  LET $Emplid       = LTRIM(RTRIM(&AFIP.EMPLID   , ' '),' ')
  LET $Seg_End_Dt   = LTRIM(RTRIM(&AFIP.SEG_END_DT   , ' '),' ')
  LET $lasthiredt   = LTRIM(RTRIM(&AFIP.LAST_HIRE_DT   , ' '),' ')
  LET $Union_Cd     = LTRIM(RTRIM(&AFIP.UNION_CD   , ' '),' ')
  IF $Union_Cd <> ''
    LET $Work_CCT           = '1'
  ELSE
    LET $Work_CCT           = '0'
  END-IF

  DO DepBenARG($Emplid, $Seg_End_Dt, #NbrBenARG, #NbrChild, #Spouse)

  LET $Married    = 0
  IF #Spouse >0
    LET $Married = '1'
  END-IF
  #Debug show 'Emplid: '  $Emplid ', Rcd: ' #Empl_Rcd ', #NbrChild: ' #NbrChild ', #Spouse: ' #Spouse ', Seg_End_Dt: ' $Seg_End_Dt ', $Married: ' $Married ', $Union_Cd: ' $Union_Cd
  DO GetAddl_SIJP($Emplid, #EmplRcd, $Seg_End_Dt, $HireModCd, $EmpSit_Cd, $Condition_Cd, $AccidentCd, $GeoZone, $ActivityCd, $WorkedTime)

  LET $Sons       = EDIT(TO_CHAR(#NbrChild),'00')
  LET $Comprate   = EDIT(TO_CHAR(&AFIP.COMPRATE),'0000000000000V00')
  LET $JobCode    = LTRIM(RTRIM(&AFIP.JOBCODE   , ' '),' ')
  LET $HireDt     = LTRIM(RTRIM(&AFIP.HIRE_DT   , ' '),' ')
  LET $Ter_Dt     = LTRIM(RTRIM(&AFIP.TERMINATION_DT   , ' '),' ')
  LET $Empl_Ctg   = LTRIM(RTRIM(&AFIP.EMPL_CTG   , ' '),' ')
  LET $Reg_Temp   = LTRIM(RTRIM(&AFIP.REG_TEMP   , ' '),' ')

  DO GetNid($Emplid, 'ARG', 'CUIL', $CUIL)
  LET $CUIL = EDIT($CUIL, '00000000000')

  DO Convert-To-DTU-Date($HireDt, $Dtu_HireDt)
  Do dtu-parse-date($Dtu_HireDt, #dtu_yr, #dtu_mo, #dtu_da)
  LET $HireDt = EDIT(TO_CHAR(#dtu_yr),'0000')||EDIT(TO_CHAR(#dtu_mo),'00')||EDIT(TO_CHAR(#dtu_da),'00')
  DO Convert-To-DTU-Date($Ter_Dt, $Dtu_Ter_Dt)
  Do dtu-parse-date($Dtu_Ter_Dt, #dtu_yr, #dtu_mo, #dtu_da)
  LET $Ter_Dt = EDIT(TO_CHAR(#dtu_yr),'0000')||EDIT(TO_CHAR(#dtu_mo),'00')||EDIT(TO_CHAR(#dtu_da),'00')

  DO Soc_Sec_Cd_Arg($Emplid, #Empl_Rcd, $Seg_End_Dt, $SocSecCd, #Soc_Sec_Cd)
  #Debug show '$Emplid:' $Emplid ', #Empl_Rcd:' #Empl_Rcd ', $Seg_End_Dt:' $Seg_End_Dt ', $SocSecCd:' $SocSecCd ', #Soc_Sec_Cd' #Soc_Sec_Cd
  LET $SocSecCon = EDIT(TO_CHAR(#Soc_Sec_Cd), '00')
  DO BankAccount

  DO DigBookLayout2
  DO ReadAFIP
  DO SitRev
  DO SICOSS
  DO DigBookLayout4
  IF $Reg_Temp = 'T'
    DO DigBookLayout5
  END-IF
  FROM PS_GPAR_AFIP_TMP A 
WHERE A.CAL_RUN_ID       = $Cal_Run_Id
  AND A.PROCESS_INSTANCE = #prcs_process_instance
End-Select
End-Procedure ReadDigBookDtl


Begin-Procedure ReadAFIP
!**********************************
Begin-Select 
A.GPAR_LGL_CODE                                 &AFIPT.GPAR_LGL_CODE
A.PIN_NUM                                       &AFIPT.PIN_NUM      
C.PIN_NM                                        &AFIPT.PIN_NM       
C.PIN_TYPE                                      &AFIPT.PIN_TYPE     
B.GP_PAYGROUP                                   &AFIPT.GP_PAYGROUP  
B.CAL_ID                                        &AFIPT.CAL_ID       
B.RSLT_SEG_NUM                                  &AFIPT.RSLT_SEG_NUM 
B.BASE_RSLT_VAL                                 &AFIPT.BASE_RSLT_VAL
B.BASE_ADJ_VAL                                  &AFIPT.BASE_ADJ_VAL 
B.RATE_RSLT_VAL                                 &AFIPT.RATE_RSLT_VAL
B.UNIT_RSLT_VAL                                 &AFIPT.UNIT_RSLT_VAL
B.UNIT_ADJ_VAL                                  &AFIPT.UNIT_ADJ_VAL 
B.PCT_RSLT_VAL                                  &AFIPT.PCT_RSLT_VAL 
SUM( B.CALC_RSLT_VAL+ B.CALC_ADJ_VAL)           &AFIPT.SUMVAL

  DO DigBookVar3
  LET $LegalCd  = EDIT(RTRIM(LTRIM(&AFIPT.GPAR_LGL_CODE, ' '), ' '),'0000000000')
  LET $PayDays  = EDIT(TO_CHAR(&AFIPT.UNIT_RSLT_VAL),'000V00')
  LET $Amount   = EDIT(TO_CHAR(&AFIPT.SUMVAL),'0000000000000V00')
  LET $PinType  = RTRIM(LTRIM(&AFIPT.PIN_TYPE, ' '), ' ')
  LET $PinNm    = RTRIM(LTRIM(&AFIPT.PIN_NM, ' '), ' ')

  EVALUATE $PinType
  WHEN = 'ER'
    LET $Cred_Deb = 'C'
  WHEN = 'DD'
    LET $Cred_Deb = 'D'
  END-EVALUATE
   #debug show '$Emplid: ' $Emplid ', #Empl_Rcd: ' #Empl_Rcd ', $LegalCd: ' $LegalCd ', $PinNm: ' $PinNm ', $PayDays:' $PayDays ', $Amount:' $Amount
  DO DigBookLayout3

  FROM PS_GPAR_PRC_TP_DTL A, PS_GP_RSLT_ERN_DED B, PS_GP_PIN C 
  WHERE A.EFFDT             = (SELECT MAX(A_ED.EFFDT) FROM PS_GPAR_PRC_TP_DTL A_ED 
                                WHERE A.GPAR_PRCS_TYPE = A_ED.GPAR_PRCS_TYPE 
                                  AND A_ED.EFF_STATUS  = 'A' 
                                  AND A_ED.EFFDT      <= $Prd_End_Dt) 
     AND A.EFF_STATUS       = 'A' 
     AND A.GPAR_PRCS_TYPE   = $Gpar_Prcs_Type
     AND A.PIN_NUM          = B.PIN_NUM 
     AND B.CAL_RUN_ID       = $Cal_Run_Id
     AND B.EMPLID           = $Emplid
     AND B.EMPL_RCD         = #Empl_Rcd
     AND B.ORIG_CAL_RUN_ID  = B.CAL_RUN_ID 
     AND A.PIN_NUM          = C.PIN_NUM 
  GROUP BY A.GPAR_LGL_CODE,  A.PIN_NUM,  C.PIN_NM,  C.PIN_TYPE,  B.GP_PAYGROUP,  B.CAL_ID,  B.RSLT_SEG_NUM,  B.BASE_RSLT_VAL,  B.BASE_ADJ_VAL,  B.RATE_RSLT_VAL,  B.UNIT_RSLT_VAL,  B.UNIT_ADJ_VAL,  B.PCT_RSLT_VAL
End-Select
End-Procedure ReadAFIP

!**********************************
! Procedure - InsertTemp
!**********************************
Begin-Procedure InsertTemp

LET $prcs_process_instance = TO_CHAR(#prcs_process_instance)

Begin-SQL
INSERT INTO PS_GPAR_AFIP_TMP (PROCESS_INSTANCE
 , EMPLID
 , EMPL_RCD
 , CAL_RUN_ID
 , CAL_ID
 , GP_PAYGROUP
 , SEG_BGN_DT
 , SEG_END_DT
 , UNION_CD
 , JOBCODE
 , HIRE_DT
 , TERMINATION_DT
 , EMPL_CTG
 , COMPRATE
 , LAST_HIRE_DT
 , REG_TEMP) 
 SELECT DISTINCT $prcs_process_instance
,A.EMPLID        
,A.EMPL_RCD      
,A.CAL_RUN_ID    
,A.CAL_ID        
,A.GP_PAYGROUP   
,A.SEG_BGN_DT
,A.SEG_END_DT    
,B.UNION_CD      
,B.JOBCODE       
,B.HIRE_DT       
,B.TERMINATION_DT
,B.EMPL_CTG      
,B.COMPRATE      
,B.LAST_HIRE_DT
,B.REG_TEMP
  FROM PS_GP_PYE_SEG_STAT A 
  , PS_JOB B 
  , PS_GP_RSLT_ERN_DED C
 WHERE A.CAL_RUN_ID     = $Cal_run_id
   AND A.PYE_CALC_STAT IN ('50','70','75') 
   AND B.EMPLID            = A.EMPLID 
   AND B.EMPL_RCD          = A.EMPL_RCD 
   AND B.EFFDT             = (SELECT MAX(C.EFFDT) 
                                FROM PS_JOB C 
                               WHERE C.EMPLID = B.EMPLID 
                                 AND C.EMPL_RCD = B.EMPL_RCD 
                                 AND C.EFFDT <= A.SEG_END_DT) 
   AND B.EFFSEQ            = (SELECT MAX(D.EFFSEQ) 
                                FROM PS_JOB D 
                               WHERE D.EMPLID = B.EMPLID 
                                 AND D.EMPL_RCD = B.EMPL_RCD 
                                 AND D.EFFDT = B.EFFDT)
   AND A.ORIG_CAL_RUN_ID   = A.CAL_RUN_ID 
   AND A.RSLT_SEG_NUM      = (SELECT MAX(AA.RSLT_SEG_NUM) 
                                FROM PS_GP_PYE_SEG_STAT AA 
                               WHERE AA.EMPLID    = A.EMPLID 
                                 AND AA.EMPL_RCD  = A.EMPL_RCD 
                                 AND AA.CAL_RUN_ID= A.CAL_RUN_ID 
                                 AND AA.CAL_ID    = A.CAL_ID) 
   AND A.EMPLID            = C.EMPLID 
   AND A.EMPL_RCD          = C.EMPL_RCD 
   AND A.CAL_RUN_ID        = C.CAL_RUN_ID
   AND A.GP_PAYGROUP       = C.GP_PAYGROUP
   AND A.CAL_ID            = C.CAL_ID
   AND A.ORIG_CAL_RUN_ID   = C.ORIG_CAL_RUN_ID
   AND A.RSLT_SEG_NUM      = C.RSLT_SEG_NUM
   AND B.COMPANY           = $Company
End-SQL
End-Procedure InsertTemp


!**********************************
Begin-Procedure BankAccount
!**********************************
Begin-Select 
A.INSTANCE                  &BNK.INSTANCE        
A.PAYMENT_MTHD              &BNK.PAYMENT_MTHD    
A.ACCOUNT_ID                &BNK.ACCOUNT_ID      
A.PARTIAL_ALLOWED           &BNK.PARTIAL_ALLOWED 
A.PRIMARY_ACCT_IND          &BNK.PRIMARY_ACCT_IND
A.PERCENT_RATE              &BNK.PERCENT_RATE    
A.DISTRIB_AMT               &BNK.DISTRIB_AMT     
A.CURRENCY_CD               &BNK.CURRENCY_CD     
B.CBU_ARG                   &BNK.CBU_ARG         
 

  LET $CBU_ARGBnk      = LTRIM(RTRIM(&BNK.CBU_ARG    , ' '),' ')
  LET $Payment_Mthd    = LTRIM(RTRIM(&BNK.PAYMENT_MTHD    , ' '),' ')
  
  EVALUATE $Payment_Mthd
  WHEN ='A' !Cash
    LET $PayMethd = '1'
  WHEN ='C' !Check
    LET $PayMethd = '2'
  WHEN ='T' !Transfer Account
    LET $PayMethd = '3'
    LET $CBU_ARG  = $CBU_ARGBnk
  WHEN-OTHER
    LET $PayMethd = ' '
  END-EVALUATE

 FROM PS_GP_NET_DIST_DTL A  
 LEFT OUTER JOIN  PS_PYE_BANK_ARG B 
              ON  A.EMPLID = B.EMPLID 
              AND B.ACCOUNT_ID = A.ACCOUNT_ID
WHERE A.EFFDT = (SELECT MAX(A_ED.EFFDT) FROM PS_GP_NET_DIST_DTL A_ED 
                  WHERE A.EMPLID = A_ED.EMPLID 
                    AND A.EMPL_RCD = A_ED.EMPL_RCD 
                    AND A.RUN_TYPE = A_ED.RUN_TYPE 
                    AND A_ED.EFFDT <= $Prd_End_Dt) 
  AND A.EMPLID    = $Emplid
  AND A.EMPL_RCD  = #Empl_Rcd
  AND A.RUN_TYPE  = $RunType
End-Select
Begin-Select 
A.CITY                &CMPY_City
A.STATE               &CMPY_State

  Let $DepRevista  = &CMPY_City || ' ' || &CMPY_State
  
FROM PS_COMPANY_TBL A
WHERE A.EFFDT = (SELECT MAX(AA.EFFDT) FROM PS_COMPANY_TBL AA 
WHERE AA.COMPANY = A.COMPANY AND AA.EFFDT <= $Prd_End_Dt)
AND A.COMPANY = $Company
End-Select
End-Procedure BankAccount
 
!**********************************
Begin-Procedure SICOSS
!**********************************
LET $Obra_Contrib      = '000000000000000'
LET $Base_Obra_Aport   = '000000000000000'
LET $Base_Obra_Contrib = '000000000000000'
LET $Base_Work_Risk    = '000000000000000'

DO Acum-SICOSS($Emplid, #Empl_Rcd, $Cal_Run_Id, '0170', #AdverseWrkArea)
LET $AdverseWrkArea = EDIT(TO_CHAR(#AdverseWrkArea), '000V00')

DO Acum-SICOSS($Emplid, #Empl_Rcd, $Cal_Run_Id, '0190', #JubACDaysWrk)
LET $JubACDaysWrk = EDIT(TO_CHAR(#JubACDaysWrk), '00')

DO Acum-SICOSS($Emplid, #Empl_Rcd, $Cal_Run_Id, '0300', #WorkedHrs)
LET $WorkedHrs = EDIT(TO_CHAR(#WorkedHrs), '000')

DO Acum-SICOSS($Emplid, #Empl_Rcd, $Cal_Run_Id, '0290', #DifTAX_Contrib)
LET $DifTAX_Contrib = EDIT(TO_CHAR(#DifTAX_Contrib), '000V00')

DO Acum-SICOSS($Emplid, #Empl_Rcd, $Cal_Run_Id, '0270', #RemAdj)
LET $RemAdj = EDIT(TO_CHAR(#RemAdj), '0000000000000V00')

DO Acum-SICOSS($Emplid, #Empl_Rcd, $Cal_Run_Id, '0260', #MaternityRem)
LET $MaternityRem = EDIT(TO_CHAR(#MaternityRem), '0000000000000V00')

DO Acum-SICOSS($Emplid, #Empl_Rcd, $Cal_Run_Id, '0140', #RemunBruta)
LET $RemunBruta = EDIT(TO_CHAR(#RemunBruta), '0000000000000V00')

DO Acum-SICOSS($Emplid, #Empl_Rcd, $Cal_Run_Id, '0020', #Base1)
LET $Base1 = EDIT(TO_CHAR(#Base1), '0000000000000V00')

DO Acum-SICOSS($Emplid, #Empl_Rcd, $Cal_Run_Id, '0040', #Base2)
LET $Base2 = EDIT(TO_CHAR(#Base2), '0000000000000V00')

DO Acum-SICOSS($Emplid, #Empl_Rcd, $Cal_Run_Id, '0050', #Base3)
LET $Base3 = EDIT(TO_CHAR(#Base3), '0000000000000V00')

DO Acum-SICOSS($Emplid, #Empl_Rcd, $Cal_Run_Id, '0060', #Base4)
LET $Base4 = EDIT(TO_CHAR(#Base4), '0000000000000V00')

DO Acum-SICOSS($Emplid, #Empl_Rcd, $Cal_Run_Id, '0200', #Base5)
LET $Base5 = EDIT(TO_CHAR(#Base5), '0000000000000V00')

DO Acum-SICOSS($Emplid, #Empl_Rcd, $Cal_Run_Id, '0210', #Base6)
LET $Base6 = EDIT(TO_CHAR(#Base6), '0000000000000V00')

DO Acum-SICOSS($Emplid, #Empl_Rcd, $Cal_Run_Id, '0230', #Base7)
LET $Base7 = EDIT(TO_CHAR(#Base7), '0000000000000V00')

DO Acum-SICOSS($Emplid, #Empl_Rcd, $Cal_Run_Id, '0310', #Base8 )
LET $Base8 = EDIT(TO_CHAR(#Base8), '0000000000000V00')

DO Acum-SICOSS($Emplid, #Empl_Rcd, $Cal_Run_Id, '0280', #Base9)
LET $Base9 = EDIT(TO_CHAR(#Base9), '0000000000000V00')

DO Acum-SICOSS($Emplid, #Empl_Rcd, $Cal_Run_Id, '0320', #Base_DiffInput)
LET $Base_DiffInput = EDIT(TO_CHAR(#Base_DiffInput), '0000000000000V00')

DO Acum-SICOSS($Emplid, #Empl_Rcd, $Cal_Run_Id, '0330', #Base_DiffOuput)
LET $Base_DiffOuput = EDIT(TO_CHAR(#Base_DiffOuput), '0000000000000V00')

DO Acum-SICOSS($Emplid, #Empl_Rcd, $Cal_Run_Id, '0350', #Base10)
LET $Base10 = EDIT(TO_CHAR(#Base10), '0000000000000V00')

DO Acum-SICOSS($Emplid, #Empl_Rcd, $Cal_Run_Id, '0360', #Base_Law26473)
LET $Base_Law26473 = EDIT(TO_CHAR(#Base_Law26473), '0000000000000V00')
End-Procedure SICOSS


!**********************************
Begin-Procedure Acum-SICOSS($Emplid,#Empl_Rcd, $Cal_Run_Id, $Concpt, :#RsltVal)
!**********************************
LET #RsltVal = 0
Begin-Select 
SUM( A.CALC_RSLT_VAL+ A.USER_ADJ_VAL)                 &SumACMSicoss

  LET #RsltVal = &SumACMSicoss

 FROM PS_GP_RSLT_ACUM A
    , PS_GPAR_SIJP_PARM1 B 
WHERE A.ORIG_CAL_RUN_ID   = A.CAL_RUN_ID 
  AND A.EMPLID            = $Emplid
  AND A.EMPL_RCD          = #Empl_Rcd
  AND A.CAL_RUN_ID        = $Cal_Run_Id
  AND A.PIN_NUM           = B.PIN_NUM 
  AND B.EFFDT             = (SELECT MAX(B_ED.EFFDT) FROM PS_GPAR_SIJP_PARM1 B_ED, PS_GPAR_SIJP_PARM C_ED
                              WHERE B_ED.EFFDT      = C_ED.EFFDT
                                AND C_ED.EFF_STATUS = 'A'
                                AND B_ED.EFFDT     <= A.SLICE_END_DT) 
  AND B.GPAR_SIJP_CONCEPT = $Concpt 
End-Select
End-Procedure Acum-SICOSS


!**********************************
Begin-Procedure SitRev
!**********************************
LET $SIT_CODE1 = '00'
LET $DAY_CODE1 = '00' 
LET $SIT_CODE2 = '00' 
LET $DAY_CODE2 = '00' 
LET $SIT_CODE3 = '00' 
LET $DAY_CODE3 = '00'
!/*Begin Extraction from data from Job and Abs take */
!/*Job*/
BEGIN-SELECT DISTINCT
C.GPAR_SIJP_TYPE          &SIJP.GPAR_SIJP_TYPE
C.EMPSIT_CD_ARG           &SIJP.EMPSIT_CD_ARG 
B.EFFDT                   &SIJP.JOBDATE         
B.ACTION                  &SIJP.ACTION        

   LET $JOBDATE = LTRIM(RTRIM(&SIJP.JOBDATE,' '),' ')

   If $JOBDATE <> ''
     DO Convert-To-DTU-Date($JOBDATE, $Dtu_JOBDATE)
     Do dtu-parse-date($Dtu_JOBDATE, #dtu_yr, #dtu_mo, #dtu_da)
      LET $JOBDATE_DAY = EDIT(TO_CHAR(#dtu_da),'00')
      LET $jobday = TO_CHAR(#dtu_da)
   Else
      LET $JOBDATE_DAY = '00'
      LET $jobday = '0'
   End-If

 FROM PS_JOB B 
     ,PS_GPAR_SIJP_PARM2 C 
WHERE B.ACTION_REASON   = C.ACTION_REASON 
  AND B.ACTION          = C.ACTION 
  AND C.GPAR_SIJP_TYPE  = 'JO' 
  AND B.EFFDT BETWEEN (SELECT D.PRD_BGN_DT 
                         FROM PS_GP_CAL_RUN_DTL D 
                        WHERE D.CAL_RUN_ID = $Cal_Run_id 
                          AND CALC_TYPE    = 'P') 
                  AND ($Prd_End_Dt) 
  AND B.EMPLID          = $Emplid 
  AND B.EMPL_RCD        = #Empl_Rcd
  AND B.EFFDT >=$Prd_Bgn_Dt and B.EFFDT<=($Prd_End_Dt)
END-SELECT

DO AT-Event($Emplid, $Cal_Run_id, #Empl_Rcd, $Prd_Bgn_Dt, $Prd_End_Dt, $ATBGNDATE_DAY1, $ATENDDATE_DAY1, $day1, $day1_end, $variable_dt2, $empsit_cd_argat1)
DO AT-Event($Emplid, $Cal_Run_id, #Empl_Rcd, $variable_dt2, $Prd_End_Dt, $ATBGNDATE_DAY2, $ATENDDATE_DAY2, $day2, $day2_end, $variable_dt3, $empsit_cd_argat2)
DO AT-Event($Emplid, $Cal_Run_id, #Empl_Rcd, $variable_dt3, $Prd_End_Dt, $ATBGNDATE_DAY3, $ATENDDATE_DAY3, $day3, $day3_end, $variable_dt4, $empsit_cd_argat3)


  If $ATBGNDATE1 > $Prd_Bgn_Dt 
     LET $SIT_CODE1 = '01'
     DO Convert-To-DTU-Date($Prd_Bgn_Dt, $Dtu_Prd_Bgn_Dt)
     Do dtu-parse-date($Dtu_Prd_Bgn_Dt, #dtu_yr, #dtu_mo, #dtu_da)
     LET $DAY_CODE1 = EDIT(TO_CHAR(#dtu_da),'00')
     LET $SIT_CODE2 = $empsit_cd_argat1
     LET $DAY_CODE2 = $ATBGNDATE1_DAY
     If $ATBGNDATE2 > $ATENDDATE1 Or ($ATBGNDATE2 = '' And $ATENDDATE1 <= $Prd_End_Dt)
        LET $SIT_CODE3 = '01'
        LET $DAY_CODE3 = $ATENDDATE1_DAY
     Else
        LET $SIT_CODE3 = $empsit_cd_argat2
        LET $DAY_CODE3 = $ATBGNDATE2_DAY
     End-If
  Else
     LET $SIT_CODE1 = $empsit_cd_argat1
     LET $DAY_CODE1 = $ATBGNDATE1_DAY
     If $ATBGNDATE2 > $ATENDDATE1 Or ($ATBGNDATE2 = '' And  $ATENDDATE1 <= $Prd_End_Dt And
              $SIT_CODE1 <> '') 
        LET $SIT_CODE2 = '01'
        LET $DAY_CODE2 = $ATENDDATE1_DAY
        LET $SIT_CODE3 = $empsit_cd_argat2
        LET $DAY_CODE3 = $ATBGNDATE2_DAY
     Else
        LET $SIT_CODE2 = $empsit_cd_argat2
        LET $DAY_CODE2 = $ATBGNDATE2_DAY
        If $ATBGNDATE3 > $ATENDDATE2 Or
              ($ATBGNDATE3 = '' And
                 $ATENDDATE2 <= $Prd_End_Dt And
                 $SIT_CODE2 <> '') 
           LET $SIT_CODE3 = '01'
           LET $DAY_CODE3 = $ATENDDATE2_DAY
        Else
           LET $SIT_CODE3 = $empsit_cd_argat3
           LET $DAY_CODE3 = $ATBGNDATE3_DAY
        End-If
     End-If
  End-If
  
  If $SIT_CODE1 = '' 
     LET $SIT_CODE1 = '01'
     LET $DAY_CODE1 = '01'
  End-If
  
  If $lasthiredt > $Prd_End_Dt
     Do dtu-parse-date($Dtu_Prd_Bgn_Dt, #dtu_yr, #dtu_mo, #dtu_da)
     LET $DAY_CODE1 = EDIT(TO_CHAR(#dtu_da),'00')
  End-If
  
  LET $day1 = $DAY_CODE1
  LET $day2 = $DAY_CODE2
  LET $day3 = $DAY_CODE3
  
  If $JOBDATE <> ''
     If $jobday < $day1 
        LET $SIT_CODE2 = $SIT_CODE3
        LET $DAY_CODE2 = $DAY_CODE3
        LET $SIT_CODE3 = '00'
        LET $DAY_CODE3 = '00'
        LET $SIT_CODE1 = $empsit_cd_argjob
        LET $DAY_CODE1 = $JOBDATE_DAY
     Else
        If $day1 < $jobday And
              ($day2 > $jobday Or
                 $day2 = '0') 
           LET $SIT_CODE3 = $SIT_CODE3
           LET $DAY_CODE3 = $DAY_CODE3
           LET $SIT_CODE2 = $empsit_cd_argjob
           LET $DAY_CODE2 = $JOBDATE_DAY
        Else
           If $day2 < $jobday And
                 ($day3 > $jobday Or
                    $day3 = '0') 
              LET $SIT_CODE3 = $empsit_cd_argjob
              LET $DAY_CODE3 = $JOBDATE_DAY
           End-If
        End-If
     End-If
  End-If
  
  LET $SIT_CODE1 = EDIT($SIT_CODE1,'00')
  LET $SIT_CODE2 = EDIT($SIT_CODE2,'00')
  LET $SIT_CODE3 = EDIT($SIT_CODE3,'00')
  

End-Procedure SitRev

!**********************************
Begin-Procedure AT-Event($Emplid, $Cal_Run_id, #Empl_Rcd, $variable_dt, $Prd_End_Dt, :$ATBGNDATE_DAY, :$ATENDDATE_DAY, :$day, :$day_end, :$variable_dt2, :$empsit_cd_argat)
!**********************************
LET $ATBGNDATE_DAY    = '00'
LET $ATENDDATE_DAY    = '00'
LET $day              = '0'
LET $day_end          = '0'
LET $variable_dt2     = ''
LET $empsit_cd_argat  = ''
BEGIN-SELECT DISTINCT
I.GPAR_SIJP_TYPE                &sijp_typeat
I.EMPSIT_CD_ARG                 &empsit_cd_argat
G.ABSENCE_DATE                  &ATBGNDATE
G.ABS_END_DT                    &ATENDDATE

    !/*AT event 01
   LET $ATBGNDATE         = LTRIM(RTRIM(&ATBGNDATE,' '),' ')
   LET $empsit_cd_argat   = LTRIM(RTRIM(&empsit_cd_argat,' '),' ')
   If $ATBGNDATE <> ''
      DO Convert-To-DTU-Date($ATBGNDATE, $Dtu_ATBGNDATE)
      Do dtu-parse-date($Dtu_ATBGNDATE, #dtu_yr, #dtu_mo, #dtu_da)
      DO Convert-To-DTU-Date($ATENDDATE, $Dtu_ATENDDATE)
      Do dtu-parse-date($Dtu_ATENDDATE, #dtu_yr2, #dtu_mo2, #dtu_da2)
      DO dtu-add-days($Dtu_ATENDDATE, 1 , $variable_dt2)
      DO Convert-From-DTU-Date($variable_dt2, $variable_dt2)
      LET $ATBGNDATE_DAY  = EDIT(TO_CHAR(#dtu_da),'00')
      LET $ATENDDATE_DAY  = EDIT(TO_CHAR(#dtu_da2),'00')
      LET $day            = TO_CHAR(#dtu_da1)
      LET $day_end        = TO_CHAR(#dtu_da2)
   End-If

 FROM PS_GP_RSLT_ABS G 
     ,PS_GP_PIN H 
     ,PS_GPAR_SIJP_PARM2 I 
WHERE G.PIN_TAKE_NUM    = I.PIN_NUM 
  AND I.GPAR_SIJP_TYPE  = 'AT' 
  AND G.PIN_TAKE_NUM    = H.PIN_NUM 
  AND G.PIN_TAKE_NUM    = I.PIN_NUM 
  AND G.ABSENCE_DATE    = (SELECT MIN(AA.ABSENCE_DATE) 
                             FROM PS_GP_RSLT_ABS AA 
                            WHERE AA.EMPLID       = G.EMPLID 
                              AND AA.CAL_RUN_ID   = G.CAL_RUN_ID 
                              AND AA.EMPL_RCD     = G.EMPL_RCD 
                              AND AA.PIN_TAKE_NUM = G.PIN_TAKE_NUM) 
  AND G.ABS_END_DT      = (SELECT MAX(J.ABS_END_DT) 
                             FROM PS_GP_RSLT_ABS J 
                            WHERE J.EMPLID        = G.EMPLID 
                              AND J.CAL_RUN_ID    = G.CAL_RUN_ID 
                              AND J.EMPL_RCD      = G.EMPL_RCD 
                              AND J.CAL_ID        = G.CAL_ID 
                              AND J.PIN_TAKE_NUM  = G.PIN_TAKE_NUM) 
  AND G.EMPLID        = $Emplid
  AND G.CAL_RUN_ID    = $Cal_Run_id 
  AND G.EMPL_RCD      = #Empl_Rcd
  AND G.absence_date >= $variable_dt 
  and G.ABS_BGN_DT   <= $Prd_End_Dt
ORDER BY G.ABSENCE_DATE
END-SELECT
End-Procedure AT-Event


!***************************************************************!
! Funcin: Open-File.
! Descripcin: Open File
!***************************************************************!
BEGIN-PROCEDURE Open-File($Filename, #File_Num, :#Filestat)
  OPEN $Filename as #File_Num FOR-WRITING record=1500:vary status=#Filestat
  IF #Filestat < 0
        show 'File Cannot be Open' $Filename
        stop 
  END-IF
END-PROCEDURE 


!***************************************************************!
! Funcin: Close_file.
! Descripcin:  Cierra archivo que se indique
!***************************************************************!
BEGIN-PROCEDURE Close_file (#File_Num)
  CLOSE #File_Num
  move 0 to #End-File 
END-PROCEDURE 


!**********************************
! Procedure - AportContrib
!**********************************
Begin-Procedure AportContrib($ContrTp, $PinTp, :$VarContr, :$VarAport)
LET $VarContr = '0'
LET $VarAport = '0'
EVALUATE $ContrTp
  WHEN = 'A'
    IF $PinTp = 'DD'
      LET $VarAport       = ' '
    ELSE
      LET $VarAport       = '1'
    END-IF
  WHEN = 'C'
    IF $PinTp = 'DD'
      LET $VarContr       = ' '
    ELSE
      LET $VarContr       = '1'
    END-IF
  WHEN = 'B'
    IF $PinTp = 'DD'
      LET $VarContr       = ' '
      LET $VarAport       = ' '
    ELSE
      LET $VarContr       = '1'
      LET $VarAport       = '1'
    END-IF
END-EVALUATE

End-Procedure AportContrib


!**********************************
! Procedure - DelTempTable
!**********************************
Begin-Procedure DelTempTable
Begin-SQL
DELETE FROM  PS_GPAR_AFIP_TMP 
 WHERE PROCESS_INSTANCE = #prcs_process_instance
End-SQL
End-Procedure ! DelTempTable


!***********************************************************************
! Procedure  : DigBookVar1                                             *
! Descripcion: Write First Load Layout1 Tp_Reg =01                     *
!**********************************************************************
BEGIN-PROCEDURE DigBookVar1
                          
LET $Period          = ' '
LET $Frecuency       = ' '
LET $CountEmpl       = ' '
End-procedure DigBookVar1

!***********************************************************************
! Procedure  : DigBookLayout2                                          *
! Descripcion: Write First Load Layout1 Tp_Reg =02                     *
!**********************************************************************
BEGIN-PROCEDURE DigBookVar2
                          
LET $Emplid        = ' '
LET $DepRevista    = ' '
LET $CBU_Arg       = ' '
LET $PayMethd      = ' '
End-procedure DigBookVar2

!***********************************************************************
! Procedure  : DigBookLayout1                                          *
! Descripcion: Write First Load Layout1 Tp_Reg =03                     *
!**********************************************************************
BEGIN-PROCEDURE DigBookVar3
                          
LET $LegalCd    = ' '
LET $PayDays    = ' '
LET $Amount     = ' '
LET $Retro    = '000000'
LET $Units    = 'M'
LET $Cred_Deb = ' '
End-procedure DigBookVar3

!***********************************************************************
! Procedure  : DigBookVar4                                             *
! Descripcion: Write First Load Layout1 Tp_Reg =04                     *
!**********************************************************************
BEGIN-PROCEDURE DigBookVar4
                          
LET $Cov_SCVO           = '0'
LET $Reduc              = '0'
LET $LegalType          = ' '
LET $OperationCd        = '0'
LET $JubACDaysWrk       = ' '
LET $WorkedHrs          = ' '
LET $DifTAX_Contrib     = ' '
LET $AdverseWrkArea     = ' '
LET $SocSecCd           = ' '
LET $SocSecCon          = ' '
LET $RemAdj             = ' '
LET $Obra_Contrib       = ' '
LET $Base_Obra_Aport    = ' '
LET $Base_Obra_Contrib  = ' '
LET $Base_Work_Risk     = ' '
LET $MaternityRem       = ' '
LET $RemunBruta         = ' '
LET $Base1              = ' '
LET $Base2              = ' '
LET $Base3              = ' '
LET $Base4              = ' '
LET $Base5              = ' '
LET $Base6              = ' '
LET $Base7              = ' '
LET $Base8              = ' '
LET $Base9              = ' '
LET $Base_DiffInput     = ' '
LET $Base_DiffOuput     = ' '
LET $Base10             = ' '
LET $Base_Law26473      = ' '

End-procedure DigBookVar4

!***********************************************************************
! Procedure  : DigBookVar5                                          *
! Descripcion: Write First Load Layout1 Tp_Reg =05                     *
!**********************************************************************
BEGIN-PROCEDURE DigBookVar5
                          
LET $Empl_Ctg = ' '
LET $JobCode  = ' '
LET $HireDt   = ' '
LET $Ter_Dt   = ' '
LET $Comprate = ' '
End-procedure DigBookVar5


!***********************************************************************
! Procedure  : FistLoadLayout                                          *
! Descripcion: Write First Load Layout                                 *
!***********************************************************************
BEGIN-PROCEDURE FistLoadLayout

   Write #File_FstLoad  From $AFIP:6
                        $ConcEmpleador:10
                        $DescrConcEmpl:150
                        $RepMark:1
                        $SIPA_Aport:1
                        $SIPA_Contrib:1
                        $INSSJyP_Aport:1
                        $INSSJyP_Contrib:1
                        $ObraSocial_Aport:1
                        $ObraSocial_Contrib:1
                        $ANSSAL_Aport:1
                        $ANSSAL_Contrib:1
                        $RENATEA_Aport:1
                        $RENATEA_Contrib:1
                        $Free:1
                        $Family_Contrib:1
                        $Free:1
                        $NatEmp_Contrib:1
                        $Free:1
                        $WorkRisk_Contrib:1
                        $DifReg_Aport:1
                        $Free:1
                        $SpecialReg_Aport:1
                        $Free:9
End-procedure FistLoadLayout  
                             
!***********************************************************************
! Procedure  : DigBookLayout1                                          *
! Descripcion: Write First Load Layout1 Tp_Reg =01                     *
!**********************************************************************
BEGIN-PROCEDURE DigBookLayout1
                          
   Write #File_DigBook  From  '01':2
                              $Cuit:11
                              'SJ':2
                              $Period:6
                              $Frecuency:1
                              $Ter_Nbr:5
                              '30':2
                              $CountEmpl:6
End-procedure DigBookLayout1

!***********************************************************************
! Procedure  : DigBookLayout2                                          *
! Descripcion: Write First Load Layout1 Tp_Reg =02                     *
!**********************************************************************
BEGIN-PROCEDURE DigBookLayout2
                          
   Write #File_DigBook  From  '02':2
                              $CUIL:11
                              $Emplid:10
                              $DepRevista:50
                              $CBU_Arg:22
                              $Days_Per:3
                              $Pymt_Dt:8
                              $SendDate:8
                              $PayMethd:1
End-procedure DigBookLayout2

!***********************************************************************
! Procedure  : DigBookLayout1                                          *
! Descripcion: Write First Load Layout1 Tp_Reg =03                     *
!**********************************************************************
BEGIN-PROCEDURE DigBookLayout3
                          
   Write #File_DigBook  From  '03':2
                              $CUIL:11
                              $LegalCd:10
                              $PayDays:5
                              $Units:1
                              $Amount:15
                              $Cred_Deb:1
                              $Retro:6
End-procedure DigBookLayout3

!***********************************************************************
! Procedure  : DigBookLayout4                                          *
! Descripcion: Write First Load Layout1 Tp_Reg =04                     *
!**********************************************************************
BEGIN-PROCEDURE DigBookLayout4
                          
   Write #File_DigBook  From  '04':2
                              $CUIL:11
                              $Married:1
                              $Sons:2
                              $Work_CCT:1
                              $Cov_SCVO:1
                              $Reduc:1
                              $LegalType:1
                              $OperationCd:1
                              $EmpSit_Cd:2
                              $Condition_Cd:2
                              $ActivityCd:3
                              $HireModCd:3
                              $AccidentCd:2
                              $GeoZone:2
                              $SIT_CODE1:2
                              $DAY_CODE1:2
                              $SIT_CODE2:2
                              $DAY_CODE2:2
                              $SIT_CODE3:2
                              $DAY_CODE3:2
                              $JubACDaysWrk:2
                              $WorkedHrs:3
                              $DifTAX_Contrib:5
                              $AdverseWrkArea:5
                              $SocSecCd:6
                              $SocSecCon:2
                              $RemAdj:15
                              $Obra_Contrib:15
                              $Base_Obra_Aport:15
                              $Base_Obra_Contrib:15
                              $Base_Work_Risk:15
                              $MaternityRem:15
                              $RemunBruta:15
                              $Base1:15
                              $Base2:15
                              $Base3:15
                              $Base4:15
                              $Base5:15
                              $Base6:15
                              $Base7:15
                              $Base8:15
                              $Base9:15
                              $Base_DiffInput:15
                              $Base_DiffOuput:15
                              $Base10:15
                              $Base_Law26473:15
End-procedure DigBookLayout4

!***********************************************************************
! Procedure  : DigBookLayout5                                          *
! Descripcion: Write First Load Layout1 Tp_Reg =05                     *
!**********************************************************************
BEGIN-PROCEDURE DigBookLayout5
                          
   Write #File_DigBook  From  '05':2
                              $CUIL:11
                              $Empl_Ctg:6
                              $JobCode:4
                              $HireDt:8
                              $Ter_Dt:8
                              $Comprate:15
                              $Cuit:11
End-procedure DigBookLayout5

#Include 'curdttim.sqc'  !Get-Current-DateTime procedure
#Include 'datetime.sqc'  !Routines for date and time formatting
#Include 'number.sqc'    !Routines to format numbers
#Include 'stdapi.sqc'    !Update Process API
#Include 'datemath.sqc'  !Datemath Routines
#Include 'sqrtrans.sqc'
#Include 'readxlat.sqc'  ! Procedure to read values from xlattable
#include 'brfile01.sqc'  !File Utilities


