!***********************************************************************
!  GPESFNQ1.SQR  Company Certificate                                   *
!***********************************************************************
!----------------------------------------------------------------------
!                                                                      *
!                                                                      *
!                                                                      *
!                                                                      *
! This software and related documentation are provided under a         *
! license agreement containing restrictions on use and                 *
! disclosure and are protected by intellectual property                *
! laws. Except as expressly permitted in your license agreement        *
! or allowed by law, you may not use, copy, reproduce,                 *
! translate, broadcast, modify, license, transmit, distribute,         *
! exhibit, perform, publish or display any part, in any form or        *
! by any means. Reverse engineering, disassembly, or                   *
! decompilation of this software, unless required by law for           *
! interoperability, is prohibited.                                     *
! The information contained herein is subject to change without        *
! notice and is not warranted to be error-free. If you find any        *
! errors, please report them to us in writing.                         *
!                                                                      *
!                                                                      *
! Copyright (C) 1988, 2020, Oracle and/or its affiliates.              *
! All Rights Reserved.                                                 *
!-----------------------------------------------------------------------
!                                                                      *
!       $Release:  HR92                                                *
!           $Bug:  31141964                                            *
!                                                                      *
!***********************************************************************

#include 'setenv.sqc'    !Set environment

!************************************************************************
!  Report Setup
!************************************************************************
begin-setup
!#include 'ptset01.sqc'
!***********************************************************************
!  PTSET01:  Printer and Page-Size Initialization (Portrait)           *
!      Orientation: Portrait                                           *
!      Max Columns: 125                                                *
!      Max Lines:   79                                                 *
!***********************************************************************
! SourceSafe Information:                                              *
!                                                                      *
! $Header:: /PT81/SQR/ptset01.sqc 4     7/26/00 5:10p Jmacnaug        $*
!                                                                      *
!***********************************************************************
#include 'setupdb.sqc'               ! Database specific setup

#define PTSETxx-Included

declare-printer DEFAULT-HP
!#ifdef PRINT_JAPANESE
!      init-string=<27>&t31P
!      font=28825
!      symbol-set=19K
!#endif

    point-size=7.2           !Original setting at ptset01.sqc 7.2
    pitch=17                 !Original setting at ptset01.sqc 17
end-declare

declare-printer DEFAULT-PD
    point-size=7.2
    pitch=17
end-declare

#ifndef EBCDIC
declare-printer DEFAULT-LP
!#ifdef PRINT_JAPANESE
!   init-string=<27>E<27>(19K<27>&l0O<27>&l6C<27>&l0E<27>&I95F<27>(s16.66H<27>&k2G<27>&t31P<27>(s0p7.25v0s0b28825T
!!
!!     <27>(19K : Command to print out single-byte Katakana
!!     <27>&t31P : 2byte mode
!!     <27>(s0p7.25v0s0b28825T : Kanji font
!!
!#else
    init-string=<27>E<27>(0N<27>&l0O<27>&l6C<27>&l0E<27>&l95F<27>(s16.66H<27>&k2G
!               |    |      |       |       |       |        |           |
!               |    |      |       |       |       |        |            --> CR
!               |    |      |       |       |       |         --> Line Prntr font
!               |    |      |       |       |        --> Number of Lines
!               |    |      |       |        --> Top Margin
!               |    |      |        --> Vertical Motion Index (1/48" increments)
!               |    |       --> Portrait orientation
!               |     --> ISO 8859-1 symbol set
!                --> Reset
!#endif
    before-bold=<27>(s3B
    after-bold=<27>(s0B
end-declare

#else

declare-printer DEFAULT-LP
end-declare
#endif

declare-printer DEFAULT-PS
    point-size=7.2
!
! add STARTUP-FILE=dir\filename to change the symbol set
!
end-declare


#define PAGE_ORIENTATION PORTRAIT


#Define ColR 122  !Column # referenced by Standard Headings. Original value: 108


! Define Parameters
#if {PAPER_SIZE} = 'LETTER'
    #define PAGE_PAPER_SIZE (LETTER)
    #define LINE_HEIGHT 7
    #define CHAR_WIDTH  4.32

#End-if

#if {PAPER_SIZE} = 'A4'

    #define PAGE_PAPER_SIZE (A4)
    #define LINE_HEIGHT 8          !Original setting at ptset01.sqc: 9
    #define CHAR_WIDTH   4.15      !Original setting at ptset01.sqc: 4.32

#endif

declare-layout DEFAULT
    paper-size={PAGE_PAPER_SIZE}
    orientation={PAGE_ORIENTATION}
    line-height={LINE_HEIGHT}              ! 72/printer_point-size
    char-width={CHAR_WIDTH}                ! points, to handle max cols
    left-margin=.25
    top-margin=.05
    bottom-margin=.01

end-declare

declare-report DEFAULT
    layout=DEFAULT
    printer-type=HP
end-declare

!** Allow for 23.3 big decimals
!** To turn on: add "#define DECIMAL26" to your code before calling this sqc.
#ifdef DECIMAL26
    declare-variable
        Default-Numeric=DECIMAL(26)
    end-declare
#end-if


  declare-variable
    !date $Hire-Dt
    !date $Termination-Dt
    date $ERE-ENDDT
    date $Litig_Bgn_Dt-XML
    date $Litig_End_Dt-XML
    !date $Contract-Bgn-Dt
    !date $Contract-End-Dt
    !date $Select-ASOFDATE
  end-declare

end-setup


#include 'sqrtrans.sqc'


#define ACTN_REASON_TBL
#define EMPL_CTG_L1
!#define STATE_NAMES_TBL
#define JOBCODE_TBL
#define COMPANY_TBL
#define RGM_BSE_ESP_TBL
#define OCCUPATN_CD_ESP
#define SOC_SEC_RGM_TBL
!#define RGM_CTZ_ESP_TBL
#define GPES_ACRSN_INEM
#define PSXLATITEM
#define GPES_JOB_CNO_VW
#define GP_PIN
#Include 'rellang.sqc'   ! Translations File

!************************************************************************
! Report Section
!************************************************************************
begin-report
#debug do Fin-Debug-Msg('begin-report')

  do Init-Report
  do Process-Main
  do Stdapi-Term

end-report


!************************************************************************
! Procedure Init-Report
!************************************************************************
begin-procedure Init-Report
#debug do Fin-Debug-Msg('init-report')
  move 'GPESFNQ1' to $ReportID
  move 'Certificado de Empresa' to $ReportTitle

  do Init-DateTime
  do Init-Number
  do Get-Current-DateTime

  do Stdapi-Init

end-procedure


!************************************************************************
! Procedure Main
!************************************************************************
begin-procedure Process-Main
#debug do Fin-Debug-Msg('Process-Main')

#debugd show 'Operator Id:     ' $prcs_oprid
#debugd show 'Run Control Id:  ' $prcs_run_cntl_id

let $Responsible-Name = ''
let $Responsible-DNI = ''
let $Reponsible-JobCode = ''
let $reason_cd = ''
let $Responsable-Puesto = ''
let $Responsable-OrganismoEmpresa = ''
let $Responsable-NumeroCCC = ''
let $Responsable-Domicilio = ''
let $Responsable-NumDomicilio = ''
let $Responsable-Cp = ''
let $Responsable-Localidad = ''
let $Responsable-Provincia = ''
let $Termination-Dt = ''
let $Job-EffDt-Select = ''
let $from-date =''
let $to-date =''
let $EffseqWhere = ''

#debugd show 'As Of Today: ' $AsOfToday

Do Delete-XML-EE

let $sql-statement = 'GPESFNQ1.SQR,Process-Main,Select,PS_GPES_RC_FNQ1'
Begin-SELECT on-error=SQL-Error
A.RESPONSIBLE_ID    &RESPONSIBLE_ID
A.EMPL_RCD          &RESPONSIBLE_RCD
A.GPES_FROM_DT      &GPES_FROM_DATE
A.GPES_TO_DT        &GPES_TO_DATE
A.LANGUAGE_CD
  let $Curr_Language_Cd = &A.LANGUAGE_CD

A.GPES_MULT_CONTRACT &MULT_CNTRCT
A.GPES_EMPLOYEE_TYPE
B.EMPLID
B.EMPL_RCD
B.GPES_REASON
   let $reason_cd = RTRIM(&B.GPES_REASON,' ')

{DateOut-Prefix}B.BGN_DT{DateOut-Suffix}          &B.BGN_DT
{DateOut-Prefix}B.END_DT{DateOut-Suffix}          &B.END_DT
{DateOut-Prefix}B.ORIG_BEGIN_DT{DateOut-Suffix}   &B.ORIG_BEGIN_DT
B.ASOFDATE
B.LST_ASGN_START_DT
B.EFFDT_L1
B.TERMINATION_DT
B.GENERATE_REPORT

        do Determine-Images

        let $Hire-Dt = &B.LST_ASGN_START_DT
        let $from-date = LTRIM(RTRIM(&GPES_FROM_DATE ,' '),' ')
        if &A.GPES_EMPLOYEE_TYPE = 'A'
           let $to-date = ''
           let $Select-ASOFDATE = LTRIM(RTRIM(&B.ASOFDATE ,' '),' ')
        else
           let $to-date = LTRIM(RTRIM(&GPES_TO_DATE ,' '),' ')
           let $Select-ASOFDATE = &B.TERMINATION_DT
        end-if
        #debugd show 'As Of Date: ' $Select-ASOFDATE '(' &B.EMPLID ')'

        if $Responsible-Name = ''
           Do GET-RESPONSIBLE-DATA
        End-if

        let $Select-EMPLID = &B.EMPLID
        If $Prev_Emplid = ''
           Let $Prev_Emplid = $Select-EMPLID
        Else
           new-page
        End-if

        let #Select-EMPL-RCD = &B.EMPL_RCD
        let $Job-EffDt-Select = &B.EFFDT_L1
        let $Termination-Dt = RTRIM(&B.TERMINATION_DT,' ')

        if ($to-date = '' and $reason_cd = 'INE') Or $Termination-Dt =  ''
           let $Termination-Dt = RTRIM(&B.ASOFDATE,' ')
           Let $Empl-Terminated   =  'N'
           let $EffseqWhere = ' AND ACTION NOT IN (SELECT ACTION FROM PS_GPES_TERM_AC_VW AC'
           let $EffseqWhere = $EffseqWhere || ' WHERE AC.EFFDT = (SELECT MAX(EFFDT)'
           let $EffseqWhere = $EffseqWhere || ' FROM  PS_GPES_TERM_AC_VW WHERE ACTION = AC.ACTION'
           let $EffseqWhere = $EffseqWhere || ' AND EFFDT <= ''' || $Select-ASOFDATE || '''))'

        else
           Let $Empl-Terminated   =  'Y'
           let $EffseqWhere = ' AND (ACTION IN (SELECT ACTION FROM  PS_GPES_TERM_AC_VW AC'
           let $EffseqWhere = $EffseqWhere || ' WHERE AC.EFFDT = (SELECT MAX(EFFDT)'
           let $EffseqWhere = $EffseqWhere || ' FROM  PS_GPES_TERM_AC_VW WHERE ACTION = AC.ACTION'
           let $EffseqWhere = $EffseqWhere || ' AND EFFDT <= ''' || $Select-ASOFDATE || '''))'
           let $EffseqWhere = $EffseqWhere || '  OR EMPL_STATUS = ''D'')'

        End-If


        let $fecha_inicio_baja =  &B.BGN_DT
        let $fecha_fin_baja    =  &B.END_DT
        let $orig_begin_date =  &B.ORIG_BEGIN_DT
        Evaluate $reason_cd
           When = 'IT'
           When = 'AT'
           When = 'MT'
           When = 'RE'
           When = 'ERE'
              DO GET-EVENT-BRD-DATE

           When = 'IT'
           When = 'AT'
              DO GET-IT-DATA
              Break

           When = 'INS'
              DO GET-ON-DEMAND-DATA
              Break

           When = 'INE'
           When = 'ERE'
              Let $Prepare-XML = 'Y'
              Let $Stored-CC-EE-Data = 'N'
              Let $Stored-PT-Workdays = 'N'
              Let $Print-Certificate = 'Y'

              DO GET-BAJ-DATA
              Break

           When = 'MT'
              DO GET-MAT-DATA
              Break

           When = 'RE'
              DO GET-RE-DATA
              Break
        End-Evaluate


FROM PS_GPES_RC_FNQ1 A
    ,PS_GPES_RC_FNQ1_EE B

WHERE A.OPRID = $prcs_oprid
 AND  A.RUN_CNTL_ID = $prcs_run_cntl_id
 AND  B.OPRID = A.OPRID
 AND  B.RUN_CNTL_ID = A.RUN_CNTL_ID
 AND  B.GENERATE_REPORT = 'Y'
End-SELECT


If $Prepare-XML = 'Y'
  Do Populate-XML-ITEM
End-If

end-procedure


!************************************************************************
! Procedure Determine-Images
!************************************************************************
begin-procedure Determine-Images
#debug do Fin-Debug-Msg('Determine-Images')

  let #pos  = instr($sqr-program,'gpesfnq1',0)
  let #pos  = #pos - 1
  let $path = substr($sqr-program,1,#pos)

  If $reason_cd = 'IT' or $reason_cd = 'AT' or $reason_cd = 'RE'
    let $FileName = 'mtrabjit.jpg'
    let $FileName2 = 'inemit.jpg'
  Else
    If $reason_cd = 'MT' OR $reason_cd = 'INS'
      let $FileName = 'mntrabjo.jpg'
      let $FileName2 = 'inemmt.jpg'
    Else
      let $FileName = 'mntrabjo.jpg'
      let $FileName2 = 'inem.jpg'
      let $FileName3 = 'gpesfoot.jpg'
    End-If
  End-If
  let $FileDir = $path || $FileName
  let $FileDir2 = $path || $FileName2
  let $FileDir3 = $path || $FileName3

  #ifdef MVS
     let $FileDir = $path || substr($FileName, 1, instr($FileName,'.',0) - 1)
     let $FileDir2 = $path || substr($FileName2, 1, instr($FileName2,'.',0) - 1)
     let $FileDir3 = $path || substr($FileName3, 1, instr($FileName3,'.',0) - 1)
  #end-if

  #ifdef OS390
     let $FileDir = $path || substr($FileName, 1, instr($FileName,'.',0) - 1)
     let $FileDir2 = $path || substr($FileName2, 1, instr($FileName2,'.',0) - 1)
     let $FileDir3 = $path || substr($FileName3, 1, instr($FileName3,'.',0) - 1)
  #end-if

  #ifdef OS400
     let $FileDir = $path || substr($FileName, 1, instr($FileName,'.',0) - 1)
     let $FileDir2 = $path || substr($FileName2, 1, instr($FileName2,'.',0) - 1)
     let $FileDir3 = $path || substr($FileName3, 1, instr($FileName3,'.',0) - 1)
  #end-if

  #debugd show 'FileDir:  ' $FileDir
  #debugd show 'FileDir2: ' $FileDir2
  
end-procedure


!************************************************************************
! Procedure GET-RESPONSIBLE-DATA
!************************************************************************
begin-procedure GET-RESPONSIBLE-DATA
#debug do Fin-Debug-Msg('GET-RESPONSIBLE-DATA')

    let $Select-EMPLID = &RESPONSIBLE_ID
    Do RESPONSIBLE-JOB
    Do PERSONAL-DATA
    let $Responsible-Name = $Empl-Name
    let $Responsible-DNI = $Empl-DNI
    let $Reponsible-JobCode = $Empl-JobCode-Desc

End-Procedure


!************************************************************************
! Procedure GET-EVENT-BRD-DATE
!************************************************************************
begin-procedure GET-EVENT-BRD-DATE
#debug do Fin-Debug-Msg('GET-EVENT-BRD-DATE')

Let $CLI_VR_RECAIDA_XX = 'A'

let $sql-statement = 'GPESFNQ1.SQR,GET-EVENT-BRD-DATE,Select,PS_GP_VARIABLE VR'
Begin-SELECT on-error=SQL-Error
VR1.CHARACTER_VALUE
       Let $CLI_VR_RECAIDA_XX = &VR1.CHARACTER_VALUE

FROM PS_GP_PIN GPP
   , PS_GP_VARIABLE VR1
WHERE ((GPP.PIN_CODE = 'CLI VR RECAIDA AT ESP' AND $reason_cd = 'AT')
    OR (GPP.PIN_CODE = 'CLI VR RECAIDA IT ESP' AND $reason_cd = 'IT')
    OR (GPP.PIN_CODE = 'CLI VR RECAIDA MT ESP' AND $reason_cd = 'MT')
    OR (GPP.PIN_CODE = 'CLI VR RECAIDA RE ESP' AND $reason_cd = 'RE'))
  AND GPP.PIN_NUM = VR1.PIN_NUM
  AND VR1.EFFDT = (SELECT MAX(EFFDT) FROM PS_GP_VARIABLE
                   WHERE PIN_NUM = VR1.PIN_NUM
                     AND EFFDT <= $fecha_fin_baja)
End-SELECT

#debugd show 'CLI_VR_RECAIDA_XX: ' $CLI_VR_RECAIDA_XX '  as of: ' $fecha_fin_baja

If $CLI_VR_RECAIDA_XX = 'A'
  Let $BRD_Date = $orig_begin_date
Else
  Let $BRD_Date = $fecha_inicio_baja
End-if

End-Procedure


!************************************************************************
! Procedure GET-BAJ-DATA
!************************************************************************
begin-procedure GET-BAJ-DATA
#debug do Fin-Debug-Msg('GET-BAJ-DATA')

    Do JOB
    ! Do Hire-Dt-Empl
    Do PERSONAL-DATA
    Do COMPANY-TBL
    Do CMPNY-SCHEME-ID
    Do ERE-DATA
    Do TERMINATION-DATA
    !Do Print-Certificate-BAJ
    !Do Print-PartTime-WorkDays
    Do Print-Contribution-Bases-BAJ

end-procedure

!************************************************************************
! Procedure GET-IT-DATA
!************************************************************************
begin-procedure GET-IT-DATA
#debug do Fin-Debug-Msg('GET-IT-DATA')

    Do DATOS-PARTE
    Do JOB
    Do PERSONAL-DATA
    Do COMPANY-TBL
    Do Print-Certificate-IT
    Let $Bases-AntIT-Loaded = 'N'
    Do Print-Contribution-Bases-AntIT
    Do Print-Contribution-Bases-IT

end-procedure


!************************************************************************
! Procedure GET-MAT-DATA
!************************************************************************
begin-procedure GET-MAT-DATA
#debug do Fin-Debug-Msg('GET-MAT-DATA')

    Do JOB
    Do PERSONAL-DATA
    Do COMPANY-TBL
    Do GET-GUARDA-LEGAL
    Do GET-PATERNITY-DATES
    Do Print-Certificate-MAT
    !Do Print-Contribution-Bases-MAT

end-procedure

!************************************************************************
! Procedure GET-RE-DATA
!************************************************************************
begin-procedure GET-RE-DATA
#debug do Fin-Debug-Msg('GET-RE-DATA')

    !Do Check-Empl-ERE !TEMPORAL
    Do JOB
    Do PERSONAL-DATA
    Do COMPANY-TBL

    Do Print-Certificate-RE
    Do Print-Contribution-Bases-RE
end-procedure


!************************************************************************
! Procedure GET-ON-DEMAND-DATA
!************************************************************************
begin-procedure GET-ON-DEMAND-DATA
#debug do Fin-Debug-Msg('GET-ON-DEMAND-DATA')

    Do JOB
    Do PERSONAL-DATA
    Do COMPANY-TBL
    !Do Print-Certificate-ON-DEMAND
    Do Print-Contribution-Bases-ON-DEMAND

end-procedure

!************************************************************************
! Procedure Hire-Dt-Empl
!************************************************************************
begin-procedure Hire-Dt-Empl
#debug do Fin-Debug-Msg(' Hire-Dt-Empl')
#debugd show 'Employee:                ' $Select-EMPLID
#debugd show 'Employee record:         ' #Select-EMPL-RCD

let $Hire-Dt = ''
let $sql-statement = 'GPESICIT.SQR, Hire-Dt-Empl,Select, PS_JOB '
begin-SELECT on-error=SQL-Error
JHIRE.EMPLID
JHIRE.EMPL_RCD
{DateOut-Prefix}JHIRE.EFFDT{DateOut-Suffix}   &JHIRE.EFFDT
JHIRE.EFFSEQ
JHIRE.ACTION

        If &JHIRE.ACTION = 'HIR' Or &JHIRE.ACTION = 'REH'
           Let $Hire-Dt         =   &JHIRE.EFFDT
           EXIT-SELECT
        end-if

FROM PS_JOB JHIRE

WHERE JHIRE.EMPLID = $Select-EMPLID
  AND JHIRE.EMPL_RCD = #Select-EMPL-RCD
  AND JHIRE.EFFDT <$Job-EffDt-Select
  AND JHIRE.ACTION IN ('HIR','REH')
ORDER BY JHIRE.EMPLID, JHIRE.EMPL_RCD, JHIRE.EFFDT DESC, JHIRE.EFFSEQ DESC
end-SELECT

end-procedure

!************************************************************************
! Procedure TERMINATION-DATA
!************************************************************************
begin-procedure TERMINATION-DATA
#debug do Fin-Debug-Msg('TERMINATION-DATA')

let #days_lit = 0
let $days_lit = ''
let $LITIG_BGN_DT = ''
let $LITIG_END_DT = ''
let $Litig_Bgn_Dt-XML = ''
let $Litig_End_Dt-XML = ''
let $LITIG_BGN_DT-PRINT = ''
let $LITIG_END_DT-PRINT = ''


let $sql-statement = 'GPESFNQ1.SQR,TERMINATION-DATA,Select,PS_GPES_TERM_MNG'
Begin-SELECT on-error=SQL-Error
MN.TERMINATION_DT
MN.GPES_FINAL_DT

   Let $Litig_End_Dt-XML = &MN.GPES_FINAL_DT
   do Convert-To-DTU-Date(&MN.GPES_FINAL_DT ,$LITIG_END_DT )
   EXIT-SELECT

FROM PS_GPES_TERM_MNG MN
WHERE MN.EMPLID = $Select-EMPLID
 AND MN.EMPL_RCD = #Select-EMPL-RCD
 !---AND MN.TERMINATION_DT < $Select-ASOFDATE
 AND MN.GPES_FINAL_DT BETWEEN $Hire-Dt and $Termination-Dt
 ORDER BY MN.EMPLID, MN.EMPL_RCD, MN.TERMINATION_DT DESC

end-SELECT


If $LITIG_END_DT <> ''

let $sql-statement = 'GPESFNQ1.SQR,TERMINATION-DATA,Select,PS_JOB'
Begin-SELECT on-error=SQL-Error
MIN(JJBB.EFFDT)   &JJBB.EFFDT

   Let $Litig_Bgn_Dt-XML = &JJBB.EFFDT
   do Convert-To-DTU-Date(&JJBB.EFFDT ,$LITIG_BGN_DT)
   !do dtu-add-days($LITIG_BGN_DT, 1, $LITIG_BGN_DT)
   do dtu-diff-days($LITIG_BGN_DT, $LITIG_END_DT,  #days_lit)
   do Format-DateTime(&JJBB.EFFDT, $LITIG_BGN_DT-PRINT, {DEFDMY}, '', '')
   do Format-DateTime(&MN.GPES_FINAL_DT, $LITIG_END_DT-PRINT, {DEFDMY}, '', '')
   let $days_lit = to_char(#days_lit + 1)

FROM PS_JOB JJBB
WHERE JJBB.EMPLID = $Select-EMPLID
 AND JJBB.EMPL_RCD = #Select-EMPL-RCD
 AND JJBB.EFFDT BETWEEN &MN.TERMINATION_DT AND &MN.GPES_FINAL_DT
 AND JJBB.ACTION IN ('HIR', 'REH')

end-SELECT

end-if

#Debugd show 'Unfair dissmisal termination date:    ' &MN.TERMINATION_DT
#Debugd show 'Job litigation rehire date:           ' &JJBB.EFFDT
#Debugd show 'Final date for job litigation salary: ' &MN.GPES_FINAL_DT

End-Procedure

!************************************************************************
! Procedure Responsible-JOB
!************************************************************************
begin-procedure Responsible-JOB
#debug do Fin-Debug-Msg('Responsible-JOB')

let $sql-statement = 'GPESFNQ1.SQR,Responsible-JOB,Select,PS_JOB'
begin-SELECT on-error=SQL-Error
J.EMPLID
J.EMPL_RCD
J.EFFDT
J.EFFSEQ
J.SETID_JOBCODE
J.JOBCODE
  let $JC-EffDt = &J.EFFDT
  let $JC-SetID-JobCode = &J.SETID_JOBCODE
  let $JC-JobCode = &J.JOBCODE

  Do Get-JobCode-Descr

  EXIT-SELECT

FROM PS_JOB J

WHERE J.EMPLID = &RESPONSIBLE_ID
  AND J.EMPL_RCD = &RESPONSIBLE_RCD
  AND J.EFFDT <= $Select-ASOFDATE

ORDER BY J.EMPLID, J.EMPL_RCD, J.EFFDT DESC, J.EFFSEQ DESC
end-SELECT

  #debugd show 'Employee:                ' &RESPONSIBLE_ID
  #debugd show 'Employee record:         ' &RESPONSIBLE_RCD
  #debugd show 'Effective Date:          ' $Select-ASOFDATE

end-procedure



!************************************************************************
! Procedure JOB
!************************************************************************
begin-procedure JOB
#debug do Fin-Debug-Msg('JOB')

  let $Actn-Rsn-INEM =''
  let $Actn-Rsn-INEM_Desc =''
  let $Empl-Ctg-Desc = ''
  let $Empl-SS-Grp-Desc = ''
  let $Empl-JobCode-Desc = ''
  let $Cmpny-Ssn = ''
  let $JOB-Full-Part-Time = ''
  let $Schedule_Days = 0
  let #Job-EffSeq-Select = 0


let $sql-statement = 'GPESFNQ1.SQR,JOB,Select,PS_JOB'
begin-SELECT on-error=SQL-Error
JOB.EMPLID
JOB.EMPL_RCD
JOB.EFFDT
JOB.COMPANY
JOB.SETID_LOCATION
JOB.LOCATION
JOB.REG_REGION
JOB.FULL_PART_TIME
JOB.GP_PAYGROUP
JOB.REG_TEMP
JOB.FTE
JOB.HR_STATUS

  let $JOB-Full-Part-Time = &JOB.FULL_PART_TIME
  let $Pay_Group = &JOB.GP_PAYGROUP

  LET $CMP = &JOB.COMPANY

JOB.SETID_LBR_AGRMNT
  let $SetId_Lbr_Agrmnt = &JOB.SETID_LBR_AGRMNT

JOB.LABOR_AGREEMENT
JOB.EMPL_CTG
JOB.CONTRACT_NUM

JOB.ESTABID
  let $EstabID = rtrim(&JOB.ESTABID, ' ')

JOB.ACTION
  let $FieldName = 'ACTION'
  let $FieldValue = &JOB.ACTION
  let $AsOfDate = $Select-ASOFDATE
  Do Read-Translate-Table
  let $Action_Reason_Desc = $XLatLongName

JOB.ACTION_REASON
JOBJR.SSN_EMPLOYER
  let $Cmpny-Ssn = &JOBJR.SSN_EMPLOYER

CNTRCT.REG_REGION
!CNTRCT.SCHEME_ID_ESP
!CNTRCT.CONTRIB_ID_ESP
  !let $SchemeId = &CNTRCT.SCHEME_ID_ESP
  !let $ContribId = &CNTRCT.CONTRIB_ID_ESP

CNTRCT.CONTRACT_BEGIN_DT
CNTRCT.CONTRACT_END_DT
!CNTRCT.CONTRCT_EXP_END_DT
  Let $BeginDt1 = &CNTRCT.CONTRACT_BEGIN_DT
  Let $Contract-Bgn-Dt = &CNTRCT.CONTRACT_BEGIN_DT
  if $Empl-Terminated = 'Y'
     Let $EndDt1 = &CNTRCT.CONTRACT_END_DT
  else
     let $EndDt1 = $Select-ASOFDATE
  End-if
  !Let $ExpEndDt1 = &CNTRCT.CONTRCT_EXP_END_DT
  Do Check-Duration

JOB.SOC_SEC_RISK_CODE
JOB.POSITION_NBR
JOB.SETID_JOBCODE
JOB.JOBCODE
  Let $Position = &JOB.POSITION_NBR
  Let $SetId-JobCode = &JOB.SETID_JOBCODE
  Let $JobCode = &JOB.JOBCODE
  Do Get-CNO-Descr

  Do Get-Action-Reason-Descr
  Do Get-Empl-Ctg-Descr
  Do Get-Contrct-Type-Descr


FROM PS_JOB JOB
    ,PS_JOB_JR JOBJR
    ,PS_CONTRACT_DATA CNTRCT


WHERE JOB.EMPLID = $Select-EMPLID
  AND JOB.EMPL_RCD = #Select-EMPL-RCD
  AND JOB.EFFDT = $Job-EffDt-Select
  AND JOB.EFFSEQ = (SELECT MAX(EFFSEQ) FROM PS_JOB
                    WHERE EMPLID = JOB.EMPLID
                     AND  EMPL_RCD = JOB.EMPL_RCD
                     AND  EFFDT = JOB.EFFDT
                     [$EffseqWhere])
  AND JOBJR.EMPLID = JOB.EMPLID
  AND JOBJR.EMPL_RCD = JOB.EMPL_RCD
  AND JOBJR.EFFDT = JOB.EFFDT
  AND JOBJR.EFFSEQ = JOB.EFFSEQ
  AND CNTRCT.EMPLID = JOB.EMPLID
  AND CNTRCT.CONTRACT_NUM = JOB.CONTRACT_NUM
end-SELECT
  #debugd show '$EffseqWhere:    ' $EffseqWhere


  #debugd show 'Employee:        ' $Select-EMPLID
  #debugd show 'Employee record: ' #Select-EMPL-RCD
  #debugd show 'As Of:           ' $Select-ASOFDATE
  #debugd show 'Establishment:   ' $EstabID

end-procedure



!************************************************************************
! Procedure Get-Action-Reason-Descr
!************************************************************************
begin-procedure Get-Action-Reason-Descr
#debug do Fin-Debug-Msg('Get-Action-Reason-Descr')

let $Actn-Rsn-INEM      = ''
let $Actn-Rsn-INEM_Desc = ''

let $sql-statement = 'GPESFNQ1.SQR,Get-Action-Reason-Descr,Select,PS_GPES_ACRSN_INEM'
begin-SELECT on-error=SQL-Error
ACTNRSNI.ACTION
ACTNRSNI.ACTION_REASON
ACTNRSNI.EFFDT
ACTNRSNI.GPES_ACTNRSN_INEM
ACTNRSNI.DESCR254

  let $Actn-Rsn-INEM = rtrim(&ACTNRSNI.GPES_ACTNRSN_INEM, ' ')
  let $GPES_ACRSN_INEM-DESCR50 = rtrim(&ACTNRSNI.DESCR254, ' ')
  Do Get_Related_GPES_ACTNRSN_INEM(&ACTNRSNI.EFFDT,&ACTNRSNI.ACTION_REASON,&ACTNRSNI.ACTION)
  let $Actn-Rsn-INEM_Desc = $GPES_ACRSN_INEM-DESCR50


FROM PS_GPES_ACRSN_INEM ACTNRSNI

WHERE ACTNRSNI.ACTION = &JOB.ACTION
 AND  ACTNRSNI.ACTION_REASON = &JOB.ACTION_REASON
 AND  ACTNRSNI.EFFDT = (SELECT MAX(EFFDT)
                        FROM PS_GPES_ACRSN_INEM ACTNRSN2
                        WHERE ACTNRSN2.ACTION = ACTNRSNI.ACTION
                         AND  ACTNRSN2.ACTION_REASON = ACTNRSNI.ACTION_REASON
                         AND  ACTNRSN2.EFFDT <= $Select-ASOFDATE)
 AND  ACTNRSNI.EFF_STATUS <> 'I'

end-SELECT

  #debugd show 'Action:              ' &JOB.ACTION
  #debugd show 'Action reason:       ' &JOB.ACTION_REASON
  #debugd show 'Effective date:      ' &ACTNRSN.EFFDT
  #debugd show 'INEM actn/rsn Descr: ' $Actn-Rsn-INEM_Desc
  #debugd show 'Process Lang:        ' $Curr_Language_Cd
  #debugd show 'Base Lang:           ' $PSoptions_language_cd


If $Actn-Rsn-INEM_Desc = ''

let $sql-statement = 'GPESFNQ1.SQR,Get-Action-Reason-Descr,Select,PS_ACTN_REASON_TBL'
begin-SELECT on-error=SQL-Error
ACTNRSN.ACTION
ACTNRSN.ACTION_REASON
ACTNRSN.EFFDT
ACTNRSN.DESCR

  let $Actn-Rsn-INEM = rtrim(&ACTNRSN.ACTION_REASON, ' ')
  let $ACTN_REASON_TBL-DESCR = rtrim(&ACTNRSN.DESCR, ' ')
  Do Get_Related_ACTN_REASON_TBL(&ACTNRSN.ACTION,&ACTNRSN.ACTION_REASON,&ACTNRSN.EFFDT)
  let $Actn-Rsn-INEM_Desc = $ACTN_REASON_TBL-DESCR
  !let $Actn-Rsn-INEM_Desc = $Actn-Rsn-INEM || ' - ' || $Actn-Rsn-INEM_Desc


FROM PS_ACTN_REASON_TBL ACTNRSN

WHERE ACTNRSN.ACTION = &JOB.ACTION
 AND  ACTNRSN.ACTION_REASON = &JOB.ACTION_REASON
 AND  ACTNRSN.EFFDT = (SELECT MAX(EFFDT)
                        FROM PS_ACTN_REASON_TBL ACTNRSN2
                        WHERE ACTNRSN2.ACTION = ACTNRSN.ACTION
                         AND  ACTNRSN2.ACTION_REASON = ACTNRSN.ACTION_REASON
                         AND  ACTNRSN2.EFFDT <= $Select-ASOFDATE)
 AND  ACTNRSN.EFF_STATUS <> 'I'

end-SELECT

  #debugd show 'Job actn/rsn Descr: ' $Actn-Rsn-INEM_Desc

end-if


end-procedure


!************************************************************************
! Procedure Get-Empl-Ctg-Descr
!************************************************************************
BEGIN-PROCEDURE Get-Empl-Ctg-Descr
#debug do Fin-Debug-Msg('Get-Empl-Ctg-Descr')

 ! Do Get-setid(&JOB.REG_REGION, 'EMPL_CTG_L1', $SetId_Lbr_Agrmnt)

let $Empl-SS-Grp-Cd = ''

let $sql-statement = 'GPESFNQ1.SQR,Get-Empl-Ctg-Descr,Select,PS_EMPL_CTG_L1'
BEGIN-SELECT on-error=SQL-Error
CTG.SETID
CTG.LABOR_AGREEMENT
CTG.EMPL_CTG
CTG.EFFDT
CTG.DESCR
CTG.SOC_SEC_WRK_GRP_CD

  let $EMPL_CTG_L1-DESCR = rtrim(&CTG.DESCR, ' ')
  Do Get_Related_EMPL_CTG_L1(&CTG.EFFDT,&CTG.EMPL_CTG,&CTG.LABOR_AGREEMENT,&CTG.SETID)
  let $Empl-Ctg-Desc = $EMPL_CTG_L1-DESCR
  !
  !LET $Empl-SS-Grp-Cd = &CTG.SOC_SEC_WRK_GRP_CD

FROM PS_EMPL_CTG_L1 CTG

  WHERE CTG.SETID = $SetId_Lbr_Agrmnt
   AND  CTG.LABOR_AGREEMENT = &JOB.LABOR_AGREEMENT
   AND  CTG.EMPL_CTG = &JOB.EMPL_CTG
   AND  CTG.EFFDT = (SELECT MAX(EFFDT)
                        FROM PS_EMPL_CTG_L1
                        WHERE SETID = CTG.SETID
                         AND  LABOR_AGREEMENT = CTG.LABOR_AGREEMENT
                         AND  EMPL_CTG = CTG.EMPL_CTG
                         AND  EFFDT <= $Select-ASOFDATE)

END-SELECT


let $sql-statement = 'GPESFNQ1.SQR,Get-Empl-Ctg-Descr,Select,PS_WKF_CNT_AFI_ESP'
begin-SELECT on-error=SQL-Error
CNAES.SOC_SEC_WRK_GRP_CD
  LET $Empl-SS-Grp-Cd = &CNAES.SOC_SEC_WRK_GRP_CD

FROM PS_WKF_CNT_AFI_ESP CNAES

WHERE CNAES.EMPLID = $Select-EMPLID
  and CNAES.CONTRACT_NUM = &JOB.CONTRACT_NUM
  and CNAES.EFFDT = (SELECT MAX(EFFDT) FROM PS_WKF_CNT_AFI_ESP
                   WHERE EMPLID = CNAES.EMPLID
                     AND CONTRACT_NUM = CNAES.CONTRACT_NUM
                     AND EFFDT <= $Select-ASOFDATE)
end-SELECT


let $Soc-Sec-Cntb-Freq = ''

let $sql-statement = 'GPESFNQ1.SQR,Get-Empl-Ctg-Descr,Select,PS_RGM_BSE_ESP_TBL'
BEGIN-SELECT on-error=SQL-Error
SSWGRP.CURRENCY_CD
SSWGRP.EFFDT
SSWGRP.DESCR50
SSWGRP.WRK_GRP_FREQ_ESP

  let $RGM_BSE_ESP_TBL-DESCR50 = rtrim(&SSWGRP.DESCR50, ' ')
  Do Get_Related_RGM_BSE_ESP_TBL(&SSWGRP.CURRENCY_CD,&SSWGRP.EFFDT,$SchemeID,$Empl-SS-Grp-Cd)
  let $Empl-SS-Grp-Desc = $Empl-SS-Grp-Cd || ' - ' || $RGM_BSE_ESP_TBL-DESCR50
  LET $Soc-Sec-Cntb-Freq = &SSWGRP.WRK_GRP_FREQ_ESP

FROM PS_RGM_BSE_ESP_TBL SSWGRP

  WHERE SSWGRP.SCHEME_ID_ESP = $SchemeID
   AND  SSWGRP.SOC_SEC_WRK_GRP_CD = $Empl-SS-Grp-Cd
   AND  SSWGRP.EFFDT = (SELECT MAX(EFFDT)
                        FROM PS_RGM_BSE_ESP_TBL
                        WHERE SCHEME_ID_ESP = SSWGRP.SCHEME_ID_ESP
                         AND  SOC_SEC_WRK_GRP_CD = SSWGRP.SOC_SEC_WRK_GRP_CD
                         AND  EFFDT <= $Select-ASOFDATE)

END-SELECT

  #debugd show 'Labor agreement setid: ' $SetId_Lbr_Agrmnt
  #debugd show 'Labor agreement:       ' &JOB.LABOR_AGREEMENT
  #debugd show 'EE Categorization Cd:  ' &JOB.EMPL_CTG
  #debugd show 'Descr:                 ' $Empl-Ctg-Desc
  #debugd show 'Scheme ID:             ' $SchemeID
  #debugd show 'Social Security Group: ' $Empl-SS-Grp-Cd
  #debugd show 'Soc Sec Grp Descr:     ' $Empl-SS-Grp-Desc
  #debugd show 'Effective date:        ' &SSWGRP.EFFDT
  #debugd show 'Process Lang:          ' $Curr_Language_Cd
  #debugd show 'Base Lang:             ' $PSoptions_language_cd
  #debugd show 'Contribution Freq:     ' $Soc-Sec-Cntb-Freq

let $sql-statement = 'GPESFNQ1.SQR,Get-Empl-Ctg-Descr,Select,PS_GP_PYE_SOVR SOVR'
begin-SELECT on-error=SQL-Error
SOVR.SOVR_VAL_CHAR
   If &SOVR.SOVR_VAL_CHAR <> 'NO'
     LET $Soc-Sec-Cntb-Freq = 'M'    !Process daily EE as monthly flag.
     #debugd show 'Contribution Freq Override due to SS VR TRATA MNSUAL'
   End-if

FROM PS_GP_PYE_SOVR SOVR

WHERE SOVR.EMPLID =$Select-EMPLID
 AND  SOVR.EMPL_RCD = #Select-EMPL-RCD
 AND  SOVR.PIN_NUM = (SELECT PIN_NUM FROM PS_GP_PIN
                        WHERE PIN_NUM = SOVR.PIN_NUM
                         AND  PIN_CODE ='SS VR TRATA MNSUAL ESP')
 AND ((SOVR.BGN_DT <= $EndDt1 AND SOVR.END_DT >= $BeginDt1)
   OR (SOVR.BGN_DT = (SELECT MAX(BGN_DT) FROM PS_GP_PYE_SOVR
                        WHERE EMPLID = SOVR.EMPLID
                         AND  EMPL_RCD = SOVR.EMPL_RCD
                         AND  PIN_NUM = SOVR.PIN_NUM
                         AND  BGN_DT BETWEEN $BeginDt1 AND $EndDt1) AND SOVR.END_DT IS NULL))

end-SELECT

END-PROCEDURE


!************************************************************************
! Procedure Get-Jobcode-Descr
!************************************************************************
begin-procedure Get-JobCode-Descr
#debug do Fin-Debug-Msg('Get-JobCode-Descr')

let $sql-statement = 'GPESFNQ1.SQR,Get-JobCode-Descr,Select,PS_JOBCODE_TBL'
begin-SELECT on-error=SQL-Error
JOBCODE.SETID
JOBCODE.JOBCODE
JOBCODE.EFFDT
JOBCODE.DESCR

  let $JOBCODE_TBL-DESCR = rtrim(&JOBCODE.DESCR, ' ')
  Do Get_Related_JOBCODE_TBL(&JOBCODE.EFFDT,&JOBCODE.JOBCODE,&JOBCODE.SETID)
  let $Empl-JobCode-Desc = $JOBCODE_TBL-DESCR


FROM PS_JOBCODE_TBL JOBCODE

WHERE JOBCODE.SETID = $JC-SetID-JobCode
  AND JOBCODE.JOBCODE = $JC-JobCode
  AND JOBCODE.EFFDT = (SELECT MAX(EFFDT) FROM PS_JOBCODE_TBL JOBCODE2
                        WHERE JOBCODE2.SETID = JOBCODE.SETID
                         AND  JOBCODE2.JOBCODE = JOBCODE.JOBCODE
                         AND  JOBCODE2.EFFDT <= $Select-ASOFDATE)
  AND JOBCODE.EFF_STATUS <> 'I'
end-SELECT

  #debugd show 'Job Code Setid: ' $JC-SetID-JobCode
  #debugd show 'Job Code:       ' $JC-JobCode
  #debugd show 'Effective date: ' $JC-EffDt
  #debugd show 'Description:    ' $Empl-JobCode-Desc
  #debugd show 'Process Lang:   ' $Curr_Language_Cd
  #debugd show 'Base Lang:      ' $PSoptions_language_cd

end-procedure


!************************************************************************
! Procedure Get-CNO-Descr
!************************************************************************
begin-procedure Get-CNO-Descr
#debug do Fin-Debug-Msg('Get-CNO-Descr')

Let $CNO-Reg-Region = ' '
Let $CNO-SetId = ' '
Let $CNO-Code = ' '
Let $Empl-CNO-Desc = ' '

          !let $sql-statement = 'GPESFNQ1.SQR,Get-CNO-Descr,Select,PS_GPES_CNO_P_INEM'
          !begin-SELECT on-error=SQL-Error
          !OPCODE.GPES_CNO_CODE
          !  Let $CNO-Reg-Region = 'ESP'
          !  Let $CNO-Code = &OPCODE.GPES_CNO_CODE
          !  #Debugd show 'Position CNO: ' $Position
          !
          !FROM PS_GPES_CNO_P_INEM OPCODE
          !
          !WHERE OPCODE.POSITION_NBR = $Position
          !  AND OPCODE.EFFDT = (SELECT MAX(EFFDT) FROM PS_GPES_CNO_P_INEM
          !                      WHERE POSITION_NBR = OPCODE.POSITION_NBR
          !                        AND  EFFDT <= $Select-ASOFDATE)
          !  AND OPCODE.EFF_STATUS <> 'I'
          !end-SELECT
          !
          !If $CNO-Code = ' '   ! CNO Mapping not found for position. Trying with job code.
          !let $sql-statement = 'GPESFNQ1.SQR,Get-CNO-Descr,Select,PS_GPES_CNO_INEM'
          !begin-SELECT on-error=SQL-Error
          !OCCCODE.REG_REGION
          !OCCCODE.GPES_CNO_CODE
          !  Let $CNO-Reg-Region = &OCCCODE.REG_REGION
          !  Let $CNO-Code = &OCCCODE.GPES_CNO_CODE
          !  #Debugd show 'Job Code CNO: ' $SetId-JobCode '/' $JobCode
          !
          !FROM PS_GPES_CNO_INEM OCCCODE
          !
          !WHERE OCCCODE.SETID = $SetId-JobCode
          !  and OCCCODE.JOBCODE = $JobCode
          !  AND OCCCODE.EFFDT = (SELECT MAX(EFFDT) FROM PS_GPES_CNO_INEM
          !                        WHERE SETID = OCCCODE.SETID
          !                          AND JOBCODE = OCCCODE.JOBCODE
          !                          AND  EFFDT <= $Select-ASOFDATE)
          !  AND OCCCODE.EFF_STATUS <> 'I'
          !end-SELECT
          !End-If

let $sql-statement = 'GPESFNQ1.SQR,Get-CNO-Descr,Select,PS_WKF_CNT_TYP_ESP'
begin-SELECT on-error=SQL-Error
CNTES.CNO_ESP
CNTES.SCHEME_ID_ESP
CNTES.CONTRIB_ID_ESP

  let $CNO-Code = &CNTES.CNO_ESP
  let $SchemeId = &CNTES.SCHEME_ID_ESP  
  let $ContribId = &CNTES.CONTRIB_ID_ESP

FROM PS_WKF_CNT_TYP_ESP CNTES

WHERE CNTES.EMPLID = $Select-EMPLID
  and CNTES.CONTRACT_NUM = &JOB.CONTRACT_NUM
  and CNTES.EFFDT = (SELECT MAX(EFFDT) FROM PS_WKF_CNT_TYP_ESP
                   WHERE EMPLID = CNTES.EMPLID
                     AND CONTRACT_NUM = CNTES.CONTRACT_NUM
                     AND EFFDT <= $Select-ASOFDATE)
  and CNTES.EFFSEQ = (SELECT MAX(EFFSEQ) FROM PS_WKF_CNT_TYP_ESP
                     WHERE EMPLID = CNTES.EMPLID
                     AND CONTRACT_NUM = CNTES.CONTRACT_NUM
                     AND EFFDT = CNTES.EFFDT)
end-SELECT

Let $CNO-Reg-Region = 'ESP'
Do Get-setid($CNO-Reg-Region, 'GPES_CNO_INEM', $CNO-SetId)

let $sql-statement = 'GPESFNQ1.SQR,Get-CNO-Descr,Select,PS_GPES_CNO_INEM'
begin-SELECT on-error=SQL-Error
CNODESC.SETID
CNODESC.GPES_CNO_CODE
CNODESC.DESCR50
  let $GPES_JOB_CNO_VW-DESCR50 = rtrim(&CNODESC.DESCR50, ' ')
  Do Get_Related_GPES_JOBCD_CNO_VW(&CNODESC.GPES_CNO_CODE, &CNODESC.SETID)
  let $Empl-CNO-Desc = $GPES_JOB_CNO_VW-DESCR50

FROM PS_GPES_JOB_CNO_VW CNODESC

WHERE CNODESC.SETID = $CNO-SetId
  and CNODESC.GPES_CNO_CODE = $CNO-Code
end-SELECT


#debugd show 'CNO SetId:       ' $CNO-SetId
#debugd show 'CNO Code:        ' $CNO-Code
#debugd show 'Description:     ' $Empl-CNO-Desc
#debugd show 'SchemeId:        ' $SchemeId
#debugd show 'ContribId:       ' $ContribId
#debugd show 'Process Lang:    ' $Curr_Language_Cd
#debugd show 'Base Lang:       ' $PSoptions_language_cd

end-procedure


!************************************************************************
! Procedure PERSONAL-DATA
!************************************************************************
begin-procedure PERSONAL-DATA
#debug do Fin-Debug-Msg('PERSONAL-DATA')

  let $Empl-Name = ''
  let $Empl-Address = ''
  let $Empl-Cp = ''
  let $Empl-Localidad = ''
  let $Empl-City = ''
  let $Empl-State-Desc = ''
  let $Empl-Postal = ''
  let $Empl-Provincia = ''
  let $Empl-DNI = ''
  let $Empl-SSN = ''
  let $Empl-LocalidadNacimiento = ''
  let $Empl-ProvinciaNacimiento = ''
  let $Empl-FechaNacimiento = ''

let $sql-statement = 'GPESFNQ1.SQR,PERSONAL-DATA,Select,PS_PERSONAL_DT_FST'
begin-SELECT on-error=SQL-Error
DNI.NATIONAL_ID
PDTA.NAME
  let $Empl-Name = &PDTA.NAME

PDTA.FIRST_NAME
PDTA.LAST_NAME
PDTA.SECOND_LAST_NAME
PDTA.BIRTHCOUNTRY
PDTA.BIRTHDATE
PDTA.BIRTHPLACE
PDTA.BIRTHSTATE
  let $Empl-FechaNacimiento = &PDTA.BIRTHDATE
  let $Empl-LocalidadNacimiento = &PDTA.BIRTHPLACE
  let $Country = &PDTA.BIRTHCOUNTRY
  let $State = &PDTA.BIRTHSTATE
  Do State-Names-Tbl
  let $Empl-ProvinciaNacimiento = rtrim($State-Desc,' ')

PDTA.MAR_STATUS
  let $FieldName = 'MAR_STATUS'
  let $FieldValue = &PDTA.MAR_STATUS
  let $AsOfDate = $Select-ASOFDATE
  Do Read-Translate-Table
  let $Empl-Mar-Status-Desc = $XLatLongName

  Do Get-Empl-Address
  Do Get-Empl-Nid


FROM PS_PERSONAL_DT_FST PDTA
         , PS_PERS_NID DNI

WHERE PDTA.EMPLID = $Select-EMPLID
        AND PDTA.EMPLID = DNI.EMPLID

end-SELECT

  #debugd show 'Employee ID: ' $Select-EMPLID
  #debugd show 'Employee name: ' $Empl-Name
  #debugd show 'Employee address: ' $Empl-address
  #debugd show 'Employee city: ' $Empl-City
  #debugd show 'Employee state: ' $Empl-State-Desc
  #debugd show 'Employee postal: ' $Empl-Postal
  #debugd show 'Marital Status: ' $Empl-Mar-Status-Desc

end-procedure


!************************************************************************
! Procedure Get-Empl-Address
!************************************************************************
begin-procedure Get-Empl-Address
#debug do Fin-Debug-Msg('Get-Empl-Address')

let $sql-statement = 'GPESFNQ1.SQR,Get-Empl-Address,Select,PS_PERSON_ADDRESS'
begin-SELECT on-error=SQL-Error
ADDRT.ADDRESS_TYPE
ADDRT.ORDER_BY_SEQ

PADDR.COUNTRY
PADDR.ADDRESS1
PADDR.ADDRESS2
PADDR.ADDRESS3
PADDR.ADDRESS4
PADDR.CITY
PADDR.NUM1
PADDR.NUM2
PADDR.HOUSE_TYPE
PADDR.ADDR_FIELD1
PADDR.ADDR_FIELD2
PADDR.ADDR_FIELD3
PADDR.COUNTY
PADDR.STATE
PADDR.POSTAL
PADDR.GEO_CODE
PADDR.IN_CITY_LIMIT

          let $Empl-ADDLINE2=LTRIM(RTRIM(&PADDR.ADDR_FIELD1,' ')||' ',' ')||LTRIM(RTRIM(&PADDR.ADDRESS1,' ')||' ',' ')
          let $Empl-ADDLINE3=LTRIM(RTRIM(&PADDR.ADDRESS2,' ')||' ',' ')
          let $Empl-ADDLINE31=LTRIM(RTRIM(&PADDR.ADDR_FIELD2,' ')||' ',' ')||LTRIM(RTRIM(&PADDR.ADDR_FIELD3,' ')||' ',' ')
          let $Empl-ADDLINE4=LTRIM(RTRIM(&PADDR.ADDRESS4,' ')||' ',' ')||LTRIM(RTRIM(&PADDR.NUM1,' ')||' ',' ')||LTRIM(RTRIM(&PADDR.NUM2,' ')||' ',' ')||LTRIM(RTRIM(&PADDR.HOUSE_TYPE,' ')||' ',' ')
          let $Empl-address = $Empl-ADDLINE2||$Empl-ADDLINE3||$Empl-ADDLINE31||$Empl-ADDLINE4
          let $Empl-City = &PADDR.CITY
          let $Country = &PADDR.COUNTRY
          let $DPersonales-Domicilio = &PADDR.addr_field1 || ' ' || &PADDR.address1
          let $DPersonales-Numero    = &PADDR.addr_field2
          let $DPersonales-Bloque    = &PADDR.address3
          let $DPersonales-Escalera  = &PADDR.addr_field3
          let $DPersonales-Piso      = &PADDR.num1
          let $DPersonales-Puerta    = &PADDR.num2
          let $Nacionalidad-Codigo   = &PADDR.country
          let $State = &PADDR.STATE
          Do State-Names-Tbl
          let $Empl-State-Desc = rtrim($State-Desc,' ')
          let $Empl-Postal = &PADDR.POSTAL
          #debugd show 'Address Seq#: ' &ADDRT.ORDER_BY_SEQ
          #debugd show 'Address Type: ' &ADDRT.ADDRESS_TYPE
          EXIT-SELECT

FROM PS_PERSON_ADDRESS PADDR
   , PS_ADDRESS_TYP_TBL ADDRT


WHERE PADDR.EMPLID = $Select-EMPLID
  AND PADDR.ADDRESS_TYPE = ADDRT.ADDRESS_TYPE

ORDER BY ADDRT.ORDER_BY_SEQ

end-SELECT

end-procedure

!**********************************************************************!
! PROCEDURE:     GET-EMPL_NID                                          !
! DESCRIPTION:   GET EMPLOYEE NATIONAL ID                              !
!**********************************************************************!

BEGIN-PROCEDURE  Get-Empl-Nid
#debug do Fin-Debug-Msg('Get-Empl-Nid')

let $sql-statement = 'GPESFNQ1.SQR,Get-Empl-Nid,Select,PS_PERS_NID (DNI)'
BEGIN-SELECT on-error=SQL-Error
NID1.NATIONAL_ID_TYPE
NID1.NATIONAL_ID

  let $Empl-DNI = &NID1.NATIONAL_ID
  EXIT-SELECT


FROM PS_PERS_NID NID1

WHERE NID1.EMPLID = $Select-EMPLID
 AND  NID1.COUNTRY = &PADDR.COUNTRY
 AND  NID1.NATIONAL_ID_TYPE IN ('DNI', 'NIE')
ORDER BY NID1.NATIONAL_ID_TYPE ASC

END-SELECT


let $sql-statement = 'GPESFNQ1.SQR,Get-Empl-Nid,Select,PS_PERS_NID (SSN)'
BEGIN-SELECT on-error=SQL-Error
NID2.NATIONAL_ID

  let $Empl-SSN = &NID2.NATIONAL_ID


FROM PS_PERS_NID NID2

WHERE NID2.EMPLID = $Select-EMPLID
 AND  NID2.COUNTRY = &PADDR.COUNTRY
 AND  NID2.NATIONAL_ID_TYPE = 'SSN'

END-SELECT

  #debugd show 'Employee id: ' $Select-EMPLID
  #debugd show 'Country: ' &PADDR.COUNTRY
  #debugd show 'DNI: ' $Empl-DNI
  #debugd show 'SSN: ' $Empl-SSN

END-PROCEDURE


!************************************************************************
! Procedure COMPANY-TBL
!************************************************************************
begin-procedure COMPANY-TBL
#debug do Fin-Debug-Msg('COMPANY-TBL')

let $Cmpny-EffDt = ''
let $Cmpny-Descr = ''
!let $Cmpny-Ssn = ''
let $Cmpny-State-Desc = ''
let $Cmpny-City = ''
let $Cmpny-Postal = ''
let $Cmpny-ADDLINE2 = ''
let $Cmpny-ADDLINE3 = ''
let $Cmpny-ADDLINE31 = ''
let $Cmpny-ADDLINE4 = ''
let $Cmpny-address = ''
let $Cmpny-Numero = ''
let $Cmpny-address-Cmp = ''
let $Cmpny-Domicilio = ''
let $Cmpny-Bloque = ''
let $Cmpny-Escalera = ''
let $Cmpny-Piso = ''
let $Cmpny-Puerta = ''

let $sql-statement = 'GPESFNQ1.SQR,COMPANY-TBL,Select,PS_COMPANY_TBL'
begin-SELECT on-error=SQL-Error
CMPNY.COMPANY
CMPNY.EFFDT
CMPNY.DESCR
CMPNY.COUNTRY
CMPNY.ADDRESS1
CMPNY.ADDRESS2
CMPNY.ADDRESS3
CMPNY.ADDRESS4
CMPNY.CITY
CMPNY.NUM1
CMPNY.NUM2
CMPNY.HOUSE_TYPE
CMPNY.ADDR_FIELD1
CMPNY.ADDR_FIELD2
CMPNY.ADDR_FIELD3
CMPNY.COUNTY
CMPNY.STATE
CMPNY.POSTAL
CMPNY.GEO_CODE
CMPNY.IN_CITY_LIMIT
CMPNY.FISCAL_ID_CD

  let $Cmpny-EffDt = &CMPNY.EFFDT
  let $COMPANY_TBL-DESCR = rtrim(&CMPNY.DESCR, ' ')
  Do Get_Related_COMPANY_TBL(&CMPNY.COMPANY,&CMPNY.EFFDT)
  let $Cmpny-Descr = $COMPANY_TBL-DESCR
  !let $Cmpny-Ssn = &JOBJR.SSN_EMPLOYER
  let $Country = &CMPNY.COUNTRY
  let $State = &CMPNY.STATE
  Do State-Names-Tbl
  let $Cmpny-State-Desc = $State-Desc
  let $Cmpny-City = &CMPNY.CITY
  let $Cmpny-Postal = RTRIM(&CMPNY.POSTAL, ' ')
  let $Fiscal_ID_Cd = rtrim(&CMPNY.FISCAL_ID_CD, ' ')

  If $reason_cd = 'INE'
     let $Cmpny-ADDLINE2=LTRIM(RTRIM(&CMPNY.ADDR_FIELD1,' ')||' ',' ')||LTRIM(RTRIM(&CMPNY.ADDRESS1,' ')||' ',' ')
     let $Cmpny-ADDLINE3=LTRIM(RTRIM(&CMPNY.ADDRESS2,' ')||' ',' ')
     let $Cmpny-ADDLINE31=LTRIM(RTRIM(&CMPNY.ADDR_FIELD2,' ')||' ',' ')||LTRIM(RTRIM(&CMPNY.ADDR_FIELD3,' ')||' ',' ')
     let $Cmpny-ADDLINE4=LTRIM(RTRIM(&CMPNY.ADDRESS4,' ')||' ',' ')||LTRIM(RTRIM(&CMPNY.NUM1,' ')||' ',' ')||LTRIM(RTRIM(&CMPNY.NUM2,' ')||' ',' ')||LTRIM(RTRIM(&CMPNY.HOUSE_TYPE,' ')||' ',' ')
     !let $Cmpny-address = $Cmpny-ADDLINE2||$Cmpny-ADDLINE3||$Cmpny-ADDLINE31||$Cmpny-ADDLINE4
     let $Cmpny-address = &CMPNY.addr_field1 || ' ' || &CMPNY.address1
     let $Cmpny-Numero = &CMPNY.ADDR_FIELD2
     let $Cmpny-address-Cmp = $Cmpny-address||'  '||RTRIM($Cmpny-City, ' ')||'  ('||RTRIM(&CMPNY.POSTAL, ' ')||')'
  Else
     let $Cmpny-Domicilio = &CMPNY.addr_field1 || ' ' || &CMPNY.address1
     let $Cmpny-Numero = &CMPNY.ADDR_FIELD2
     let $Cmpny-Bloque  = &CMPNY.address3
     let $Cmpny-Escalera  = &CMPNY.addr_field3
     let $Cmpny-Piso      = &CMPNY.num1
     let $Cmpny-Puerta    = &CMPNY.num2
  End-if


FROM PS_COMPANY_TBL CMPNY

WHERE CMPNY.COMPANY = $CMP
  AND CMPNY.EFFDT = (SELECT MAX(EFFDT) FROM PS_COMPANY_TBL CMPNY2
                     WHERE CMPNY2.COMPANY = CMPNY.COMPANY
                      AND  CMPNY2.EFFDT <= $Select-ASOFDATE)
  AND CMPNY.EFF_STATUS <> 'I'

end-SELECT

#debugd show 'Company Addr for compare w/establishment: ' $Cmpny-address-Cmp ' .vs. ' $Cmpny-Domicilio

let $Cmpny-Telefono = ' '

let $sql-statement = 'GPESFNQ1.SQR,COMPANY-TBL,Select,PS_COMP_PHONE_TBL'
begin-SELECT on-error=SQL-Error
PHONE.PHONE
  let $Cmpny-Telefono = &PHONE.PHONE
  let $Cmpny-Fax = $Cmpny-Fax

FROM PS_COMP_PHONE_TBL PHONE

WHERE PHONE.COMPANY = $CMP
  AND PHONE.EFFDT = $Cmpny-EffDt

end-SELECT


if $EstabID = ''
  let $sql-statement = 'GPESFNQ1.SQR,ESTABLISHMENT LOCATION,Select,LOCATION_TBL'
begin-SELECT on-error=SQL-Error
LOC.ESTABID
LOC.EFFDT
  let $EstabID = &LOC.ESTABID

  FROM PS_LOCATION_TBL LOC
  WHERE SETID = &JOB.SETID_LOCATION
    AND LOCATION = &JOB.LOCATION
    AND EFFDT = (SELECT MAX(EFFDT) FROM PS_LOCATION_TBL
                  WHERE SETID = LOC.SETID
                   AND LOCATION = LOC.LOCATION
                   AND EFFDT <= $Select-ASOFDATE)
end-SELECT

  #debugd show 'Location: ' &JOB.SETID_LOCATION '/' &JOB.LOCATION
  #debugd show 'EffDT: ' &LOC.EFFDT
  #debugd show 'Establishment ID: ' $EstabID

end-if


let $Wrk-Center-State-Desc = ''
let $Wrk-Center-City = ''
let $Wrk-Center-Postal = ''
let $Wrk-Center-ADDLINE2 = ''
let $Wrk-Center-ADDLINE3 = ''
let $Wrk-Center-ADDLINE31 = ''
let $Wrk-Center-ADDLINE4 = ''
let $Wrk-Center-Numero = ''
let $Wrk-Center-Addr = ''
let $Wrk-Center-City = ''
let $Wrk-Center-Addr = ''
let $Wrk-Center-Domicilio = ''
let $Wrk-Center-Bloque = ''
let $Wrk-Center-Escalera = ''
let $Wrk-Center-Piso = ''
let $Wrk-Center-Puerta = ''

let $sql-statement = 'GPESFNQ1.SQR,ESTABLISHMENT ADDRESS,Select,ESTAB_TBL'
begin-SELECT on-error=SQL-Error
L.EFFDT
L.COUNTRY
L.ADDRESS1
L.ADDRESS2
L.ADDRESS3
L.ADDRESS4
L.CITY
L.NUM1
L.NUM2
L.HOUSE_TYPE
L.ADDR_FIELD1
L.ADDR_FIELD2
L.ADDR_FIELD3
L.COUNTY
L.STATE
L.POSTAL
L.GEO_CODE
L.IN_CITY_LIMIT

        let $Wrk-Center-EffDt = &L.EFFDT
        let $Country = &L.COUNTRY
        let $State = &L.STATE
        Do State-Names-Tbl
        let $Wrk-Center-State-Desc = $State-Desc
        let $Wrk-Center-City = &L.CITY
        let $Wrk-Center-Postal = RTRIM(&L.POSTAL, ' ')

        IF $reason_cd = 'INE'
          let $Wrk-Center-ADDLINE2=LTRIM(RTRIM(&L.ADDR_FIELD1,' ')||' ',' ')||LTRIM(RTRIM(&L.ADDRESS1,' ')||' ',' ')
          let $Wrk-Center-ADDLINE3=LTRIM(RTRIM(&L.ADDRESS2,' ')||' ',' ')
          let $Wrk-Center-ADDLINE31=LTRIM(RTRIM(&L.ADDR_FIELD2,' ')||' ',' ')||LTRIM(RTRIM(&L.ADDR_FIELD3,' ')||' ',' ')
          let $Wrk-Center-ADDLINE4=LTRIM(RTRIM(&L.ADDRESS4,' ')||' ',' ')||LTRIM(RTRIM(&L.NUM1,' ')||' ',' ')||LTRIM(RTRIM(&L.NUM2,' ')||' ',' ')||LTRIM(RTRIM(&L.HOUSE_TYPE,' ')||' ',' ')
          let $Wrk-Center-Numero = &CMPNY.ADDR_FIELD2
          !let $Wrk-Center-Addr = $Wrk-Center-ADDLINE2||$Wrk-Center-ADDLINE3||$Wrk-Center-ADDLINE31||$Wrk-Center-ADDLINE4
          let $Wrk-Center-Addr = &L.addr_field1 || ' ' || &L.address1
          let $Wrk-Center-City = RTRIM(&L.CITY, ' ')
          let $Wrk-Center-Addr = $Wrk-Center-Addr||'  '||RTRIM(&L.CITY, ' ')||'  ('||RTRIM(&L.POSTAL, ' ')||')'
        Else
          let $Wrk-Center-Domicilio = &L.addr_field1 || ' ' || &L.address1
          let $Wrk-Center-Numero = &L.ADDR_FIELD2
          let $Wrk-Center-Bloque  = &L.address3
          let $Wrk-Center-Escalera  = &L.addr_field3
          let $Wrk-Center-Piso      = &L.num1
          let $Wrk-Center-Puerta    = &L.num2
        End-If

FROM PS_ESTAB_TBL L

WHERE L.ESTABID = $EstabID
  AND L.EFFDT = (SELECT MAX(EFFDT) FROM PS_ESTAB_TBL
                  WHERE ESTABID = L.ESTABID
                    AND EFFDT <= $Select-ASOFDATE)

end-SELECT

#Debugd show 'Establishment address:                    ' $Wrk-Center-Addr '' $Wrk-Center-Domicilio

let $Wrk-Center-Telefono = ''

let $sql-statement = 'GPESFNQ1.SQR,ESTABLISHMENT ADDRESS,Select,ESTAB_TBL'
begin-SELECT on-error=SQL-Error
EPHONE.PHONE
        let $Wrk-Center-Telefono = &EPHONE.PHONE

FROM PS_ESTAB_PHONE EPHONE

WHERE EPHONE.ESTABID = $EstabID
  AND EPHONE.EFFDT = $Wrk-Center-EffDt

end-SELECT


let $sql-statement = 'GPESFNQ1.SQR,COMPANY-TBL,Select,RGM_CTZ_ESP_TBL'
begin-SELECT on-error=SQL-Error
C.SSN_EMPL_TYPE
  let $SSN_Empl_Type = &C.SSN_EMPL_TYPE


FROM PS_RGM_CTZ_ESP_TBL C

WHERE C.SCHEME_ID_ESP = $SchemeId
  AND C.CONTRIB_ID_ESP = $ContribId
  AND C.EFFDT = (SELECT MAX(EFFDT) FROM PS_RGM_CTZ_ESP_TBL
                   WHERE SCHEME_ID_ESP = C.SCHEME_ID_ESP
                    AND CONTRIB_ID_ESP = C.CONTRIB_ID_ESP
                    AND EFFDT <= $Select-ASOFDATE)

end-SELECT


  #debugd show 'Company: ' $CMP
  #debugd show 'As Of: ' $Select-ASOFDATE
  #debugd show 'EE Contract num: ' &JOB.CONTRACT_NUM
  #debugd show 'Scheme ID: ' $SchemeID
  #debugd show 'SSN EMPL TYPE: ' $SSN_Empl_Type
  #debugd show 'Wrk Center Addr: ' $Wrk-Center-Addr
  #debugd show 'Company Desc: ' $Cmpny-Descr
  #debugd show 'Company Addr: ' $Cmpny-Address
  #debugd show 'Company City: ' $Cmpny-City
  #debugd show 'Company Postal: ' $Cmpny-Postal
  #debugd show 'Company State: ' $Cmpny-State-Desc
  #debugd show 'Company phone: ' $Cmpny-Telefono
  #debugd show 'Establishment: ' $EstabID
  !#debugd show 'SSN Employer: ' &JOBJR.SSN_EMPLOYER
  !#debugd show 'Company SSN: ' $Cmpny-Ssn

end-procedure


!************************************************************************
! Procedure CMPNY-SCHEME-ID
!************************************************************************
begin-procedure CMPNY-SCHEME-ID
#debug do Fin-Debug-Msg('CMPNY-SCHEME-ID')

let $CMP-MainSSN = ' '

let $sql-statement = 'GPESFNQ1.SQR,CMPNY-SCHEME-ID,Select,PS_SOCS_SETHD_ESP'
begin-SELECT on-error=SQL-Error
CMPSCHM.SSN_MAIN_ESP

  Let $CMP-MainSSN = &CMPSCHM.SSN_MAIN_ESP


FROM PS_SOCS_SETHD_ESP CMPSCHM

WHERE CMPSCHM.COMPANY = $CMP
  AND CMPSCHM.EFFDT = (SELECT MAX(EFFDT) FROM PS_SOCS_SETHD_ESP
                       WHERE COMPANY = CMPSCHM.COMPANY
                         AND EFFDT <= $Select-ASOFDATE)

end-SELECT

  #debugd show 'Company: ' $CMP
  #debugd show 'Main SSN: ' $CMP-MainSSN


Let $CMP-SchemeID = ' '

let $sql-statement = 'GPESFNQ1.SQR,COMPANY MAIN SSN SCHEME ID,Select,ESTAB_TBL'
begin-SELECT on-error=SQL-Error
ESTABSCHM.SCHEME_ID_ESP

  Let $CMP-SchemeID = &ESTABSCHM.SCHEME_ID_ESP

FROM PS_ESTAB_SSEC_ESP ESTABSCHM

WHERE ESTABSCHM.EFFDT = (SELECT MAX(EFFDT) FROM PS_ESTAB_SSEC_ESP
                          WHERE ESTABID = ESTABSCHM.ESTABID
                           AND  EFFDT <= $Select-ASOFDATE
                           AND  SSN_EMPLOYER = ESTABSCHM.SSN_EMPLOYER)
  AND ESTABSCHM.SSN_EMPLOYER = $CMP-MainSSN
end-SELECT

  #debugd show 'Company Scheme ID: ' $CMP-SchemeID


Let $CMP-Scheme-DESCR = ' '

let $sql-statement = 'GPESFNQ1.SQR,COMPANY MAIN SSN SCHEME DESCR,Select,SOC_SEC_RGM_TBL'
begin-SELECT on-error=SQL-Error
SCHMDEF.SCHEME_ID_ESP
SCHMDEF.CURRENCY_CD
SCHMDEF.EFFDT
SCHMDEF.DESCR50

  let $SOC_SEC_RGM_TBL-DESCR50 = rtrim(&SCHMDEF.DESCR50, ' ')
  Do Get_Related_SOC_SEC_RGM_TBL(&SCHMDEF.CURRENCY_CD,&SCHMDEF.EFFDT,&SCHMDEF.SCHEME_ID_ESP)
  let $CMP-Scheme-DESCR = $SOC_SEC_RGM_TBL-DESCR50

FROM PS_SOC_SEC_RGM_TBL SCHMDEF

WHERE SCHMDEF.SCHEME_ID_ESP = $CMP-SchemeID
  AND SCHMDEF.EFFDT = (SELECT MAX(EFFDT) FROM PS_SOC_SEC_RGM_TBL
                        WHERE SCHEME_ID_ESP = SCHMDEF.SCHEME_ID_ESP
                         AND  CURRENCY_CD = SCHMDEF.CURRENCY_CD
                         AND  EFFDT <= $Select-ASOFDATE)
end-SELECT

  #debugd show 'Company Scheme Description: ' $CMP-Scheme-DESCR


end-procedure


!************************************************************************
! Procedure ERE-DATA
!************************************************************************
begin-procedure ERE-DATA
#debug do Fin-Debug-Msg('ERE-DATA')

Let $ERE-ID      =  ''
Let $ERE-TYPE    =  ''
Let $ERE-ENDDT   =  ''
Let $ERE-BGNDT   =  ''
Let $ERE-NBR     =  ''
Let #ERE-WRD-PCT =  ''


let $sql-statement = 'GPESFNQ1.SQR,ERE-DATA,Select,PS_ERE_ALL_EE_VW'
begin-SELECT on-error=SQL-Error
EEDT.ERE_ID_ESP
EEDT.ERE_U_ACTN_TYP_ESP
EEDT.ERE_BGNDT_ESP
EEDT.ERE_ENDDT_ESP
EEDT.ERE_WKD_RD_PCT_ESP
  Let $ERE-ID      =  &EEDT.ERE_ID_ESP
  Let $ERE-TYPE    =  &EEDT.ERE_U_ACTN_TYP_ESP
  If $ERE-TYPE <> 'P'
    Let $ERE-BGNDT   =  &EEDT.ERE_BGNDT_ESP
    Let $ERE-ENDDT   =  &EEDT.ERE_ENDDT_ESP
    Let #ERE-WRD-PCT =  &EEDT.ERE_WKD_RD_PCT_ESP

    do Convert-To-DTU-Date($ERE-BGNDT, $ERE-BGNDT-DTU)
    !Do dtu-add-months($ERE-BGNDT-DTU, -1, $ERE-BGNDT-DTU)
    !Do DTU-Month-End($ERE-BGNDT-DTU, $ERE-BGNDT-DTU)
    do dtu-add-days($ERE-BGNDT-DTU, -1, $ERE-BGNDT-DTU)
    do Convert-From-DTU-Date($ERE-BGNDT-DTU, $Termination-Dt)
  End-If
  
FROM PS_ERE_ALL_EE_VW EEDT

WHERE EEDT.EMPLID = $Select-EMPLID
  AND EEDT.EMPL_RCD = #Select-EMPL-RCD
  AND ((&A.GPES_EMPLOYEE_TYPE = 'A' AND EEDT.ERE_BGNDT_ESP <= $Select-ASOFDATE AND $Select-ASOFDATE <= EEDT.ERE_ENDDT_ESP)
    OR (&A.GPES_EMPLOYEE_TYPE = 'I' AND EEDT.TERMINATION_DT = $Select-ASOFDATE))
end-SELECT

#Debugd show 'ERE Id:         ' $ERE-ID
#Debugd show 'ERE Type:       ' $ERE-TYPE
#Debugd show 'ERE Bg/End Dts: ' $ERE-BGNDT '/' $ERE-ENDDT

let $sql-statement = 'GPESFNQ1.SQR,ERE-DATA,Select,PS_ERE_DATA_ESP'
begin-SELECT on-error=SQL-Error
EDT.ERE_NBR_ESP
EDT.ERE_WKD_RD_PCT_ESP
  Let $ERE-NBR     =  &EDT.ERE_NBR_ESP

FROM PS_ERE_DATA_ESP EDT

WHERE EDT.ERE_ID_ESP = $ERE-ID
end-SELECT

end-procedure


!************************************************************************
! Procedure STATE-NAMES-TBL
!************************************************************************
begin-procedure STATE-NAMES-TBL
#debug do Fin-Debug-Msg('STATE-NAMES-TBL')

  let $State-Desc = ''

let $sql-statement = 'GPESFNQ1.SQR,STATE-NAMES-TBL,Select,PS_STATE_NAMES_TBL'
begin-SELECT on-error=SQL-Error
STE.DESCR

  if $Curr_Language_Cd <> $PSoptions_language_cd
    do Get-State-Lang
  end-if
  if $State-Desc = ''
    let $State-Desc = &STE.DESCR
  end-if


FROM PS_STATE_NAMES_TBL STE

WHERE STE.COUNTRY = $Country
  AND STE.STATE = $State

end-SELECT

  #debugd show 'Country: ' $Country
  #debugd show 'State cd: ' $State
  #debugd show 'Description: ' $State-Desc

end-procedure


!************************************************************************
! Procedure Get-State-Lang
!************************************************************************
BEGIN-PROCEDURE Get-State-Lang
#debug do Fin-Debug-Msg('Get-State-Lang')

let $sql-statement = 'GPESFNQ1.SQR,Get-State-Lang,Select,PS_STATE_NAMES_LNG'
BEGIN-SELECT on-error=SQL-Error
STELNG.DESCR

  LET $State-Desc = &STELNG.DESCR


FROM PS_STATE_NAMES_LNG STELNG

WHERE STELNG.COUNTRY = $Country
  AND STELNG.STATE = $State
  AND STELNG.LANGUAGE_CD = $Curr_Language_Cd

END-SELECT

END-PROCEDURE


!************************************************************************
! Procedure INDSTRY-ACT-ESP
!************************************************************************
begin-procedure INDSTRY-ACT-ESP
#debug do Fin-Debug-Msg('INDSTRY-ACT-ESP')

let $Cmpny-Indstry-Cd   = ''
let $Cmpny-Indstry-Desc = ''

#debugd show 'Establishment: ' $EstabID
#debugd show 'SSN Empl Type: ' $SSN_Empl_Type
#debugd show 'SSN Employer:  ' $Cmpny-Ssn

let $sql-statement = 'GPESFNQ1.SQR,ESTAB_SSEC_ESP,Select,'
begin-SELECT on-error=SQL-Error
ESTAB.INDSTRY_CD_ESP
ESTAB.EFFDT
   !Let $Industry_cd_desc = &ESTAB.INDSTRY_CD_ESP

FROM PS_ESTAB_SSEC_ESP ESTAB
WHERE ESTAB.ESTABID = $EstabID
  ! AND ESTAB.SSN_EMPL_TYPE = $SSN_Empl_Type     Commented out as it is not a key field.
  AND ESTAB.SSN_EMPLOYER = $Cmpny-Ssn
  AND ESTAB.EFFDT = (SELECT MAX(EFFDT) FROM PS_ESTAB_SSEC_ESP
                     WHERE ESTABID = ESTAB.ESTABID
                       AND SSN_EMPL_TYPE = ESTAB.SSN_EMPL_TYPE
                       AND SSN_EMPLOYER = ESTAB.SSN_EMPLOYER
                       AND EFFDT <= $Select-ASOFDATE)
end-SELECT


let $sql-statement = 'GPESFNQ1.SQR,INDSTRY-ACT-ESP,Select,PS_INDSTRY_ACT_ESP'
begin-SELECT on-error=SQL-Error
IND.DESCR50

  let $Cmpny-Indstry-Cd = &ESTAB.INDSTRY_CD_ESP
  let $Cmpny-Indstry-Desc = &IND.DESCR50

FROM PS_INDSTRY_ACT_ESP IND

WHERE IND.INDSTRY_CD_ESP = &ESTAB.INDSTRY_CD_ESP
  AND IND.EFFDT = (SELECT MAX(EFFDT) FROM PS_INDSTRY_ACT_ESP
                     WHERE INDSTRY_CD_ESP = IND.INDSTRY_CD_ESP
                      AND  EFFDT <= $Select-ASOFDATE)
  AND IND.EFF_STATUS <> 'I'

end-SELECT

#debugd show 'Company SSN:           ' $Cmpny-Ssn
#debugd show 'Industry cd:           ' &ESTAB.INDSTRY_CD_ESP
#debugd show 'As of:                 ' $Select-ASOFDATE
#debugd show 'Company Industry Desc: ' $Cmpny-Indstry-Desc

end-procedure

!********************************************************************************************
! Procedure GetEmployeeContractCompany
! New procedure to get the company code of the employee's contract.
! This is under the assumption that an employee will belong to only one company in a contract
!********************************************************************************************
begin-procedure GetEmployeeContractCompany
#debug do Fin-Debug-Msg('GetEmployeeContractCompany')

let $EmpContractCompany = ' '

let $sql-statement = 'GPESFNQ1.SQR,GetEmployeeContractCompany,Select,PS_JOB'
begin-SELECT on-error=SQL-Error
J1.COMPANY

 let $EmpContractCompany = &J1.COMPANY

FROM PS_JOB J1
WHERE J1.EMPLID = $Select-EMPLID
AND J1.EMPL_RCD = #Select-EMPL-RCD
AND J1.CONTRACT_NUM = $mult_contract

end-SELECT

end-procedure


!********************************************************************************************
! Procedure Get-WorkDay-Reduction
!********************************************************************************************
begin-procedure Get-WorkDay-Reduction
#debug do Fin-Debug-Msg('Get-WorkDay-Reduction')

let $Workday-Reduction = ''

let $sql-statement = 'GPESFNQ1.SQR,Get-WorkDay-Reduction,Select,PS_GP_PYE_SOVR SOVR'
begin-SELECT on-error=SQL-Error
SOVR.SOVR_VAL_NUM
   If &SOVR.SOVR_VAL_NUM <> 0
     let $Workday-Reduction = 'WKR'    !Work Day Reduction
   End-if

FROM PS_GP_PYE_SOVR SOVR

WHERE SOVR.EMPLID =$Select-EMPLID
 AND  SOVR.EMPL_RCD = #Select-EMPL-RCD
 AND  SOVR.PIN_NUM = (SELECT PIN_NUM FROM PS_GP_PIN
                        WHERE PIN_NUM = SOVR.PIN_NUM
                         AND  PIN_CODE ='CLI VR RED JORNADA ESP')
 AND ((SOVR.BGN_DT <= $OVRD-End-Dt AND SOVR.END_DT >= $OVRD-Bgn-Dt)
   OR (SOVR.BGN_DT = (SELECT MAX(BGN_DT) FROM PS_GP_PYE_SOVR
                        WHERE EMPLID = SOVR.EMPLID
                         AND  EMPL_RCD = SOVR.EMPL_RCD
                         AND  PIN_NUM = SOVR.PIN_NUM
                         AND  BGN_DT <= $OVRD-End-Dt)
       AND SOVR.END_DT IS NULL))

end-SELECT

#debugd show 'Workday reduction override timeframe: ' $OVRD-Bgn-Dt '/' $OVRD-End-Dt
#debugd show 'Workday reduction flag (Blank is not workday reduction): ' $Workday-Reduction
#debugd show 'Workday reduction hours:                                 ' &SOVR.SOVR_VAL_NUM

end-procedure


!************************************************************************
! Procedure Get-Contrct-Type-Descr
!************************************************************************
begin-procedure Get-Contrct-Type-Descr
#debug do Fin-Debug-Msg('Get-Contrct-Type-Descr')

Let $Cntrct-Type-SetId = ''
Let $Cntrct-Type       = ''
Let $Cntrct-Type-INEM  = ''

Do Get-setid(&CNTRCT.REG_REGION, 'GPES_CNTTP_INEM', $Cntrct-Type-SetId)
Do Get-setid(&CNTRCT.REG_REGION, 'PERS_CNTRCT_TYP', $Pers-CType-SetId)
#debugd show 'Contract Type SetId:    ' $Cntrct-Type-SetId
#debugd show 'Pers Cntrct Type SetId: ' $Pers-CType-SetId

let $sql-statement = 'GPESFNQ1.SQR,Get-Contrct-Type-Descr,Select,PS_WKF_CNT_TYPE'
begin-SELECT
CTYP.CONTRACT_TYPE
  Let $Cntrct-Type = &CTYP.CONTRACT_TYPE

FROM  PS_WKF_CNT_TYPE CTYP
WHERE CTYP.EMPLID       = $Select-EMPLID
  AND CTYP.CONTRACT_NUM = &JOB.CONTRACT_NUM
  AND CTYP.EFFDT = (SELECT MAX(EFFDT) FROM PS_WKF_CNT_TYPE
                   WHERE EMPLID = CTYP.EMPLID
                     AND CONTRACT_NUM = CTYP.CONTRACT_NUM
                     AND EFFDT <= $Select-ASOFDATE)
end-SELECT
#debugd show 'Contract Type: ' $Cntrct-Type



          !let $sql-statement = 'GPESFNQ1.SQR,Get-Contrct-Type-Descr,Select,GPES_CNTTP_INEM'
          !begin-SELECT on-error=SQL-Error
          !C.GPES_CNT_TYP_INEM,
          !
          !  Let $Cntrct-Type-INEM = &C.GPES_CNT_TYP_INEM
          !
          !FROM PS_GPES_CNTTP_INEM C
          !WHERE C.SETID = $Cntrct-Type-SetId
          !  AND C.CONTRACT_TYPE = $Cntrct-Type
          !  AND C.EFFDT = (SELECT MAX(EFFDT) FROM PS_GPES_CNTTP_INEM
          !                   WHERE SETID = C.SETID
          !                     AND CONTRACT_TYPE = C.CONTRACT_TYPE
          !                     AND EFFDT <= $Select-ASOFDATE)
          !  AND C.EFF_STATUS <> 'I'
          !end-SELECT
          !
          !#debugd show 'INEM Contract Type: ' $Cntrct-Type-INEM
          !
let $sql-statement = 'GPESFNQ1.SQR,Get-Contrct-Type-Descr,Select,PS_PERS_CNTRCT_TYP'
begin-SELECT on-error=SQL-Error
PCNT.CNT_DURTN_TYPE_ESP
PCNT.CNT_SCHED_TYPE_ESP
  #debugd show 'CNT_DURTN_TYPE_ESP: ' &PCNT.CNT_DURTN_TYPE_ESP
  #debugd show 'CNT_SCHED_TYPE_ESP: ' &PCNT.CNT_SCHED_TYPE_ESP

  Evaluate &PCNT.CNT_DURTN_TYPE_ESP
  When = 'I'   !Permanent
    If &PCNT.CNT_SCHED_TYPE_ESP = 'F'        !FullTime
          Or &PCNT.CNT_SCHED_TYPE_ESP = 'P'    !PartTime
      Let $Cntrct-Type-INEM = '1'            !Unlimited
    Else                                     !'D' - Permanent-Intermitent
      Let $Cntrct-Type-INEM = '2'            !Unlimited-Discontinued
    end-if
    Break

  When = 'T'   !Temporary
  When = 'D'   !Fix Duration
    If &PCNT.CNT_SCHED_TYPE_ESP = 'F'        !FullTime
          Or &PCNT.CNT_SCHED_TYPE_ESP = 'P'    !PartTime
      Let $Cntrct-Type-INEM = '3'            !Temporary
    end-if
    Break
  End-Evaluate

FROM PS_PERS_CNTRCT_TYP PCNT
WHERE PCNT.SETID = $Pers-CType-SetId
  AND PCNT.CONTRACT_TYPE = $Cntrct-Type
  AND PCNT.EFFDT = (SELECT MAX(EFFDT) FROM PS_PERS_CNTRCT_TYP
                   WHERE SETID = PCNT.SETID
                     AND CONTRACT_TYPE = PCNT.CONTRACT_TYPE
                     AND EFFDT <= $Select-ASOFDATE)
end-SELECT

Let $FieldName = 'GPES_CNT_TYP_INEM'
Let $FieldValue = $Cntrct-Type-INEM
Do Read-XlatTable
Let $Cntrct-Type-INEM-Desc = $PSXLATITEM-XLATLONGNAME

end-procedure


!************************************************************************
! Procedure Read-XlatTable
!************************************************************************
begin-procedure Read-XlatTable
#debug do Fin-Debug-Msg('Read-XlatTable')

let $PSXLATITEM-XLATLONGNAME  = ''
let $PSXLATITEM-XLATSHORTNAME = ''

let $sql-statement = 'GPESFNQ1.SQR,Read-XlatTable,Select,PSXLATITEM'
begin-SELECT on-error=SQL-Error
XLATI.FIELDNAME
XLATI.FIELDVALUE
XLATI.EFFDT
XLATI.XLATLONGNAME
  Let $PSXLATITEM-XLATLONGNAME = &XLATI.XLATLONGNAME
  Do Get_Related_PSXLATITEM(&XLATI.EFFDT,&XLATI.FIELDNAME,&XLATI.FIELDVALUE)

FROM PSXLATITEM XLATI
WHERE XLATI.FIELDNAME   = $FieldName
  AND XLATI.FIELDVALUE  = $FieldValue
  AND XLATI.EFFDT = (SELECT MAX(EFFDT) FROM PSXLATITEM
                      WHERE FIELDNAME = XLATI.FIELDNAME
                        AND FIELDVALUE = XLATI.FIELDVALUE
                        AND EFFDT <= $Select-AsOfDate)
  AND XLATI.EFF_STATUS <> 'I'
end-SELECT

#debugd show 'XlatItem Value: ' $FieldName '/' $FieldValue ': ' $PSXLATITEM-XLATLONGNAME

end-procedure


!************************************************************************
! Procedure Get-PartTime-Contract-Workdays
!************************************************************************
begin-procedure Get-PartTime-Contract-Workdays
#debug do Fin-Debug-Msg('Get-PartTime-Contract-Workdays')

let $Schedule_SetID = ''
let $Schedule_ID    = ''
let $Use-Dflt-WS    = ''
let $Sch-AdHoc-Ind  = ''
let $Rotation-Id    = ''
let $Sch-Eff-Bgn-Dt = ''
let $Sch-Eff-End-Dt = ''

! -------------------------------------------------------------------------------------------------------
! In case of contract days are the scheduled ones, instead of the worked ones,
! initialize the varibale below to zero.
let #Schedule_Wk_Days = &SSTCWK_DAYS
! -------------------------------------------------------------------------------------------------------

let #Cntrt-Week-Workdays = 0
let $Schedule_type = 'R'
let $Max-Date = '12/31/9999'
do Format-DateTime($Max-Date, $Max-Date, {DEFDATE},'','native')
#debugd show 'Schedule assignment max. date: ' $Max-Date


let $sql-statement = 'GPESFNQ1.SQR, Get-PartTime-Contract-Workdays,Select, PS_SCH_ASSIGN SCHO'
Begin-SELECT on-error=SQL-Error
SCHO.EMPLID
SCHO.EMPL_RCD
SCHO.EFFDT
SCHO.END_EFFDT
SCHO.SCHEDULE_GRP
SCHO.SETID
SCHO.SCH_ADHOC_IND
SCHO.SCHEDULE_ID
SCHO.ROTATION_ID
SCHO.USE_DFLT_WS

  If $Sch-Eff-End-Dt = ''                ! First loop.
    do Format-DateTime(&SCHO.EFFDT, $SCHO-EFFDT-CMP, {DEFCMP}, '', '')
    do Format-DateTime($Contract-Bgn-Dt, $Contract-Bgn-Dt-CMP, {DEFCMP}, '', '')
    If $SCHO-EFFDT-CMP > $Contract-Bgn-Dt-CMP    ! Work Schedule assignment does not necessary match
                                                 ! with the employee contract begin date. In such a case,
                                                 ! we need to retrieve the paygroup default schedule day count.
      let $Sch-Eff-Bgn-Dt = $Contract-Bgn-Dt
      do Convert-To-DTU-Date(&SCHO.EFFDT ,$Sch-Eff-End-Dt)
      do dtu-subtract-days($Sch-Eff-End-Dt, 1, $Sch-Eff-End-Dt)
      do Convert-From-DTU-Date($Sch-Eff-End-Dt, $Sch-Eff-End-Dt)
      Do Dflt-Schedule-Workdays-Count
    End-If
  End-If

 !Valid values for USE_DFLT_WS: 'A'  Create Personal Sched, 'Y'  Use Dflt Sched, 'N'  Predefined Sched.
  If &SCHO.USE_DFLT_WS = 'Y'              ! It does use the default work schedule
    let $Sch-Eff-Bgn-Dt = &SCHO.EFFDT
    let $Sch-Eff-End-Dt = &SCHO.END_EFFDT
    If $Sch-Eff-End-Dt = $Max-Date
      let $Sch-Eff-End-Dt = $Contract-End-Dt
    End-if
    Do Dflt-Schedule-Workdays-Count
  Else
    let $Schedule_SetID = &SCHO.SETID
    let $Schedule_ID = &SCHO.SCHEDULE_ID
    let $Use-Dflt-WS = &SCHO.USE_DFLT_WS
    let $Sch-AdHoc-Ind = &SCHO.SCH_ADHOC_IND
    let $Rotation-Id = &SCHO.ROTATION_ID
    let $Sch-Eff-Bgn-Dt = &SCHO.EFFDT
    let $Sch-Eff-End-Dt = &SCHO.END_EFFDT
    If $Sch-Eff-End-Dt = $Max-Date
      let $Sch-Eff-End-Dt = $Contract-End-Dt
    End-if
    Do Schedule-Workdays-Count
  End-If


FROM PS_SCH_ASSIGN SCHO

WHERE SCHO.EMPLID = $Select-EMPLID
  AND $Contract-Bgn-Dt <= SCHO.END_EFFDT AND $Contract-End-Dt >= SCHO.EFFDT
ORDER BY SCHO.EMPLID, SCHO.EFFDT

End-SELECT

If $Sch-Eff-End-Dt = ''
  let $Sch-Eff-Bgn-Dt = $Contract-Bgn-Dt
  let $Sch-Eff-End-Dt = $Contract-End-Dt
  Do Dflt-Schedule-Workdays-Count
End-If

If $Schedule_type = 'R'
  Let #Schedule_Wk_Days = #Cntrt-Week-Workdays
else
  let #Sched-Hrs = Round(#Sched-Hrs-Tot / #workdays-Tot, 2)     !Schedule hours average for part-time irregular employees
  #debugd show 'Irregular part time workday Schedule hours average: ' #Sched-Hrs-Tot '/' #workdays-Tot ' = ' #Sched-Hrs
End-If
#debugd show 'Part time type:         ' $Schedule_type
#debugd show 'Contract week workdays: ' #Cntrt-Week-Workdays
#debugd show 'Schedule Work days:     ' #Schedule_Wk_Days

end-procedure


!************************************************************************
! Procedure Dflt-Schedule-Workdays-Count
!************************************************************************
begin-procedure Dflt-Schedule-Workdays-Count
#debug do Fin-Debug-Msg('Dflt-Schedule-Workdays-Count ')


let $Pgrp-Sch-Eff-Bgn-Dt = $Sch-Eff-Bgn-Dt
let $Pgrp-Sch-Eff-End-Dt = $Sch-Eff-End-Dt
let $Sch-Eff-End-Dt = ''
let $Schedule_SetID = ''
let $Schedule_ID = ''
let $Sch-AdHoc-Ind = ''
let $Rotation-Id = ''

#debugd show 'Paygroup:            ' $Pay_Group
#debugd show 'Schedule begin date: ' $Pgrp-Sch-Eff-Bgn-Dt
#debugd show 'Schedule end date:   ' $Pgrp-Sch-Eff-End-Dt

let $sql-statement = 'GPESFNQ1.SQR, Dflt-Schedule-Workdays-Count,Select,PS_GP_PYGRP_DTL'
begin-SELECT on-error=SQL-Error
PYGRP.GP_PAYGROUP
PYGRP.EFFDT
PYGRP.SETID
PYGRP.SCHEDULE_ID
PYGRP.ROTATION_ID

  #debugd show 'Paygroup Effdt:      ' &PYGRP.EFFDT

  If $Schedule_SetID <> ''
     do Convert-To-DTU-Date(&PYGRP.EFFDT ,$Sch-Eff-End-Dt)
     do dtu-subtract-days($Sch-Eff-End-Dt, 1, $Sch-Eff-End-Dt)
     do Convert-From-DTU-Date($Sch-Eff-End-Dt, $Sch-Eff-End-Dt)
     Do Schedule-Workdays-Count
     let $Sch-Eff-Bgn-Dt = &PYGRP.EFFDT
  End-If

  let $Schedule_SetID = &PYGRP.SETID
  let $Schedule_ID = &PYGRP.SCHEDULE_ID
  let $Sch-AdHoc-Ind = '1'
  let $Rotation-Id = &PYGRP.ROTATION_ID

FROM PS_GP_PYGRP_DTL  PYGRP

WHERE PYGRP.GP_PAYGROUP = $Pay_Group
  AND PYGRP.EFFDT BETWEEN (SELECT MAX(EFFDT) FROM PS_GP_PYGRP_DTL
                          WHERE GP_PAYGROUP = PYGRP.GP_PAYGROUP
                            AND EFFDT <= $Pgrp-Sch-Eff-Bgn-Dt)
                   AND   (SELECT MAX(EFFDT) FROM PS_GP_PYGRP_DTL
                          WHERE GP_PAYGROUP = PYGRP.GP_PAYGROUP
                            AND EFFDT <= $Pgrp-Sch-Eff-End-Dt)

ORDER BY EFFDT
end-SELECT

If $Schedule_SetID = ''
  SHOW '******************************************************************************************************'
  show 'Paygroup '  $Pay_Group ' not found as of '  $Pgrp-Sch-Eff-Bgn-Dt ' and/or as of ' $Pgrp-Sch-Eff-End-Dt
  SHOW '******************************************************************************************************'  
End-if

let $Sch-Eff-End-Dt = $Pgrp-Sch-Eff-End-Dt
Do Schedule-Workdays-Count


end-procedure


!************************************************************************
! Procedure Schedule-Workdays-Count
!************************************************************************
begin-procedure Schedule-Workdays-Count
#debug do Fin-Debug-Msg('Schedule-Workdays-Count')


#debugd show 'Schedule SetId:      ' $Schedule_SetID
#debugd show 'Schedule Id:         ' $Schedule_ID
#debugd show 'Ad Hoc Ind:          ' $Sch-AdHoc-Ind
#debugd show 'Rotation Id:         ' $Rotation-Id

!!!************************************************************************
!!!--Work-day reduction employees are hardcoded 5 work-days by week.
!!!--We remove this hardcode to get the work-days from the work schedule.
!!!let $OVRD-Bgn-Dt = $Sch-Eff-Bgn-Dt
!!!let $OVRD-End-Dt = $Sch-Eff-End-Dt
!!!Do Get-WorkDay-Reduction
!!!--Work-day reduction employees are hardcoded 5 work-days by week.
!!!--We remove this hardcode to get the work-days from the work schedule.
!************************************************************************

Let #Sched-Hrs = 0

If substr($Sch-Eff-End-Dt, 1, 10) = $Max-Date
let $sql-statement = 'GPESFNQ1.SQR, Schedule-Workdays-Count,Select, GPES_SYS_WEEK'
begin-SELECT on-error=SQL-Error
MAX(GPES_WEEK_END_DT)  &MAX_DATE

  let $SchEffEndDt = &MAX_DATE

FROM PS_GPES_SYS_WEEK
end-SELECT
Else
  let $SchEffEndDt = $Sch-Eff-End-Dt
End-If

do Format-DateTime($Sch-Eff-Bgn-Dt, $Sch-Eff-Bgn-Dt-CMP, {DEFCMP}, '', '')
do Format-DateTime($Hire-Dt, $Hire-Dt-CMP, {DEFCMP}, '', '')
If $Hire-Dt-CMP > $Sch-Eff-Bgn-Dt-CMP
  Let $Sch-Eff-Bgn-Dt = $Hire-Dt
End-If
#debugd show 'Schedule begin date: ' $Sch-Eff-Bgn-Dt
#debugd show 'Schedule end date:   ' $SchEffEndDt
#debugd show 'Schedule SetID:      ' $Schedule_SetID
#debugd show 'Schedule ID:         ' $Schedule_ID
#debugd show 'Schedule AdHoc:      ' $Sch-AdHoc-Ind
#debugd show 'Schedule RotationID: ' $Rotation-Id


let $sql-statement = 'GPESFNQ1.SQR, Schedule-Workdays-Count,Select, PS_SCH_CLND_VW SCH, GPES_SYS_WEEK WEK'
begin-SELECT on-error=SQL-Error
WEK.GPES_WEEK_BGN_DT
MIN(WEK.GPES_WEEK_END_DT)    &WEK.GPES_WEEK_END_DT
MAX(SCH.SCHED_HRS)           &SCH.SCHED_HRS
SUM(SCH.SCHED_HRS)           &SCH.WEEK_SCH_HRS
COUNT(*)                     &WEK.WEEK_WORKDAYS
  Let #Sched-Hrs = &SCH.SCHED_HRS
  ! !!************************************************************************
  ! !!--Work-day reduction employees are hardcoded 5 work-days by week.
  ! !!--If $Workday-Reduction <> ''
  ! !!--  do Schedule-Workdays-Reduction     !Assigns value to #Week_workdays
  ! !!--else
    let #Week-workdays = &WEK.WEEK_WORKDAYS
  ! !!--end-if
  ! !!************************************************************************

  let #workdays-Tot = #workdays-Tot + #Week-workdays
  let #Sched-Hrs-Tot = #Sched-Hrs-Tot + &SCH.WEEK_SCH_HRS

  If #Cntrt-Week-Workdays = 0
    Let #Cntrt-Week-Workdays = #Week-workdays
  Else
    If #Cntrt-Week-Workdays <> #Week-workdays
      Let $Schedule_type = 'I'
    End-If
  End-If

 ! -------------------------------------------------------------------------------------------------------
 ! CONTRACT SCHEDULED WORKDAYS.
 ! In case of contract days are the scheduled ones, instead of the worked ones,
 ! remove the comment mark from the two lines of code below.
 ! Let #Schedule_Wk_Days = #Schedule_Wk_Days + #Week-workdays
 ! #debugd show 'Week workdays/Schedule workdays: ' #Week-workdays '/' #Schedule_Wk_Days
 !--------------------------------------------------------------------------------------------------------
 #debugd show 'Week workdays: ' #Week-workdays '   Week Sched Hours: ' &WEK.WEEK_WORKDAYS ' (Accumulated(Days/Hrs): #workdays-Tot/#Sched-Hrs-Tot)'

FROM PS_SCH_CLND_VW SCH, PS_GPES_SYS_WEEK WEK

WHERE SCH.SETID = $Schedule_SetID
  AND SCH.SCHEDULE_ID = $Schedule_ID
  AND SCH.SCH_ADHOC_IND = $Sch-AdHoc-Ind
  AND SCH.ROTATION_ID = $Rotation-Id
  AND SCH.DUR BETWEEN WEK.GPES_WEEK_BGN_DT AND WEK.GPES_WEEK_END_DT
  AND WEK.GPES_WEEK_BGN_DT >= (SELECT MAX(GPES_WEEK_BGN_DT) FROM PS_GPES_SYS_WEEK WHERE GPES_WEEK_BGN_DT <= $Sch-Eff-Bgn-Dt)
  AND WEK.GPES_WEEK_END_DT <= (SELECT MIN(GPES_WEEK_END_DT) FROM PS_GPES_SYS_WEEK WHERE GPES_WEEK_END_DT >= $SchEffEndDt)
  AND SCH.WRKDAY_ID <> 'OFF'
GROUP BY WEK.GPES_WEEK_BGN_DT
ORDER BY WEK.GPES_WEEK_BGN_DT

end-SELECT

! -------------------------------------------------------------------------------------------------------
! CONTRACT SCHEDULED WORKDAYS.
! In case of contract days are the scheduled ones, instead of the worked ones,
! remove all the comment marks below.
!let #Cntrt-Week-Workdays-Adj = 0
!
!let $sql-statement = 'GPESFNQ1.SQR, Schedule-Workdays-Count,Select, PS_SCH_CLND_VW SCH1, GPES_SYS_WEEK WEK1'
!begin-SELECT on-error=SQL-Error
!WEK1.GPES_WEEK_BGN_DT
!COUNT(*)                &WEK1.WEEK_WORKDAYS
!
!  let #Cntrt-Week-Workdays-Adj = #Cntrt-Week-Workdays-Adj + &WEK1.WEEK_WORKDAYS
!  #debugd show 'Week workdays begining/ending week adjustment: ' &WEK1.WEEK_WORKDAYS '/' #Cntrt-Week-Workdays-Adj
!
!FROM PS_SCH_CLND_VW SCH1, PS_GPES_SYS_WEEK WEK1
!
!WHERE SCH1.SETID = $Schedule_SetID
!  AND SCH1.SCHEDULE_ID = $Schedule_ID
!  AND SCH1.SCH_ADHOC_IND = $Sch-AdHoc-Ind
!  AND SCH1.ROTATION_ID = $Rotation-Id
!  AND (($Sch-Eff-Bgn-Dt >= WEK1.GPES_WEEK_BGN_DT AND $Sch-Eff-Bgn-Dt <= WEK1.GPES_WEEK_END_DT
!       AND SCH1.DUR >= WEK1.GPES_WEEK_BGN_DT AND SCH1.DUR < $Sch-Eff-Bgn-Dt)
!      OR ($Sch-Eff-End-Dt >= WEK1.GPES_WEEK_BGN_DT AND $Sch-Eff-End-Dt <= WEK1.GPES_WEEK_END_DT
!       AND SCH1.DUR > $Sch-Eff-End-Dt AND SCH1.DUR <= WEK1.GPES_WEEK_END_DT))
!  AND SCH1.WRKDAY_ID <> 'OFF'
!GROUP BY WEK1.GPES_WEEK_BGN_DT
!ORDER BY WEK1.GPES_WEEK_BGN_DT
!
!end-SELECT
!
!let #Schedule_Wk_Days = #Schedule_Wk_Days - #Cntrt-Week-Workdays-Adj
! -------------------------------------------------------------------------------------------------------
#debugd show 'Final Schedule workdays: ' #Schedule_Wk_Days

end-procedure


!************************************************************************
! Procedure Schedule-Workdays-Reduction
!************************************************************************
begin-procedure Schedule-Workdays-Reduction
#debug do Fin-Debug-Msg('Schedule-Workdays-Reduction')

let #Week-workdays = 0

let $sql-statement = 'GPESFNQ1.SQR,Schedule-Workdays-Reduction,Select,PS_GP_PYE_SOVR SOVR1'
begin-SELECT DISTINCT on-error=SQL-Error
SOVR2.SOVR_VAL_NUM
   If &SOVR2.SOVR_VAL_NUM <> 0
     let #Week-workdays = 5
   End-if

FROM PS_GP_PYE_SOVR SOVR2

WHERE SOVR2.EMPLID =$Select-EMPLID
 AND  SOVR2.EMPL_RCD = #Select-EMPL-RCD
 AND  SOVR2.PIN_NUM = (SELECT PIN_NUM FROM PS_GP_PIN
                        WHERE PIN_NUM = SOVR2.PIN_NUM
                         AND  PIN_CODE ='CLI VR RED JORNADA ESP')
 AND ((SOVR2.BGN_DT <= &WEK.GPES_WEEK_END_DT AND SOVR2.END_DT >= &WEK.GPES_WEEK_BGN_DT)
   OR (SOVR2.BGN_DT = (SELECT MAX(BGN_DT) FROM PS_GP_PYE_SOVR
                        WHERE EMPLID = SOVR2.EMPLID
                         AND  EMPL_RCD = SOVR2.EMPL_RCD
                         AND  PIN_NUM = SOVR2.PIN_NUM
                         AND  BGN_DT <= &WEK.GPES_WEEK_END_DT)
   AND SOVR2.END_DT IS NULL))

end-SELECT

#debugd show 'Workday reduction bgn/end dt: ' &WEK.GPES_WEEK_BGN_DT '/' &WEK.GPES_WEEK_END_DT

end-procedure


!***********************************************************************
!   GPES_ACRSN_INEM --> GPES_RS_INM_LNG
!
! CUT AND PASTE INTO YOUR SELECT STATEMENT THE FIELDS BELOW
!
!#define GPES_ACRSN_INEM
!
!ACTION              &ACTION
!ACTION_REASON       &ACTION_REASON
!EFFDT               &EFFDT
!DESCR254            &GPES_ACRSN_INEM-DESCR50
!
!   Get_Related_GPES_ACTNRSN_INEM
!
!   Key Structure:
!      ACTION
!      ACTION_REASON
!      EFFDT
!
!   Calling Syntax:
!   Get_Related_GPES_ACTNRSN_INEM($EFFDT,$ACTION_REASON,$ACTION)
!
!***********************************************************************

#ifdef GPES_ACRSN_INEM

BEGIN-PROCEDURE Get_Related_GPES_ACTNRSN_INEM(
  $EFFDT,
  $ACTION_REASON
  $ACTION
)
if $_PSOptions_Language_Cd <> $_curr_language_cd

BEGIN-SELECT
RSINEMLN.DESCR254
  do FillVar(&RSINEMLN.DESCR254, $_GPES_ACRSN_INEM-DESCR50)

FROM PS_GPES_RS_INM_LNG RSINEMLN
WHERE  RSINEMLN.EFFDT  = $EFFDT
   AND RSINEMLN.ACTION_REASON  = $ACTION_REASON
   AND RSINEMLN.ACTION  = $ACTION
   AND RSINEMLN.LANGUAGE_CD = $_curr_language_cd
END-SELECT
end-if
END-PROCEDURE

#endif GPES_ACRSN_INEM


!***********************************************************************
!   GPES_JOB_CNO_VW --> GPES_JOBCNO_LNG
!
! CUT AND PASTE INTO YOUR SELECT STATEMENT THE FIELDS BELOW
!
!#define GPES_JOB_CNO_VW
!
!SETID               &ACTION
!GPES_CNO_CODE       &GPES_CNO_CODE
!DESCR50             &GPES_JOB_CNO_VW-DESCR50
!
!   Get_Related_GPES_JOBCD_CNO_VW
!
!   Key Structure:
!      SETID
!      GPES_CNO_CODE
!
!   Calling Syntax:
!   Get_Related_GPES_JOBCD_CNO_VW($GPES_CNO_CODE,$SETID)
!
!***********************************************************************

#ifdef GPES_JOB_CNO_VW

BEGIN-PROCEDURE Get_Related_GPES_JOBCD_CNO_VW(
  $GPES_CNO_CODE,
  $SETID
)
if $_PSOptions_Language_Cd <> $_curr_language_cd

BEGIN-SELECT
JOBCNOLNG.DESCR50
  do FillVar(&JOBCNOLNG.DESCR50, $_GPES_JOB_CNO_VW-DESCR50)

FROM PS_GPES_JOBCNO_LNG JOBCNOLNG
WHERE  JOBCNOLNG.GPES_CNO_CODE  = $GPES_CNO_CODE
   AND JOBCNOLNG.SETID  = $SETID
   AND JOBCNOLNG.LANGUAGE_CD = $_curr_language_cd
END-SELECT
end-if

END-PROCEDURE

#endif GPES_JOB_CNO_VW


!************************************************************************
! Procedure INSERT-ERROR-MESSAGE
!************************************************************************
begin-procedure INSERT-ERROR-MESSAGE
#debug do Fin-Debug-Msg('INSERT-ERROR-MESSAGE')

begin-Select
Z.PROCESS_INSTANCE
Z.MESSAGE_SEQ
Z.MESSAGE_NBR

  let #Message_Seq = &Z.MESSAGE_SEQ

FROM PS_MESSAGE_LOG Z
WHERE Z.PROCESS_INSTANCE = #prcs_process_instance and
      Z.MESSAGE_SEQ = (SELECT MAX(Z1.MESSAGE_SEQ)
                       FROM PS_MESSAGE_LOG Z1
                       WHERE Z1.PROCESS_INSTANCE = Z.PROCESS_INSTANCE and
                             Z1.MESSAGE_SEQ = Z.MESSAGE_SEQ)
ORDER BY PROCESS_INSTANCE, MESSAGE_SEQ
end-Select

if isnull(&Z.MESSAGE_SEQ)
  let #Message_Seq = 0
end-if

let #Message_seq = #message_seq + 1
let $Jobid = 'GPESFNQ1'
let $Program_Name = $Jobid
let #PrcsMessageSetNbr = 17170
let #Message_Severity = 10

let $sql-statement = 'GPESFNQ1.SQR,INSERT-ERROR-MESSAGE,Insert,PS_MESSAGE_LOG'
begin-SQL on-error=SQL-Error
INSERT INTO PS_MESSAGE_LOG
  (PROCESS_INSTANCE, MESSAGE_SEQ, JOBID, PROGRAM_NAME, MESSAGE_SET_NBR,
  MESSAGE_NBR, MESSAGE_SEVERITY, DTTM_STAMP_SEC)
VALUES
  (#Prcs_Process_Instance, #Message_Seq, $Jobid, $Program_Name,
  #PrcsMessageSetNbr, #Message_Nbr, #Message_Severity,
  {DATETIMEIN-PREFIX}$SYSDATETIME{DATETIMEIN-SUFFIX})
end-SQL

Let #Parm_Seq = 1
Let $Message_Parm = $Select-EMPLID
begin-SQL
INSERT INTO PS_MESSAGE_LOGPARM
(PROCESS_INSTANCE, MESSAGE_SEQ, PARM_SEQ, MESSAGE_PARM)
VALUES
(#Prcs_Process_Instance, #Message_Seq, #Parm_Seq, $Message_Parm)
end-SQL

end-procedure



!************************************************************************
! Procedure Delete-XML-EE
!************************************************************************
begin-procedure Delete-XML-EE
#debug do Fin-Debug-Msg('Delete-XML-EE')

let $sql-statement = 'GPESFNQ1.SQR,Delete, Delete-XML-EE, PS_GPES_CC_CONTRB'
begin-sql on-error=SQL-Error
DELETE
 FROM PS_GPES_CC_EE_DTA
 WHERE OPRID = $prcs_oprid
  AND  RUN_CNTL_ID = $prcs_run_cntl_id;

DELETE
 FROM PS_GPES_CC_PT_DAYS
 WHERE OPRID = $prcs_oprid
  AND  RUN_CNTL_ID = $prcs_run_cntl_id;

DELETE
 FROM PS_GPES_CC_CNTRB
 WHERE OPRID = $prcs_oprid
  AND  RUN_CNTL_ID = $prcs_run_cntl_id;
end-sql

end-procedure


!---------------------------------------------------!
! Debug Msg                                                          !
!--------------------------------------------------------------------!
begin-procedure Fin-Debug-Msg($procedure_name)
   display ' '
   display '----------------------------------'
   display $procedure_name
   #debugt date-time () {Native-DateTime} &SysDateTime
   #debugt move &SysDateTime to $SysDateTime
   #debugt show 'TIMING, ' $procedure_name ', ' $SysDateTime
   display ' '
end-procedure ! Fin-Debug-Msg




!***************************************************************
!Include SQCs
!***************************************************************
#Include 'getsetid.sqc'
#Include 'readxlat.sqc'
#Include 'sqlerr.sqc'
#Include 'reset.sqc'            !Reset printer procedure
#Include 'curdttim.sqc'         !Get-Current-DateTime procedure
#Include 'datetime.sqc'         !Routines for date and time formatting
#Include 'number.sqc'           !Routines to format numbers
#Include 'stdapi.sqc'           !Update Process API
#include 'datemath.sqc'         !Routines for date calculation
#include 'gpesfbaj.sqc'         !Routines to Print Report for Employees in Not in Absence
#include 'gpesfnit.sqc'         !Routines to Print Report for Employees in Sickness
#include 'gpesfnmt.sqc'         !Routines to Print Report for Employees in Maternity
#include 'gpesfnre.sqc'         !Routines to Print Report for Employees in Risk in Pregnancy
#include 'gpesodmn.sqc'         !Routines to Print Report for Active Employees in Not in Absence



