!***********************************************************************
!  GPMXIM08 :  Instituto Mexicano Del Seguro Social,                   *
!             AVISO DE MODIFICACION DE SALARIO DEL ASEGURADO           *
!                                                                      *
!***********************************************************************
!***********************************************************************
!                                                                      *
!                                                                      *
!                                                                      *
!                                                                      *
! This software and related documentation are provided under a         *
! license agreement containing restrictions on use and                 *
! disclosure and are protected by intellectual property                *
! laws. Except as expressly permitted in your license agreement        *
! or allowed by law, you may not use, copy, reproduce,                 *
! translate, broadcast, modify, license, transmit, distribute,         *
! exhibit, perform, publish or display any part, in any form or        *
! by any means. Reverse engineering, disassembly, or                   *
! decompilation of this software, unless required by law for           *
! interoperability, is prohibited.                                     *
! The information contained herein is subject to change without        *
! notice and is not warranted to be error-free. If you find any        *
! errors, please report them to us in writing.                         *
!                                                                      *
!                                                                      *
! Copyright (C) 1988, 2020, Oracle and/or its affiliates.              *
! All Rights Reserved.                                                 *
!***********************************************************************
!                                                                      *
!       $Release:  HR92                                                !
!           $Bug:  31781263                                            !
!                                                                      *
!***********************************************************************
#include 'setenv.sqc' !Set environment
!#Include 'setup02.sqc'  !Printer and page-size initialization

Begin-Setup                          
#include 'setupdb.sqc'
End-Setup

!***************************************************
! Procedure Main                                   *
!***************************************************
begin-report
  do Init-Report
  do Reset
  do Stdapi-Term
end-report

!***************************************************
! Procedure Init-Report                            *
!***************************************************
begin-procedure Init-Report
  move 'GPMXIM08' to $ReportID
!  display 'Con cejilla'

  display $ReportId
  move 'Instituto Mexicano Del Seguro Social' to $ReportTitle
  Display $ReportTitle
  date-time () hh:mi:ss &timeBegan
  display 'Report Began: ' noline
  display &timeBegan
  show 'Ver.2020 PUM37.01'
  show ' '
  do Init-DateTime
  do Init-Number
  do Stdapi-Init
  do Get-Current-DateTime
  
  Let #EmployeeCount = 0
  Let $openXMLFile = 'N'
  let #FNum  = 1
  let #FStat = 0
  
  let $FileName = 'gpmxim08.xml'
  let $FileName = Rtrim(LTRIM($FileName, ' '), ' ') 
  let $FileName = '{IMPORTPREFIX}' || $FileName || '{IMPORTSUFFIX}'
  
  Let $TemplateID = 'GPMX_IMSSLRY'
  Let $ReportDefn = 'GPMX_IMSSLRY' 

  Do Delete-RunControl
       
  Do Insert-XML-RunControl

  let $xml_cntrl_begin = '<'
  let $xml_cntrl_end   = '>'
  let $xml_text_delim  = '"'
  let $xml_tag_end     = '/'
  let $xml_tag         = ''
  let $xml_content     = ''
  let $xml_output_line = ''
  
  do Select-Parameters-IMSS

  move $Estabid to $Reg_Patronal
  
  Let $Job_Tbl = 'A'
  Let $Reg_Patronal_RC = $Reg_Patronal
  
  Do Get-Establishment-Optn
 
 ! show ' $WhereLocation: ' $WhereLocation
  Do Report
  
  if #EmployeeCount = 0 and $openXMLFile = 'N'
     Do Open-XML-File($FileName, #FNum, #FStat, $openXMLFile) 
     
     let $xml_tag = 'EMPLOYEE_DATA'      
     Do Process-XML-Tag-Beg
     let $xml_tag = 'EMPLID'
     let $xml_content = 'REPORTE SIN EMPLEADOS'
     do Process-XML-Tag-Content
     let $xml_tag = 'EMPLOYEE_DATA'      
     Do Process-XML-Tag-End
     
     show 'No hay empleados procesados'
  End-if
  
  Do Close-File
end-procedure


!***************************************************
! Procedure Report                                 *
!***************************************************
begin-procedure Report
move 0 to #EECount
let $v_select = ''
let $v_espacio = ''
let $v_flag = ''
LET $EmplLocation_Aux = ''
Let $Reg_Patronal_Aux = ''
do Advice-Flag

let $EmpIni = rtrim($EmpIni, ' ')
let $Empfin = rtrim($Empfin, ' ')

if ($empini <> '') and ($empfin = '')
   Let $v_select = ' AND A.EMPLID = ' || '''' ||  $EmpIni || ''''
end-if

if ($empini <> '') and ($empfin <> '')
   let $v_select = ' AND A.EMPLID BETWEEN ''' || $EmpIni || ''' AND '''  || $EmpFin || ''''
end-if

if ($BegDt <> '') and ($EndDt <> '')
   let $v_select = $v_select || ' AND T.EFFDT BETWEEN ''' || $BegDt || ''' AND ''' || $EndDt || ''''
end-if

if $TipSal <> '0040'
  let $v_select = $v_select || ' AND JBJR.SALARY_TYPE_MEX = ''' || rtrim($TipSal,' ') ||  ''''
end-if

! SHOW ' $v_select: '  $v_select

begin-SELECT DISTINCT
A.EMPLID
A.LOCATION
A.SETID_LOCATION
A.COMPANY    
A.ACTION_DT             
T.EMPLID
A.EMPL_RCD
T.EFFDT
T.GPMX_SDI_FIX
T.GPMX_SDI_VAR
T.GPMX_SDI_TOT
T.ACTION
T.ACTION_REASON
S.IMS_PRN_FORM_MEX
JBJR.WORKER_TYPE_MEX
JBJR.SALARY_TYPE_MEX
JBJR.REDUCED_WEEK_MEX
A.STD_HOURS
A.JOBCODE
A.SETID_JOBCODE
NAM.FIRST_NAME
NAM.LAST_NAME
NAM.SECOND_LAST_NAME
J.DESCR
J_LNG.DESCR
PER.SEX
A.GP_PAYGROUP
A.ESTABID

  Let $Salary_Fix = ''
  Let $Salary_Mix = ''
  Let $Salary_Var = ''
  Let $Male = ''
  Let $Female = ''
  Let $RFC = ''
  Let $CURP= ''
  let $IMSS_ID = ''
  Let $Occupation = ''
  Let $Worker_Type  = ''
  Let $Name         = ''
  Let $PaternalName = ''
  Let $MaternalName = ''
  let $Occupation = ''
  Let $Gp_Paygroup = ''
  Let $Working_Days = ''
  Let $Wrkr_Permant = '' 
  Let $Wrkr_Eventual = '' 
  Let $Wrkr_Constr = ''
  Let $Location = ''
  Let $SetIDLoc = ''
  LET $EmplEstabId = ''

  move  0 to #IDSCurr
  
  Let $Emplid       = RTrim(&A.EMPLID, ' ')
  Let #Empl_Rcd     = &A.EMPL_RCD
  Let $Worker_Type  = &JBJR.WORKER_TYPE_MEX
  Let $Name         = &NAM.FIRST_NAME
  Let $PaternalName = &NAM.LAST_NAME
  Let $MaternalName = &NAM.SECOND_LAST_NAME
  Let $Company      = &A.Company
  Let $Gp_Paygroup  = &A.GP_PAYGROUP
  Let $DatemaxIds   = &T.EFFDT
  Let $Location     = Rtrim(&A.LOCATION, ' ')
  Let $SetIDLoc     = Rtrim(&A.SETID_LOCATION, ' ')
  LET $EmplEstabId  = Rtrim(&A.ESTABID, ' ')
  
  do to_upper($Name)
  do to_upper($PaternalName)
  do to_upper($MaternalName)
  
  If &J_LNG.DESCR = ''
    let $Occupation = UPPER(RTRIM(LTRIM(&J.DESCR, ' '), ' '))
  Else
    let $Occupation = UPPER(RTRIM(LTRIM(&J_LNG.DESCR, ' '), ' '))
  End-IF
  
  
  If &J_LNG.DESCR = ''
    let $Occupation = UPPER(RTRIM(LTRIM(&J.DESCR, ' '), ' '))
  Else
    let $Occupation = UPPER(RTRIM(LTRIM(&J_LNG.DESCR, ' '), ' '))
  End-IF
  
  If Rtrim(&PER.SEX, ' ') = 'M'
     Let $Male = 'X'
  else
     Let $Female = 'X'
  end-if
  
  let   #FixedCurr = &T.GPMX_SDI_FIX
  let   #VarCurr   = &T.GPMX_SDI_VAR
  let   #IDSCurr   = &T.GPMX_SDI_TOT
  
 ! show 'current SDI: '  #IDSCurr 
  
  !do Get-Max-DFSalary
  
  Let #IDSCurr_Comp = #IDSCurr
    
   If $topado = 'Y'
      Let $UMA_Effdt = $DatemaxIds
      Do Get-Max-UMA
      
      if #IDSCurr > #SDIMax
         let #IDSCurr = #SDIMax
      end-if
   end-if
  
  move 'N' to $FoundPrevIDS
 
  do Get-IDS-Prev
  if $FoundPrevIDS = 'Y'
     if $Topado = 'Y'
       !  show #IDSCurr   ' = ' #IDSPrev 
      
         if  #IDSCurr  = #IDSPrev 
             move 'N' to $printNotice
             Show 'No existe cambio de salario para este empleado ' $Emplid
         else
             move 'Y' to $printNotice
         end-if
      else
   !      show #IDSCurr_Comp  ' = ' #IDSPrev_Comp
             
        if  #IDSCurr_Comp  = #IDSPrev_Comp
            move 'N' to $printNotice
             Show 'No existe cambio de salario para este empleado ' $Emplid
        else
             move 'Y' to $printNotice
         end-if      
     end-if
  else
     Show 'No existe registro anterior de SDI ' $Emplid
  end-if
  


 if $printNotice = 'Y'
    
    do Get-Company-Data-Mex
    Let $CompanyName = upper($CompanyName)
    
    let $DescrCT = $CompanyName
!    show 'Location: ' $Location
   if rtrim($Reg_Patronal_RC,' ') = '' 
    If $Estab_Opt = 'J'
      
       LET $Reg_Patronal = $EmplEstabId

    Else
    
      If $Location <> $EmplLocation_Aux
        Let $EmplLocation_Aux = $Location
        DO GetLocEstabid
      End-if  
    
    End-if  
   end-if
   
   If $Reg_Patronal <> $Reg_Patronal_Aux
    Do Get-IMSS-Location
    Let $Reg_Patronal_Aux = $Reg_Patronal
    do Obtain_Number($IMSSStreet1, $CompnyNum_Ext, $CompnyNum_Int)
   End-if  
    
  Evaluate $Worker_Type
    When = '0010'
      Let $Wrkr_Permant = 'X'
     Break
    When = '0020'
      Let $Wrkr_Eventual = 'X'
     Break
    When = '0030'
      Let $Wrkr_Constr = 'X'
     Break
  End-Evaluate
  
    do GetNationalID
    Do Get-Person
           
    evaluate &JBJR.SALARY_TYPE_MEX
      when = '0010' !Fijo
        move 'X' to $Salary_Fix
        break
      when = '0020' !mixto
        move 'X' to $Salary_Mix 
        break
      when = '0030' !variable
       move 'X' to $Salary_Var
       break
    end-evaluate
    
    do    convert-to-dtu-date (&T.EFFDT,$ChangeDt-dtu)
    do    dtu-parse-date($ChangeDt-dtu,#dtu-yr,#dtu-mo,#dtu-da)
    move  #dtu-yr to $dtu-yr 9999
    let   $Chg-yy = substr ($dtu-yr,1,4)
    move  #dtu-da to $Chg-da 09
    move  #dtu-mo to $Chg-mo 09
    
    do convert-to-dtu-date ($Current-Date,$CurrentDt-dtu)
    do dtu-parse-date($CurrentDt-dtu,#dtu-yr,#dtu-mo,#dtu-da)
    
    move #dtu-yr to $dtu-yr 9999
    let $Currt_Yr = substr ($dtu-yr,1,4)
    move  #dtu-da to $Currt_Dy 09
    move  #dtu-mo to $Currt_Mn 09
    
    if &JBJR.REDUCED_WEEK_MEX <> '0000'
     !'SEMANA REDUCIDA'
      move 'Y' to $ReducedWk
      do Shedule_Reduce_Week
    end-if
    
    do to_upper($CompanyName)
    !do Obtain_Number($IMSSStreet1, $CompnyNum_Ext, $CompnyNum_Int)
    do to_upper($IMSSStreet1)
    do to_upper($IMSSStreet2)
    do to_upper($IMSSStreet3)
    do to_upper($IMSSStreet4)
    do to_upper($IMSSState)
    do to_upper($Job_LegalRep)
    
    !Let $Reg_Patronal =  $IMSSer
    
    If #EmployeeCount = 0
      Do Open-XML-File($FileName, #FNum, #FStat, $openXMLFile)
    end-if
    
    Let #i = 1
  
    While #i <=3
      Let $xml_tag = 'EMPLOYEE_DATA'      
      Do Process-XML-Tag-Beg  
      Do Create-Employee-XML
      Let $xml_tag = 'EMPLOYEE_DATA'      
      Do Process-XML-Tag-End  
    
      Add 1 to #i
    End-while
    
    Add 1 to #EmployeeCount
  
  end-if
  
  #debug show '------------------------------------------------------------------'
  #debug show 'Emplid       : ' $Emplid
  #debug show 'Reg Ptronal  : ' $Reg_Patronal
  #debug show '$Paternalname: ' $Paternalname
  #debug show '$Maternalname: ' $Maternalname
  #debug show '$Name        : ' $Name        
  #debug show '$Worker_Type : ' $Worker_Type ' - ' $Wrkr_Permant ' - ' $Wrkr_Eventual ' - ' $Wrkr_Constr
  #debug show '$SalaryType  : ' &JBJR.SALARY_TYPE_MEX ' - ' $Salary_Fix ' - ' $Salary_Mix ' - ' $Salary_Var
  #debug show '$IMSS_ID     : ' $IMSS_ID ' - $tipfor ' $tipfor '$IMSSer  ' $IMSSer !numero de SS
  #debug show '$RFC         : ' $RFC
  #debug show 'CURP         : ' $CURP
  #debug show 'IMSS         : ' $IMSS_ID
  #debug show '#IDS Curr    : ' #IDSCurr  ' IDS Previo '  #IDSPrev !salario base de cotización
  #debug show 'Change Date  : '  $Chg-da ' - '  $Chg-mo ' - ' $Chg-yy
  #debug show 'Current Date : ' $Currt_Dy ' - ' $Currt_Mn ' - ' $Currt_Yr 
  #debug show '$Sex         : ' &PER.SEX ' - ' $Male ' - ' $Female
  #debug show 'Reduced Week : ' &JBJR.REDUCED_WEEK_MEX
  #debug show '$Occupation  : ' $Occupation ' - ' &A.JOBCODE ' - ' &A.SETID_JOBCODE
  #debug show '$Bith Place  : ' $BPlace
  #debug show 'Birth date   : ' $Day_Birth ' - ' $Month_Birth ' - ' $Year_Birth 
  #debug show '$CompanyName : ' $CompanyName      
  #debug show '$CompNameAbb : ' $CompanyNameAbbrv 
  #debug show '$Basedate    : ' $Basedate         
  #debug show '$IMSSStreet1 : ' $IMSSStreet1
  #debug show '$IMSSStreet2 : ' $IMSSStreet2
  #debug show '$IMSSStreet3 : ' $IMSSStreet3
  #debug show '$IMSSStreet4 : ' $IMSSStreet4
  #debug show '$IMSSCity    : ' $IMSSCity             
  #debug show '$IMSSState   : ' $IMSSState            
  #debug show '$IMSSZip     : ' $IMSSZip             
  #debug show 'CompnyNum_Ext: ' $CompnyNum_Ext    
  #debug show 'CompnyNum_Int: ' $CompnyNum_Int
  #debug show '$Company_RFC : ' $Company_RFC
  
  move 'N' to $printNotice

FROM  PS_JOB                  A,
      PS_JOB_JR            JBJR,
      PS_GPMX_SDI_WA          T,       
      PS_ACT_RSN_TBL_MEX      S,
      PS_NAMES              NAM,
      (PS_JOBCODE_TBL     J LEFT OUTER JOIN PS_JOBCODE_LANG J_LNG ON J.SETID = J_LNG.SETID AND J.JOBCODE = J_LNG.JOBCODE  AND J.EFFDT = J_LNG.EFFDT  AND J_LNG.LANGUAGE_CD = 'ESP'),
      PS_PERS_DATA_EFFDT    PER
      
WHERE A.COMPANY               = $Company
  AND A.EFFDT = (SELECT MAX(A_ED.EFFDT) 
                 FROM PS_JOB A_ED 
                 WHERE A.EMPLID = A_ED.EMPLID 
                 AND A.EMPL_RCD = A_ED.EMPL_RCD 
                 AND A_ED.EFFDT <= $EndDt)
  AND A.EFFSEQ = (SELECT MAX(A_ES.EFFSEQ) 
                 FROM PS_JOB A_ES 
                 WHERE A.EMPLID = A_ES.EMPLID 
                 AND A.EMPL_RCD = A_ES.EMPL_RCD 
                 AND A.EFFDT = A_ES.EFFDT) 
  AND A.EMPLID                = JBJR.EMPLID
  AND A.EFFDT                 = JBJR.EFFDT
  AND A.EFFSEQ                = JBJR.EFFSEQ
  AND A.EMPL_RCD              = JBJR.EMPL_RCD
  AND A.EMPLID                =       T.EMPLID   
  AND T.EFFDT = (SELECT MAX(EFFDT) FROM PS_GPMX_SDI_WA WHERE EMPLID  = A.EMPLID AND EFFDT <= $EndDt AND EMPL_RCD = A.EMPL_RCD)
  AND T.SEQNUM                = (SELECT MAX(SEQNUM) FROM PS_GPMX_SDI_WA WHERE EMPLID = A.EMPLID AND EFFDT = T.EFFDT AND EMPL_RCD = A.EMPL_RCD)
  AND S.ACTION                = T.ACTION 
  AND S.ACTION_REASON         = T.ACTION_REASON  
  AND S.IMS_PRN_FORM_MEX      = '0030'
  AND     A.EMPLID    = NAM.EMPLID
  AND     NAM.EFFDT   = (SELECT MAX(NAM_ED.EFFDT) FROM PS_NAMES NAM_ED 
                          WHERE NAM.EMPLID    = NAM_ED.EMPLID 
                            AND NAM.NAME_TYPE = NAM_ED.NAME_TYPE 
                            AND NAM_ED.EFFDT <= A.EFFDT) 
  AND   J.JOBCODE   = A.JOBCODE
  AND   J.SETID     = A.SETID_JOBCODE
  AND   J.EFFDT     = (SELECT MAX(EFFDT) FROM PS_JOBCODE_TBL 
                        WHERE JOBCODE  = J.JOBCODE 
                          AND SETID = J.SETID 
                          AND EFFDT <= $EndDt)                         
  AND   PER.EMPLID  = A.EMPLID
  AND   PER.EFFDT   = (SELECT MAX(P.EFFDT) FROM PS_PERS_DATA_EFFDT P
                        WHERE P.EMPLID = PER.EMPLID)
[$WhereForEstablishment]
[$v_flag]
[$v_select]
Order By T.EFFDT DESC,A.EMPLID
end-SELECT
  #debug show ''
  #debug show '$WhereForEstablishment ' $WhereForEstablishment
  #debug show '$V_SELECT     '  $V_SELECT
  #debug show '$V_FLAG       ' $V_FLAG
  #debug show '$enddt        ' $enddt
end-procedure

!**********************************************************
begin-procedure GetNationalID
let $RFC = ''
let $CURP = ''
let $IMSS_ID = ''
let $MedicalUnit = ''

begin-select
RFC.NATIONAL_ID_TYPE
RFC.NATIONAL_ID
    Evaluate &RFC.NATIONAL_ID_TYPE 
      When = 'RFC'
        let $RFC = &RFC.NATIONAL_ID
        Break
      
      When = 'CURP'
        let $CURP = &RFC.NATIONAL_ID
        Break
        
      When ='IMSS'  
        let $IMSS_ID = &RFC.NATIONAL_ID
        Break
        
      When = 'UFM'
        let $MedicalUnit = &RFC.NATIONAL_ID
        Break  
    End-Evaluate  
    
FROM PS_PERS_NID RFC WHERE RFC.NATIONAL_ID_TYPE IN ('RFC', 'CURP', 'IMSS','UMF') AND RFC.EMPLID=$Emplid AND RFC.COUNTRY='MEX'
end-select
end-procedure

!***************************************************
! Procedure Advice-Flag                            *
!***************************************************
begin-procedure Advice-Flag
 let $YesFlag = 'Y'
 
 if $Reimp_Av = 'N' or rtrim($Reimp_Av,' ') = ''
    let $v_flag = 'AND A.EMPLID NOT IN (SELECT FLG.EMPLID FROM PS_GPMX_IMS_CONTR FLG '
    let $v_flag = $v_flag || 'WHERE FLG.EMPLID = A.EMPLID '
    let $v_flag = $v_flag || 'AND   FLG.EFFDT  = T.EFFDT '
    let $v_flag = $v_flag || 'AND FLG.GPMX_IMS_MOD_ADV = ''' || $YesFlag || ''''
!    let $v_flag = $v_flag || ' OR FLG.GPMX_IMS_MOD_DEV = ''' || $YesFlag || ''''
!    let $v_flag = $v_flag || ' OR FLG.GPMX_IMS_MOD_EDI = ''' || $YesFlag || ''''
    let $v_flag = $v_flag || ')'

 else
    let $v_flag = ''
 end-if
end-procedure


!***************************************************
! Procedure Search                                 *
!***************************************************
begin-procedure Search
Let #Total = 0
begin-select
COUNT(*) &TOTAL
  Let #Total = &TOTAL
FROM PS_GPMX_IMS_CONTR  P4
WHERE P4.EMPLID  = &A.EMPLID
  AND P4.EFFDT   = &T.EFFDT
end-select
if #Total = 0
   do INSERT_1
  else
   do UPDATE_1
end-if
end-procedure

!***************************************************
! Procedure Insert_1                               *
!***************************************************
begin-procedure INSERT_1
begin-sql
INSERT INTO PS_GPMX_IMS_CONTR
       (EMPLID,
        EFFDT,
        GPMX_IMS_HIRE_ADV,
        GPMX_IMS_MOD_ADV,
        GPMX_IMS_TER_ADV,
        GPMX_IMS_HIRE_DEV,
        GPMX_IMS_MOD_DEV,
        GPMX_IMS_TER_DEV,
        GPMX_IMS_HIRE_EDI,
        GPMX_IMS_MOD_EDI,
        GPMX_IMS_TER_EDI)
VALUES (&A.EMPLID,
        &T.EFFDT,
        'N',
        'Y',
        'N',
        'N',
        'N',
        'N',
        'N',
        'N',
        'N')
end-sql
end-procedure

!***************************************************
! Procedure Update_1                               *
!***************************************************
begin-procedure UPDATE_1
begin-sql
UPDATE PS_GPMX_IMS_CONTR
SET  GPMX_IMS_MOD_ADV = 'Y'
WHERE EMPLID  = &A.EMPLID
  AND EFFDT   = &T.EFFDT
end-sql
end-procedure

!***************************************
begin-procedure Get-Max-DFSalary
! show 'Get-Max-DFSalary'



begin-select
WZ.GPMX_MINIMUM_WAGE         &minSalary

!  show '&minSalary: ' &minSalary 
   let #SDImaxDF = 25 *  &minSalary
!   show '#SDImaxDF: ' #SDImaxDF 
   let #IDSCurr_Comp = #IDSCurr
  
   if $topado = 'Y'
      if #IDSCurr > #SDIMaxDF
         let #IDSCurr = #SDImaxDF
      end-if
   end-if
   
 !   show '#IDSCurr: ' #IDSCurr 
   
FROM PS_GPMX_MIN_WAGE WZ
WHERE  WZ.WAGE_ZONE_MEX  = '10'
AND    WZ.EFFDT      = (SELECT MAX(EFFDT) 
                        FROM   PS_GPMX_MIN_WAGE
                        WHERE WAGE_ZONE_MEX = WZ.WAGE_ZONE_MEX
                        AND EFFDT <= $DatemaxIds)
end-select


end-procedure

!***************************************************
! Procedure Get-IDS-Prev                           *
!***************************************************
begin-procedure Get-IDS-Prev
move  0 to #IDSPrev
begin-SELECT
M.EFFDT
  move &M.EFFDT to $DatemaxIdsPrev
M.GPMX_SDI_FIX
M.GPMX_SDI_VAR
M.GPMX_SDI_TOT
  
  let   #FixedPrev = &M.GPMX_SDI_FIX
  let   #VarPrev   = &M.GPMX_SDI_VAR
  let   #IDSPrev   = &M.GPMX_SDI_TOT
  
   
  
  move 'Y' to $FoundPrevIDS
  !do  Get-Max-DFSalary_Prev
      
  let #IDSPrev_Comp = #IDSPrev
  
  if $topado = 'Y'  !When Capping from Run Control Page
     Let $UMA_Effdt = $DatemaxIdsPrev
     do Get-Max-UMA
     
     if #IDSPrev > #SDIMax         ! Compares SDI with Capped
        let #IDSPrev = #SDImax     ! Cappes SDI when > 25 UMA
     end-if
  end-if
  
!  show '#IDSPrev: ' #IDSPrev
  
FROM    PS_GPMX_SDI_WA      M

WHERE   M.EMPLID                =       $Emplid
AND     M.EMPL_RCD              =       #emplRcd
AND     M.EFFDT = (SELECT MAX(EFFDT) FROM PS_GPMX_SDI_WA
                   WHERE EMPLID  = M.EMPLID
                   AND EMPL_RCD = M.EMPL_RCD
                   AND EFFDT   < &T.EFFDT)
AND M.SEQNUM = (SELECT MAX(SEQNUM)
                       FROM PS_GPMX_SDI_WA
                       WHERE EMPLID = M.EMPLID
                       AND EMPL_RCD = M.EMPL_RCD
                       AND EFFDT = M.EFFDT)
end-SELECT

end-procedure

!***************************************************
! Procedure Get-Max-DFSalary_Prev                     *
!***************************************************
begin-procedure Get-Max-DFSalary_Prev
let #SDImaxDFPrev = 0

begin-select
WZ2.GPMX_MINIMUM_WAGE        &prevMinSalary

  let #SDImaxDFPrev = 25 *  &prevMinSalary
  
FROM PS_GPMX_MIN_WAGE WZ2
WHERE  WZ2.WAGE_ZONE_MEX  = '10'
AND    WZ2.EFFDT      = (SELECT MAX(EFFDT) 
                        FROM   PS_GPMX_MIN_WAGE
                        WHERE WAGE_ZONE_MEX = '10'
                        AND EFFDT <= $DatemaxIdsPrev)
end-select

end-procedure Get-Max-DFSalary_Prev


!*************************************
begin-procedure Get-IMSS-Location
!*************************************
begin-SELECT
IMSS.DESCR,
IMSS.DESCR_AC,
IMSS.ADDRESS1,
IMSS.ADDRESS2,
IMSS.ADDRESS3,
IMSS.ADDRESS4,
IMSS.CITY,
IMSS.COUNTY,
IMSS.STATE,
IMSS.POSTAL,
IMSS.COUNTRY,
IMSS.ESTABID,

  let $IMSSName     = rtrim(&IMSS.DESCR,' ')
  let $IMSSAbbrv    = rtrim(&IMSS.DESCR_AC,' ')
  let $IMSSStreet1  = rtrim(&IMSS.ADDRESS1,' ')
  let $IMSSStreet2  = rtrim(&IMSS.ADDRESS2,' ')
  let $IMSSStreet3  = rtrim(&IMSS.ADDRESS3,' ')
  let $IMSSStreet4  = rtrim(&IMSS.ADDRESS4,' ')
  let $IMSSCity     = rtrim(&IMSS.CITY,' ')
  let $IMSSCounty   = rtrim(&IMSS.COUNTY,' ')
  
  move &IMSS.STATE to $state1
  do get-Statename
  
  move &STDESCRIP        to $IMSSState
  move &IMSS.POSTAL      to $IMSSZip
  move &IMSS.COUNTRY     to $IMSSCountry
  let $IMSSer           = &IMSS.ESTABID


FROM  PS_ESTAB_TBL IMSS

WHERE IMSS.ESTABID = $Reg_Patronal
  AND IMSS.EFFDT = (SELECT MAX(EFFDT)
                   FROM   PS_ESTAB_TBL
                   WHERE  ESTABID = IMSS.ESTABID)
end-SELECT


end-procedure

!***************************************************
! Procedure Get-StateName                          *
!***************************************************
begin-procedure Get-StateName
begin-Select
ST.DESCR      &STDESCRIP
FROM PS_STATE_NAMES_TBL  ST
WHERE ST.COUNTRY = 'MEX'
AND   ST.STATE   = $state1
end-select
end-Procedure

!***************************************************
! Procedure TO_UPPER                               *
!***************************************************
begin-procedure TO_UPPER(:$in)
  let $in = rtrim($in,' ')
  let #c = 1
  let $cad = ''
  
  Let $Name_In = $in  
  
  while #c <= length($in)

    let $Found = 'N'
    let $Test_Char = Substr($Name_In, #c, 1)
    let #Test_Code = ascii(Upper($Test_Char))
    if (#Test_Code > 64
    and #Test_Code < 91)
      let $cad  = $cad  || Upper($Test_Char)
      let $Found = 'Y'
    end-if
     
   if ($Found = 'N')  
     let $char_aux = substr($in,#c,1)
     let $char = lower($char_aux)     
     evaluate $char
        when = 'á'
        when = 'â'
        when = 'ä'
        when = 'à'
        when = 'å'
           let $let = 'A'
           let $cad = $cad || $let             
           break
        when = 'é'
        when = 'ê'
        when = 'ë'
        when = 'è'
           let $let = 'E'
           let $cad = $cad || $let             
           break
        when = 'í'
        when = 'ï'
        when = 'î'
        when = 'ì'
           let $let = 'I'
           let $cad = $cad || $let             
           break
        when = 'ô'
        when = 'ö'
        when = 'ò'
        when = 'ó'
           let $let = 'O'
           let $cad = $cad || $let             
           break
        when = 'ü'
        when = 'û'
        when = 'ù'
        when = 'ú'
           let $let = 'U'
           let $cad = $cad || $let             
           break
        when = 'ñ'
           let $let = 'N'
           let $cad = $cad || $let             
           break
        when = ' '                         
           let $let = ' '
           let $cad = $cad || $let  
        when-other
           break
     end-evaluate
    end-if
     let #c = #c + 1
  end-while
  let $in = $cad
  let $cad = ''
end-procedure

!**************************************
begin-procedure Get-Person
!show '** Get-Person **'
!**************************************
Let $Day_Birth   = ''
Let $Month_Birth = ''
Let $Year_Birth  = ''
let $BPlace      = ''

begin-SELECT 
PS.BIRTHPLACE
PS.BIRTHDATE
PS.BIRTHSTATE
ST.DESCR  
  let $BPlace = &ST.DESCR 
  do to_upper($BPlace)
  
  let $BirthDt = &PS.BIRTHDATE
  do Convert-To-DTU-Date($BirthDt,$BirthDt)
  
  Let $Day_Birth   = substr($BirthDt,9,2)
  Let $Month_Birth = substr($BirthDt,6,2)
  Let $Year_Birth  = substr($BirthDt,1,4)
  
FROM  PS_PERSON         PS   
     ,PS_STATE_TBL  ST
WHERE PS.EMPLID  = $Emplid
  AND ST.COUNTRY = PS.BIRTHCOUNTRY
  AND ST.STATE   = PS.BIRTHSTATE
end-SELECT
end-procedure Get-Person
 !*********************************************************
Begin-Procedure Obtain_Number($in, :$Ext, :$Int)
!*********************************************************
  let $in = rtrim($in,' ')
  let #c = 1
  let $cad = ''
  Let $Ext = ''
  Let $Int = ''
  
  Let $Name_In = $in  
  #debug show 'Obtain_Number ' $in 
  while #c <= length($in)
  
    let $Found = 'N'
    let $Test_Char = Substr($Name_In, #c, 1)
    let #Test_Code = ascii(Upper($Test_Char))
    
    If #Test_Code = 32  and $cad <> ''
      Let $Ext = $cad
      Let $cad = ''
    End-If  
      
    If (#Test_Code > 47 and #Test_Code < 58) 
      Let $cad  = $cad  || $Test_Char
    End-if 
    
    let #c = #c + 1
  end-while
  
  If $Ext <> '' 
   let $Int = $cad
  Else
   Let $Ext = $cad
  End-if 
  
  let $cad = ''
End-procedure
    

!***************************************************************!
! Function: Shedule_Reduce_Week
!***************************************************************!
BEGIN-PROCEDURE Shedule_Reduce_Week
Let #Duracion = 0
Let #Days = 0
Let $Working_Days = ''
!Gets the schedule assigned to the employee
BEGIN-SELECT 
DEFN.EFFDT
DEFN.DAYNUM
    
    LET #Duracion = 1
    Let #Days = &DEFN.DAYNUM - 1
    
    do Convert-To-DTU-Date(&DEFN.EFFDT, $tmpDate)
    do dtu-add-days($tmpDate, #days, $tmpDate)
    do dtu-dayofweek$($tmpDate, $WeekDay)
    
    If #Days > 0
      Let $Working_Days = $Working_Days || ', ' || $WeekDay
    Else
      Let $Working_Days = $WeekDay
    End-If
    
FROM PS_SCH_ASSIGN ASG, PS_SCH_DEFN_DTL DEFN
 WHERE ASG.EFFDT = (SELECT MAX(ASG_ED.EFFDT) 
                    FROM PS_SCH_ASSIGN ASG_ED 
                      WHERE ASG.EMPLID = ASG_ED.EMPLID 
                        AND ASG.EMPL_RCD = ASG_ED.EMPL_RCD 
                        AND ASG_ED.EFFDT <= $ENDDT) 
   AND DEFN.SETID = ASG.SETID 
   AND DEFN.SCH_ADHOC_IND = ASG.SCH_ADHOC_IND 
   AND DEFN.SCHEDULE_ID = ASG.SCHEDULE_ID  
   AND DEFN.EFFDT = (SELECT MAX(DEFN_ED.EFFDT) 
                      FROM PS_SCH_DEFN_DTL DEFN_ED 
                        WHERE DEFN.SETID = DEFN_ED.SETID 
                          AND DEFN.SCH_ADHOC_IND = DEFN_ED.SCH_ADHOC_IND
                          AND DEFN.SCHEDULE_ID = DEFN_ED.SCHEDULE_ID) 
   AND DEFN.OFFDAY_IND = 'N' 
   AND ASG.EMPLID   = $Emplid 
   AND ASG.EMPL_RCD = #Empl_Rcd
END-SELECT   

      IF #Duracion = 0 
        !Gets the schedule assigned to the employee's pay group
BEGIN-SELECT 
DFN.EFFDT
DFN.DAYNUM

    Let #Days = &DFN.DAYNUM - 1
    
    do Convert-To-DTU-Date(&DFN.EFFDT, $tmpDate)
    do dtu-add-days($tmpDate, #days, $tmpDate)
    do dtu-dayofweek$($tmpDate, $WeekDay)
    
    If #Days > 0
      Let $Working_Days = $Working_Days || ', ' || $WeekDay
    Else
      Let $Working_Days = $WeekDay
    End-If
    
FROM PS_GP_PYGRP_DTL PYG, PS_SCH_DEFN_DTL DFN
 WHERE PYG.EFFDT =(SELECT MAX(PYG_ED.EFFDT) 
                    FROM PS_GP_PYGRP_DTL PYG_ED 
                      WHERE PYG.GP_PAYGROUP = PYG_ED.GP_PAYGROUP 
                      AND PYG_ED.EFFDT <= $ENDDT) 
   AND DFN.SETID = PYG.SETID 
   AND DFN.SCHEDULE_ID = PYG.SCHEDULE_ID 
   AND DFN.EFFDT = (SELECT MAX(DFN_ED.EFFDT) 
                    FROM PS_SCH_DEFN_DTL DFN_ED 
                      WHERE DFN.SETID = DFN_ED.SETID 
                        AND DFN.SCH_ADHOC_IND = DFN_ED.SCH_ADHOC_IND 
                        AND DFN.SCHEDULE_ID = DFN_ED.SCHEDULE_ID)
   AND DFN.OFFDAY_IND = 'N'                          
   AND PYG.GP_PAYGROUP = $Gp_Paygroup
END-SELECT   
    END-IF

let $Working_Days = UPPER($Working_Days)    
#debug show '$Working_Days:  ' $Working_Days    
END-PROCEDURE !Shedule_Reduce_Week    

!**************************************
begin-procedure Process-XML-Tag-Content
!**************************************

   let $xml_content   = replace($xml_content, '&',  '&amp;')
   let $xml_content   = replace($xml_content, '"',  '&quot;')
   let $xml_content   = replace($xml_content, '<',  '&lt;')
   let $xml_content   = replace($xml_content, '>',  '&gt;')
   let $xml_content   = replace($xml_content, '''', '&apos;')


   let $xml_content   = ltrim($xml_content, ' ')
   let $xml_content   = rtrim($xml_content, ' ')

   let $xml_output_line = ''
   let $xml_output_line = $xml_cntrl_begin || $xml_tag || $xml_cntrl_end  || $xml_content
                || $xml_cntrl_begin || $xml_tag_end || $xml_tag || $xml_cntrl_end
                
                            
   #debug show '[$xml_tag:]            ' $xml_tag
   #debug show '[$xml_output_line:]    ' $xml_output_line
   
   write #FNum from $xml_output_line

end-procedure

!**********************************************************
begin-procedure Process-XML-Tag-Beg
#debug show 'Procedure: Process-XML-Tag-Beg'
!**********************************************************
   let $xml_output_line = ''
   let $xml_output_line = $xml_cntrl_begin || $xml_tag|| $xml_cntrl_end
   
   #debug show '[$xml_output_line:]    ' $xml_output_line
   write #FNum from $xml_output_line
   
   #debug show '[$xml_output_line:]    ' $xml_output_line

end-procedure

!****************************************************************
begin-procedure Process-XML-Tag-End
#debug show 'Procedure: Process-XML-Tag-End'
!****************************************************************
  let $xml_output_line = ''
  let $xml_output_line = $xml_cntrl_begin || $xml_tag_end || $xml_tag|| $xml_cntrl_end
  write #FNum from $xml_output_line
  
  #debug show '[$xml_output_line:]    ' $xml_output_line

end-procedure


!**************************************
begin-procedure Open-XML-File($FileName, #FNum, :#FStat, :$openXMLFile)
#debug show 'Procedure: Open-XML-File'
!**************************************
   
   if $openXMLFile = 'N'
 
   open $FileName as #FNum for-writing
         record=200:vary status = #FStat
         
   if #FStat = 0
      let $xml_output_line = '<?xml version="1.0" encoding="iso-8859-1"?>'       
      write #FNum from $xml_output_line
      write #FNum from '<start>'
   end-if
  
   Let $openXMLFile = 'Y'
   
   end-if
   
  
end-procedure

!**************************************
begin-procedure Insert-XML-RunControl
#debug show 'Procedure: Insert-XML-RunControl'
#debug show '[$FileName:]              ' $FileName
#debug show '[$Prcs_OprID], [$Prcs_Run_Cntl_ID], [#prcs_process_instance]'    
#debug show '[$ReportID]               ' $ReportID             
#debug show '[#prcs_job_instance]      ' #prcs_job_instance    
#debug show '[$TemplateID:]            ' $TemplateID
#debug show '[$FileName]               ' $FileName             
#debug show '[$ReportDefn]             ' $ReportDefn
#debug show '[#Copies:]                ' #Copies
#debug show '-'
!**************************************

let $err-statement1 = 'Procedure: Insert-XML-RunControl'
let $err-statement2 = 'INSERT'

begin-SQL on-error=Error-Display
INSERT INTO PS_GPMX_XML_RCTL
(OPRID
,RUN_CNTL_ID
,PROCESS_INSTANCE
,PRCSNAME
,JOBINSTANCE
,TMPLDEFN_ID
,GPMX_FILE_NAME
,REPORT_DEFN_ID
,GPMX_COPIES)
Values
($Prcs_OprID
,$Prcs_Run_Cntl_ID
,#prcs_process_instance
,$ReportID
,#prcs_job_instance
,$TemplateID
,$FileName
,$ReportDefn
,1)
end-SQL
end-procedure Insert-XML-RunControl

!**************************************
begin-procedure Delete-XML-RunControl
#debug show 'Procedure: Delete-XML-RunControl'
!**************************************

let $err-statement1 = 'Procedure: Delete-XML-RunControl'
let $err-statement2 = 'DELETE '


begin-SQL on-error=Error-Display
DELETE FROM PS_GPMX_XML_RCTL
WHERE OPRID = $Prcs_OprID
AND   RUN_CNTL_ID = $Prcs_Run_Cntl_ID
end-SQL

do Commit-Transaction


end-procedure Delete-XML-RunControl

!**************************************
begin-procedure Delete-RunControl
#debug show 'Procedure: Delete-RunControl'
!**************************************

let $err-statement1 = 'Procedure: Delete-RunControl'
let $err-statement2 = 'DELETE '

IF #count_TRCT = 0 
  begin-SQL on-error=Error-Display
    DELETE FROM PS_GPMX_XML_RCTL
    WHERE OPRID = $Prcs_OprID
    AND   RUN_CNTL_ID = $Prcs_Run_Cntl_ID
    AND   TMPLDEFN_ID = $TemplateID
  end-SQL
  
end-if

end-procedure Delete-RunControl

begin-procedure Close-File
if $openXMLFile = 'Y'
   let $xml_tag = 'start'    
   Do Process-XML-Tag-End
   Do Close-XML-File(#Fnum)
 end-if

end-procedure

begin-procedure Close-XML-File(#Fnum)

   close #Fnum

end-procedure Close-XML-File

!**************************************
Begin-Procedure Create-Employee-XML
#debug show 'Procedure: Create-Employee-XML **'
!**************************************
   let $xml_tag = 'EMPLID'
   let $xml_content = $Emplid || ' ' || edit(to_char(#emplRcd), '99')
   do Process-XML-Tag-Content

   let #TagCount = 1
   let #TagField = 0
 
   let $xml_tag = 'Field' || ltrim(edit(#TagCount,'b999'), ' ')
   let $xml_content = $Currt_Dy
   do Process-XML-Tag-Content

   
   let #TagCount = #TagCount + 1

   let $xml_tag = 'Field' || ltrim(edit(#TagCount,'b999'), ' ')
   let $xml_content = $Currt_Mn
   do Process-XML-Tag-Content
   
   let #TagCount = #TagCount + 1
  
   let $xml_tag = 'Field' || ltrim(edit(#TagCount,'b999'), ' ')
   let $xml_content = $Currt_Yr
   do Process-XML-Tag-Content
   
  let #TagCount = #TagCount + 1
  
  if rtrim($IMSS_ID , ' ') <> '' 
   let $xml_tag = 'Field' || ltrim(edit(#TagCount,'b999'), ' ')
   let $xml_content = ltrim($IMSS_ID ,' ')
   do Process-XML-Tag-Content
  End-if
  
   
  let #TagCount = #TagCount + 1
  
  If rtrim($CURP , ' ') <> '' 
   let $xml_tag = 'Field' || ltrim(edit(#TagCount,'b999'), ' ')
   let $xml_content = ltrim($CURP  ,' ')
   do Process-XML-Tag-Content
  End-if 
   
   
  let #TagCount = #TagCount + 1
   
  if rtrim($RFC , ' ') <> '' 
   let $xml_tag = 'Field' || ltrim(edit(#TagCount,'b999'), ' ')
   let $xml_content = ltrim($RFC ,' ')
   do Process-XML-Tag-Content
  End-if
   
  let #TagCount = #TagCount + 1
 
  if rtrim($Name , ' ') <> ''  
      let $xml_tag = 'Field' || ltrim(edit(#TagCount,'b999'), ' ')
     let $xml_content = ltrim($Name  ,' ')
     do Process-XML-Tag-Content
  end-if  
   
  let #TagCount = #TagCount + 1
 
  if rtrim($Paternalname, ' ') <> ''  
      let $xml_tag = 'Field' || ltrim(edit(#TagCount,'b999'), ' ')
      let $xml_content = ltrim($Paternalname,' ')
      do Process-XML-Tag-Content
  end-if
   
  let #TagCount = #TagCount + 1
    
  if rtrim($Maternalname, ' ') <> ''  
      let $xml_tag = 'Field' || ltrim(edit(#TagCount,'b999'), ' ')
      let $xml_content = ltrim($Maternalname,' ')
      do Process-XML-Tag-Content
  end-if  
 
 let #TagCount = #TagCount + 1
    
  if rtrim($Day_Birth, ' ') <> ''  
      let $xml_tag = 'Field' || ltrim(edit(#TagCount,'b999'), ' ')
      let $xml_content = ltrim($Day_Birth,' ')
      do Process-XML-Tag-Content
  end-if 
  
  let #TagCount = #TagCount + 1
    
  if rtrim($Month_Birth, ' ') <> ''  
      let $xml_tag = 'Field' || ltrim(edit(#TagCount,'b999'), ' ')
      let $xml_content = ltrim($Month_Birth,' ')
      do Process-XML-Tag-Content
  end-if 
  
  let #TagCount = #TagCount + 1
    
  if rtrim($Year_Birth, ' ') <> ''  
      let $xml_tag = 'Field' || ltrim(edit(#TagCount,'b999'), ' ')
      let $xml_content = ltrim($Year_Birth,' ')
      do Process-XML-Tag-Content
  end-if 
  
  let #TagCount = #TagCount + 1
    
  if rtrim($BPlace, ' ') <> ''  
      let $xml_tag = 'Field' || ltrim(edit(#TagCount,'b999'), ' ')
      let $xml_content = ltrim($BPlace,' ')
      do Process-XML-Tag-Content
  end-if 
  
  let #TagCount = #TagCount + 1
    
  if rtrim($Occupation, ' ') <> ''  
      let $xml_tag = 'Field' || ltrim(edit(#TagCount,'b999'), ' ')
      let $xml_content = ltrim($Occupation,' ')
      do Process-XML-Tag-Content
  end-if 
  
  
  let #TagCount = #TagCount + 1
    
  if rtrim($Working_Days, ' ') <> ''  
      let $xml_tag = 'Field' || ltrim(edit(#TagCount,'b999'), ' ')
      let $xml_content = ltrim($Working_Days,' ')
      do Process-XML-Tag-Content
  end-if 
  
  
  let #TagCount = #TagCount + 1
    
  if rtrim(to_char(#IDSPrev), ' ') <> ''  
      let $xml_tag = 'Field' || ltrim(edit(#TagCount,'b999'), ' ')
      let $xml_content = ltrim(edit(#IDSPrev,'b999,999,999,999.99'), ' ')
      do Process-XML-Tag-Content
  end-if 
  
  
  let #TagCount = #TagCount + 1
    
  if rtrim(to_char(#IDSCurr), ' ') <> ''  
      let $xml_tag = 'Field' || ltrim(edit(#TagCount,'b999'), ' ')
      let $xml_content = ltrim(edit(#IDSCurr,'b999,999,999,999.99'), ' ')
      do Process-XML-Tag-Content
  end-if 
  
  
  let #TagCount = #TagCount + 1
    
  if rtrim($Chg-da , ' ') <> ''  
      let $xml_tag = 'Field' || ltrim(edit(#TagCount,'b999'), ' ')
      let $xml_content = ltrim($Chg-da ,' ')
      do Process-XML-Tag-Content
  end-if 
  
  
  let #TagCount = #TagCount + 1
    
  if rtrim($Chg-mo, ' ') <> ''  
      let $xml_tag = 'Field' || ltrim(edit(#TagCount,'b999'), ' ')
      let $xml_content = ltrim($Chg-mo,' ')
      do Process-XML-Tag-Content
  end-if 
  
  
  let #TagCount = #TagCount + 1
    
  if rtrim($Chg-yy, ' ') <> ''  
      let $xml_tag = 'Field' || ltrim(edit(#TagCount,'b999'), ' ')
      let $xml_content = ltrim($Chg-yy,' ')
      do Process-XML-Tag-Content
  end-if 
  
  
   
  !********* Company info*****************
  let #TagCount = #TagCount + 1
    
  if rtrim($Reg_Patronal, ' ') <> ''  
      let $xml_tag = 'Field' || ltrim(edit(#TagCount,'b999'), ' ')
      let $xml_content = ltrim($Reg_Patronal,' ')
      do Process-XML-Tag-Content
  end-if 
  
  
  let #TagCount = #TagCount + 1 !CURP just for person
  
  let #TagCount = #TagCount + 1
    
  if rtrim($Company_RFC, ' ') <> ''  
      let $xml_tag = 'Field' || ltrim(edit(#TagCount,'b999'), ' ')
      let $xml_content = ltrim($Company_RFC,' ')
      do Process-XML-Tag-Content
  end-if 
  
  let #TagCount = #TagCount + 1
    
  if rtrim($CompanyName, ' ') <> ''  
      let $xml_tag = 'Field' || ltrim(edit(#TagCount,'b999'), ' ')
      let $xml_content = ltrim($CompanyName,' ')
      do Process-XML-Tag-Content
  end-if 
  
  
  let #TagCount = #TagCount + 1
    
  if rtrim($IMSSZip, ' ') <> ''  
      let $xml_tag = 'Field' || ltrim(edit(#TagCount,'b999'), ' ')
      let $xml_content = ltrim($IMSSZip,' ')
      do Process-XML-Tag-Content
  end-if 
  
  
  let #TagCount = #TagCount + 1
    
  if rtrim($IMSSStreet1, ' ') <> ''  
      let $xml_tag = 'Field' || ltrim(edit(#TagCount,'b999'), ' ')
      let $xml_content = ltrim($IMSSStreet1,' ')
      do Process-XML-Tag-Content
  end-if 
  
  
  let #TagCount = #TagCount + 1
    
  if rtrim($CompnyNum_Ext, ' ') <> ''  
      let $xml_tag = 'Field' || ltrim(edit(#TagCount,'b999'), ' ')
      let $xml_content = ltrim($CompnyNum_Ext,' ')
      do Process-XML-Tag-Content
  end-if 
  
  
  let #TagCount = #TagCount + 1
    
  if rtrim($CompnyNum_Int, ' ') <> ''  
      let $xml_tag = 'Field' || ltrim(edit(#TagCount,'b999'), ' ')
      let $xml_content = ltrim($CompnyNum_Int,' ')
      do Process-XML-Tag-Content
  end-if 
  
  let #TagCount = #TagCount + 1
    
  if rtrim($IMSSStreet3, ' ') <> ''  
      let $xml_tag = 'Field' || ltrim(edit(#TagCount,'b999'), ' ')
      let $xml_content = ltrim($IMSSStreet3,' ')
      do Process-XML-Tag-Content
  end-if 
  
  
  let #TagCount = #TagCount + 1 !Localidad Cia
  
  let #TagCount = #TagCount + 1
    
  if rtrim($IMSSStreet4 , ' ') <> ''  
      let $xml_tag = 'Field' || ltrim(edit(#TagCount,'b999'), ' ')
      let $xml_content = ltrim($IMSSStreet4 ,' ')
      do Process-XML-Tag-Content
  end-if 
  
  
  let #TagCount = #TagCount + 1
    
  if rtrim($IMSSState, ' ') <> ''  
      let $xml_tag = 'Field' || ltrim(edit(#TagCount,'b999'), ' ')
      let $xml_content = ltrim($IMSSState,' ')
      do Process-XML-Tag-Content
  end-if 
  
  
  let #TagField = #TagField + 1
    
  if rtrim($Male, ' ') <> ''  
      let $xml_tag = 'Fd' || ltrim(edit(#TagField,'b999'), ' ')
      let $xml_content = ltrim($Male,' ')
      do Process-XML-Tag-Content
  end-if 
  
  
  let #TagField = #TagField + 1
    
  if rtrim($Female, ' ') <> ''  
      let $xml_tag = 'Fd' || ltrim(edit(#TagField,'b999'), ' ')
      let $xml_content = ltrim($Female,' ')
      do Process-XML-Tag-Content
  end-if 
  
  
  let #TagField = #TagField + 1
    
  if rtrim($Wrkr_Permant, ' ') <> ''  
      let $xml_tag = 'Fd' || ltrim(edit(#TagField,'b999'), ' ')
      let $xml_content = ltrim($Wrkr_Permant,' ')
      do Process-XML-Tag-Content
  end-if 
  
  
  let #TagField = #TagField + 1
    
  if rtrim($Wrkr_Eventual, ' ') <> ''  
      let $xml_tag = 'Fd' || ltrim(edit(#TagField,'b999'), ' ')
      let $xml_content = ltrim($Wrkr_Eventual,' ')
      do Process-XML-Tag-Content
  end-if 
  
  
  let #TagField = #TagField + 1
    
  if rtrim($Wrkr_Constr, ' ') <> ''  
      let $xml_tag = 'Fd' || ltrim(edit(#TagField,'b999'), ' ')
      let $xml_content = ltrim($Wrkr_Constr,' ')
      do Process-XML-Tag-Content
  end-if 
  
  
  let #TagField = #TagField + 1
    
  if rtrim($Salary_Fix, ' ') <> ''  
      let $xml_tag = 'Fd' || ltrim(edit(#TagField,'b999'), ' ')
      let $xml_content = ltrim($Salary_Fix,' ')
      do Process-XML-Tag-Content
  end-if 
  
  
  let #TagField = #TagField + 1
    
  if rtrim($Salary_Var, ' ') <> ''  
      let $xml_tag = 'Fd' || ltrim(edit(#TagField,'b999'), ' ')
      let $xml_content = ltrim($Salary_Var,' ')
      do Process-XML-Tag-Content
  end-if 


  let #TagField = #TagField + 1
    
  if rtrim($Salary_Mix, ' ') <> ''  
      let $xml_tag = 'Fd' || ltrim(edit(#TagField,'b999'), ' ')
      let $xml_content = ltrim($Salary_Mix,' ')
      do Process-XML-Tag-Content
  end-if 

  
  
End-Procedure Create-Employee-XML
!*******************************************************************************
#Include 'reset.sqc'     !Reset Printer procedure
#Include 'curdttim.sqc'  !Get-Current-DateTime procedure
#include 'datetime.sqc'
#include 'number.sqc'
#Include 'readxlat.sqc'  !Read-Translate-Table procedure
#include 'prcsapi.sqc'   !Process API
#include 'stdapi.sqc'    !Update Process API
#include 'datemath.sqc'  !Routines for date
#include 'prcslng.sqc'   !Process Language
#include 'gpmxnotf.sqc'  !Get-Run-Control procedure
#include 'gpmxrtn1.sqc'
#include 'getmonnm.sqc'  !Get-Month-name
#include 'getcomex.sqc'  !Get-Company-data-Mex
#include 'sqrtrans.sqc'  !Translate SQR strings
#include 'stderror.sqc' 
