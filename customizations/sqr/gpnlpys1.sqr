!***********************************************************************
!  GPNLPYS1:   Payslip for GP Netherlands                              *
!***********************************************************************
!***********************************************************************
!                                                                      *
!                                                                      *
!                                                                      *
! This software and related documentation are provided under a         *
! license agreement containing restrictions on use and                 *
! disclosure and are protected by intellectual property                *
! laws. Except as expressly permitted in your license agreement        *
! or allowed by law, you may not use, copy, reproduce,                 *
! translate, broadcast, modify, license, transmit, distribute,         *
! exhibit, perform, publish or display any part, in any form or        *
! by any means. Reverse engineering, disassembly, or                   *
! decompilation of this software, unless required by law for           *
! interoperability, is prohibited.                                     *
! The information contained herein is subject to change without        *
! notice and is not warranted to be error-free. If you find any        *
! errors, please report them to us in writing.                         *
!                                                                      *
!
! Copyright (C) 1988, 2013, Oracle and/or its affiliates.              *
! All Rights Reserved.                                                 *
!----------------------------------------------------------------------
!
!          $Date:  2013/01/18:03:56:59                                 !
!       $Release:  HR92                                                !
!      $Revision:  103                                                 !
!                                                                      *
!***********************************************************************
!***********************************************************************
!Comments:   Put your comments here about what the program does        *
!            and be sure to sign and date your entry.  Any additions   *
!            to this section should be similarly dated with some       *
!            space between comments for readability.                   *
!            (JLyons 10-13-98)                                         *
!                                                                      *
!************************************************************************
!************************************************************************
! T A B L E S / V I E W S    R E F E R E N C E D                        *
!************************************************************************
! TABLE/VIEW NAME:       ALIAS:          USAGE:                         *
!                                                                       *
! <Table Name>          <A,B,C...>      <Read,Updt,Del,Select>          *
!                                                                       *
!************************************************************************


#include 'setenv.sqc'    !Set environment
#define MaxRows        130
#define MaxRowsAcu     10
#define bodyline       2
#define maxbodyline    48
#define acumline       61



!************************************************************************
! Report Section
!************************************************************************
begin-report
  do Init-Report
  do Process-Main
  if (#Total_employees > 0)  ! only when ee's have been processed
    do GP-ePay-Control ! if ePay installed have a control row inserted.
  end-if
  do Reset
  do Stdapi-Term
end-report

!************************************************************************
! Procedure Init-Report
!************************************************************************
begin-procedure Init-Report
  do Init-DateTime
  do Init-Number
  do Get-Current-DateTime
  do Stdapi-Term

  alter-locale
   thousand-separator = '.'
   decimal-separator  = ','

   move '-' to $ReportDDelimiter
   move '1' to $ReportYear4

   #define HEADER-SIZE     20
   #define BODY-SIZE     1000
   #define FOOTER-SIZE    240
   #define BANK-SIZE       16
   #define MSG-SIZE       300
   #define ACC-SIZE       200


  CREATE-ARRAY NAME = Header-Data SIZE = {HEADER-SIZE}

    FIELD=descr_comp_h:Char
    FIELD=address_c_h:Char
    FIELD=city_postal_c_h:Char
    FIELD=empl_name_h:Char
    FIELD=empl_address_h:Char
    FIELD=empl_city_postal_h:Char
    FIELD=Employee_Lit:Char
    FIELD=Emplid_h:Char
    FIELD=Birth_Date_Lit:Char
    FIELD=Birth_Dt_h:Char
    FIELD=Hire_Date_Lit:Char
    FIELD=Hire_dt_h:Char
    FIELD=Term_Date_Lit:Char
    FIELD=Term_dt_h:Char
    FIELD=Gender_Lit:Char
    FIELD=Sex_h:Char
    FIELD=Marital_St_Lit:Char
    FIELD=Marital_St_h:Char
    FIELD=Tax_Credits_Lit:Char
    FIELD=tax_Credits_h:Char
    FIELD=Tax_table_Lit:Char
    FIELD=tax_table_h:Char
    FIELD=Percent_tax_Lit:Char
    FIELD=PCT_TAX_h:Number
    FIELD=Soc_Fiscal_Nbr_Lit:Char
    FIELD=National_id_h:Char
    FIELD=Period_Lit:Char
    FIELD=Period_dt_h:Char
    FIELD=Location_Lit:Char
    FIELD=Location_name_h:Char
    FIELD=Department_Lit:Char
    FIELD=DEPT_NAME_h:Char
    FIELD=Hours_Week_Lit:Char
    FIELD=std_hours_h:Number
    FIELD=Days_Week_Lit:Char
    FIELD=workdays_h:Number
    FIELD=Grade_Step_Lit:Char
    FIELD=grade_h:Char
    FIELD=Full_time_Lit:Char
    FIELD=comprate_h:Number
    FIELD=Part_time_Lit:Char
    FIELD=fte_h:Number
    FIELD=Min_Salary_Lit:Char
    FIELD=Min_Salary_h:Number
    FIELD=ZW_WW_WAO_ZFW_Lit:Char
    FIELD=ZW_WAO_WW_ZVW_LIT:Char
    FIELD=Socsec_value_h:Char
    FIELD=Payslip_lit:Char
    FIELD=Description_Lit:Char
    FIELD=Hour_unit_Lit:Char
    FIELD=Factor_Lit:Char
    FIELD=Percent_Lit:Char
    FIELD=Bases_Lit:Char
    FIELD=Regular_Lit:Char
    FIELD=Non_Regular_Lit:Char
    FIELD=Sum_Lit:Char


  CREATE-ARRAY NAME = Body-Line SIZE = {BODY-SIZE}
    FIELD=Ed_Type:Char
    FIELD=Pin_Num:Number
    FIELD=Description:Char
    FIELD=Unit:Number
    FIELD=Rate:Number
    FIELD=Base:Number
    FIELD=Percentage:Number
    FIELD=Earn_Ded:Number
    FIELD=Lines-Before:Number
    FIELD=Lines-After:Number
    FIELD=Retro_Type:Char
    FIELD=Separator:Char
    FIELD=Base_Print:Char
    FIELD=Pct_Print:Char
    FIELD=Factor_Print:Char
    FIELD=Unit_Print:Char
    FIELD=Reg_Col:Char
    FIELD=Regtot_Col:Char
    FIELD=Nreg_Col:Char
    FIELD=Nrgtot_Col:Char
    FIELD=Tot_Col:Char
    FIELD=Descr15:Char
    FIELD=Prnt-After:Char

  CREATE-ARRAY NAME = Body-Line-Retro SIZE = {BODY-SIZE}
    FIELD=Ed_Type_ret:Char
    FIELD=Pin_Num_ret:Number
    FIELD=Description_ret:Char
    FIELD=Unit_ret:Number
    FIELD=Rate_ret:Number
    FIELD=Base_ret:Number
    FIELD=Percentage_ret:Number
    FIELD=Earn_Ded_ret:Number
    FIELD=Lines-Before_ret:Number
    FIELD=Lines-After_ret:Number
    FIELD=Retro_Type_ret:Char
    FIELD=Separator_ret:Char
    FIELD=Base_Print_ret:Char
    FIELD=Pct_Print_ret:Char
    FIELD=Factor_Print_ret:Char
    FIELD=Unit_Print_ret:Char
    FIELD=Reg_Col_ret:Char
    FIELD=Regtot_Col_ret:Char
    FIELD=Nreg_Col_ret:Char
    FIELD=Nrgtot_Col_ret:Char
    FIELD=Tot_Col_ret:Char
    FIELD=Descr15_ret:Char
    FIELD=Prnt-After_ret:Char

  CREATE-ARRAY NAME = Bank-Line SIZE = {BANK-SIZE}
    FIELD=ACCOUNT_TYP_PYE:Char
    FIELD=Bank_Cd:Char
    FIELD=Branch_Ec_Cd:Char
    FIELD=Account_Ec_Id:Char
    FIELD=Check_Digit:Char
    FIELD=Bank_Amount:Number

  CREATE-ARRAY NAME = Msg-Line SIZE = {MSG-SIZE}
    FIELD=Message_lit:Char

  CREATE-ARRAY NAME = Accum-Line SIZE = {ACC-SIZE}
    FIELD=Descr_Acum:Char
    FIELD=Rslt_Acum:Number
    FIELD=Acum_Counter:Number
        FIELD=Column_Counter:Number


    move 'GPNLPYS1' to $ReportID
end-procedure


! This procedure is used to read language-dependent text from the database.
begin-procedure Report-Translation
  do Init_Report_Translation($ReportID,$language_cd)
  do Get_Field_Information ('GPNLPYS1',     'ACCUMULATIVE_LIT',    $ACCUMULATIVE_LIT,   #DW)
  do Get_Field_Information ('GPNLPYS1',     'BANK_ACCOUNT_LIT',    $BANK_ACCOUNT_LIT,   #DW)
  do Get_Field_Information ('GPNLPYS1',     'BASES_LIT',           $BASES_LIT,   #DW)
  do Get_Field_Information ('GPNLPYS1',     'BIRTH_DATE_LIT',      $BIRTH_DATE_LIT,   #DW)
  do Get_Field_Information ('GPNLPYS1',     'DAYS_WEEK_LIT',       $DAYS_WEEK_LIT,   #DW)
  do Get_Field_Information ('GPNLPYS1',     'DEPARTMENT_LIT',      $DEPARTMENT_LIT,   #DW)
  do Get_Field_Information ('GPNLPYS1',     'DESCRIPTION_LIT',     $DESCRIPTION_LIT,   #DW)
  do Get_Field_Information ('GPNLPYS1',     'EMPLOYEE_LIT',        $EMPLOYEE_LIT,   #DW)
  do Get_Field_Information ('GPNLPYS1',     'FACTOR_LIT',          $FACTOR_LIT,   #DW)
  do Get_Field_Information ('GPNLPYS1',     'FULL_TIME_LIT',       $FULL_TIME_LIT,   #DW)
  do Get_Field_Information ('GPNLPYS1',     'GENDER_LIT',          $GENDER_LIT,   #DW)
  do Get_Field_Information ('GPNLPYS1',     'GRADE_STEP_LIT',      $GRADE_STEP_LIT,   #DW)
  do Get_Field_Information ('GPNLPYS1',     'HIRE_DATE_LIT',       $HIRE_DATE_LIT,   #DW)
  do Get_Field_Information ('GPNLPYS1',     'HOURS_WEEK_LIT',      $HOURS_WEEK_LIT,   #DW)
  do Get_Field_Information ('GPNLPYS1',     'HOUR_UNIT_LIT',       $HOUR_UNIT_LIT,   #DW)
  do Get_Field_Information ('GPNLPYS1',     'LOCATION_LIT',        $LOCATION_LIT,   #DW)
  do Get_Field_Information ('GPNLPYS1',     'MARITAL_ST_LIT',      $MARITAL_ST_LIT,   #DW)
  do Get_Field_Information ('GPNLPYS1',     'MESSAGE_LIT',         $MESSAGE_LIT,   #DW)
  do Get_Field_Information ('GPNLPYS1',     'MIN_SALARY_LIT',      $MIN_SALARY_LIT,   #DW)
  do Get_Field_Information ('GPNLPYS1',     'NEXT_PAGE_LIT',       $NEXT_PAGE_LIT,   #DW)
  do Get_Field_Information ('GPNLPYS1',     'NON_REGULAR_LIT',     $NON_REGULAR_LIT,   #DW)
  do Get_Field_Information ('GPNLPYS1',     'PART_TIME_LIT',       $PART_TIME_LIT,   #DW)
  do Get_Field_Information ('GPNLPYS1',     'PAYSLIP_LIT',         $PAYSLIP_LIT,   #DW)
  do Get_Field_Information ('GPNLPYS1',     'PERCENT_LIT',         $PERCENT_LIT,   #DW)
  do Get_Field_Information ('GPNLPYS1',     'PERCENT_TAX_LIT',     $PERCENT_TAX_LIT,   #DW)
  do Get_Field_Information ('GPNLPYS1',     'PERIOD_LIT',          $PERIOD_LIT,   #DW)
  do Get_Field_Information ('GPNLPYS1',     'RECAL_LIT',           $RECAL_LIT,   #DW)
  do Get_Field_Information ('GPNLPYS1',     'REGULAR_LIT',         $REGULAR_LIT,   #DW)
  do Get_Field_Information ('GPNLPYS1',     'RETRO_LIT',           $RETRO_LIT,   #DW)
  do Get_Field_Information ('GPNLPYS1',     'SOC_FISCAL_NBR_LIT',  $SOC_FISCAL_NBR_LIT,   #DW)
  do Get_Field_Information ('GPNLPYS1',     'SUM_LIT',             $SUM_LIT,   #DW)
  do Get_Field_Information ('GPNLPYS1',     'TAX_CREDITS_LIT',     $TAX_CREDITS_LIT,   #DW)
  do Get_Field_Information ('GPNLPYS1',     'TAX_TABLE_LIT',       $TAX_TABLE_LIT,   #DW)
  do Get_Field_Information ('GPNLPYS1',     'TERM_DATE_LIT',       $TERM_DATE_LIT,   #DW)
  do Get_Field_Information ('GPNLPYS1',     'ZW_WW_WAO_ZFW_LIT',   $ZW_WW_WAO_ZFW_LIT,   #DW)
  do Get_Field_Information ('GPNLPYS1',     'ZW_WAO_WW_ZVW_LIT',   $ZW_WAO_WW_ZVW_LIT,   #DW)
end-procedure

!************************************************************************
! Procedure Main
!************************************************************************
begin-procedure Process-Main
    do Stdapi-Init
  let $language_cd = $CURR_LANGUAGE_CD
  If $PSOptions_Language_Cd <> $language_cd
     let #RelatedLabel = 1
  else
     let #RelatedLabel = 0
  End-If

  If $prcs_process_instance = ''
!     no prompt
  else
     Let #FiscalYear = 2006
     do Get-Values
     do GP-ePay-Init ! Initialize ePay Variables
     do Get-Order
  End-If

  do Init_Printer
  do Clean-Temp-Tables

end-procedure



!************************************************************************
!  Report Setup
!************************************************************************

begin-setup
#include 'ptset01.sqc'

DECLARE-IMAGE Logo
  type         = BMP-FILE
  image-size   = (15,5)
  source       = 'gpnlpys1.bmp'
end-declare

end-setup


!***********************************************************************
! Get-Values                                                           *
!                                                                      *
! Description: Load parameters from run control                        *
!                                                                      *
!***********************************************************************
begin-procedure Get-Values

let $sql-statement = 'GPNLPYS1.sqr,Get-Values'
BEGIN-SELECT ON-ERROR=SQL-Error
A.CAL_RUN_ID      &calrunid
A.GPNL_RC_RETRO      &rc_retro
A.GPNL_PAYSLIP_ID &payslip_id
A.GPNL_RC_PS_SIGN &payslip_sign

  let $calrunid     = rtrim(&calrunid, ' ')
  let $rc_retro     = rtrim(&rc_retro, ' ')
  let $payslip_id   = rtrim(&payslip_id, ' ')
  let $payslip_sign = rtrim(&payslip_sign, ' ')

  evaluate $payslip_sign
  when = '4'
     let $WhereClause_sign ='AND TMP.PIN_NET_VAL < 0 '
  when = '3'
     let $WhereClause_sign ='AND TMP.PIN_NET_VAL > 0 '
  when = '2'
     let $OrderClause_sign ='TMP.PIN_NET_VAL,'
  end-evaluate

  let $Header_Retro = 'N'
  evaluate $rc_retro
  when = 'N'
      let $whereclause = ' AND V.CAL_ID = ' || '''' || $CAL_ID || ''''
      let $whereclause = ' AND V.DESCR15 = ' || '''CURRENT'''
      let $retro-sw = 'N'
  when = 'O'
      let $whereclause = ' AND V.SRC_CAL_RUN_ID = ' || '''' || $CALRUNID || ''''
      let $whereclause = $whereclause || ' AND V.DESCR15 = ' || '''RETRO'''
      let $retro-sw = 'Y'
      let $Header_Retro = 'Y'
  when = 'Y'
      let $whereclause = ' AND V.SRC_CAL_RUN_ID = ' || '''' || $CALRUNID || ''''
      let $retro-sw = 'A'
      let $Header_Retro = 'Y'
  end-evaluate

FROM PS_GPNL_RC_PAYSLIP A
WHERE OPRID = $prcs_oprid
   AND RUN_CNTL_ID = $prcs_run_cntl_id

END-SELECT
end-procedure


!************************************************************************
! Get-order
!************************************************************************
begin-procedure Get-Order

ALTER-PRINTER
point-size=7.2

let $sql-statement = 'GPNLPYS1.sqr,Select,Get-Order '

BEGIN-SELECT ON-ERROR=SQL-Error  LOOPS = 1

TMP.JOBINSTANCE         &TMPJOBINSTANCE
TMP.GPNL_PAYSLIP_ID     &TMP_GPNL_PAYSLIP_ID
A.GPNL_FIELDNAME1       &GPNL_FIELDNAME1
A.GPNL_PYSL_SK_ORD1      &GPNL_PYSL_SK_ORD1
A.GPNL_FIELDNAME2       &GPNL_FIELDNAME2
A.GPNL_PYSL_SK_ORD2      &GPNL_PYSL_SK_ORD2
A.GPNL_FIELDNAME3       &GPNL_FIELDNAME3
A.GPNL_PYSL_SK_ORD3      &GPNL_PYSL_SK_ORD3
A.GPNL_FIELDNAME4       &GPNL_FIELDNAME4
A.GPNL_PYSL_SK_ORD4      &GPNL_PYSL_SK_ORD4
A.GPNL_FIELDNAME5       &GPNL_FIELDNAME5
A.GPNL_PYSL_SK_ORD5      &GPNL_PYSL_SK_ORD5

  let $Field1 =  rtrim(&GPNL_FIELDNAME1 , ' ')
  let $Field2 =  rtrim(&GPNL_FIELDNAME2 , ' ')
  let $Field3 =  rtrim(&GPNL_FIELDNAME3 , ' ')
  let $Field4 =  rtrim(&GPNL_FIELDNAME4 , ' ')
  let $Field5 =  rtrim(&GPNL_FIELDNAME5 , ' ')


  let $OrderByclause  = 'ORDER BY '
  If isblank($orderclause_sign)
    If not(isblank($Field1))
       let $OrderByclause =  $OrderByclause  || 'JOB.' || $Field1 || ' ' || rtrim(&GPNL_PYSL_SK_ORD1 , ' ') || ','
       let $Selectclause1  = 'JOB.' || $Field1
    End-If
    If not(isblank($Field2))
       let $OrderByclause =  $OrderByclause  || 'JOB.' || $Field2 || ' ' || rtrim(&GPNL_PYSL_SK_ORD2 , ' ') || ','
       let $Selectclause2  = 'JOB.' || $Field2
    End-If
    If not(isblank($Field3))
       let $OrderByclause =  $OrderByclause  || 'JOB.' || $Field3 || ' ' || rtrim(&GPNL_PYSL_SK_ORD3 , ' ') || ','
       let $Selectclause3  = 'JOB.' || $Field3
    End-If
    If not(isblank($Field4))
       let $OrderByclause =  $OrderByclause  || 'JOB.' || $Field4 || ' ' || rtrim(&GPNL_PYSL_SK_ORD4 , ' ') || ','
       let $Selectclause4  = 'JOB.' || $Field4
    End-If
    If not(isblank($Field5))
       let $OrderByclause =  $OrderByclause  || 'JOB.' || $Field5 || ' ' || rtrim(&GPNL_PYSL_SK_ORD5 , ' ') || ','
       let $Selectclause5  = 'JOB.' || $Field5
    End-If
  else
     let $OrderByclause =  $OrderByclause || 'TMP.PIN_NET_VAL, '
  End-If

   let #TMPJOBINSTANCE = &TMPJOBINSTANCE
   do GET-TMP

FROM PS_GPNL_PSLIP_TMP TMP,
     PS_GPNL_PSLIP_SKEY  A
    WHERE TMP.CAL_RUN_ID =  $calrunid
     AND TMP.JOBINSTANCE = (SELECT MAX(JOBINSTANCE)
                           FROM PS_GPNL_PSLIP_TMP
                           WHERE CAL_RUN_ID =  $calrunid)
     AND TMP.GPNL_PAYSLIP_ID = A.GPNL_PAYSLIP_ID
 AND TMP.GPNL_PAYSLIP_ID = $payslip_id
ORDER BY TMP.GPNL_PAYSLIP_ID

END-SELECT

end-procedure


!************************************************************************
! Get-Tmp
!************************************************************************
begin-procedure Get-Tmp
let #Total_employees = 0
let $Prev-Emplid     =  '----'
let #PreV-SEG-NUM    =  9999
let #Prev-Empl-Rcd   =  9999
let #Header-Data-Row =  0
let #Body-Data-Row   =  0
let #Footer-Data-Row =  0
let #Bank-Data-Row   =  0
let #Msg-Data-Row    =  0
let #Accum-Data-Row  =  0
let #I_retro         =  0
let #ePslp_id_nbr    =  0


let $sql-statement = 'GPNLPYS1.sqr,Select,Get-Tmp'
BEGIN-SELECT ON-ERROR=SQL-Error

[$Selectclause1]             &D1=Char
[$Selectclause2]             &D2=Char
[$Selectclause3]             &D3=Char
[$Selectclause4]             &D4=Char
[$Selectclause5]             &D5=Char

TMP.PIN_NET_VAL               &pin_net_val
TMP.EMPLID                    &EMPLID
TMP.EMPL_RCD                  &EMPL_RCD
TMP.GP_PAYGROUP               &GP_PAYGROUP
TMP.CAL_ID                    &CAL_ID
TMP.RSLT_SEG_NUM              &RSLT_SEG_NUM
TMP.SEG_BGN_DT
TMP.SEG_END_DT
TMP.PYMT_DT
TMP.PYMT_KEY1                 &pymt_key1
TMP.PYMT_KEY2                 &pymt_key2
TMP.PYMT_KEY3                 &pymt_key3
TMP.PYMT_KEY4                 &pymt_key4
TMP.GPNL_PAYSLIP_ID           &GPNL_PAYSLIP_ID
TMP.PYE_CALC_STAT             &PYE_CALC_STAT
TMP.CURRENCY_CD               &CURRENCY_CD
TMP.PRD_END_DT
      let $EMPLID         = rtrim (&EMPLID, ' ')
      let #EMPL_RCD       = &EMPL_RCD
      let $GP_PAYGROUP    = RTRIM (&GP_PAYGROUP, ' ')
      let $CAL_ID         = RTRIM (&CAL_ID, ' ')
      let #RSLT_SEG_NUM   = &RSLT_SEG_NUM
      let $seg_beg_date   = &TMP.seg_bgn_dt
      do Format-DateTime($seg_beg_date, $seg_bg_date, {DEFDMY}, '', '')
      let $seg_end_date   = &TMP.seg_end_dt
      move '/' to $DDelimiter
      do Format-DateTime($seg_end_date, $seg_ed_date, {DEFDMY}, '', '')
      let $Period_dt = {PS-substr}($seg_ed_date,4,10)
      let $PYMT_DT        = &TMP.pymt_dt
      let $PRD_END_DT = &TMP.PRD_END_DT
      let $Period_dt_header = &TMP.PRD_END_DT
      do Format-DateTime($Period_dt_header, $out, {DEFMDY}, '', '')
      let $MonthCd =  {PS-substr}($out,1,2)
      do Get-Month-Name ($MonthCd, $MonthName)

      let #pin_net_val    = &pin_net_val
      let $PAYSLIPID      = &GPNL_PAYSLIP_ID
      let $PYMT_KEY1      = &pymt_key1
      let $PYMT_KEY2      = &pymt_key2
      let $PYMT_KEY3      = &pymt_key3
      let $PYMT_KEY4      = &pymt_key4
      let $PYE_CALC_STAT  = &PYE_CALC_STAT

     do Format-DateTime(&TMP.PRD_END_DT, $BGN_Proc_Dt, {DEFCMP}, '', '')
     do Get-DateComponents($BGN_Proc_Dt, #CurrentYear,#CurrentMonth,#CurrentDate)

      If ($Prev-Emplid <> $Emplid or #PreV-EMPL-RCD <> #EMPL_RCD or #PreV-SEG-NUM <> #RSLT_SEG_NUM) and $Employee_found = 'Y'
             Or $retro-sw = 'N'

        do Print-Payslip
        let #Header-Data-Row =  0
        let #Body-Data-Row   =  0
        let #Footer-Data-Row =  0
                let #Bank-Data-Row   =  0
                let #Msg-Data-Row    =  0
                let #Accum-Data-Row  =  0
                let #I_retro         =  0
     End-If
      let #bodyline = 1
          let $Employee_found = 'N'

      do  Get-Gp-Rslt-Ern-Ded
      If #Body-Data-Row > 0 and ($Prev-Emplid <> $Emplid or #PreV-EMPL-RCD <> #EMPL_RCD or #PreV-SEG-NUM <> #RSLT_SEG_NUM)
        let #Total_employees = #Total_employees + 1

        do  Get-Heading-Info
        do  Get-Bank
        do  Get-Message
        let #acum_counter      =  0
            let #column_counter    =  0
         do  Get-Footer
        End-If
        let $Prev-Emplid     =  $Emplid
        let #PreV-SEG-NUM = #RSLT_SEG_NUM
        let #PreV-EMPL-RCD = #EMPL_RCD

FROM PS_GPNL_PSLIP_TMP TMP, PS_JOB JOB
WHERE JOB.EMPLID = TMP.EMPLID
     AND  JOB.EMPL_RCD=TMP.EMPL_RCD
     AND JOB.EFFDT =
         (SELECT MAX(B_ED.EFFDT) FROM PS_JOB B_ED
          WHERE JOB.EMPLID = B_ED.EMPLID
          AND JOB.EMPL_RCD = B_ED.EMPL_RCD)
     AND JOB.EFFSEQ =
         (SELECT MAX(B_ES.EFFSEQ) FROM PS_JOB B_ES
          WHERE JOB.EMPLID = B_ES.EMPLID
          AND JOB.EMPL_RCD = B_ES.EMPL_RCD
          AND JOB.EFFDT = B_ES.EFFDT)
     AND TMP.CAL_RUN_ID =  $calrunid
     AND TMP.JOBINSTANCE = #TMPJOBINSTANCE
     AND TMP.GPNL_PAYSLIP_ID = $payslip_id
     [$whereclause_sign]
     [$OrderByclause]
     TMP.EMPLID,TMP.EMPL_RCD,TMP.PRD_END_DT DESC

END-SELECT

do Print-Payslip

Display ' '
Display '----------------------------------'
Display 'Total Employees selected : ' noline
Display #Total_employees 999
Display 'Calendar Group ID .......: ' noline
Display $Calrunid
Display 'Payslip ID ..............: ' noline
Display $Payslip_id

end-procedure


!*****************************************************************
!  Heading Information                                          *
!*****************************************************************
begin-procedure Get-Heading-Info
!Personal data
let $sql-statement = 'GPNLPYS1.sqr,Select, Get-Heading-Information, Personal Data'
BEGIN-SELECT ON-ERROR=SQL-Error
A.NAME          &name
{DATEOUT-PREFIX}D.BIRTHDATE{DATEOUT-SUFFIX}      &Birth_dt
{DATEOUT-PREFIX}C.LAST_HIRE_DT{DATEOUT-SUFFIX}   &Hire_dt
{DATEOUT-PREFIX}C.TERMINATION_DT{DATEOUT-SUFFIX} &Term_dt
D.SEX           &Sex
D.MAR_STATUS    &Marital_St

  Let  $empl_name     = rtrim(&name,   ' ')
  Let  $Birth_dt     = &Birth_dt
  do Format-DateTime($Birth_dt,$Birth_dt,{DEFDMY},'','')
  Let  $Hire_dt     = &Hire_dt
  do Format-DateTime($Hire_dt,$Hire_dt,{DEFDMY},'','')
  Let  $Term_dt     = &Term_dt
  do Format-DateTime($Term_dt,$Term_dt,{DEFDMY},'','')

  let $Marital_St     = rtrim(&Marital_St,   ' ')
  let $FieldName = 'MAR_STATUS'
  let $FieldValue = $Marital_St
  do Read-Translate-Table
  let $Marital_St = rtrim($XLatLongName, ' ')

  Let  $Sex     = rtrim(&Sex,   ' ')
  let $FieldName = 'SEX'
  let $FieldValue = $Sex
  do Read-Translate-Table
  let $Sex = rtrim($XLatLongName, ' ')

FROM PS_PERSON_NAME A, PS_PER_ORG_ASGN_VW C, PS_PERSONAL_DT_FST D
WHERE A.EMPLID = $EMPLID
  AND C.EMPLID = A.EMPLID
  AND C.EMPL_RCD = #EMPL_RCD
  AND D.EMPLID = A.EMPLID
END-SELECT

let #exists_mail = 0
let #exists_address = 0

let $sql-statement = 'GPNLPYS1.sqr,Select, Get-Heading-Information, ADDRES_TYPE'
BEGIN-SELECT ON-ERROR=SQL-Error
COUNT(*)  &exists_mail

  Let  #exists_mail     = &exists_mail
  If    #exists_mail > 0
        let $address_type = 'MAIL'
  else
        let $address_type = 'HOME'
  End-If
FROM PS_PERSON_ADDRESS B
WHERE    B.EMPLID = $EMPLID
     AND B.ADDRESS_TYPE='MAIL'
END-SELECT

let $sql-statement = 'GPNLPYS1.sqr,Select, Get-Heading-Information, Address Information'
BEGIN-SELECT ON-ERROR=SQL-Error
B.ADDRESS3      &addr3
B.NUM1          &num1
B.NUM2          &num2
B.CITY          &city
B.POSTAL        &postal
B.COUNTRY

  Let  $empl_address     = rtrim(&addr3,  ' ') || ' ' || rtrim(&num1,  ' ')|| ' ' || rtrim(&num2,  ' ')
  Let  $empl_city_postal   = rtrim(&postal, ' ') || ' ' || &city
  Let #exists_address = 1

FROM PS_PERSON_ADDRESS B
WHERE B.EMPLID = $EMPLID
  AND B.ADDRESS_TYPE = $address_type
END-SELECT

if #exists_address = 0
  Let  $empl_address     = ''
  Let  $empl_city_postal = ''
  Let $log_message = 'Warning - there is no address information available for Employee ID: ' || $EMPLID
  display $log_message
end-if

!National ID
  Let  $national_id =  ''
let $sql-statement = 'GPNLPYS1.sqr,Select, Get-Heading-Information, National ID'
BEGIN-SELECT ON-ERROR=SQL-Error
A.NATIONAL_ID          &national_id
  Let  $national_id =  RTRIM (&national_id, ' ')
FROM  PS_PERS_NID A
WHERE A.EMPLID           = $EMPLID
      AND   A.COUNTRY          = 'NLD'
      AND   A.NATIONAL_ID_TYPE = 'PR'
END-SELECT

!Job Data

!initializatie:
  Let  #std_hours      = 0
  Let  $grade          = ' '
  Let  $step           = ' '
  Let  #comprate       = 0
  Let  #fte            = 0
  Let  #workdays       = 0
  Let  $business_unit  = ' '
  Let  $deptid         = ' '
  Let  $setid_deptid   = ' '
  Let  $location       = ' '
  Let  $setid_location = ' '
  Let  $tax_table      = ' '
  Let  $Tax_Credits    = ' '
  Let  $Socsec_value   = ' '


let $sql-statement = 'GPNLPYS1.sqr,Select, Get-Heading-Information, Job Data'
BEGIN-SELECT ON-ERROR=SQL-Error

B.REG_REGION           &reg_region
B.COMPANY              &company
B.BUSINESS_UNIT        &business_unit
B.DEPTID               &deptid
B.SETID_DEPT           &setid_deptid
B.LOCATION             &location
B.SETID_LOCATION       &setid_location
B.FTE  *100            &fte
B.STD_HOURS            &std_hours
B.GRADE                &grade
B.STEP                 &step
B.COMPRATE             &comprate
C.WORKDAYS_NLD         &workdays
D.GPNL_TAX_TABLE       &tax_table
D.GPNL_TAX_CREDITS     &tax_credits
E.GPNL_ZW_CNTR_MTHD    &ZW
E.GPNL_WAO_CNTR_MTHD   &WAO
E.GPNL_WW_CNTR_MTHD    &WW
E.GPNL_ZFW_CNTR_MTHD   &ZFW
E.GPNL_ZVW_CNTR_MTHD   &ZVW

  Let  $reg_region     = rtrim(&reg_region,   ' ')
  Let  $company        = rtrim(&company,  ' ')
  Let  $business_unit  = rtrim(&business_unit,  ' ')
  Let  $deptid         = rtrim(&deptid,  ' ')
  Let  $setid_deptid   = rtrim(&setid_deptid,  ' ')
  Let  $location       = rtrim(&location,   ' ')
  Let  $setid_location = rtrim(&setid_location,   ' ')
  Let  #fte            = &fte
  Let  #std_hours      = &std_hours
  Let  $grade          = rtrim(&grade,  ' ')
  Let  $step           = to_char(&step)
  Let  $grade          = $grade || '/' || $step
  !Let  #comprate       = &comprate
  Let  #workdays       = &workdays
  Let  $tax_table      = rtrim(&tax_table, ' ')
  let $FieldName = 'GPNL_TAX_TABLE'
  let $FieldValue = $tax_table
  do Read-Translate-Table
  let $tax_table = rtrim($XLatShortName, ' ')
  Let  $Tax_Credits   = rtrim(&tax_credits, ' ')
  let $FieldName = 'GPNL_Y_N'
  let $FieldValue = $tax_credits
  do Read-Translate-Table
  let $tax_credits = rtrim($XLatLongName, ' ')
  let $ZW        = rtrim(&ZW, ' ')
  let $WAO       = rtrim(&WAO, ' ')
  let $WW        = rtrim(&WW, ' ')
  let $ZFW       = rtrim(&ZFW, ' ')
  let $ZVW       = rtrim(&ZVW, ' ')

  evaluate $ZW
  when = '00'
  when = '03'
      Let $ZW_value = 'N'
  when = '01'
  when = '02'
       Let $ZW_value = 'Y'
  end-evaluate

  let $FieldName = 'GPNL_Y_N'
  let $FieldValue = $ZW_value
  do Read-Translate-Table
  let $ZW_value = rtrim($XLatShortName, ' ')

  evaluate $WAO
  when = '00'
  when = '03'
      Let $WAO_value = 'N'
  when = '01'
  when = '02'
      Let $WAO_value = 'Y'
  end-evaluate

  let $FieldName = 'GPNL_Y_N'
  let $FieldValue = $WAO_value
  do Read-Translate-Table
  let $WAO_value = rtrim($XLatShortName, ' ')

  evaluate $WW
  when = '00'
  when = '03'
      Let $WW_value = 'N'
  when = '01'
  when = '02'
      Let $WW_value = 'Y'
  end-evaluate

  let $FieldName = 'GPNL_Y_N'
  let $FieldValue = $WW_value
  do Read-Translate-Table
  let $WW_value = rtrim($XLatShortName, ' ')

  evaluate $ZFW
  when = '00'
  when = '03'
  when = '04'
      Let $ZFW_value = 'N'
  when = '01'
  when = '02'
       Let $ZFW_value = 'Y'
  end-evaluate

  let $FieldName = 'GPNL_Y_N'
  let $FieldValue = $ZFW_value
  do Read-Translate-Table
  let $ZFW_value = rtrim($XLatShortName, ' ')

  evaluate $ZVW
  when = '00'
  when = '03'
  when = '04'
      Let $ZVW_value = 'N'
  when = '01'
  when = '02'
       Let $ZVW_value = 'Y'
  end-evaluate

  let $FieldName = 'GPNL_Y_N'
  let $FieldValue = $ZVW_value
  do Read-Translate-Table
  let $ZVW_value = rtrim($XLatShortName, ' ')

! As of 2006 report changed
  if #CurrentYear < #fiscalYear
    let $Socsec_value =  $ZW_value || '/' || $WAO_value || '/' || $WW_value || '/' || $ZFW_value
  else
    let $Socsec_value =  $ZW_value || '/' || $WAO_value || '/' || $WW_value || '/' || $ZVW_value
  end-if

  do Company-Label
  do Dept-Id-Label
  do Location-Label

 FROM PS_GPNL_PS_SEG_TMP A, PS_JOB B, PS_JOB_JR C, PS_GPNL_EE_TAX D, PS_GPNL_EE_SOC_INS E
 WHERE  A.EMPLID = B.EMPLID
     AND A.EMPL_RCD = B.EMPL_RCD
     AND A.GP_PAYGROUP = B.GP_PAYGROUP
     AND B.EFFDT =
        (SELECT MAX(B_ED.EFFDT) FROM PS_JOB B_ED
        WHERE B.EMPLID = B_ED.EMPLID
          AND B.EMPL_RCD = B_ED.EMPL_RCD
          AND B_ED.EFFDT <= A.SEG_END_DT)
    AND B.EFFSEQ =
        (SELECT MAX(B_ES.EFFSEQ) FROM PS_JOB B_ES
        WHERE B.EMPLID = B_ES.EMPLID
          AND B.EMPL_RCD = B_ES.EMPL_RCD
          AND B.EFFDT = B_ES.EFFDT)
     AND B.EMPLID = C.EMPLID
     AND B.EMPL_RCD = C.EMPL_RCD
     AND C.EFFDT =
        (SELECT MAX(C_ED.EFFDT) FROM PS_JOB_JR C_ED
        WHERE C.EMPLID = C_ED.EMPLID
          AND C.EMPL_RCD = C_ED.EMPL_RCD
          AND C_ED.EFFDT <= A.SEG_END_DT)
    AND C.EFFSEQ =
        (SELECT MAX(C_ES.EFFSEQ) FROM PS_JOB_JR C_ES
        WHERE C.EMPLID = C_ES.EMPLID
          AND C.EMPL_RCD = C_ES.EMPL_RCD
          AND C.EFFDT = C_ES.EFFDT)
    AND B.EMPLID = D.EMPLID
    AND B.EMPL_RCD = D.EMPL_RCD
    AND D.EFFDT =
        (SELECT MAX(D_ED.EFFDT) FROM PS_GPNL_EE_TAX D_ED
        WHERE D.EMPLID = D_ED.EMPLID
          AND D.EMPL_RCD = D_ED.EMPL_RCD
          AND D_ED.EFFDT <= A.SEG_END_DT)
     AND A.EMPLID = E.EMPLID
     AND A.EMPL_RCD = E.EMPL_RCD
     AND E.EFFDT =
        (SELECT MAX(E_ED.EFFDT) FROM PS_GPNL_EE_SOC_INS E_ED
        WHERE E.EMPLID = E_ED.EMPLID
          AND E.EMPL_RCD = E_ED.EMPL_RCD
          AND E_ED.EFFDT <= A.SEG_END_DT)
    AND A.EMPLID = $Emplid
    AND A.EMPL_RCD = #EMPL_RCD

END-SELECT

  do Get-fulltime-Sal
  do Get-Minimum-Wave
  do Get-Non-Regular-Tax-Perc
  do Store-Header

end-procedure

!***********************************************************************
! Get-Minimum-Wave                                                     *
!***********************************************************************

begin-procedure Get-Minimum-Wave
let $sql-statement = 'GPNLPYS1.sqr,Select, Minimum Wave'

  Let #Min_Salary = 0

BEGIN-SELECT ON-ERROR=SQL-Error
A.CALC_RSLT_VAL   &A.CALC_RSLT_VAL
 Let #Min_Salary = &A.CALC_RSLT_VAL
FROM PS_GP_RSLT_PIN_VW A
WHERE A.EMPLID           =   $EMPLID
     AND   A.CAL_RUN_ID       =   $calrunid
     AND   A.EMPL_RCD         =   #EMPL_RCD
     AND   A.GP_PAYGROUP      =   $GP_PAYGROUP
     AND   A.CAL_ID           =   $CAL_ID
     AND   A.RSLT_SEG_NUM     =   #RSLT_SEG_NUM
!     AND   A.SLICE_BGN_DT     =   {DATETIMEIN-PREFIX}$Slice_Begin_Dt{DATETIMEIN-SUFFIX}
!     AND   A.SLICE_END_DT     =   {DATETIMEIN-PREFIX}$Slice_End_Dt{DATETIMEIN-SUFFIX}
     AND   A.PIN_NM = 'SST FM MN LOON'
END-SELECT

end-procedure

!***********************************************************************
! Get-fulltime-Sal                                                     *
!***********************************************************************

begin-procedure Get-fulltime-Sal
let $sql-statement = 'GPNLPYS1.sqr,Select, Get-fulltime-Sal'

 Let #comprate = 0
BEGIN-SELECT ON-ERROR=SQL-Error
D.CALC_RSLT_VAL   &D.CALC_RSLT_VAL
 Let #comprate = &D.CALC_RSLT_VAL
FROM PS_GP_RSLT_PIN_VW D
WHERE D.EMPLID           =   $EMPLID
     AND   D.CAL_RUN_ID       =   $calrunid
     AND   D.EMPL_RCD         =   #EMPL_RCD
     AND   D.GP_PAYGROUP      =   $GP_PAYGROUP
     AND   D.CAL_ID           =   $CAL_ID
     AND   D.RSLT_SEG_NUM     =   #RSLT_SEG_NUM
!     AND   D.SLICE_BGN_DT     =   {DATETIMEIN-PREFIX}$Slice_Begin_Dt{DATETIMEIN-SUFFIX}
!     AND   D.SLICE_END_DT     =   {DATETIMEIN-PREFIX}$Slice_End_Dt{DATETIMEIN-SUFFIX}
     AND   D.PIN_NM = 'VER VR SAL VT'
END-SELECT

end-procedure

!***********************************************************************
! Get-Non-Regular-Tax-Perc                                             *
!***********************************************************************

begin-procedure Get-Non-Regular-Tax-Perc
let $sql-statement = 'GPNLPYS1.sqr,Select, Non-Regular Tax Percentage'

let #PCT_TAX = 0

BEGIN-SELECT ON-ERROR=SQL-Error
B.CALC_RSLT_VAL   &B.CALC_RSLT_VAL
 let #PCT_TAX = &B.CALC_RSLT_VAL

FROM PS_GP_RSLT_PIN_VW B
WHERE B.EMPLID           =   $EMPLID
     AND   B.CAL_RUN_ID       =   $calrunid
     AND   B.EMPL_RCD         =   #EMPL_RCD
     AND   B.GP_PAYGROUP      =   $GP_PAYGROUP
     AND   B.CAL_ID           =   $CAL_ID
     AND   B.RSLT_SEG_NUM     =   #RSLT_SEG_NUM
!     AND   B.SLICE_BGN_DT     =   {DATETIMEIN-PREFIX}$Slice_Begin_Dt{DATETIMEIN-SUFFIX}
!     AND   B.SLICE_END_DT     =   {DATETIMEIN-PREFIX}$Slice_End_Dt{DATETIMEIN-SUFFIX}
     AND   B.PIN_NM = 'BEL VR BT PCT'
END-SELECT

end-procedure

!***********************************************************************
! Get-Bank                                                             *
!***********************************************************************
begin-procedure Get-Bank

  do Get-RUN-TYPE

!Net Distribution

let $PAYMENT_MTHD  = ''
let #ACCOUNT_ID    = 0
let $sql-statement = 'GPNLPYS1.sqr,Select, Get-Bank, Net Distribution '

BEGIN-SELECT ON-ERROR=SQL-Error
A.PAYMENT_MTHD           &PAYMENT_MTHD
A.ACCOUNT_ID             &ACCOUNT_ID
 let $PAYMENT_MTHD = &PAYMENT_MTHD
 move 'PAYMENT_MTHD'     to $FieldName
 move $PAYMENT_MTHD      to $FieldValue
 do Read-Translate-Table
 let $PAYMENT_MTHD_NM    =  rtrim($XlatLongName, ' ')
 let #ACCOUNT_ID   = &ACCOUNT_ID
FROM  PS_GP_NET_DIST_DTL  A
WHERE       A.EMPLID     = $EMPLID
      AND A.EMPL_RCD   = #EMPL_RCD
      AND A.RUN_TYPE   = $RUN_TYPE
      AND A.EFFDT     <= $SEG_END_DATE
      AND A.PAYMENT_MTHD = 'T'
      AND A.EFFDT = (SELECT MAX(B.EFFDT) FROM PS_GP_NET_DIST_DTL B
                     WHERE B.EMPLID   = A.EMPLID
                           AND B.EMPL_RCD = A.EMPL_RCD
                           AND B.RUN_TYPE = A.RUN_TYPE
                           AND B.EFFDT   <= $SEG_END_DATE)
END-SELECT

!BANK ACCOUNT INFO
let $sql-statement    = 'GPNLPYS1.sqr,Select, Get-Bank, Bank Account Info'
let $bank_printed = 'N'

Let $SEPA_Enable = 'N'
BEGIN-SELECT ON-ERROR=SQL-Error
A.COUNTRY
   Let $SEPA_Enable = 'Y'
FROM   PS_GP_APP_PKG_DEFN A
WHERE A.COUNTRY='NLD' 
  AND A.STATUS = 'A'
END-SELECT

if $SEPA_Enable = 'Y' 
Do Get-SEPA-BankAcct-Info
else
Do Get-NonSEPA-BankAcct-Info
end-if
end-procedure

!***********************************************************************
! Get-NonSEPA-BankAcct-Info                                                             *
!***********************************************************************
begin-procedure Get-NonSEPA-BankAcct-Info

!BANK ACCOUNT INFO
let $sql-statement    = 'GPNLPYS1.sqr,Select, Get-NonSEPA-BankAcct-Info, Non SEPA Bank Account Info'
let $bank_printed = 'N'

BEGIN-SELECT ON-ERROR=SQL-Error
A.ACCOUNT_TYPE_PYE          &ACCOUNT_TYP_PYE
A.BANK_CD                   &BANK_CD
A.BRANCH_EC_CD              &BRANCH_EC_CD
A.ACCOUNT_EC_ID             &ACCOUNT_EC_ID
!A.CHECK_DIGIT              &CHECK_DIGIT
A.GP_PMT_AMT                &GP_PMT_AMT
A.PMT_TYPE                  &PMT_TYPE 
   Let $ACCOUNT_TYP_PYE = &ACCOUNT_TYP_PYE
   Let $BANK_CD         =  rtrim(&BANK_CD, ' ')
   Let $BRANCH_EC_CD    =  rtrim(&BRANCH_EC_CD, ' ')
   Let $ACCOUNT_EC_ID   =  rtrim(&ACCOUNT_EC_ID, ' ')
   Let $CHECK_DIGIT     =  0
   Let #Bank_amount     =  &GP_PMT_AMT
   Let $PMT_TYPE        =  &PMT_TYPE

  do store-bank

FROM   PS_GPNL_PAYMENT A
WHERE  A.EMPLID     = $EMPLID
     AND A.EMPL_RCD = #EMPL_RCD
     AND A.CAL_RUN_ID = $Calrunid
     AND A.RSLT_SEG_NUM = 0
     AND A.PMT_TYPE <> '02'
END-SELECT
end-procedure



!***********************************************************************
! Get-SEPA-BankAcct-Info                                                             *
!***********************************************************************
begin-procedure Get-SEPA-BankAcct-Info

!BANK ACCOUNT INFO
let $sql-statement    = 'GPNLPYS1.sqr,Select, Get-SEPA-BankAcct-Info, SEPA Bank Account Info'
let $bank_printed = 'N'

BEGIN-SELECT ON-ERROR=SQL-Error
B.ACCOUNT_TYPE_PYE          &B.ACCOUNT_TYP_PYE
B.GP_DEST_BANK_ID           &B.GP_DEST_BANK_ID
B.GP_DEST_BRANCH            &B.GP_DEST_BRANCH
B.GP_DEST_ACCT_NUM          &B.GP_DEST_ACCT_NUM
B.GP_PMT_AMT                &B.GP_PMT_AMT
B.PMT_TYPE                  &B.PMT_TYPE 
   Let $ACCOUNT_TYP_PYE = &B.ACCOUNT_TYP_PYE
   Let $BANK_CD         =  rtrim(&B.GP_DEST_BANK_ID, ' ')
   Let $BRANCH_EC_CD    =  rtrim(&B.GP_DEST_BRANCH, ' ')
   Let $ACCOUNT_EC_ID   =  rtrim(&B.GP_DEST_ACCT_NUM, ' ')
   Let $CHECK_DIGIT     =  0
   Let #Bank_amount     =  &B.GP_PMT_AMT
   Let $PMT_TYPE        =  &B.PMT_TYPE

  do store-bank

FROM   PS_GP_PAYMENT_FG B
WHERE  B.EMPLID     = $EMPLID
     AND B.EMPL_RCD = #EMPL_RCD
     AND B.CAL_RUN_ID = $Calrunid
     AND B.RSLT_SEG_NUM = 0
     AND B.PMT_TYPE <> '02'
END-SELECT
end-procedure

!***********************************************************************
! Get-RUN-TYPE                                                         *
!***********************************************************************
begin-procedure Get-RUN-TYPE

let $RUN_TYPE           =  ''
let $sql-statement = 'GPNLPYS1.sqr,Select , Get-RUN-TYPE'
BEGIN-SELECT ON-ERROR=SQL-Error
A.RUN_TYPE               &RUN_TYPE
   let $RUN_TYPE = rtrim(&RUN_TYPE, ' ')
FROM   PS_GP_CALENDAR  A
WHERE   A.GP_PAYGROUP       = $GP_PAYGROUP
    AND A.CAL_ID            = $CAL_ID
END-SELECT
end-procedure


!***********************************************************************
! Get-Gp-Rslt-Ern-Ded                                                  *
!***********************************************************************
begin-procedure Get-Gp-Rslt-Ern-Ded
let $sql-statement    = 'GPNLPYS1.sqr,Select, Select, Get-Gp-Rslt-Ern-Ded'


BEGIN-SELECT ON-ERROR=SQL-Error
V.JOBINSTANCE
V.GPNL_PAYSLIP_ID
V.GPNL_ELEMENT_ORD
V.PIN_NUM
VA.DESCR
VA.GPNL_PIN_TYPE
VA.GPNL_LINES_BEFORE
VA.GPNL_LINES_AFTER
VA.GPNL_PSLIP_SEPARAT
VA.GPNL_BASE_PRINT
VA.GPNL_PCT_PRINT
VA.GPNL_RATE_PRINT
VA.GPNL_UNIT_PRINT
VA.GPNL_PS_REG_COL
VA.GPNL_PS_REGTOT_COL
VA.GPNL_PS_NREG_COL
VA.GPNL_PS_NRGTOT_COL
VA.GPNL_PS_TOT_COL
VA.GPNL_RETRO_TYPE
V.DESCR15
V.FORWARD_IND
V.CURRENCY_CD2
V.CURRENCY_CD
{DATETIMEOUT-PREFIX}V.SLICE_BGN_DT{DATETIMEOUT-SUFFIX} &V.SLICE_BGN_DT
{DATETIMEOUT-PREFIX}V.SLICE_END_DT{DATETIMEOUT-SUFFIX} &V.SLICE_END_DT
V.INSTANCE
V.RATE_RSLT_VAL
V.CALC_RSLT_VAL                                   &V.CALC_RSLT_VAL
V.BASE_RSLT_VAL                                   &V.BASE_RSLT_VAL
V.UNIT_RSLT_VAL                                   &V.UNIT_RSLT_VAL
V.SRC_CAL_RUN_ID
V.PCT_RSLT_VAL
V.SRC_CAL_ID
V.RSLT_SEG_NUM2


        let $GP_PAYSLIP_ID      =    &V.GPNL_PAYSLIP_ID
        let #GPNL_ELEMENT_ORD   =    &V.GPNL_ELEMENT_ORD
        let #PIN_NUM            =    &V.PIN_NUM
        let $Description        = rtrim(&VA.DESCR, ' ')

        !*** Retrieve PSLIP_LINES description.  Retrieve related language entry if
        !***   necessary.

        if $curr_language_cd <> $Psoptions_Language_Cd
          do PSLIP_LINES-Related
        end-if
        let $ED_Type            = &VA.GPNL_PIN_TYPE
        let #lines_before       = &VA.GPNL_LINES_BEFORE
        let #lines_After        = &VA.GPNL_LINES_AFTER
        let $separator          = &VA.GPNL_PSLIP_SEPARAT
        let $base_print         = &VA.GPNL_BASE_PRINT
        let $pct_print          = &VA.GPNL_PCT_PRINT
        let $factor_print       = &VA.GPNL_RATE_PRINT
        let $unit_print         = &VA.GPNL_UNIT_PRINT
        let $reg_col            = &VA.GPNL_PS_REG_COL
        let $reg_tot_col        = &VA.GPNL_PS_REGTOT_COL
        let $nreg_col           = &VA.GPNL_PS_NREG_COL
        let $nreg_tot_col       = &VA.GPNL_PS_NRGTOT_COL
        let $tot_col            = &VA.GPNL_PS_TOT_COL
        let $retro_type         = &VA.GPNL_RETRO_TYPE

        let #JOBINSTANCE        =    &V.JOBINSTANCE
        let #EARN_DED           =    &V.CALC_RSLT_VAL
        let #BASE               =    &V.BASE_RSLT_VAL
        let #RATE               =    &V.RATE_RSLT_VAL
        let #UNIT               =    &V.UNIT_RSLT_VAL
        let #PERCENTAGE         =    &V.PCT_RSLT_VAL
        let $CURRENCY_CD        =    rtrim(&V.CURRENCY_CD, ' ')
        let $CURRENCY_CD2       =    rtrim(&V.CURRENCY_CD2, ' ')
        let $SRC_CAL_ID         =    &V.SRC_CAL_ID
        let #RSLT_SEG_NUM2      =    &V.RSLT_SEG_NUM2
        let $DESCR15            =    rtrim(&V.DESCR15, ' ')
        let $FORWARD_IND        =    &V.FORWARD_IND
        let $Employee_found     = 'Y'
        let $Slice_Begin_dt     =    &V.SLICE_BGN_DT
        let $Slice_End_dt       =    &V.SLICE_END_DT
!        Do Format-DateTime($Slice_Begin_dt, $Slice_Begin_dt, {DEFDATE},'','')
!        Do Format-DateTime($Slice_End_dt, $Slice_End_dt, {DEFDATE},'','')

                do store-body-line

FROM PS_GPNL_PS_ERN_TMP V, PS_GPNL_PSLIP_LN VA
WHERE V.GPNL_PAYSLIP_ID = VA.GPNL_PAYSLIP_ID
     AND   V.PIN_NUM = VA.GPNL_PIN_PS1_NUM
     AND   V.JOBINSTANCE      =   #TMPJOBINSTANCE
     AND   V.EMPLID           =   $Emplid
     AND   V.CAL_RUN_ID       =   $calrunid
     AND   V.EMPL_RCD         =   #EMPL_RCD
     AND   V.GP_PAYGROUP      =   $GP_PAYGROUP
     AND   V.RSLT_SEG_NUM     =   #RSLT_SEG_NUM
     AND   V.GPNL_PAYSLIP_ID  =   $payslip_id
[$WHERECLAUSE]
ORDER BY V.GPNL_PAYSLIP_ID ASC,  V.GPNL_ELEMENT_ORD ASC,
         V.PIN_NUM, V.DESCR15, V.CURRENCY_CD2, V.SLICE_BGN_DT, V.SLICE_END_DT, V.INSTANCE

END-SELECT
end-procedure

!********************************************************************
! Procedure PSLIP-LINES-Related Related DESCR                                *
!********************************************************************
begin-procedure PSLIP_LINES-Related

  let $sql-statement = 'GPNLPYS1.sqr, Select, PSLIP_LINES-Related Rel-Lang.'
BEGIN-SELECT ON-ERROR=SQL-Error
A.DESCR                     &descr_lines_LNG

  let $Description        = rtrim(&descr_lines_LNG, ' ')

FROM  PS_GPNL_PSLPLN_LNG A
WHERE  A.GPNL_PAYSLIP_ID = &V.GPNL_PAYSLIP_ID
    AND A.GPNL_ELEMENT_ORD = &V.GPNL_ELEMENT_ORD
    AND A.LANGUAGE_CD = $language_cd
END-SELECT

end-procedure




!***********************************************************************
! Store-Header                                                         *
!***********************************************************************
begin-procedure Store-Header

let #Header-Data-Row = #Header-Data-Row + 1

PUT $descr_comp  $address_c  $city_postal_c  $empl_name  $empl_address
    $empl_city_postal $Employee_Lit $Emplid $Birth_Date_Lit $Birth_Dt $Hire_Date_Lit
    $Hire_dt  $Term_Date_Lit  $Term_dt $Gender_Lit  $Sex  $Marital_St_Lit $Marital_St
    $Tax_Credits_Lit  $tax_Credits $Tax_table_Lit $tax_table  $Percent_tax_Lit
    #PCT_TAX  $Soc_Fiscal_Nbr_Lit $National_id  $Period_Lit  $Period_dt $Location_Lit
    $Location_name $Department_Lit $DEPT_NAME  $Hours_Week_Lit #std_hours $Days_Week_Lit
    #workdays $Grade_Step_Lit $grade $Full_time_Lit #comprate $Part_time_Lit #fte
    $Min_Salary_Lit  #Min_Salary  $ZW_WW_WAO_ZFW_Lit $ZW_WAO_WW_ZVW_Lit $Socsec_value $Payslip_lit
    $Description_Lit $Hour_unit_Lit $Factor_Lit $Percent_Lit $Bases_Lit $Regular_Lit
    $Non_Regular_Lit $Sum_Lit
        INTO  Header-Data (#Header-Data-Row)
end-procedure


!***********************************************************************
! Print-Header                                                         *
!***********************************************************************
begin-procedure Print-Header

   #define Start_Header          2
   #define Start_Body           16
   #define Start_Footer         71

   #define colh1   2
   #define colh2   51
   #define colh3   70
   #define colh4   90
   #define colh5   109

   #define colbl1   2
   #define colbl2   38
   #define colbl3   51
   #define colbl4   63
   #define colbl5   77
   #define colbl6   89
   #define colbl7   103
   #define colbl8   122

   #define colb1   2
   #define colb2   38
   #define colb2b  32
   #define colb3   51
   #define colb4   63
   #define colb5   74
   #define colb6   88
   #define colb7   103
   #define colb8   119

   #define colf1 3
   #define colf2 28
   #define colf3 45
   #define colf4 72
   #define colf5 89
   #define colf6 117

   #define foot_line_pr1 71
   #define foot_line_pr2 72
   #define foot_line_pr3 73
   #define foot_line_pr4 74
   #define foot_line_pr5 75
   #define foot_line_pr6 76


 GET $descr_comp_h  $address_c_h  $city_postal_c_h  $empl_name_h  $empl_address_h
    $empl_city_postal_h $Employee_Lit $Emplid_h $Birth_Date_Lit $Birth_Dt_h $Hire_Date_Lit
    $Hire_dt_h  $Term_Date_Lit  $Term_dt_h $Gender_Lit  $Sex_h  $Marital_St_Lit $Marital_St_h
    $Tax_Credits_Lit  $tax_Credits_h $Tax_table_Lit $tax_table_h  $Percent_tax_Lit
    #PCT_TAX_h  $Soc_Fiscal_Nbr_Lit $National_id_h  $Period_Lit  $Period_dt_h $Location_Lit
    $Location_name_h $Department_Lit $DEPT_NAME_h  $Hours_Week_Lit #std_hours_h $Days_Week_Lit
    #workdays_h $Grade_Step_Lit $grade_h $Full_time_Lit #comprate_h $Part_time_Lit #fte_h
    $Min_Salary_Lit  #Min_Salary_h  $ZW_WW_WAO_ZFW_Lit $ZW_WAO_WW_ZVW_Lit $Socsec_value_h $Payslip_lit
    $Description_Lit $Hour_unit_Lit $Factor_Lit $Percent_Lit $Bases_Lit $Regular_Lit
    $Non_Regular_Lit $Sum_Lit
        FROM  Header-Data (#Header-Data-Row)

 print-image logo (1,1)
 alter-printer  font=3 point-size=10
 do Report-Translation
!Company
 ALTER-PRINTER
 point-size=8
 print $descr_comp_h         ( 6,{colh1}) bold
 If isblank($address_c_h) = 0
    print   $address_c_h     (+1, {colh1})
 End-If
 If isblank($city_postal_c_h) = 0
    print   $city_postal_c_h (+1, {colh1})
 End-If

!Employee Address
 print      $empl_name_h      (10, {colh1}) BOLD
 If isblank($empl_address_h)  = 0
     print   $empl_address_h     (+1, {colh1})
 End-If
 If isblank($empl_city_postal_h) = 0
    print   $empl_city_postal_h    (+1, {colh1})
 End-If

!HEADER LINE

ALTER-PRINTER
point-size=7.5
!First Group
  print $Employee_Lit       ({Start_Header},{colh2})
  print $Emplid_h             (,{colh3})
  print $Birth_Date_Lit     (+1,{colh2})
  print $Birth_Dt_h           (,{colh3})
  print $Hire_Date_Lit      (+1,{colh2})
  print $Hire_dt_h            (,{colh3})
  print $Term_Date_Lit      (+1,{colh2})
  print $Term_dt_h            (,{colh3})
  print $Gender_Lit         (+1,{colh2})
  print $Sex_h                (,{colh3})
  print $Marital_St_Lit     (+1,{colh2})
  print $Marital_St_h         (,{colh3})
  print $Tax_Credits_Lit    (+1,{colh2})
  print $tax_Credits_h        (,{colh3})
  print $Tax_table_Lit      (+1,{colh2})
  print $tax_table_h          (,{colh3})
  print $Percent_tax_Lit    (+1,{colh2})
  print #PCT_TAX_h            (,{colh3})  EDIT 99.99
  print $Soc_Fiscal_Nbr_Lit (+1,{colh2})
  print $National_id_h        (,{colh3})
!Second Group
  print $Period_Lit         (2,{colh4})
  print $Period_dt_h          (,{colh5})
  print $Location_Lit       (+1,{colh4})
  print $Location_name_h      (,{colh5})
  print $Department_Lit     (+1,{colh4})
  print $DEPT_NAME_h          (,{colh5})
  print $Hours_Week_Lit     (+1,{colh4})
  print #std_hours_h          (,{colh5}) EDIT B9
  print $Days_Week_Lit      (+1,{colh4})
  print #workdays_h           (,{colh5})   EDIT 9
  print $Grade_Step_Lit     (+1,{colh4})
  print $grade_h              (,{colh5})
  print $Full_time_Lit      (+1,{colh4})
  print #comprate_h           (,{colh5})   !EDIT BBB,BB9.99
  print $Part_time_Lit      (+1,{colh4})
  print #fte_h                (,{colh5}) EDIT B99.99
  print $Min_Salary_Lit     (+1,{colh4})
  print #Min_Salary_h         (,{colh5})

! As of 2006 report changed
  if #CurrentYear < #fiscalYear
    print $ZW_WW_WAO_ZFW_Lit  (+1,{colh4})
  else
    print $ZW_WAO_WW_ZVW_Lit  (+1,{colh4})
  end-if
  print $Socsec_value_h       (,{colh5})

  let $Payslip_lit = $Payslip_lit || ' ' ||$Monthname

  print $Payslip_lit         (+3,{colbL1}) Bold
  print $Description_Lit     (+1,{colbL1})
  print $Hour_unit_Lit       (,{colbL2})
  print $Factor_Lit          (,{colbL3})
  print $Percent_Lit         (,{colbL4})
  print $Bases_Lit           (,{colbL5})
  print $Regular_Lit         (,{colbL6})
  print $Non_Regular_Lit     (,{colbL7})
  print $Sum_Lit             (,{colbL8})

  graphic (+1,{colb1},{MaxRows}) horz-line 6

  let #bodyline = {Start_Body}

end-procedure

!***********************************************************************
! For ePay Purposes: Online Payslip                                    *
! Procedure : GP-ePay-Init                                             *
! Initialize variables that we’ll use for ePay processing              *
!***********************************************************************
begin-procedure GP-ePay-Init
  let $sql-statement = 'GPNLPYS1.sqr, GP-ePay-Init '

  do Check-ePay-installed ($ePay_Installed)

  If $ePay_Installed = 'Y'

    let #eV4 =  To_number($prcs_process_instance)

    !* have the Output Working Directory from the Process parameters table
    do Get-Output-Wrk-Directory(#eV4, $PRCSOUTPUTDIR, $PRCSNAME)

    !* have the URL id from the Payslip option table
    do Get-ePay-URLid ('NLD', $eV18)

    !* Global ePay variable settings used in GP-ePay procedures in this SQR
    let $eV1 = $prcs_oprid
    let $eV2 = $prcs_run_cntl_id
    let $eV3 = $ReportID          

  End-If

end-procedure !GP-ePay-Init

!***********************************************************************
! For ePay Purposes: Online Payslip                                    *
! Procedure : GP-ePay-Guide                                            *
! Insert one row per payslip into the temporary guide table for ePay   *
!***********************************************************************

begin-procedure GP-ePay-Guide

let $sql-statement = 'GPNLPYS1.sqr,GP-ePay-Guide'

 If $ePay_Installed = 'Y'

   ! For NLD we do need to retrieve the runtype value other CE's may have it already
   do Get-RUN-TYPE

   let $eV5  = rtrim($Emplid_h, ' ')
   let $eV5  = ltrim($eV5, ' ')
   let $eV6 = rtrim($calrunid,' ')
   let $eV6 = ltrim($eV6,' ')
   let $eV7  = 'GPNLD'
   let $eV8  = rtrim($CAL_ID, ' ') || '_' || $eV5 || '_' || to_char(#PreV-EMPL-RCD) || '_' || to_char(#PreV-SEG-NUM)!gp epay payslip id
   let $eV9  = $Pymt_Dt
   let $eV10 = $PRD_END_DT
   let $eV11 = $seg_beg_date


   let #eV12 = #EARN_DED                                !net pay
   let $eV13 = rtrim($DEPT_NAME_h, ' ')                 ! payslip description
   if $eV13 = ''
     let $eV13 = 'No DESCRIPTION'
   end-if
   let $eV14 = rtrim($RUN_TYPE, ' ')
   let $eV15 = 'ORIG'                           ! payslip status ORIGINAL
   let $eV16 = $eV6 || '_' ||$eV7 || '_' ||$eV8 || '.pdf'                   !sysfilename of the payslip pdf since emplid is in payslip id-don't add emplid
   let $eV17 = $eV16                                                        !userfilename  - what the payee sees filename as
   let #eV19 = #page-count                                                  !begin page number of payslip in output report
   let #eV20 = #page-count                                                  !end page number of payslip in output report

   !Input:OPRID, RUN_CNTL_ID, PROCNAME, DATAINST, EMPLID, CAL_RUN_ID, SRCPRODUCT,
   !GP_PSLP_ID,PYMT_DT,PRD_END_DT,PRD_BGN_DT,#NET_PAY,Payslip DESCR,RUN_TYPE,
   !PSLP_STATUS,ATTACHSYSFILENAME, ATTACHUSERFILE,FILEURLID, BGNPGNBR,ENDPGNBR


    do Insert-ePay-Guide-Data($eV1,$eV2,$eV3,#eV4,$eV5,$eV6,$eV7,$eV8,$eV9,$eV10,$eV11,#eV12,$eV13,$eV14,$eV15,$eV16,$eV17,$eV18,#eV19,#eV20)

 End-If

end-procedure ! GP-ePay-Guide

!***********************************************************************
! For ePay Purposes: Online Payslip                                    *
! Procedure : GP-ePay-Control                                          *
! Insert one row per report into the epay run control table            *
!***********************************************************************
begin-procedure GP-ePay-Control

let $sql-statement = 'GPNLPYS1.sqr,GP-ePay-Control '

 If $ePay_Installed = 'Y'

! After copy command is supplied and implemented we will provide a procedure in gpsspslp.sqc to close the
! report file and make a copy of it – storing in the files directory.  PSLP_SOURCEFILE AND OR
! PSLP_SOURCELOC variables may be effected by this change.

   !Do Copy-Report-For-ePay

   let $rptid = lower($ReportID)
   let $eCV7  = $rptid || '_' || $prcs_process_instance || '.PDF'
   let $eCV8  = rtrim($PRCSOUTPUTDIR, ' ')

   ! Input:OPRID,RUN_CNTL_ID,PROCNAME,SPLIT_IND,ATCH_IND,CTLFILE,SOURCEFILE,
   ! SOURCELOC,WORKINGLOC,FILELAYOUT,DATAINST,FILEURLID,CLEANUP

  do Insert-ePay-RunControl($eV1,$eV2,$eV3,'Y', 'Y', ' ', $eCV7,$eCV8,' ',' ',#eV4,$eV18,'Y')

 End-If

end-procedure !GP-ePay-Control


!***********************************************************************
! Print-Payslip                                                        *
!***********************************************************************

begin-procedure Print-Payslip

 If #Body-Data-Row > 0
   do Print-Header
   let #BodyLine    =  {START_BODY}
   do Print-Body
   If #Bank-Data-Row > 0
      do Print-Bank
   End-If
   If #Msg-Data-Row > 0
          do Print-Msg
   End-If
   If #Accum-Data-Row > 0
      do print-footer
   End-If
   do GP-ePay-Guide
   New-page
 End-If

end-procedure

!***********************************************************************
! Print-Body                                                           *
!***********************************************************************
begin-procedure Print-Body

  let #nrg_line = 0  
  let #I = 1

  While #I <= #Body-Data-Row
    GET  $ED_Type #Pin_Num $Description #Unit #Rate #Base #Percentage #Earn_Ded
         #Lines_Before #Lines_After $Retro_Type $Separator $Base_Print
         $Pct_Print $Factor_Print $Unit_Print $Reg_Col $Reg_tot_Col $Nreg_Col
         $Nreg_tot_Col $Tot_Col $Descr15 $Prnt-After
         FROM Body-Line(#I)

    if #Earn_Ded <> 0
      do Print-Lines-Payslip
    End-If
    let #I = #I + 1
  End-While

  If #I_retro <> 0
     let #I = 1

     let $Retro_hdr = 'Y'
     While #I <= #I_retro
       GET  $ED_Type #Pin_Num $Description #Unit #Rate #Base #Percentage #Earn_Ded
         #Lines_Before #Lines_After $Retro_Type $Separator $Base_Print
         $Pct_Print $Factor_Print $Unit_Print $Reg_Col $Reg_tot_Col $Nreg_Col
         $Nreg_tot_Col $Tot_Col $Descr15 $Prnt-After
         FROM Body-Line-retro(#I)

           if #Earn_Ded <> 0
             if $Retro_hdr = 'Y'  ! print Retro Header once.
               add 5 to #bodyline
               do Line-Control
               do Add_Report_Translation ($ReportID, $language_cd)
               do Get_Field_Information ('GPNLPYS1','RECAL_LIT',         $RECAL_LIT,   #DW)
               do Get-Retro-Date

               let $Recal_lit = $REcal_lit || '  ' || $Retro_date
               print $REcal_lit          (+3,{colbL1}) Bold
               print $Description_Lit     (+1,{colbL1})
               print $Hour_unit_Lit       (,{colbL2})
               print $Factor_Lit          (,{colbL3})
               print $Percent_Lit         (,{colbL4})
               print $Bases_Lit           (,{colbL5})
               print $Regular_Lit         (,{colbL6})
               print $Non_Regular_Lit     (,{colbL7})
               print $Sum_Lit             (,{colbL8})

               graphic (+1,{colb1},{MaxRows}) horz-line 6

               let #bodyline = #bodyline + 1
               do Line-Control
               
               let $Retro_hdr = 'N'
             end-if
             do Print-Lines-Payslip
           End-If
                 let #I = #I + 1
     End-While
  End-If
end-procedure

!***********************************************************************
! Print-Inside-Lines                                                   *
!***********************************************************************
begin-procedure Print-Inside-Lines
  
 let #bodyline = #bodyline + #lines_before
 do Line-Control
 print $DESCRIPTION               (#bodyline, {colb1})

!Check for Factor, Unit, Pct and Base
 If $factor_print = 'Y'
    print #RATE         (, {colb3})   edit B99,999.99
 End-If
 If $unit_print ='Y'
    print #UNIT                     (, {colb2})   edit B99,999.99
 End-If
 If $pct_print = 'Y'
    print #PERCENTAGE               (, {colb4})   edit B99,999.99
 End-If
 If $base_print = 'Y'
    print #BASE                     (, {colb5})   edit B99,999.99
 End-If
!Check for Regular, Non REgular, Reg and Total Non reg an Total
 If $ED_Type = 'DD0'
    let #EARN_DED = #EARN_DED * -1
 End-If
 If $reg_col = 'Y'
    print #EARN_DED                 (, {colb6})   edit 99,999,999.99
 End-If
 If $reg_tot_col = 'Y'
    print #EARN_DED                 (, {colb6})   edit 99,999,999.99
    print #EARN_DED                 (, {colb8})   edit 99,999,999.99
 End-If
 If $nreg_col = 'Y'
    print #EARN_DED                 (, {colb7})   edit 99,999,999.99
 End-If
 If $nreg_tot_col = 'Y'
    print #EARN_DED                 (, {colb7})   edit 99,999,999.99
    print #EARN_DED                 (, {colb8})   edit 99,999,999.99
 End-If
 If $tot_col = 'Y'
    print #EARN_DED                 (, {colb8})   edit 99,999,999.99
 End-If

let #bodyline = #bodyline + #lines_after
do Line-Control

end-procedure !Print-Inside-Lines

!***********************************************************************
! Print-Lines-Payslip                                                  *
!***********************************************************************
begin-procedure Print-Lines-Payslip


 If $separator = 'Y'
   add 1 to #bodyline
   do Line-Control
   If $reg_col = 'Y'
       LET #nrg_line = #bodyline
       print '_' (#bodyline,{colb6},13) fill
   End-If
   If $nreg_col = 'Y'
!       print '_' (#bodyline,{colb7},13) fill
   End-If
   If $reg_tot_col = 'Y'
       print '_' (#bodyline,{colb6},13) fill
       print '_' (#bodyline,{colb8},13) fill
   End-If
   If $nreg_tot_col = 'Y'
       print '_' (#bodyline,{colb7},13) fill
       print '_' (#bodyline,{colb8},13) fill
   End-If
   If $tot_col = 'Y'
       print '_' (#bodyline,{colb8},13) fill
   End-If
   If $reg_tot_col = 'Y'
       print '_' (#bodyline,{colb6},13) fill
       print '_' (#bodyline,{colb8},13) fill
   End-If
 End-If
 add 1 to #bodyline
 do Line-Control

!* in order to be able to print SOC Insurance regular/non regular on the same line
!* Idem. for Period Base regular/non regular on the same line

let $PINCODE = ''
BEGIN-SELECT ON-ERROR=SQL-Error
PINCD.PIN_CODE   &PINCODE
  let $PINCODE = rtrim(&PINCODE, ' ')
FROM PS_GP_PIN PINCD
WHERE PINCD.PIN_NUM = #PIN_NUM
END-SELECT

If ($PINCODE <> 'SOC AC LOON BT NLD' and $PINCODE <> 'BEL AC BSNRG STHB NLD' and $PINCODE <> 'SOC AC LOON ZVW BT NLD')

 do Print-Inside-Lines

else

If ($previous_PINCODE = 'SOC AC LOON TAB NLD' OR $previous_PINCODE = 'BEL AC BSREG STHB NLD' OR $previous_PINCODE = 'SOC AC LOON ZVW NT NLD')

! let #bodyline = #bodyline + #lines_before + #lines_after - 2
 let #bodyline = #bodyline + #lines_before - 1
 
 do Line-Control
 
!Check for Regular, Non REgular, Reg and Total Non reg an Total
 If $ED_Type = 'DD0'
     let #EARN_DED = #EARN_DED * -1
 End-If
 If $nreg_col = 'Y'
    print #EARN_DED                 (, {colb7})   edit 99,999,999.99
 End-If
 If $nreg_tot_col = 'Y'
    print #EARN_DED                 (, {colb7})   edit 99,999,999.99
 End-If

 if #nrg_line = 0
   LET #nrg_line = #bodyline  - 1
 end-if
 If $separator = 'Y'
   print '_' (#nrg_line,{colb7},14) fill
 end-if
   LET #nrg_line = 0
   let #bodyline = #bodyline + #lines_after
 else

 do Print-Inside-Lines

end-if

end-if

let $previous_PINCODE = $PINCODE

end-procedure

!************************************************************************
! Procedure Store-Body-Line
!************************************************************************
begin-procedure Store-Body-Line
#debug do Fin-Debug-Msg('Store-Body-Line')

! $retro_type: 10-Current + Deltas, 20-Current + Adj, 30-One Line

  If $Retro-SW = 'N'
     let #Body-Data-Row = #Body-Data-Row + 1
     let #I = #Body-Data-Row
     PUT $ED_Type #Pin_Num $Description #Unit #Rate #Base #Percentage #Earn_Ded
      #Lines_Before #Lines_After $Retro_Type $Separator $Base_Print
      $Pct_Print $Factor_Print $Unit_Print $Reg_Col $Reg_tot_Col $Nreg_Col
      $Nreg_tot_Col $Tot_Col $Descr15 $Prnt-After
     INTO Body-Line(#I)
  End-If

  If $Retro-SW = 'Y'
     let #Body-Data-Row = #Body-Data-Row + 1
     let #I = #Body-Data-Row

     If $retro_type = '10'

       PUT $ED_Type #Pin_Num $Description #Unit #Rate #Base #Percentage #Earn_Ded
         #Lines_Before #Lines_After $Retro_Type $Separator $Base_Print
         $Pct_Print $Factor_Print $Unit_Print $Reg_Col $Reg_tot_Col $Nreg_Col
         $Nreg_tot_Col $Tot_Col $Descr15 $Prnt-After
         INTO Body-Line(#I)
     End-If

     If $retro_type = '20' or $retro_type = '30'

         let $Found = 'N'
         let #J = 1
         While #J < #Body-Data-Row and $Found = 'N' ! and $retro_type = '20'
           GET $ED_Type_Tmp #Pin_Num_Tmp $Description_Tmp #Unit_Tmp #Rate_Tmp #Base_Tmp #Percentage_Tmp #Earn_Ded_Tmp
           #Lines_Before_Tmp #Lines_After_Tmp $Retro_Type_Tmp $Separator_Tmp $Base_Print_Tmp
           $Pct_Print_Tmp $Factor_Print_Tmp $Unit_Print_Tmp $Reg_Col_Tmp $Reg_tot_Col_Tmp $Nreg_Col_Tmp
           $Nreg_tot_Col_Tmp $Tot_Col_Tmp $Descr15_Tmp $Prnt-After_tmp
           FROM Body-Line(#J)
         If #Pin_Num_Tmp = #Pin_Num
              let #Unit       =  #Unit + #Unit_Tmp
              let #Rate       =  #Rate !+ #Rate_Tmp
              let #Base       =  #Base + #Base_Tmp
              let #Percentage =  #Percentage !+ #Percentage_Tmp
              let #Earn_Ded     =  #Earn_Ded + #Earn_Ded_Tmp
              let $Found = 'Y'
              PUT $ED_Type #Pin_Num $Description #Unit #Rate #Base #Percentage #Earn_Ded
                  #Lines_Before #Lines_After $Retro_Type $Separator $Base_Print
                  $Pct_Print $Factor_Print $Unit_Print $Reg_Col $Reg_tot_Col $Nreg_Col
                  $Nreg_tot_Col $Tot_Col $Descr15 $Prnt-After
                  INTO Body-Line(#J)
           Else
             let #J = #J + 1
           End-If
         End-While
         If $Found = 'N'
            PUT $ED_Type #Pin_Num $Description #Unit #Rate #Base #Percentage #Earn_Ded
               #Lines_Before #Lines_After $Retro_Type $Separator $Base_Print
               $Pct_Print $Factor_Print $Unit_Print $Reg_Col $Reg_tot_Col $Nreg_Col
               $Nreg_tot_Col $Tot_Col $Descr15 $Prnt-After
               INTO Body-Line(#I)


          Else
            let #Body-Data-Row = #Body-Data-Row - 1
         End-If
      End-If
  End-If

  If $Retro-SW = 'A'

     let #Body-Data-Row = #Body-Data-Row + 1
     let #I = #Body-Data-Row

     If $retro_type = '30'

         let $Found = 'N'
         let #J = 1

         While #J < #Body-Data-Row and $Found = 'N' ! and $retro_type = '20'
           GET $ED_Type_Tmp #Pin_Num_Tmp $Description_Tmp #Unit_Tmp #Rate_Tmp #Base_Tmp #Percentage_Tmp #Earn_Ded_Tmp
           #Lines_Before_Tmp #Lines_After_Tmp $Retro_Type_Tmp $Separator_Tmp $Base_Print_Tmp
           $Pct_Print_Tmp $Factor_Print_Tmp $Unit_Print_Tmp $Reg_Col_Tmp $Reg_tot_Col_Tmp $Nreg_Col_Tmp
           $Nreg_tot_Col_Tmp $Tot_Col_Tmp $Descr15_Tmp $Prnt-After
           FROM Body-Line(#J)

           If #Pin_Num_Tmp = #Pin_Num

              let #Unit       =  #Unit + #Unit_Tmp
              let #Rate       =  #Rate !+ #Rate_Tmp
              let #Base       =  #Base + #Base_Tmp
              let #Percentage =  #Percentage !+ #Percentage_Tmp
              let #Earn_Ded     =  #Earn_Ded + #Earn_Ded_Tmp
              let $Found = 'Y'
              PUT $ED_Type #Pin_Num $Description #Unit #Rate #Base #Percentage #Earn_Ded
                  #Lines_Before #Lines_After $Retro_Type $Separator $Base_Print
                  $Pct_Print $Factor_Print $Unit_Print $Reg_Col $Reg_tot_Col $Nreg_Col
                  $Nreg_tot_Col $Tot_Col $Descr15 $Prnt-After
                  INTO Body-Line(#J)
           Else
             let #J = #J + 1

           End-If

         End-While
         If $Found = 'N'
            PUT $ED_Type #Pin_Num $Description #Unit #Rate #Base #Percentage #Earn_Ded
               #Lines_Before #Lines_After $Retro_Type $Separator $Base_Print
               $Pct_Print $Factor_Print $Unit_Print $Reg_Col $Reg_tot_Col $Nreg_Col
               $Nreg_tot_Col $Tot_Col $Descr15 $Prnt-After
               INTO Body-Line(#I)


         Else
            let #Body-Data-Row = #Body-Data-Row - 1

        End-If
     End-If

     If $retro_type = '20'     !Current + Adj
!Forwarded Retro will be printed on one line (added to the Body)
       If $descr15 = 'CURRENT' or $FORWARD_IND = 'Y' 
          PUT $ED_Type #Pin_Num $Description #Unit #Rate #Base #Percentage #Earn_Ded
          #Lines_Before #Lines_After $Retro_Type $Separator $Base_Print
          $Pct_Print $Factor_Print $Unit_Print $Reg_Col $Reg_tot_Col $Nreg_Col
          $Nreg_tot_Col $Tot_Col $Descr15 $Prnt-After
          INTO Body-Line(#I)
       Else

         let $Found = 'N'
         let #J = 1

         While #J <= #I_retro and $Found = 'N' ! and $retro_type = '20'
           GET $ED_Type_Tmp #Pin_Num_Tmp $Description_Tmp #Unit_Tmp #Rate_Tmp #Base_Tmp #Percentage_Tmp #Earn_Ded_Tmp
           #Lines_Before_Tmp #Lines_After_Tmp $Retro_Type_Tmp $Separator_Tmp $Base_Print_Tmp
           $Pct_Print_Tmp $Factor_Print_Tmp $Unit_Print_Tmp $Reg_Col_Tmp $Reg_tot_Col_Tmp $Nreg_Col_Tmp
           $Nreg_tot_Col_Tmp $Tot_Col_Tmp $Descr15_Tmp $Prnt-After
           FROM Body-Line-Retro(#J)

           If #Pin_Num_Tmp = #Pin_Num

              let #Unit       =  #Unit + #Unit_Tmp
              let #Rate       =  #Rate !+ #Rate_Tmp
              let #Base       =  #Base + #Base_Tmp
              let #Percentage =  #Percentage !+ #Percentage_Tmp
              let #Earn_Ded     =  #Earn_Ded + #Earn_Ded_Tmp
              let $Found = 'Y'
              PUT $ED_Type #Pin_Num $Description #Unit #Rate #Base #Percentage #Earn_Ded
                  #Lines_Before #Lines_After $Retro_Type $Separator $Base_Print
                  $Pct_Print $Factor_Print $Unit_Print $Reg_Col $Reg_tot_Col $Nreg_Col
                  $Nreg_tot_Col $Tot_Col $Descr15 $Prnt-After
                  INTO Body-Line-Retro(#J)
           Else
             let #J = #J + 1

           End-If

         End-While
         If $Found = 'N'
            let $Prnt-After = 'Y'
            let #I_retro = #I_retro + 1
            PUT $ED_Type #Pin_Num $Description #Unit #Rate #Base #Percentage #Earn_Ded
            #Lines_Before #Lines_After $Retro_Type $Separator $Base_Print
            $Pct_Print $Factor_Print $Unit_Print $Reg_Col $Reg_tot_Col $Nreg_Col
            $Nreg_tot_Col $Tot_Col $Descr15 $Prnt-After
            INTO Body-Line-Retro(#I_retro)
        End-If

        let #Body-Data-Row = #Body-Data-Row - 1

       End-If
    End-If

    If $retro_type = '10'  !Current + Deltas
!Forwarded Retro will be printed on one line (added to the Body)

      If $descr15 = 'RETRO' and $FORWARD_IND <> 'Y'
          let $Prnt-After = 'Y'
                  let #I_retro = #I_retro + 1
          PUT $ED_Type #Pin_Num $Description #Unit #Rate #Base #Percentage #Earn_Ded
          #Lines_Before #Lines_After $Retro_Type $Separator $Base_Print
          $Pct_Print $Factor_Print $Unit_Print $Reg_Col $Reg_tot_Col $Nreg_Col
          $Nreg_tot_Col $Tot_Col $Descr15 $Prnt-After
          INTO Body-Line-Retro(#I_retro)

          let #Body-Data-Row = #Body-Data-Row - 1
      else
          PUT $ED_Type #Pin_Num $Description #Unit #Rate #Base #Percentage #Earn_Ded
          #Lines_Before #Lines_After $Retro_Type $Separator $Base_Print
          $Pct_Print $Factor_Print $Unit_Print $Reg_Col $Reg_tot_Col $Nreg_Col
          $Nreg_tot_Col $Tot_Col $Descr15 $Prnt-After
          INTO Body-Line(#I)

       End-If
      End-If
  End-If

  #debugd show 'Emplid:        ' $Emplid
  #debugd show 'E&D Type:      ' $ED_Type
  #debugd show 'Pin Number:    ' #Pin_Num
  #debugd show 'Description:   ' $Description
  #debugd show 'Unit:          ' #Unit
  #debugd show 'Rate:          ' #Rate
  #debugd show 'Base:          ' #Base
  #debugd show 'Percentage:    ' #Percentage
  #debugd show 'Earn/Ded:      ' #Earn_Ded
  #debugd show 'Lines Before:  ' #Lines_Before
  #debugd show 'Lines After:   ' #Lines_After
  #debugd show 'Retro Type:    ' $Retro_Type
  #debugd show 'Separator:     ' $Separator
  #debugd show 'Base Print:    ' $Base_Print
  #debugd show 'Percentage Prt:' $Pct_Print
  #debugd show 'Factor Print:  ' $Factor_Print
  #debugd show 'Unit Print:    ' $Unit_Print
  #debugd show 'Regular:       ' $Reg_Col
  #debugd show 'Regular Total: ' $Reg_tot_Col
  #debugd show 'Non Regular:   ' $Nreg_Col
  #debugd show 'Non Reg Tot:   ' $Nreg_tot_Col
  #debugd show 'Total:         ' $Tot_Col
  #debugd show 'Descr15        ' $Descr15
  #debugd show 'Print After:   ' $Prnt-After


  If #Body-Data-Row = {BODY-SIZE}
     !ERROR
  End-If

end-procedure


!************************************************************************
! Procedure Store-Bank
!************************************************************************
begin-procedure Store-Bank
#debug do Fin-Debug-Msg('Store-Bank')

let #Bank-Data-Row = #Bank-Data-Row + 1

 PUT $ACCOUNT_TYP_PYE  $Bank_Cd  $Branch_Ec_Cd  $Account_Ec_Id  $Check_Digit  #Bank_amount
    INTO Bank-Line(#Bank-Data-Row)
end-procedure

!************************************************************************
! Procedure Print-Bank
!************************************************************************
begin-procedure Print-Bank

let #I = 1
let #bodyline = #bodyline + 2
do Line-Control
  graphic (#bodyline,{colb1},{MaxRows}) horz-line 6
!print '_' (#bodyline,{colb1},35) fill
add 1 to #bodyline
do Line-Control
do Add_Report_Translation ($ReportID, $language_cd)
do Get_Field_Information ('GPNLPYS1',     'BANK_ACCOUNT_LIT',         $BANK_ACCOUNT_LIT,   #DW)
do Get_Field_Information ('GPNLPYS1',     'CASH_AMOUNT_LIT',         $CASH_AMOUNT_LIT,   #DW)

while #I <= #Bank-Data-Row
  GET  $ACCOUNT_TYP_PYE $Bank_Cd  $Branch_Ec_Cd  $Account_Ec_Id  $Check_Digit  #Bank_amount
    FROM Bank-Line(#I)

    if #Bank_Amount >= 0  
     print $BANK_ACCOUNT_LIT          (#bodyline, {colb1})

!     if $ACCOUNT_TYP_PYE = 'G' or Length($Account_Ec_Id) < 8
     if Length($Account_Ec_Id) < 8
      ! Giro or Post Bank Account
       print $Account_Ec_Id             (, {colb2b}) edit xxxxxxxxx
     else
       print $Account_Ec_Id             (, {colb2b}) edit xxx.xx.xx.xxx
     End-if
    else 
!When negative amount print Cash Text i.e:  Cash Payment
     print $CASH_AMOUNT_LIT          (#bodyline, {colb1})
   end-if
   print #Bank_Amount               (, {colb3}) edit 99,999.99 bold
   print ' '                        (, {colb4})

  let #I = #I + 1
  let #bodyline = #bodyline + 1
  do Line-Control
end-while

end-procedure


!************************************************************************
! Procedure Get-Message                                                 *
!************************************************************************
begin-procedure Get-Message

let $Calrunid_msg = ''
let $Message_printed = 'N'
let $Message_to_print = 'N'
let $sql-statement    = 'GPNLPYS1.sqr,Select, Get-Message,'
BEGIN-SELECT ON-ERROR=SQL-Error
MSG.CAL_RUN_ID &CAL_RUN_ID_MSG
MSG.GPNL_MSG_NBR
  let $Calrunid_msg  = rtrim(&CAL_RUN_ID_MSG, ' ')
  let #Message_nbr = &MSG.GPNL_MSG_NBR

  If $Calrunid_msg <> ''
     If $Message_printed = 'N'
       add 1 to #Msg-Data-Row
       do Add_Report_Translation ($ReportID, $language_cd)
       do Get_Field_Information ('GPNLPYS1',     'MESSAGE_LIT',         $MESSAGE_LIT,   #DW)
       If $Message_lit <> ''
           put $Message_lit INTO Msg-line(#Msg-Data-Row)      
           add 1 to #Msg-Data-Row
       End-If
           let $Message_lit = '______________________________________________________________'
           let $Message_lit = $Message_lit || $Message_lit || '_'
           put $Message_lit INTO Msg-line(#Msg-Data-Row)

       let $Message_printed = 'Y'
     End-If

    do Check-Message
  End-If

FROM PS_GPNL_PSLIP_MSG MSG
WHERE MSG.CAL_RUN_ID = $CALRUNID
END-SELECT
! No message header print when there is no messages to print
If  $Message_to_print <> 'Y'
  let #Msg-Data-Row    =  0
End-If

end-procedure

!************************************************************************
! Procedure Print-Msg                                                   *
!************************************************************************

begin-procedure Print-Msg
let #I = 1
let #bodyline = #bodyline + 3
do Line-Control
while #I <= #Msg-Data-Row
  GET  $Message_lit FROM Msg-Line(#I)
       print $Message_lit (#bodyline, {colb1})

  let #I = #I + 1
  let #bodyline = #bodyline + 1
  do Line-Control
end-while
end-procedure

!************************************************************************
! Procedure Chek-Message                                                *
!************************************************************************
begin-procedure Check-Message

let $sql-statement = 'GPNLPYS1.sqr,Select, Check-Message'

BEGIN-SELECT ON-ERROR=SQL-Error
MSG_CHK.GP_PAYGROUP
MSG_CHK.PAY_ENTITY
MSG_CHK.EMPLID
MSG_CHK.SETID_DEPT
MSG_CHK.DEPTID
MSG_CHK.GPNL_MSGTEXT

  let $Paygroup_msg = rtrim(&MSG_CHK.GP_PAYGROUP, ' ')
  let $Pay_Entity_msg = rtrim(&MSG_CHK.PAY_ENTITY, ' ')
  let $Emplid_msg = rtrim(&MSG_CHK.EMPLID, ' ')
  let $Setid_Dept_msg = rtrim(&MSG_CHK.SETID_DEPT, ' ')
  let $Deptid_msg = rtrim(&MSG_CHK.DEPTID, ' ')
  let $Text_msg = rtrim(&MSG_CHK.GPNL_MSGTEXT, ' ')
  let $Message_Lit = rtrim(&MSG_CHK.GPNL_MSGTEXT, ' ')
!  let $Message_to_print = 'N'


  If $Pay_Entity_msg <> ''
     do Get-Pay-Entity
     If $Pay_entity_msg_chk = $Pay_entity_msg
       let $message_to_print = 'Y'
           add 1 to #Msg-Data-Row
           let $Message_lit = $Text_msg
           put $Message_lit INTO Msg-line(#Msg-Data-Row)
     End-If
  End-If

  If $PayGROUP_msg <> ''
     If $Paygroup_msg = $GP_PAYGROUP
       let $message_to_print = 'Y'
           add 1 to #Msg-Data-Row
           let $Message_lit = $Text_msg
           put $Message_lit INTO Msg-line(#Msg-Data-Row)
     End-If
  End-If

  If $EMPLID_msg <> ''
    If $emplid_msg = $emplid
       let $message_to_print = 'Y'
           add 1 to #Msg-Data-Row
           let $Message_lit = $Text_msg
           put $Message_lit INTO Msg-line(#Msg-Data-Row)
    End-If
  End-If

  If $SETID_DEPT_msg <> ''
    If $SETID_DEPT_msg = $SETID_DEPTID and $DEPTID = $Deptid_msg
       let $message_to_print = 'Y'
           add 1 to #Msg-Data-Row
           let $Message_lit = $Text_msg
           put $Message_lit INTO Msg-line(#Msg-Data-Row)
    End-If
  End-If


  If ($Pay_Entity_msg = '' and $PayGROUP_msg = '' and $EMPLID_msg = '' and $SETID_DEPT_msg = '')
    If ($message_to_print <> 'Y' and $Text_msg <> '')
       add 1 to #Msg-Data-Row
       let $Message_lit = $Text_msg
       put $Message_lit INTO Msg-line(#Msg-Data-Row)
       let $message_to_print = 'Y'
    End-If
  End-If

FROM PS_GPNL_PSLIP_MSG MSG_CHK
WHERE MSG_CHK.CAL_RUN_ID = $CALRUNID
AND MSG_CHK.GPNL_MSG_NBR = #Message_nbr
END-SELECT
end-procedure

!************************************************************************
! Procedure Get-Pay-Entity                                              *
!************************************************************************
begin-procedure Get-Pay-Entity
let $sql-statement = 'GPNLPYS1.sqr,Select,Get-Pay-Entity'

BEGIN-SELECT ON-ERROR=SQL-Error
PENT_A.PAY_ENTITY

    let $Pay_entity_msg_chk = rtrim(&PENT_A.PAY_ENTITY, ' ')

FROM PS_GP_PYENT PENT_A,
     PS_GP_PYGRP PENT_B,
     PS_GPNL_PS_SEG_TMP PENT_D
WHERE PENT_A.PAY_ENTITY= PENT_B.PAY_ENTITY
     AND  PENT_D.GP_PAYGROUP = PENT_B.GP_PAYGROUP
     AND  PENT_D.CAL_RUN_ID = $Calrunid
     AND  PENT_D.EMPLID = $Emplid
END-SELECT

end-procedure

!************************************************************************
! Procedure Get-Footer                                                  *
!************************************************************************
begin-procedure Get-Footer
let $sql-statement = 'GPNLPYS1.sqr, Select, Get-Footer '

BEGIN-SELECT DISTINCT ON-ERROR=SQL-Error
D.GPNL_ELEMENT_ORD      &GPNL_COLUMN_NO_2
E.GPNL_PIN_PS1_NUM      &GPNL_PIN_PS1_NUM
E.DESCR                 &descr_accum
A.PIN_NUM               &ACCUM_PIN_2
A.CURRENCY_CD           &CURRENCY_CD_2ar
A.CURRENCY_CD2          &CURRENCY_CD2_2ar
A.CALC_RSLT_VAL    &RSLT_ACUM_2

  let #GPNL_COLUMN_NO_2 = &GPNL_COLUMN_NO_2
  let #GPNL_PIN_PS1_NUM = &GPNL_PIN_PS1_NUM
  let $descr_accum = rtrim (&descr_accum, ' ')

!*** Retrieve PSLIP ACCUM DESCR. Retrieve related language entry if
!***   necessary.

  if $curr_language_cd <> $Psoptions_Language_Cd
    do PSLIP_ACUM-Related
  end-if

  let #rslt_ACUM = &RSLT_ACUM_2
  let #acum_counter = #acum_counter + 1
  let #column_counter = #column_counter + 1
  do Store-Accum
  If #column_counter    = 3
     let #column_counter = 0
  End-If

FROM PS_GPNL_PS_ACU_TMP A,
     PS_GPNL_PS_PIN_TMP D,
     PS_GPNL_PSLIP_ACUM E
WHERE A.JOBINSTANCE      = #TMPJOBINSTANCE
AND   A.EMPLID           = $EMPLID
AND   A.CAL_RUN_ID       = $calrunid
AND   A.EMPL_RCD         = #EMPL_RCD
AND   A.GP_PAYGROUP      = $GP_PAYGROUP
AND   A.CAL_ID           = $CAL_ID
AND   A.RSLT_SEG_NUM     = #RSLT_SEG_NUM
AND   A.JOBINSTANCE      = D.JOBINSTANCE
AND   A.GP_PAYGROUP      = D.GP_PAYGROUP
AND   A.PIN_NUM          = D.PIN_NUM
AND   D.GPNL_PAYSLIP_ID  = E.GPNL_PAYSLIP_ID
AND   D.PIN_NUM  = E.GPNL_PIN_PS1_NUM
AND   D.GPNL_PAYSLIP_ID = $PAYSLIPID

ORDER BY D.GPNL_ELEMENT_ORD
END-SELECT


end-procedure


!********************************************************************
! Procedure PSLIP_ACUM Related DESCR                                *
!********************************************************************
begin-procedure PSLIP_ACUM-Related
  let $sql-statement = 'GPNLPYS1.sqr, Select, PSLIP_ACUM Rel-Lang.'
BEGIN-SELECT ON-ERROR=SQL-Error
A.DESCR                     &descr_accum_LNG

  let $descr_accum = rtrim (&descr_accum_LNG, ' ')

FROM  PS_GPNL_PSLPAC_LNG A
WHERE  A.GPNL_PAYSLIP_ID = $PAYSLIPID
    AND GPNL_ELEMENT_ORD = #GPNL_COLUMN_NO_2
    AND GPNL_PIN_PS1_NUM = #GPNL_PIN_PS1_NUM
    AND A.LANGUAGE_CD = $language_cd
END-SELECT
end-procedure

!************************************************************************
! Procedure Store-Accum                                                 *
!************************************************************************
begin-procedure Store-Accum

let #Accum-Data-Row = #Accum-Data-Row + 1

PUT   $Descr_Accum #Rslt_Acum #Acum_Counter #Column_Counter
    INTO Accum-Line(#Accum-Data-Row)

end-procedure

!************************************************************************
! Procedure Print-Footer                                                *
!************************************************************************
begin-procedure  Print-Footer

  let #lit_foot = {colf1}
  let #var_foot = {colf2}
  let #acum_counter = 0
  let #column_counter = 0
  let $sql-statement = 'GPNLPSL.sqr,Select,Get-Footer '
  let #footline = {Start_footer}
  do Add_Report_Translation ($ReportID, $language_cd)
  do Get_Field_Information ('GPNLPYS1',     'ACCUMULATIVE_LIT',         $ACCUMULATIVE_LIT,   #DW)
  print $Accumulative_lit  (#footline,{colf1}) bold
  graphic (+1,{colb1},{MaxRows}) box 8

  let #Iftr = 1
  While #Iftr <= #Accum-Data-Row
  GET  $Descr_Accum #Rslt_Acum #Acum_Counter #Column_Counter
    FROM Accum-Line(#Iftr)

   evaluate #column_counter
    when = 1
      let #footcolumn_descr = {colf1}
      let #footcolumn_amount= {colf2}
    when = 2
      let #footcolumn_descr = {colf3}
      let #footcolumn_amount= {colf4}
    when = 3
      let #footcolumn_descr = {colf5}
      let #footcolumn_amount= {colf6}
    end-evaluate

   let #foot_line = #acum_counter / 3

   evaluate #foot_line
   when <= 1
      print ' ' ({foot_line_pr1},{colf1})
      PRINT $descr_accum    (+1,#footcolumn_descr)
      PRINT #rslt_ACUM     (,#footcolumn_amount) edit B99,999,999.99
      break
   when <= 2
      print ' ' ({foot_line_pr2},{colf1})
      PRINT $descr_accum    (+1,#footcolumn_descr)
      PRINT #rslt_ACUM     (,#footcolumn_amount) edit B99,999,999.99
      break
   when <= 3
      print ' ' ({foot_line_pr3},{colf1})
      PRINT $descr_accum    (+1,#footcolumn_descr)
      PRINT #rslt_ACUM     (,#footcolumn_amount) edit B99,999,999.99
      break
   when <= 4
      print ' ' ({foot_line_pr4},{colf1})
      PRINT $descr_accum    (+1,#footcolumn_descr)
      PRINT #rslt_ACUM     (,#footcolumn_amount) edit B99,999,999.99
      break
   when <= 5
      print ' ' ({foot_line_pr5},{colf1})
      PRINT $descr_accum    (+1,#footcolumn_descr)
      PRINT #rslt_ACUM     (,#footcolumn_amount) edit B99,999,999.99
      break
   when <= 6
      print ' ' ({foot_line_pr6},{colf1})
      PRINT $descr_accum    (+1,#footcolumn_descr)
      PRINT #rslt_ACUM     (,#footcolumn_amount) edit B99,999,999.99
      break
  end-evaluate
    let #Iftr = #Iftr + 1
  End-While
end-procedure

!***********************************************************************
! Procedure Get-Retro-Date                                             *
!***********************************************************************

begin-procedure Get-Retro-Date
   let $sql-statement = 'GPNLPSY1.sqr, Select, Get-Retro-Date'
BEGIN-SELECT ON-ERROR=SQL-Error

{DATETIMEOUT-PREFIX}RET.SEG_BGN_DT{DATETIMEOUT-SUFFIX}   &RET.SEG_BGN_DT
 Let $Retro_date = &RET.SEG_BGN_DT
 do Format-DateTime($Retro_date,$Retro_date,{DEFDMY},'','')

FROM PS_GPNL_PS_SEG_TMP RET
WHERE RET.JOBINSTANCE      =   #TMPJOBINSTANCE
AND   RET.EMPLID           =   $Prev-Emplid
AND   RET.CAL_RUN_ID       =   $calrunid
AND   RET.EMPL_RCD         =   #EMPL_RCD
AND   RET.GP_PAYGROUP      =   $GP_PAYGROUP
AND   RET.RSLT_SEG_NUM     =   #RSLT_SEG_NUM
AND   RET.PRD_TYPE = 'R'
END-SELECT

end-procedure


!********************************************************************
! Procedure  Company Label
!********************************************************************
begin-procedure Company-Label
   let $sql-statement = 'GPNLPYS1.sqr,Select, Company Label'


BEGIN-SELECT ON-ERROR=SQL-Error

A.DESCR         &descr_comp
A.ADDRESS3      &address3
A.NUM1          &num1_c
A.NUM2          &num2_c
A.POSTAL        &postal_c
A.CITY          &city_c
  let $descr_comp    = rtrim(&descr_comp , ' ')
  let $address_c     = rtrim(&address3, ' ') || ' ' ||  rtrim(&num1_c, ' ') || ' ' ||  rtrim(&num2_c, ' ')
  let $city_postal_c = rtrim(&postal_c, ' ') || ' ' || rtrim(&city_c, ' ')

FROM  PS_COMPANY_TBL A
WHERE A.COMPANY  = $company
    AND A.EFF_STATUS = 'A'
    AND A.EFFDT     <= $SEG_END_DATE
    AND A.EFFDT      = (SELECT MAX(B.EFFDT) FROM PS_COMPANY_TBL B
                         WHERE B.COMPANY = A.COMPANY
                           AND B.EFFDT  <= $SEG_END_DATE )

END-SELECT

 If #RelatedLabel = 1

 let $sql-statement = 'GPNLPYS1.sqr,Select, Company Label Rel-lang.'
BEGIN-SELECT ON-ERROR=SQL-Error
A.DESCR             &DESCR_COMPANY_LANG
  let $descr_comp  = rtrim(&DESCR_COMPANY_LANG, ' ')
FROM  PS_COMPNY_TBL_LANG A
WHERE A.COMPANY = $company
    AND A.EFFDT <= $SEG_END_DATE
    AND A.EFFDT = (SELECT MAX(B.EFFDT) FROM PS_COMPANY_TBL B
                   WHERE B.COMPANY    = A.COMPANY
                         AND B.EFF_STATUS = 'A'
                         AND B.EFFDT     <= $SEG_END_DATE )
    AND A.LANGUAGE_CD = $language_cd
END-SELECT
 End-If

end-procedure

!********************************************************************
! Procedure Department Label                                        *
!********************************************************************
begin-procedure Dept-Id-Label

 let $sql-statement = 'GPNLPYS1.sqr, Select, Department Label'
BEGIN-SELECT ON-ERROR=SQL-Error
A.DESCR     &DEPT_NAME
  let $DEPT_NAME    = rtrim(&DEPT_NAME, ' ')
  let $Dept_name    = substr($Dept_name,1,21)
  let #FoundLabel   = 1

!*** Retrieve Dept. description.  Retrieve related language entry if
!***   necessary.
  if $curr_language_cd <> $Psoptions_Language_Cd
     do Dept-Id-Related
  end-if

FROM    PS_DEPT_TBL A
WHERE   A.DEPTID     = $deptid
    AND A.SETID      = $setid_deptid
    AND A.EFF_STATUS = 'A'
    AND A.EFFDT     <= $SEG_END_DATE
    AND A.EFFDT      = (SELECT MAX(B.EFFDT) FROM PS_DEPT_TBL B
                         WHERE B.DEPTID     = A.DEPTID
                           AND B.SETID      = A.SETID
                           AND B.EFFDT     <= $SEG_END_DATE )
END-SELECT
end-procedure

!********************************************************************
! Procedure Department Related Label                                *
!********************************************************************
begin-procedure Dept-Id-Related
  If #RelatedLabel = 1
  let $sql-statement = 'GPNLPYS1.sqr, Select, Department Rel-Lang.'
BEGIN-SELECT ON-ERROR=SQL-Error
A.DESCR                     &DEPT_LNG
        let $DEPT_NAME    = rtrim(&DEPT_LNG, ' ')
        let #FoundLabel   = 1
FROM    PS_DEPT_TBL_LANG A
WHERE   A.DEPTID     = $deptid
    AND A.SETID      = $setid_deptid
    AND A.EFFDT     <= $SEG_END_DATE
    AND A.EFFDT      = (SELECT MAX(B.EFFDT) FROM PS_DEPT_TBL B
                         WHERE B.DEPTID     = A.DEPTID
                           AND B.SETID      = A.SETID
                           AND B.EFF_STATUS = 'A'
                           AND B.EFFDT     <= $SEG_END_DATE )
    AND A.LANGUAGE_CD = $language_cd
END-SELECT
   End-If
end-procedure

!********************************************************************
! Procedure Location Label
!********************************************************************
begin-procedure Location-Label
   let $sql-statement = 'GPNLPYS1.sqr, Select, Location'
BEGIN-SELECT ON-ERROR=SQL-Error
A.DESCR  &Location_name
        let $Location_name    = rtrim(&Location_name, ' ')
        let #FoundLabel   = 1

!*** Retrieve Location description.  Retrieve related language entry if
!***   necessary.

        if $curr_language_cd <> $Psoptions_Language_Cd
          do Location-Related
        end-if

FROM    PS_LOCATION_TBL A
WHERE   A.LOCATION   = $location
    AND A.SETID      = $setid_location
    AND A.EFF_STATUS = 'A'
    AND A.EFFDT     <= $SEG_END_DATE
    AND A.EFFDT      = (SELECT MAX(B.EFFDT) FROM PS_LOCATION_TBL B
                         WHERE B.LOCATION   = A.LOCATION
                           AND B.SETID      = A.SETID
                           AND B.EFFDT     <= $SEG_END_DATE )
END-SELECT

end-procedure

!********************************************************************
! Procedure Location Related Label
!********************************************************************
begin-procedure Location-Related
   If #RelatedLabel = 1
   let $sql-statement = 'GPNLPYS1.sqr, Select, Location Rel-Lang.'
BEGIN-SELECT ON-ERROR=SQL-Error
A.DESCR  &location_LNG
  let $location_NAME    = rtrim(&location_LNG, ' ')
  let #FoundLabel   = 1
FROM    PS_LOCATION_LANG A
WHERE   A.LOCATION   = $location
    AND A.SETID      = $setid_location
    AND A.EFFDT     <= $SEG_END_DATE
    AND A.EFFDT      = (SELECT MAX(B.EFFDT) FROM PS_LOCATION_TBL B
                         WHERE B.LOCATION   = A.LOCATION
                           AND B.SETID      = A.SETID
                           AND B.EFF_STATUS = 'A'
                           AND B.EFFDT     <= $SEG_END_DATE )
    AND A.LANGUAGE_CD = $language_cd
END-SELECT
   End-If
end-procedure

!************************************************************************
! Procedure Line-Control
!************************************************************************
begin-procedure Line-Control
#debug do Fin-Debug-Msg('Body-Control')

  If #Bodyline > 69
     do Add_Report_Translation ($ReportID, $language_cd)
     do Get_Field_Information ('GPNLPYS1', 'NEXT_PAGE_LIT',  $NEXT_PAGE_LIT,   #DW)
     print $Next_Page_Lit (71,80)
     do print-footer
     new-page
     do Print-Header
     let #Bodyline = {START_BODY}
     add 1 to #bodyline
  End-If

end-procedure
!--------------------------------------------------------------------!
! Debug Msg                                                          !
!--------------------------------------------------------------------!
begin-procedure Fin-Debug-Msg($procedure_name)
   display ' '
   display '----------------------------------'
   display $procedure_name
   #debugt date-time () {Native-DateTime} &SysDateTime
   #debugt move &SysDateTime to $SysDateTime
   #debugt show 'TIMING, ' $procedure_name ', ' $SysDateTime
   display ' '
end-procedure ! Fin-Debug-Msg

!************************************************************************
! Procedure Clean-Temp-Tables
!************************************************************************
begin-procedure Clean-Temp-Tables

let $sql-statement = 'GPNLPYS1.sqr, Clean-Temp-Tables'

BEGIN-SQL ON-ERROR=SQL-Error
DELETE FROM PS_GPNL_PS_PIN_TMP
WHERE JOBINSTANCE = #TMPJOBINSTANCE
END-SQL

BEGIN-SQL ON-ERROR=SQL-Error
DELETE FROM PS_GPNL_PS_SEC_TMP
WHERE JOBINSTANCE = #TMPJOBINSTANCE
END-SQL

BEGIN-SQL ON-ERROR=SQL-Error
DELETE FROM PS_GPNL_PS_SEG_TMP
WHERE JOBINSTANCE = #TMPJOBINSTANCE
END-SQL

BEGIN-SQL ON-ERROR=SQL-Error
DELETE FROM PS_GPNL_PS_GUI_TMP
WHERE JOBINSTANCE = #TMPJOBINSTANCE
END-SQL

BEGIN-SQL ON-ERROR=SQL-Error
DELETE FROM PS_GPNL_PS_ERN_TMP
WHERE JOBINSTANCE = #TMPJOBINSTANCE
END-SQL

BEGIN-SQL ON-ERROR=SQL-Error
DELETE FROM PS_GPNL_PS_ACU_TMP
WHERE JOBINSTANCE = #TMPJOBINSTANCE
END-SQL

BEGIN-SQL ON-ERROR=SQL-Error
DELETE FROM PS_GPNL_PSLIP_TMP
WHERE JOBINSTANCE = #TMPJOBINSTANCE
END-SQL

end-procedure


!***************************************************************
!Include SQCs
!***************************************************************
#Include 'reset.sqc'     !Reset printer procedure
#Include 'curdttim.sqc'  !Get-Current-DateTime procedure
#Include 'datetime.sqc'  !Routines for date and time formatting
#Include 'number.sqc'    !Routines to format numbers
#Include 'stdapi.sqc'    !Update Process API
#include 'datemath.sqc'  !Routines for date calculation
#Include 'readmsgc.sqc'   !Read the Message Catalog Table
#Include 'useprntr.sqc'
#Include 'getmonnm.sqc'
#Include 'sqrtrans.sqc'
#Include 'readxlat.sqc'   !Read Translate Table
#include 'gpsspslp.sqc'



