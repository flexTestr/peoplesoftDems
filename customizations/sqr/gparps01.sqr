!***********************************************************************
!  GPARPS01:  Pay Splip Reports                                        *
!***********************************************************************
!***********************************************************************
!                                                                      *
!                                                                      *
!                                                                      *
!                                                                      *
! This software and related documentation are provided under a         *
! license agreement containing restrictions on use and                 *
! disclosure and are protected by intellectual property                *
! laws. Except as expressly permitted in your license agreement        *
! or allowed by law, you may not use, copy, reproduce,                 *
! translate, broadcast, modify, license, transmit, distribute,         *
! exhibit, perform, publish or display any part, in any form or        *
! by any means. Reverse engineering, disassembly, or                   *
! decompilation of this software, unless required by law for           *
! interoperability, is prohibited.                                     *
! The information contained herein is subject to change without        *
! notice and is not warranted to be error-free. If you find any        *
! errors, please report them to us in writing.                         *
!                                                                      *
!                                                                      *
! Copyright (C) 1988, 2018, Oracle and/or its affiliates.              *
! All Rights Reserved.                                                 *
!***********************************************************************
!                                                                      *
!       $Release:  HR92                                                *
!           $Bug:  28059364                                            *
!                                                                      *
!***********************************************************************
#include 'setenv.sqc'   !Set environment
#include 'number.sqc'   !Routines to format numbers
#include 'datetime.sqc' !Routines for date and time formatting
#include 'prcsapi.sqc'  !Replaces stdapi.sqc
!#include 'setup01.sqc'  !Portrait
#include 'setup02.sqc'  !Landscape

Begin-Setup
  Declare Printer
  left-margin= 0
  font      =  3
  Point-Size = 10
  page-size 52 127
End-Setup


begin-report
  Do Init-Processing
  Do Select-Parameters
  Do GP-ePay-Init     ! Initialize ePay variables
  Do Process-Main
  dO GP-ePay-Control
  DO InsMobPayslip
  Do Update-Prcs-Run-Status
  date-time () hh:mi:ss &timeEnded
  display 'Report Ended: ' noline
  display &timeEnded
end-report


!***********************************************************************
!                                                                      *
! Init-Processing                                                      *
!                                                                      *
!                                                                      *
! Description:  Standard PeopleSoft initialization processing.         *
!                                                                      *
!***********************************************************************
Begin-Procedure Init-Processing
  move 'GPARPS01'         To $ReportID
  move 'Payslip'          To $ReportTitle
  move 'Ver. PI#27-01'  to $ReportVersion 
  show $ReportID
  show $ReportTitle
  Show $ReportVersion 
  Do Init-Datetime
  Do Get-Current-Datetime
  date-time () hh:mi:ss &timeBegan
  display 'Report Began: ' noline
  display &timeBegan
  Do Define-Prcs-Vars
  Do Get-Run-Control-Parms
  Do Init-Report
  Do Init-Number
  Do Get-Language-Codes
  Let $language_cd = $curr_language_cd
End-Procedure ! Init-Processing


!***********************************************************************
!                                                                      *
! Init-Report                                                          *
!                                                                      *
!                                                                      *
! Description:  Initializes defines values                             *
!                                                                      *
!***********************************************************************
Begin-Procedure Init-Report
  
  #define HDR_lin_1    6
  #define HDR_lin_2    7
  #define HDR_lin_3    8
  #define HDR_lin_4    9

  #define HDR_col_1_1  3
  #define HDR_col_1_2  19
  #define HDR_col_1_3  52
  #define HDR_col_1_4  67
  #define HDR_col_1_5  83
  #define HDR_col_1_6  116

  #define HDR_col_3_3  52
  #define HDR_col_3_6  116

  #define HDR_col_4_1  3
  #define HDR_col_4_2  35
  #define HDR_col_4_3  52
  #define HDR_col_4_4  67
  #define HDR_col_4_5  99
  #define HDR_col_4_6  116


  #define LIN_lin_1    12

  #define LIN_col_1_1  3
  #define LIN_col_1_2  8
  #define LIN_col_1_3  40
  #define LIN_col_1_4  52

  #define LIN_col_2_1  67
  #define LIN_col_2_2  72
  #define LIN_col_2_3  104
  #define LIN_col_2_4  116

  #define TOT_col_1_1  28
  #define TOT_col_1_2  40
  #define TOT_col_1_3  52
  #define TOT_col_1_4  92
  #define TOT_col_1_5  104
  #define TOT_col_1_6  116

  #define Draft_1      8
  #define Draft_2      72

  #define TOT_col_3_1  8
  #define TOT_col_3_2  72
  #Define Prd    GPARG
End-Procedure ! Init-Report



!***********************************************************************
!                                                                      *
! Select-Parameters                                                    *
!                                                                      *
!                                                                      *
! Called By:    Report                                                 *
!                                                                      *
!***********************************************************************
Begin-Procedure Select-Parameters

Begin-Select
X.COMPANY
X.CAL_RUN_ID
X.GP_ELN_APP
X.GP_ELN_APP_CNTRY

  LET $Company      = LTRIM(RTRIM(&X.COMPANY ,' '),' ')
  LET $CAL_RUN_ID   = LTRIM(RTRIM(&X.CAL_RUN_ID ,' '),' ')
  LET $GPAR_ELN_APP = LTRIM(RTRIM(&X.GP_ELN_APP ,' '),' ')
  LET $GPAR_ELN_APP_CNTRY = LTRIM(RTRIM(&X.GP_ELN_APP_CNTRY ,' '),' ')

FROM PS_GPAR_RC_PAYSLIP X
WHERE X.OPRID = $PRCS_OPRID
AND X.RUN_CNTL_ID = $PRCS_RUN_CNTL_ID
End-Select
DO InitMobPayslip
SHOW '  ' 
SHOW 'Company     : ' &X.COMPANY
SHOW 'Cal Run Id  : ' &X.CAL_RUN_ID
SHOW 'Country     : ' $GPAR_ELN_APP_CNTRY
SHOW 'Application : ' $GPAR_ELN_APP
SHOW '  ' 
End-Procedure Select-Parameters



!***********************************************************************
!                                                                      *
! Process-Main                                                         *
!                                                                      *
!                                                                      *
! Called By:    Report                                                 *
!                                                                      *
!***********************************************************************
Begin-Procedure Process-Main
#debug show 'PROCEDURE: Process-Main'

Begin-Select 
AC.COMPANY
AC.DESCR
AC.ADDRESS1
AC.ADDRESS2
AC.ADDRESS3
AC.ADDRESS4
AC.CITY
AC.STATE
AC.POSTAL

FROM PS_COMPANY_TBL AC
WHERE AC.EFFDT = (SELECT MAX(AA.EFFDT) FROM PS_COMPANY_TBL AA WHERE AA.COMPANY = AC.COMPANY AND AA.EFFDT <= $AsOfToday)
AND AC.COMPANY = &X.COMPANY
End-Select

Begin-Select 
AD.CUIT_ARG
AD.DGI_ARG
FROM PS_COMPANY_TBL_ARG AD
WHERE AD.EFFDT = (SELECT MAX(AA.EFFDT) FROM PS_COMPANY_TBL AA WHERE AA.COMPANY = AD.COMPANY AND AA.EFFDT <= $AsOfToday)
AND AD.COMPANY = &X.COMPANY
End-Select

Begin-Select
CL.DESCR

FROM PS_COMPNY_TBL_LANG CL
WHERE CL.EFFDT = (SELECT MAX(CL2.EFFDT) FROM PS_COMPANY_TBL CL2 WHERE CL2.COMPANY = CL.COMPANY AND CL2.EFFDT <= $AsOfToday)
AND CL.COMPANY = &X.COMPANY
AND CL.LANGUAGE_CD = $language_cd
End-Select

If &CL.DESCR = ''
  Let $CompanyDescr = &AC.DESCR
Else
  Let $CompanyDescr = &CL.DESCR
End-If

#Debug Show 'DESCR : ' $CompanyDescr
#Debug Show 'CUIT : ' &AD.CUIT_ARG
#Debug Show 'Address'
#Debug Show &AC.ADDRESS1 ' ' &AC.ADDRESS2 ' ' &AC.ADDRESS3 ' ' &AC.ADDRESS4
#Debug Show &AC.CITY ' ' &AC.STATE ' ' &AC.POSTAL

Let $CompanyHDR1 = &AD.CUIT_ARG || ' ' || $CompanyDescr
Let $CompanyHDR2 = &AC.ADDRESS1 || ' ' || &AC.ADDRESS2 || ' ' || &AC.ADDRESS3 || ' ' || &AC.ADDRESS4
Let $CompanyHDR3 = &AC.CITY || ' ' || &AC.STATE || ' ' || &AC.POSTAL

Begin-Select
B.EMPLID            () ON-BREAK PRINT=NEVER LEVEL=1  SAVE=$gpar_emplid
B.EMPL_RCD          () ON-BREAK PRINT=NEVER LEVEL=2
C.PIN_TYPE
C.PIN_NUM
C.DESCR
B.EFFDT
B.DEPTID
B.JOBCODE
B.LOCATION
B.SETID_DEPT
B.SETID_JOBCODE
B.SETID_LOCATION
A.CAL_RUN_ID
A.GP_PAYGROUP
A.CAL_ID
{dateout-prefix}D.SERVICE_DT{dateout-suffix} &D.SERVICE_DT
{dateout-prefix}B.TERMINATION_DT{dateout-suffix} &B.TERMINATION_DT
E.PYMT_DT           () ON-BREAK PRINT=NEVER LEVEL=3
A.RSLT_SEG_NUM      () ON-BREAK PRINT=NEVER BEFORE=PrintPaySplip AFTER=GP-ePay-Guide LEVEL=4
A.CALC_RSLT_VAL
A.CALC_ADJ_VAL
A.UNIT_RSLT_VAL
F.PYE_CALC_STAT
E.RUN_TYPE
A.SLICE_BGN_DT
A.SLICE_END_DT
F.PIN_NET_VAL
F.PIN_GROSS_VAL
G.PRD_BGN_DT
G.PRD_END_DT
A.INSTANCE

  let #unit_rslt_val = round(&A.UNIT_RSLT_VAL,0)
!  LET $PaySlipID = &A.CAL_ID||CHR(95)||&B.EMPLID||EDIT(TO_CHAR(&B.EMPL_RCD),'000')||EDIT(TO_CHAR(&A.RSLT_SEG_NUM),'00')
  LET $PaySlipID = &A.CAL_ID||CHR(95)||&B.EMPLID||EDIT(TO_CHAR(&B.EMPL_RCD),'000')||TO_CHAR(&A.RSLT_SEG_NUM)
  print #unit_rslt_val (#PrintLine,{LIN_col_1_1}) edit 9999B
  print #unit_rslt_val (#PrintLine,{LIN_col_2_1}) edit 9999B


  LET $Emplid           = LTRIM(RTRIM(&B.EMPLID,' '), ' ')
  LET #Empl_Rcd         = &B.EMPL_RCD
  LET $Cal_ID           = LTRIM(RTRIM(&A.CAL_ID,' '), ' ')
  LET $Cal_Run_Id       = LTRIM(RTRIM(&A.CAL_RUN_ID,' '), ' ')
  LET #RsltSegNum       = &A.RSLT_SEG_NUM
  LET $SegBgnDt         = LTRIM(RTRIM(&A.SLICE_BGN_DT,' '), ' ')
  LET $SegEndDt         = LTRIM(RTRIM(&A.SLICE_END_DT,' '), ' ')
  LET $Begin_Dt         = LTRIM(RTRIM(&G.PRD_BGN_DT  ,' '), ' ')
  LET $End_Dt           = LTRIM(RTRIM(&G.PRD_END_DT  ,' '), ' ')
  LET $pay_date         = LTRIM(RTRIM(&E.PYMT_DT     ,' '), ' ')
  LET $RUN_TYPE         = LTRIM(RTRIM(&E.RUN_TYPE    ,' '), ' ')
  LET $gv_paygp         = LTRIM(RTRIM(&A.GP_PAYGROUP ,' '), ' ')
  LET #Instance         = &A.INSTANCE

  Do ElementDescription
  Print $Descr_PinDescr (#PrintLine,{LIN_col_1_2})
  Print $Descr_PinDescr (#PrintLine,{LIN_col_2_2})

  Let $setidLoc       = LTRIM(RTRIM(&B.SETID_LOCATION,' '), ' ')
  Let $setidJob       = LTRIM(RTRIM(&B.SETID_JOBCODE ,' '), ' ')
  Let $setidDept      = LTRIM(RTRIM(&B.SETID_DEPT    ,' '), ' ')

  Let #Total_ER       = &F.PIN_GROSS_VAL
  Let #Total_ER-DD    = &F.PIN_NET_VAL

  LET #acumtot_er     = #Total_ER
  LET #amount_netpay  = #Total_ER-DD

  Let #calc_rslt_val = Round(&A.CALC_RSLT_VAL,2) + Round(&A.CALC_ADJ_VAL,2)
  If &C.PIN_TYPE = 'ER'
    LET $Gp_Arg_Type = 'Earnings'
    LET $MV40   = '10'
    LET #MV41   = 10  
    Print #calc_rslt_val (#PrintLine,{LIN_col_1_3}) Edit 99999999.99
    Print #calc_rslt_val (#PrintLine,{LIN_col_2_3}) Edit 99999999.99
    !Let #Total_ER = #Total_ER + #calc_rslt_val
  Else
    LET $Gp_Arg_Type = 'Deductions'
    LET $MV40   = '20'
    LET #MV41   = 20  
    Print #calc_rslt_val (#PrintLine,{LIN_col_1_4}) Edit 99999999.99
    Print #calc_rslt_val (#PrintLine,{LIN_col_2_4}) Edit 99999999.99
    !Let #Total_DD = #Total_DD + #calc_rslt_val
  End-If

  let #PrintLine = #PrintLine + 1
  let #PrintContinue = 0!
  Let #print_total =1
  
  if #PrintLine > 48 
    let #PrintContinue = 1
    Let #print_total =0
    do PrintPaySplip 
  end-if
  
  DO Gpar_Mpslp_Ed

FROM PS_GP_RSLT_ERN_DED A, 
     PS_JOB B, PS_GP_PIN C,
     PS_PER_ORG_ASGN D, 
     PS_GP_CALENDAR E, 
     PS_GP_PYE_SEG_STAT F,
     PS_GP_CAL_RUN_DTL G
WHERE B.EMPLID = A.EMPLID 
AND B.EMPL_RCD = A.EMPL_RCD 
AND D.EMPLID = A.EMPLID 
AND D.EMPL_RCD = A.EMPL_RCD 
AND B.EFFDT  = ( SELECT MAX(C.EFFDT)   FROM PS_JOB C  WHERE C.EMPLID = B.EMPLID AND C.EMPL_RCD = B.EMPL_RCD AND C.EFFDT <= A.SLICE_END_DT) 
AND B.EFFSEQ = ( SELECT MAX(D.EFFSEQ)  FROM PS_JOB D WHERE D.EMPLID = B.EMPLID AND D.EMPL_RCD = B.EMPL_RCD AND D.EFFDT = B.EFFDT) 
AND C.PIN_NUM = A.PIN_NUM
AND C.PIN_TYPE IN ('ER','DD')
AND E.GP_PAYGROUP = A.GP_PAYGROUP
AND E.CAL_ID = A.CAL_ID    
AND F.EMPLID = A.EMPLID 
AND F.CAL_RUN_ID = A.CAL_RUN_ID
AND F.EMPL_RCD = A.EMPL_RCD 
AND F.GP_PAYGROUP = A.GP_PAYGROUP
AND F.CAL_ID = A.CAL_ID
AND F.ORIG_CAL_RUN_ID = A.ORIG_CAL_RUN_ID
AND F.RSLT_SEG_NUM = A.RSLT_SEG_NUM
AND B.COMPANY = &X.COMPANY
AND A.CAL_RUN_ID = &X.CAL_RUN_ID
AND A.CAL_RUN_ID = A.ORIG_CAL_RUN_ID
AND G.CAL_RUN_ID = A.CAL_RUN_ID
AND G.GP_PAYGROUP = A.GP_PAYGROUP
AND G.CAL_ID      = A.CAL_ID    
AND G.RUN_TYPE    = E.RUN_TYPE
AND (A.CALC_RSLT_VAL <> 0 
  OR A.CALC_ADJ_VAL <> 0)
ORDER BY B.EMPLID, B.EMPL_RCD, E.PYMT_DT, A.RSLT_SEG_NUM, C.PIN_TYPE DESC, C.PIN_NUM
End-Select

End-Procedure ! Process-Main


!***********************************************************************
!                                                                      *
! Procedure - ElementDescription                                       *
!                                                                      *
!***********************************************************************
Begin-Procedure ElementDescription
#Debug show 'Procedure             : ElementDescription'
#Debug show '    $Base_Lang        : ' $Base_Lang
#Debug show '    $language_cd      : ' $language_cd
#Debug show '    $Curr_language_Cd : ' $Curr_language_Cd

let $Descr_PinDescr = ''
 
Begin-Select
PD.DESCR

 MOVE &PD.DESCR TO $Descr_PinDescr
 
FROM PS_GP_PIN_LANG PD
WHERE PD.PIN_NUM = &C.PIN_NUM
AND PD.LANGUAGE_CD = $language_cd
End-Select

If $Descr_PinDescr  = ''

Begin-Select
PD1.DESCR

 MOVE &PD1.DESCR TO $Descr_PinDescr
 
FROM PS_GP_PIN PD1
WHERE PD1.PIN_NUM = &C.PIN_NUM
End-Select

End-If

End-Procedure

!***********************************************************************
!                                                                      *
! Procedure - NameAndDescriptions                                      *
!                                                                      *
!***********************************************************************
Begin-Procedure NameAndDescriptions
#Debug show 'Procedure             : NameAndDescriptions'
#Debug show '    $Base_Lang        : ' $Base_Lang
#Debug show '    $language_cd      : ' $language_cd
#Debug show '    $Curr_language_Cd : ' $Curr_language_Cd

Begin-Select
LC.LANGUAGE_CD
  Let $Base_lang = rtrim(ltrim(&LC.LANGUAGE_CD,' '),' ')
FROM PSOPTIONS LC
End-Select

Begin-Select 
Y.DEFAULT_SETID &Setid
FROM PS_COMPANY_TBL Y
WHERE Y.EFFDT = (SELECT MAX(YY.EFFDT) FROM PS_COMPANY_TBL YY WHERE YY.COMPANY = Y.COMPANY AND YY.EFFDT <= &B.EFFDT)
AND Y.COMPANY = &X.COMPANY
End-Select

Begin-Select 
Y.NAME &EmplName
FROM PS_PERSONAL_DATA Y
WHERE Y.EMPLID = &B.EMPLID
End-Select

Let $NationalId = 'NA'
Begin-Select 
Y.NATIONAL_ID
  Let $NationalId = &Y.NATIONAL_ID
  Let $NationalId = substr($NationalId, 1, 2)||'-'||rtrim(substr($NationalId, 3, 8),' ') ||'-'||substr($NationalId,11, 1)
FROM PS_PERS_NID Y
WHERE Y.EMPLID = &B.EMPLID
AND Y.COUNTRY = 'ARG' 
AND Y.NATIONAL_ID_TYPE = 'CUIL'
End-Select


!*****************************************
! Department Description
!*****************************************

Let $Descr_Department = ''

Begin-Select
DD1.DESCR

  MOVE &DD1.DESCR   TO $Descr_Department
  
FROM PS_DEPT_TBL_LANG DD1
WHERE DD1.SETID   = $SetidDept
AND   DD1.DEPTID  = &B.DEPTID
AND   DD1.LANGUAGE_CD = $language_cd
AND   DD1.EFFDT   =  (SELECT MAX(EFFDT) FROM PS_DEPT_TBL_LANG
                      WHERE SETID    = DD1.SETID
                      AND   DEPTID   = DD1.DEPTID
                      AND   LANGUAGE_CD = DD1.LANGUAGE_CD
                      AND   EFFDT <= &B.EFFDT)
End-Select

If $Descr_Department = ''

Begin-Select
DD.DESCR

  MOVE &DD.DESCR   TO $Descr_Department
  
FROM PS_DEPT_TBL DD
WHERE DD.SETID   = $SetidDept
AND   DD.DEPTID  = &B.DEPTID
AND   DD.EFFDT   = (SELECT MAX(EFFDT) FROM PS_DEPT_TBL
                   WHERE SETID    = DD.SETID
                   AND   DEPTID   = DD.DEPTID
                   AND   EFFDT <= &B.EFFDT)
AND   DD.EFF_STATUS = 'A'                   
End-Select

End-If

#Debug show '    $Descr_Department : ' $Descr_Department


!*****************************************
! Job Code Description
!*****************************************

let $Descr_JobCode = ''
 
Begin-Select
JC.DESCR

 MOVE &JC.DESCR TO $Descr_JobCode
 
FROM PS_JOBCODE_LANG JC
WHERE JC.SETID = $SetidJob
AND JC.JOBCODE = &B.JOBCODE
AND JC.LANGUAGE_CD = $language_cd
AND JC.EFFDT = (SELECT MAX(EFFDT) 
                FROM PS_JOBCODE_LANG
                WHERE SETID = JC.SETID
                AND JOBCODE = JC.JOBCODE
                AND LANGUAGE_CD = JC.LANGUAGE_CD
                AND EFFDT <= &B.EFFDT)
End-Select

If $Descr_JobCode  = ''

Begin-Select
JC1.DESCR

 MOVE &JC1.DESCR TO $Descr_JobCode
 
FROM PS_JOBCODE_TBL JC1
WHERE JC1.SETID = $SetidJob
AND JC1.JOBCODE = &B.JOBCODE
AND JC1.EFFDT = (SELECT MAX(EFFDT) 
                FROM PS_JOBCODE_TBL
                WHERE SETID = JC1.SETID
                AND JOBCODE = JC1.JOBCODE
                AND EFFDT <= &B.EFFDT)
AND JC1.EFF_STATUS = 'A'
End-Select

End-If

#Debug show '    $Descr_JobCode    : ' $Descr_JobCode

!*****************************************
! Location Description
!*****************************************
  let $Descr_Location = ''
  
  Let $myLocation = &B.LOCATION
  
Begin-Select
L1.DESCR

  MOVE &L1.DESCR TO $Descr_Location
  
FROM PS_LOCATION_LANG L1
WHERE L1.SETID = $SetidLoc
AND L1.LOCATION = $myLocation
AND L1.LANGUAGE_CD = $language_cd
AND L1.EFFDT = (SELECT MAX(EFFDT) FROM PS_LOCATION_LANG L2
                WHERE L2.SETID = L1.SETID
                AND L2.LOCATION = L1.LOCATION
                AND L2.LANGUAGE_CD = L1.LANGUAGE_CD
                AND L2.EFFDT <= &B.EFFDT)
End-Select

IF $Descr_Location = ''

Begin-Select
L.DESCR

  MOVE &L.DESCR TO $Descr_Location

FROM PS_LOCATION_TBL L
WHERE L.SETID = $SetidLoc
AND L.LOCATION = $myLocation
AND L.EFFDT = (SELECT MAX(EFFDT) FROM PS_LOCATION_TBL
              WHERE SETID = L.SETID
              AND LOCATION = L.LOCATION
              AND EFFDT <= &B.EFFDT)
AND L.EFF_STATUS = 'A'
End-Select
End-If
#Debug show '    $Descr_Location   : ' $Descr_Location
End-Procedure NameAndDescriptions



!***********************************************************************
!                                                                      *
! Procedure - PrintPaySplip                                            *
!                                                                      *
!***********************************************************************
Begin-Procedure PrintPaySplip

    If $IsFirstPage = 'N'
      NEW-PAGE
    End-If
    Let $IsFirstPage = 'N'


    Let #pagenum=#pagenum+1
  

    let #PrintLine = {LIN_lin_1}
   
    if #PrintContinue = 0
    Let #page_ini = #pagenum    
    Let #Total_ER = 0
    Let #Total_DD = 0
    end-if
    
    Do NameAndDescriptions

    Print $CompanyHDR1   ( 2, {HDR_col_1_1})
    Print $CompanyHDR1   ( 2, {HDR_col_1_4})
    Print $CompanyHDR2   ( 3, {HDR_col_1_1})
    Print $CompanyHDR2   ( 3, {HDR_col_1_4})
    Print $CompanyHDR3   ( 4, {HDR_col_1_1})
    Print $CompanyHDR3   ( 4, {HDR_col_1_4})


    Print &B.EMPLID ({HDR_lin_1},{HDR_col_1_1},12)
    Print &B.EMPLID ({HDR_lin_1},{HDR_col_1_4},12)

    Let   $EmplName = UPPER(&EmplName)
    Print $EmplName ({HDR_lin_1},{HDR_col_1_2},30) Bold
    Print $EmplName ({HDR_lin_1},{HDR_col_1_5},30) Bold

    Do Format-DateTime(&E.PYMT_DT, $out, {DEFDMY}, '', '')
    Print $out ({HDR_lin_1},{HDR_col_1_3})
    Print $out ({HDR_lin_1},{HDR_col_1_6})

    Print $Descr_Department ({HDR_lin_2},{HDR_col_1_1})
    Print $Descr_Department ({HDR_lin_2},{HDR_col_1_4})

    Print $Descr_JobCode ({HDR_lin_3},{HDR_col_1_1})
    Print $Descr_JobCode ({HDR_lin_3},{HDR_col_1_4})

    Print $Descr_Location ({HDR_lin_4},{HDR_col_4_1})
    Print $Descr_Location ({HDR_lin_4},{HDR_col_4_4})

    Print $NationalId ({HDR_lin_4},{HDR_col_4_2})
    Print $NationalId ({HDR_lin_4},{HDR_col_4_5})

    Do Format-DateTime(&D.SERVICE_DT, $out, {DEFDMY}, '', '')
    #Debug show '  &D.SERVICE_DT : ' &D.SERVICE_DT
    #Debug show '  $out          : ' $out
    Print $out ({HDR_lin_3},{HDR_col_3_3})
    Print $out ({HDR_lin_3},{HDR_col_3_6})

    Do Format-DateTime(&B.TERMINATION_DT, $out, {DEFDMY}, '', '')
    #Debug show '  &B.TERMINATION_DT : ' &B.TERMINATION_DT
    #Debug show '  $out          : ' $out
    Print $out ({HDR_lin_4},{HDR_col_4_3})
    Print $out ({HDR_lin_4},{HDR_col_4_6})

End-Procedure PrintPaySplip


Begin-footing  3


if #print_total = 1

  !Let #Total_ER-DD = #Total_ER - #Total_DD
  Let #Total_DD = #Total_ER -  #Total_ER-DD

  Print #Total_ER (1,{TOT_col_1_2}) Edit 99999999.99
  Print #Total_ER (1,{TOT_col_1_5}) Edit 99999999.99
  Print #Total_DD (1,{TOT_col_1_3}) Edit 99999999.99
  Print #Total_DD (1,{TOT_col_1_6}) Edit 99999999.99
      
  Print #Total_ER-DD (1,{TOT_col_1_1}) Edit 99999999.99
  Print #Total_ER-DD (1,{TOT_col_1_4}) Edit 99999999.99

  Let #NetPay = #Total_ER-DD
  do Net-Pay-In-Spanish
  Print $NetInWords (3,{TOT_col_3_1})
  Print $NetInWords (3,{TOT_col_3_2})

  If &F.PYE_CALC_STAT <> '70' and &F.PYE_CALC_STAT <> '75'
    Do Init_Report_Translation ('GPARPS01', $language_cd)
    Do Append_Report_Translation ('GPARPS01')
    Do Get_Field_Information ('GPARPS01', 'DRAFT',  $Report,   #DW)
    Let $pye_calc_stat = $Report
    Print $pye_calc_stat (2,{Draft_1}) Bold
    Print $pye_calc_stat (2,{Draft_2}) Bold
  End-if  

 Let #Total_ER = 0
 Let #Total_DD = 0
 Let #Total_ER-DD =0 
End-if
  
End-footing



!***********************************************************************
! For ePay Purposes: Online Payslip                                    *
! Procedure : GP-ePay-Init                                             *
! Initialize variables that we’ll use for ePay processing              *
!***********************************************************************
Begin-procedure GP-ePay-Init
  Let $sql-statement = 'GPARPS01.sqr, GP-ePay-Init '

  Do Check-ePay-installed ($ePay_Installed)

  If $ePay_Installed = 'Y'
    Move 'GPARPS01' to $ReportID    

    let #eV4 =  #prcs_process_instance

    !* have the Output Working Directory from the Process parameters table
    do Get-Output-Wrk-Directory(#eV4, $PRCSOUTPUTDIR, $PRCSNAME)

    !* have the URL id from the Payslip option table
    do Get-ePay-URLid ('ARG', $eV18)

    !* Global ePay variable settings used in GP-ePay procedures in this SQR
    let $eV1 = $prcs_oprid
    let $eV2 = $prcs_run_cntl_id
    let $eV3 = $ReportID          !used for proc name instaed of prcsname from output wrk dir

    ! Open the file for writing epay control data
    ! Let $GUIDEFILE   = $eV3 || '.txt'
    ! Let $FILELAYOUT = 'GP_SS_PSLP_TMP'
    ! Do Open-ePay-Guide-DataFile($PRCSOUTPUTDIR,$GUIDEFILE)

    ! when we do not pass a control file
    Let $GUIDEFILE = ' '
    Let $FILELAYOUT = ' '
  End-If
End-procedure !GP-ePay-Init


!***********************************************************************
! For ePay Purposes: Online Payslip                                    *
! Procedure : GP-ePay-Guide                                            *
! Insert one row per payslip into the temporary guide table for ePay   *
!***********************************************************************
Begin-procedure GP-ePay-Guide
  let $sql-statement = 'GPARPS01.sqr,GP-ePay-Guide'
  If $ePay_Installed = 'Y'
    DO GP-Mobile-Payslip
    DO Insert_gpar_mpslp_acm
    Move #Empl_Rcd To $EmpRcd '000'
    ! For NLD we do need to retrieve the runtype value other CE's may have it already
    ! do Get-RUN-TYPE  !not an ePay procedure
    let $eV5 = ltrim(rtrim($gpar_emplid, ' '), ' ')
    let $eV6 = ltrim(rtrim(&A.CAL_RUN_ID,' '),' ')
    let $eV7 = 'GPARG'
    Move &A.RSLT_SEG_NUM To $rslt_seg_num

    let $eV8 = rtrim(&A.CAL_ID, ' ') || '_' || $eV5 || $EmpRcd || $rslt_seg_num    ! gp epay payslip id
    let $eV9 = &E.PYMT_DT
 
    let $eV10 = &A.SLICE_END_DT
    let $eV11 = &A.SLICE_BGN_DT

    Let #NetPaySlip = #amount_netpay
    let #eV12 = #NetPaySlip                                                 ! net pay
    SHOW  'Emplid: ' $Emplid ' #NetPaySlip: ' #NetPaySlip
    let $eV13 = rtrim(&A.CAL_RUN_ID, ' ')                                   ! payslip description
    If $eV13 = ''
      let $eV13 = 'No DESCRIPTION'
    end-if 
    let $eV14 = rtrim(&E.RUN_TYPE, ' ')
    
    
    let $eV15 = 'ORIG'               ! payslip status ORIGINAL
    let $eV16 = $eV5 || '_' || $eV6 || '_' ||$eV7 || '_' || $eV8 || '.pdf'   !sysfilename of the payslip pdf
    let $eV17 = $eV16                                                       !userfilename  - what the payee sees filename as
    let #eV19 = #page_ini                                                    !begin page number of payslip in output report
    let #eV20 = #pagenum                                                    !end page number of payslip in output report


    !Input:OPRID, RUN_CNTL_ID, PROCNAME, DATAINST, EMPLID, CAL_RUN_ID, SRCPRODUCT, 
    !GP_PSLP_ID,PYMT_DT,PRD_END_DT,PRD_BGN_DT,#NET_PAY,Payslip DESCR,RUN_TYPE, 
    !PSLP_STATUS,ATTACHSYSFILENAME, ATTACHUSERFILE,FILEURLID, BGNPGNBR,ENDPGNBR
    
    do Insert-ePay-Guide-Data($eV1,$eV2,$eV3,#eV4,$eV5,$eV6,$eV7,$eV8,$eV9,$eV10,$eV11,#eV12,$eV13,$eV14,$eV15,$eV16,$eV17,$eV18,#eV19,#eV20)
  End-If
End-procedure ! GP-ePay-Guide


!*=================================================================
! Procedure     :InitMobPayslip                                   !
! Comment       :This procedure Generate Mobile Payslip only if   !
!*=================================================================
begin-procedure InitMobPayslip
#debug show '      >>Init Mobile Paylslip CAL_RUN_ID: ' $CAL_RUN_ID

  DO init-mpslp($CAL_RUN_ID)
  LET $GPwhere_clause = ' '
  DO clean_mpslp_records ($CAL_RUN_ID, $GPwhere_clause)
  
#debug show ' GPinit_mob_payslp: ' $GPinit_mob_payslp

end-procedure InitMobPayslip

!*=================================================================
! Procedure     :GP-Mobile-Payslip                                !
! Comment       :This procedure Generate Mobile Payslip only if   !
!                Its Enabled                                      !
!*=================================================================
begin-procedure GP-Mobile-Payslip
#debug show '      >>Mobile Paylslip CAL_RUN_ID: ' $CAL_RUN_ID

  IF $GPinit_mob_payslp = 'Y' !Inserting in Temporal Record Header for Mobile
      DO Insert_gpar_mpslp_hdr
  END-IF

end-procedure GP-Mobile-Payslip
!*=================================================================
! Procedure     :InsMobPayslip                                    !
! Comment       :This procedure Insert Data In stage Tables for   !
!                Mobile Payslip                                   !
!*=================================================================
begin-procedure InsMobPayslip

  #debug show '        InsMobPayslip '
  LET #MobHdr = 0
  LET $GPwhere_batch = 'A.OPRID = '||CHR(39)||$prcs_oprid|| CHR(39)||' AND A.RUN_CNTL_ID = '||CHR(39)||$prcs_run_cntl_id||CHR(39) ||' AND A.PROCESS_INSTANCE = ' ||TO_CHAR(#prcs_process_instance)
  DO Record_Field_List('GP_MPSLP_STGHDR', $GPselectHDR)
  DO Record_Field_List('GP_MPSLP_STGED', $GPselectERDD)
  DO Record_Field_List('GP_MPSLP_STGACM', $GPselectACM)

  IF $GPinit_mob_payslp = 'Y'
    SHOW ' '
    SHOW '   Mobile Payslip Enabled'
    SHOW ' '
    DO insert_mpslp_hdr_batch($GPselectHDR, 'PS_GPAR_MPSP_HDR A', $GPwhere_batch)
    DO insert_mpslp_ernded_batch($GPselectERDD, 'PS_GPAR_MPSP_ED A', $GPwhere_batch)
    DO insert_mpslp_acum_batch($GPselectACM, 'PS_GPAR_MPSP_ACM A', $GPwhere_batch)
  END-IF
 DO Delete-Stage
end-procedure InsMobPayslipHdr


!***********************************************************************
! For ePay Purposes: Online Payslip                                    *
! Procedure : GP-ePay-Control                                          *
! Insert one row per report into the epay run control table            *
!***********************************************************************
begin-procedure GP-ePay-Control
  #debug show '        GP-ePay-Control '
  Let $sql-statement = 'GPARPS01.sqr,GP-ePay-Guide'
  If $ePay_Installed = 'Y'
    let $eCV7  = lower($ReportID) || '_' || $prcs_process_instance || '.PDF'
    let $eCV8  = rtrim($PRCSOUTPUTDIR, ' ')
    Do Insert-ePay-RunControl($eV1,$eV2,$eV3,'Y', 'Y', $GUIDEFILE, $eCV7,$eCV8,' ',$FILELAYOUT,#eV4,$eV18,'Y')
  End-If
end-procedure !GP-ePay-Control

!***********************************************************************
! For ePay Purposes: Mobile Payslip Field Structure                    *
!***********************************************************************
Begin-Procedure Record_Field_List($Recname, :$FieldList)

    LET $FieldList = ' '
begin-select
FLDLS.FIELDNAME
FLDLS.FIELDNUM
FLDLS.SUBRECORD

  If &FLDLS.SUBRECORD = 'Y'
    Do SubRec_Fields(&FLDLS.FIELDNAME, $SRecFields)
    LET $FieldList = $FieldList || $SRecFields
  Else
    LET $FieldList = $FieldList || &FLDLS.FIELDNAME || ' ,'
  End-If

FROM PSRECFIELD FLDLS
WHERE FLDLS.RECNAME =  $Recname
ORDER BY FLDLS.FIELDNUM
end-select
LET $FieldList   =  SUBSTR($FieldList , 1, LENGTH($FieldList)-2)
#debug5   SHOW $Recname ' = ' $FieldList
End-Procedure Record_FieldList

!***********************************************************************
! Retrieve SubRecord Field Structure                                   *
!***********************************************************************

Begin-Procedure SubRec_Fields($SRec, :$SubRecFields)
Let $SubRecFields = ''

Begin-Select
SREC.FIELDNAME

    Let $SubRecFields = $SubRecFields || &SREC.FIELDNAME || ' ,'

FROM PSRECFIELD SREC
WHERE SREC.RECNAME = $SRec
ORDER BY SREC.FIELDNUM
End-Select
End-Procedure SubRec_Fields



!***********************************************************************
! For ePay Purposes: Mobile Payslip Category                    *
!***********************************************************************
Begin-Procedure GetMobileCat($Country, $App, $Type, $Effdt, #Pin, :$MobileCat, :#Order)
#debug show ' $GetMobileCat>>   Country: ' $Country ', App: ' $App ', Type: ' $Type ', Effdt: ' $Effdt ' Pin: ' #Pin
LET $MobileCat = $Type
LET #Order     = 0
begin-select
C.GP_ORDER         &MobOrder
D.DESCR            &MobileCat

  LET $MobileCat  = LTRIM(RTRIM(&MobileCat, ' '),' ')
  LET #Order      = &MobOrder

FROM PS_GP_ELN_SET_LST  A, 
     PS_GP_ELN_PIN_ATTR B, 
     PS_GP_ELEM_GRP_MBR C, 
     PS_GP_ELN_LIST_PIN D
  WHERE  A.EFFDT            = (SELECT MAX(A_ED.EFFDT) FROM PS_GP_ELN_SET_LST A_ED 
                                 WHERE A.COUNTRY = A_ED.COUNTRY 
                                   AND A.GP_ELN_SET = A_ED.GP_ELN_SET 
                                   AND A_ED.EFFDT <= $Effdt) 
     AND A.COUNTRY          = $Country
     AND A.GP_ELN_SET       = $App
     AND A.GP_ELN_ATTR1     = $Type
     AND B.PIN_NUM          = #Pin
     AND A.COUNTRY          = D.COUNTRY
     AND A.GP_ELN_SET       = D.GP_ELN_APP
     AND B.GP_ELN_ATTR1     = D.GP_ELN_CODE
     AND B.PIN_ELEM_GRP_NUM = C.PIN_NUM
     AND B.PIN_NUM          = C.PIN_ELEM_NUM
     AND C.EFFDT            = (SELECT MAX(C_ED.EFFDT) FROM PS_GP_ELEM_GRP_MBR C_ED 
                                 WHERE C.PIN_NUM = C_ED.PIN_NUM 
                                   AND C_ED.EFFDT <= $Effdt)
     AND A.COUNTRY          = B.COUNTRY 
     AND A.GP_ELN_SET       = B.GP_ELN_SET 
     AND A.PIN_ELEM_GRP_NUM = B.PIN_ELEM_GRP_NUM 
     AND B.EFFDT            = (SELECT MAX(B_ED.EFFDT) FROM PS_GP_ELN_PIN_ATTR B_ED 
                                WHERE B.COUNTRY = B_ED.COUNTRY 
                                  AND B.GP_ELN_SET = B_ED.GP_ELN_SET 
                                  AND B_ED.EFFDT <= $Effdt)
end-select
#debug show ' $GetMobileCat>>   MobileCat: ' $MobileCat ', Order: ' $Order 
End-Procedure GetMobileCat



!***********************************************************************
! For ePay Purposes: gpar_mpslp_edr                    *
!***********************************************************************
Begin-Procedure Gpar_Mpslp_Ed
   DO GetMobileCat($GPAR_ELN_APP_CNTRY, $GPAR_ELN_APP, $Gp_Arg_Type,  &G.PRD_END_DT, &C.PIN_NUM, $MobileCat, #cont_e)
   DO Insert_gpar_mpslp_ed-Var
   DO Insert_gpar_mpslp_ed
End-Procedure Gpar_Mpslp_Ed


!***********************************************************************
! For ePay Purposes: Insert_gpar_mpslp_ed-Var                    *
!***********************************************************************
Begin-Procedure Insert_gpar_mpslp_ed-Var
LET $MV1    = $Emplid
LET $MV2    =  &X.CAL_RUN_ID
LET #MV3    = #Empl_Rcd 
LET $MV4    = $gv_paygp
LET $MV5    = $Cal_ID
LET $MV6    = $Cal_Run_Id
LET #MV7    = #RsltSegNum
LET #MV10   = #Instance
LET #MV11   = &C.PIN_NUM     
LET $MV12   = $SegBgnDt
LET $MV13   = $SegEndDt
LET #MV14   = #MV14      
LET #MV15   = #MV15      
LET #MV16   = #MV16      
LET $MV17   = $Begin_Dt
LET $MV18   = $End_Dt
LET $MV19   = $SegBgnDt
LET $MV20   = $SegEndDt
LET $MV21   = $pay_date
LET $MV22   = $RUN_TYPE
LET #MV23   = #calc_rslt_val
LET #MV24   = #MV24      
LET #MV25   = #MV25      
LET #MV26   = #MV26      
LET #MV27   = #MV27      
LET #MV28   = #MV28      
LET #MV29   = #unit_rslt_val
LET #MV30   = #MV30      
LET #MV31   = #MV31      
LET $MV32   = ' ' 
LET #MV33   =  0  
LET $MV34   = ' ' 
LET $MV35   = ' ' 
LET $MV36   = ' ' 
LET $MV37   = ' ' 
LET $MV38   = ' ' 
LET $MV39   = ' ' 
LET $MV42   = $MobileCat
LET #MV43   = #cont_e  
LET $MV44   = $Descr_PinDescr 
End-Procedure Insert_gpar_mpslp_ed-Var

!*=================================================================
! Procedure     :Insert_gpar_mpslp_hdr                            !
! Comment       :Insert in gpar_mpslp_hdr                         !
!*=================================================================
Begin-Procedure Insert_gpar_mpslp_hdr

begin-sql on-error=SQL-Error

INSERT INTO PS_GPAR_MPSP_HDR 
(OPRID, 
RUN_CNTL_ID, 
PROCESS_INSTANCE,
EMPLID, 
CAL_RUN_ID, 
EMPL_RCD, 
GP_PAYGROUP, 
CAL_ID, 
ORIG_CAL_RUN_ID, 
RSLT_SEG_NUM, 
GP_PSLP_SRCPRODUCT, 
GP_PSLP_ID,
SEG_BGN_DT,
SEG_END_DT,
PRD_BGN_DT,
PRD_END_DT,
PYMT_DT, 
GP_MPSLP_GROSS, 
CALC_DELTA_VAL, 
CALC_VAL, 
GP_MPSLP_NET, 
CALC_DELTA_VAL2, 
CALC_VAL2, 
GP_COMPANY, 
RUN_TYPE, 
GP_MPSLP_HDR1, 
GP_MPSLP_HDR2, 
GP_MPSLP_HDR3, 
GP_MPSLP_HDR4, 
GP_MPSLP_HDR5, 
GP_MPSLP_HDR6
) VALUES 
($prcs_oprid,
$prcs_run_cntl_id,
#prcs_process_instance,
$Emplid, 
$CAL_RUN_ID, 
#Empl_Rcd, 
$gv_paygp, 
$Cal_ID, 
$Cal_Run_Id, 
#RsltSegNum, 
'{Prd}', 
$PaySlipID, 
$SegBgnDt,
$SegEndDt,
$Begin_Dt,
$End_Dt,
$pay_date,
#acumtot_er, 
#MB16, 
#amount_netpay, 
#amount_netpay, 
#MB19, 
#MB20, 
$Company,
$RUN_TYPE,
' ',
' ',
' ',
' ',
' ', 
' ')
END-SQL

End-Procedure Insert_gpar_mpslp_hdr


!*=================================================================
! Procedure     :Insert_gpar_mpslp_ed                             !
! Comment       :Insert in gpar_mpslp_ed                          !
!*=================================================================
Begin-Procedure Insert_gpar_mpslp_ed

begin-sql on-error=SQL-Error

INSERT INTO PS_GPAR_MPSP_ED
(OPRID,
RUN_CNTL_ID,
PROCESS_INSTANCE,
EMPLID,
CAL_RUN_ID,
EMPL_RCD,
GP_PAYGROUP,
CAL_ID,
ORIG_CAL_RUN_ID,
RSLT_SEG_NUM,
GP_PSLP_SRCPRODUCT,
GP_PSLP_ID,
INSTANCE,
PIN_NUM,
SLICE_BGN_DT,
SLICE_END_DT,
PIN_ELEM_GRP_NUM,
ED_ASSIGN_INSTANCE,
PI_INSTANCE,
PRD_BGN_DT,
PRD_END_DT,
SEG_BGN_DT,
SEG_END_DT,
PYMT_DT,
RUN_TYPE,
CALC_RSLT_VAL,
CALC_ADJ_VAL,
CALC_DELTA_VAL,
BASE_RSLT_VAL,
BASE_ADJ_VAL,
RATE_RSLT_VAL,
UNIT_RSLT_VAL,
UNIT_ADJ_VAL,
PCT_RSLT_VAL,
RECIPIENT_ID,
RECIPIENT_TAG,
USER_FLD1,
USER_FLD2,
USER_FLD3,
USER_FLD4,
USER_FLD5,
USER_FLD6,
GP_MPSLP_SECTION,
GP_MPSLP_SPRNT_ORD,
GP_MPSLP_SUBSECTN,
GP_MPSLP_PRNT_ORD,
GP_MPSLP_PIN_DESCR,
GP_MPSLP_DISP_ZERO ,
GP_MPSLP_RET_ADJ ,
GP_MPSLP_SUM_INST ,
GP_MPSLP_ADD_TOTAL ,
GP_MPSLP_DISP_PYSL
) VALUES 
($prcs_oprid,
$prcs_run_cntl_id,
#prcs_process_instance,
$MV1,
$MV2, 
#MV3, 
$MV4, 
$MV5, 
$MV6, 
#MV7, 
'{Prd}', 
$PaySlipID, 
#MV10,
#MV11,
$MV12,
$MV13,
#MV14,
#MV15,
#MV16,
$MV17,
$MV18,
$MV19,
$MV20,
$MV21,
$MV22,
#MV23,
#MV24,
#MV25,
#MV26,
#MV27,
#MV28,
#MV29,
#MV30,
#MV31,
$MV32,
#MV33,
$MV34,
$MV35,
$MV36,
$MV37,
$MV38,
$MV39,
$MV40,
#MV41,
$MV42,
#MV43,
$MV44, 
'N',
'Y',
'N',
'Y',
'Y')
END-SQL

End-Procedure Insert_gpar_mpslp_ed


!*=================================================================
! Procedure     :Insert_gpar_mpslp_acm                            !
! Comment       :Insert in gpar_mpslp_acm                         !
!*=================================================================
Begin-Procedure Insert_gpar_mpslp_acm

begin-sql on-error=SQL-Error
INSERT INTO PS_GPAR_MPSP_ACM
(OPRID,
RUN_CNTL_ID,
PROCESS_INSTANCE,
EMPLID,
CAL_RUN_ID,
EMPL_RCD,
GP_PAYGROUP,
CAL_ID,
ORIG_CAL_RUN_ID,
GP_PSLP_SRCPRODUCT,
GP_PSLP_ID,
RSLT_SEG_NUM,
PIN_NUM,
EMPL_RCD_ACUM,
ACM_FROM_DT,
ACM_THRU_DT,
SEQ_NUM8,
PIN_ELEM_GRP_NUM,
SLICE_BGN_DT,
SLICE_END_DT,
SEG_BGN_DT,
SEG_END_DT,
PRD_BGN_DT,
PRD_END_DT,
PYMT_DT,
RUN_TYPE,
USER_KEY1,
USER_KEY2,
USER_KEY3,
USER_KEY4,
USER_KEY5,
USER_KEY6,
COUNTRY,
CALC_RSLT_VAL,
CALC_DELTA_VAL,
CALC_VAL,
GP_MPSLP_SECTION,
GP_MPSLP_SPRNT_ORD,
GP_MPSLP_SUBSECTN,
GP_MPSLP_PRNT_ORD,
GP_MPSLP_PIN_DESCR, 
GP_MPSLP_DISP_ZERO ,
GP_MPSLP_RET_ADJ ,
GP_MPSLP_SUM_INST ,
GP_MPSLP_ADD_TOTAL ,
GP_MPSLP_DISP_PYSL ,
GP_BASE ,
GP_UNIT ,
GP_RATE ,
GP_PERCENT 
) SELECT $prcs_oprid,
$prcs_run_cntl_id,
#prcs_process_instance,
C.EMPLID,
C.CAL_RUN_ID,    
C.EMPL_RCD,      
C.GP_PAYGROUP,   
C.CAL_ID,
C.ORIG_CAL_RUN_ID,
'{Prd}',
$PaySlipID,
C.RSLT_SEG_NUM,
C.PIN_NUM,
C.EMPL_RCD_ACUM,
C.ACM_FROM_DT,
C.ACM_THRU_DT,
C.SEQ_NUM8,
B.PIN_ELEM_GRP_NUM,
C.SLICE_BGN_DT,  
C.SLICE_END_DT,  
$SegBgnDt,       
$SegEndDt,       
$Begin_Dt,       
$End_Dt,         
$pay_date,       
$RUN_TYPE,       
C.USER_KEY1,     
C.USER_KEY2,     
C.USER_KEY3,     
C.USER_KEY4,     
C.USER_KEY5,     
C.USER_KEY6,     
C.COUNTRY,
C.CALC_RSLT_VAL, 
0,
C.CALC_VAL,
'40',
'40',
B.GP_ELN_ATTR1,
D.GP_ORDER,
E.DESCR,
'N',
'Y',
'N',
'Y',
'Y',
0,
0,
0,
0
FROM PS_GP_ELN_SET_LST A, 
     PS_GP_ELN_PIN_ATTR B, 
     PS_GP_RSLT_ACUM C, 
     PS_GP_ELEM_GRP_MBR D, 
     PS_GP_PIN E 
WHERE A.EFFDT             = (SELECT MAX(A_ED.EFFDT) FROM PS_GP_ELN_SET_LST A_ED 
                              WHERE A.COUNTRY     = A_ED.COUNTRY 
                                AND A.GP_ELN_SET  = A_ED.GP_ELN_SET 
                                AND A_ED.EFFDT   <= $SegEndDt) 
  AND A.COUNTRY           = $GPAR_ELN_APP_CNTRY
  AND A.GP_ELN_SET        = $GPAR_ELN_APP
  AND A.GP_ELN_ATTR1      = 'Balances' 
  AND C.EMPLID            = $Emplid
  AND C.CAL_RUN_ID        = $Cal_Run_Id
  AND C.EMPL_RCD          = #Empl_Rcd
  AND C.GP_PAYGROUP       = $gv_paygp
  AND C.CAL_ID            = $Cal_ID
  AND C.RSLT_SEG_NUM      = #RsltSegNum
  AND A.COUNTRY           = B.COUNTRY 
  AND A.GP_ELN_SET        = B.GP_ELN_SET 
  AND A.PIN_ELEM_GRP_NUM  = B.PIN_ELEM_GRP_NUM 
  AND B.EFFDT             = (SELECT MAX(B_ED.EFFDT) FROM PS_GP_ELN_PIN_ATTR B_ED 
                              WHERE B.COUNTRY     = B_ED.COUNTRY 
                                AND B.GP_ELN_SET  = B_ED.GP_ELN_SET 
                                AND B_ED.EFFDT   <= $SegEndDt) 
  AND B.COUNTRY           = C.COUNTRY 
  AND B.PIN_NUM           = C.PIN_NUM 
  AND C.ORIG_CAL_RUN_ID   = C.CAL_RUN_ID 
  AND C.CAL_RUN_ID        = $CAL_RUN_ID 
  AND A.PIN_ELEM_GRP_NUM  = D.PIN_NUM 
  AND D.EFFDT             = (SELECT MAX(D_ED.EFFDT) FROM PS_GP_ELEM_GRP_MBR D_ED 
                              WHERE D.PIN_NUM     = D_ED.PIN_NUM 
                                AND D_ED.EFFDT   <= $SegEndDt) 
  AND D.PIN_ELEM_NUM      = C.PIN_NUM 
  AND D.PIN_ELEM_NUM      = E.PIN_NUM 
END-SQL

End-Procedure Insert_gpar_mpslp_acm


!*=================================================================
! Procedure     :Delete-Stage                           !
! Comment       :Delete-Stage                         !
!*=================================================================
Begin-Procedure Delete-Stage

begin-sql on-error=SQL-Error
DELETE FROM  PS_GPAR_MPSP_HDR
WHERE PROCESS_INSTANCE <> #prcs_process_instance
END-SQL
begin-sql on-error=SQL-Error
DELETE FROM  PS_GPAR_MPSP_ACM
WHERE PROCESS_INSTANCE <> #prcs_process_instance
END-SQL
begin-sql on-error=SQL-Error
DELETE FROM  PS_GPAR_MPSP_ED
WHERE PROCESS_INSTANCE <> #prcs_process_instance
END-SQL
End-Procedure Delete-Stage
!***********************************************************************
!                                                                      *
!                                                                      *
! SQCS included in the report                                          *
!                                                                      *
!***********************************************************************
#include 'datemath.sqc'     ! Date conversion procedures
#include 'sqrtrans.sqc'     ! Translate SQR strings to given language
#include 'ldnumtrn.sqc'     ! Load-NumberLits-Array procedure
#include 'gparnwds.sqc'     ! Convert net-pay to word (spanish and english)
#include 'prcslng.sqc'      ! Replaces getrplng.sqc
#include 'readxlat.sqc'     ! Procedure to read values from xlattable
#Include 'gpsspslp.sqc'     !ePay SQC with ePay procedures
