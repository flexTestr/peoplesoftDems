!***********************************************************************
!  GPMXAA01:  Minimum Wage Salary Levels                               *
!***********************************************************************
!***********************************************************************
!                                                                      *
!                                                                      *
!                                                                      *
!                                                                      *
! This software and related documentation are provided under a         *
! license agreement containing restrictions on use and                 *
! disclosure and are protected by intellectual property                *
! laws. Except as expressly permitted in your license agreement        *
! or allowed by law, you may not use, copy, reproduce,                 *
! translate, broadcast, modify, license, transmit, distribute,         *
! exhibit, perform, publish or display any part, in any form or        *
! by any means. Reverse engineering, disassembly, or                   *
! decompilation of this software, unless required by law for           *
! interoperability, is prohibited.                                     *
! The information contained herein is subject to change without        *
! notice and is not warranted to be error-free. If you find any        *
! errors, please report them to us in writing.                         *
!                                                                      *
!                                                                      *
! Copyright (C) 1988, 2013, Oracle and/or its affiliates.              *
! All Rights Reserved.                                                 *
!***********************************************************************
!                                                                      *
!          $Date:  2013/01/18:03:49:35                                 *
!       $Release:  HR92                                                *
!      $Revision:  101                                                 *
!                                                                      *
!***********************************************************************
#include 'setenv.sqc'    !Set environment
#Include 'setup32.sqc'   !printer and page-size initialization

#define c1 35
#define c2 58
#define c3 81
#define c4 104
#define c5 127
#define c6 150

#define hc1 39
#define hc2 62
#define hc3 85
#define hc4 107
#define hc5 131
#define hc6 161

!************************************
begin-report
!************************************
   move '1' to $ReportDateType
   move '1' to $ReportYear4
   do Init-DateTime
   do Init-Number
   do Stdapi-Init
   do Get-Current-DateTime
   do Select-Parameters
   do Init-Report
   do Stdapi-Term
   do Reset
end-report


!Procedure Init-Report
!************************************
begin-procedure Init-Report

   display $ReportID
   display $ReportTitle
   date-time () hh:mi:ss &timeBegan
   display 'Report Began: ' noline
   display &timeBegan

   do Report-Translation
   do Suffix
   do Array-Initialization
   do Get-Pin
   do Get-Salario-Minimo
   do Get-Paygroup

   move 'GPMXAA01' to $ReportID
   move $ReportDescr to $ReportTitle
   date-time () hh:mi:ss &timeEnded
   display 'Report Ended: ' noline
   display &timeEnded
   show 'No. empleados total: ' #EmplTotCont
end-procedure Init-Report


begin-heading 9
    print 'PeopleSoft'      (1,) center
    print $ReportTitle      (2,1) center

    print $Report           (3,1)
    print $ReportID         (,15)
    let $pag = edit(#page-count,'9')
    let $pag = $head1 ||' ' || $pag
    print $pag              (3,155)
    print $Param1           (4,1)
    print $Company          (,15)
    print ', '              ()
    print $CompanyName      ()
    print $Head2            (4,155)
    date-time () HH:MM:SS  &ReportTime
    print &ReportTime       (,161)
    print $Param2           (5,1)
    print $AsOfYear         (,15)
    print $Head3            (5,155)
    print $ReportDate       (,161)
    print '='                      (+2,1,175)fill
    print $Head4         (+1,1)
    print $Head5 ( ,{hc1})
    print $Head6 ( ,{hc2})
    print $Head7 ( ,{hc3})
    print $Head8 ( ,{hc4})
    print $Head9 ( ,{hc5})
    print $Head10 ( ,{hc6})

    print #Salario_Minimo          (+1,{c1}) edit 9,999,999,999,999.00
    print #Salario_Minimo3         ( ,{c2})  edit 9,999,999,999,999.00
    print #Salario_Minimo5         ( ,{c3})  edit 9,999,999,999,999.00
    print #Salario_Minimo10        ( ,{c4})  edit 9,999,999,999,999.00
    print '9,999,999,999,999.99'   ( ,{c5})
    print '='                      (+1,1,175)fill
end-heading


!************************************
!Procedure Select-Parameters
!************************************
begin-procedure Select-Parameters
begin-SELECT
MEXRNCTL.OPRID
MEXRNCTL.RUN_CNTL_ID
MEXRNCTL.COMPANY
MEXRNCTL.GPMX_BALANCE_YEAR
MEXRNCTL.LANGUAGE_CD

    let $Company  = &MEXRNCTL.COMPANY
    let #AsofYear = &MEXRNCTL.GPMX_BALANCE_YEAR
    let $AsofYear = To_char (#AsofYear)
    let $lang_opr = &MEXRNCTL.LANGUAGE_CD

    move $AsofYear to $BgnDt
    move $AsofYear to $EndDt
    !Not Used
    !let  $NowDt =  datenow()
    #debug show 'Only year'
    #debug show '$BgnDt' $BgnDt
    #debug show '$EndDt' $EndDt

    concat '-01-01' with $BgnDt
    concat '-12-31' with $EndDt

    #debug show 'full date standar'
    #debug show '$BgnDt' $BgnDt
    #debug show '$EndDt' $EndDt
    !Not Used
    !do Convert-From-DTU-Date($NowDt,$NowDt)
    do Convert-From-DTU-Date($BgnDt,$BgnDt)
    do Convert-From-DTU-Date($EndDt,$EndDt)
    
    #debug show 'full date Native'
    #debug show '$BgnDt' $BgnDt
    #debug show '$EndDt' $EndDt
    
FROM  PS_GPMX_RC_ANNLADJ       MEXRNCTL
WHERE MEXRNCTL.OPRID        = $Prcs_OprID
AND   MEXRNCTL.RUN_CNTL_ID  = $Prcs_Run_Cntl_ID
end-select

  do Get-Company-Data-Mex

end-procedure Select-Parameters


!************************************
!Procedure Array-Initialization
!************************************
begin-procedure Array-Initialization
   create-array name=rep_array size=50
   field=Tipo:char
   field=Descripcion:char
   field=Rango:number:15=0

   create-array name=totales size=50
   field=Tipo:char
   field=Descripcion:char
   field=Rango:number:15=0

   do Check-Xlat-Lang
   if &Count_Lang > 0
      do Select-Level-Earn-Lang
   else
      do Select-Level-Earn
   end-if

end-procedure Array-Initialization


!************************************
!Procedure Get-Pin
!************************************
begin-procedure Get-Pin
  let #pin_var = 0
begin-SELECT
P.PIN_NUM
  let #pin_var = &P.PIN_NUM
  FROM PS_GP_PIN P
  WHERE P.PIN_NM = 'AA VR NIVEL SAL'
end-select
end-procedure Get-Pin

!************************************
!Procedure Get-Salario-Minimo
!************************************
begin-procedure Get-Salario-Minimo
begin-SELECT
WA.GPMX_MINIMUM_WAGE
  move &WA.GPMX_MINIMUM_WAGE to #Salario_Minimo
  let #Salario_Minimo   = #Salario_Minimo * 365
  let #Salario_Minimo3  = #Salario_Minimo * 3
  let #Salario_Minimo5  = #Salario_Minimo * 5
  let #Salario_Minimo10 = #Salario_Minimo * 10
FROM PS_GPMX_MIN_WAGE WA
WHERE WA.WAGE_ZONE_MEX = '10' AND
      EFFDT = (SELECT MAX(WAE.EFFDT) FROM PS_GPMX_MIN_WAGE WAE
               WHERE WAE.WAGE_ZONE_MEX = '10'  AND
                     WAE.EFFDT <= $EndDT)
end-select
end-procedure Get-Salario-Minimo


!************************************
!Procedure Suffix
!************************************
begin-procedure Suffix
  let $suffix = ''
begin-SELECT
YTD_AMT_SFX
   let $suffix = '_' || ltrim(rtrim(&YTD_AMT_SFX,' '),' ')
 from PS_GP_SUFFIX
 WHERE COUNTRY =  'MEX'
end-select
end-procedure Suffix



Begin-procedure Check-Xlat-Lang
  let $Query = ''
begin-select
count(*) &Count_Lang
   if &Count_Lang > 0
      Show '----------Language:' &Count_Lang
      
      let $Query = ' AND A2.LANGUAGE_CD = ' || '''' || $lang_opr || ''''
   end-if

FROM PSXLATITEMLANG X
WHERE X.EFFDT = (SELECT MAX(A_ED1.EFFDT) FROM PSXLATITEMLANG A_ED1
                WHERE X.FIELDNAME = A_ED1.FIELDNAME
                AND X.LANGUAGE_CD = A_ED1.LANGUAGE_CD
                AND X.FIELDVALUE = A_ED1.FIELDVALUE
                AND A_ED1.EFFDT <= $EndDt)
AND X.FIELDNAME   = 'GPMX_LEVEL_ERN_DED'
AND X.LANGUAGE_CD = $lang_opr
end-select
end-procedure Check-Xlat-Lang


!************************************
!Procedure Select-Level-Earn
!************************************
begin-procedure Select-Level-Earn
let #ContLevel = 0
begin-select
A.FIELDVALUE
A.XLATLONGNAME
   let $LevelValue = &A.FIELDVALUE
   let $LevelName  = &A.XLATLONGNAME
   put $LevelValue $LevelName into totales(#ContLevel) tipo descripcion
   put 0 0 0 0 0 into totales(#ContLevel) rango(0) rango(1) rango(2) rango(3) rango(4)
   put $LevelValue $LevelName into rep_array(#ContLevel) tipo descripcion
   put 0 0 0 0 0 into rep_array(#ContLevel) rango(0) rango(1) rango(2) rango(3) rango(4)
   let #ContLevel = #ContLevel + 1

FROM PSXLATITEM A
  WHERE A.EFFDT = (SELECT MAX(A_ED.EFFDT) FROM PSXLATITEM A_ED
        WHERE A.FIELDNAME = A_ED.FIELDNAME
          AND A.FIELDVALUE = A_ED.FIELDVALUE
          AND A_ED.EFFDT <= $EndDt)
     AND A.FIELDNAME = 'GPMX_LEVEL_ERN_DED'
ORDER BY A.FIELDVALUE
end-select
end-procedure Select-Level-Earn
     
     
begin-procedure Select-Level-Earn-Lang
let #ContLevel = 0
begin-select
A2.FIELDVALUE
A2.XLATLONGNAME
   let $LevelValue = &A2.FIELDVALUE
   let $LevelName  = &A2.XLATLONGNAME
   put $LevelValue $LevelName into totales(#ContLevel) tipo descripcion
   put 0 0 0 0 0 into totales(#ContLevel) rango(0) rango(1) rango(2) rango(3) rango(4)
   put $LevelValue $LevelName into rep_array(#ContLevel) tipo descripcion
   put 0 0 0 0 0 into rep_array(#ContLevel) rango(0) rango(1) rango(2) rango(3) rango(4)
   let #ContLevel = #ContLevel + 1
     
FROM PSXLATITEMLANG A2
  WHERE A2.EFFDT = (SELECT MAX(A_ED2.EFFDT) FROM PSXLATITEMLANG A_ED2
        WHERE A2.FIELDNAME = A_ED2.FIELDNAME
          AND A2.LANGUAGE_CD = A_ED2.LANGUAGE_CD
          AND A2.FIELDVALUE = A_ED2.FIELDVALUE
          AND A_ED2.EFFDT <= $EndDt)
     AND A2.FIELDNAME = 'GPMX_LEVEL_ERN_DED'
[$Query]
ORDER BY A2.FIELDVALUE
end-select
end-procedure Select-Level-Earn-Lang



!************************************
!Procedure Find-LevelType-Array
!************************************
begin-procedure Find-LevelType-Array
   let #ContFind = 0
   let $Find = 'N'
   while (#ContFind <= (#ContLevel - 1)) and ($Find = 'N')
     get $tipof from rep_array(#ContFind) tipo
     if rtrim($tipof,' ') = $tipo
        let $Find = 'Y'
     end-if
     let #ContFind = #ContFind + 1
   end-while
    let #ContFind = #ContFind - 1
end-procedure

!************************************
!Procedure Get-Paygroup
!************************************
begin-procedure Get-Paygroup
let #EmplTotCont = 0
begin-SELECT DISTINCT
GP_PAYGROUP
   let $Actual_Paygroup = &GP_PAYGROUP
   do Process-Main
FROM PS_JOB
WHERE COMPANY = $Company
end-select
   do Company-Totals
end-procedure Get-Paygroup

!************************************
!Procedure Process-Main
!************************************
begin-procedure Process-Main
  move 0 to #Empl_Col0       !Inicializa Contadores Empleados
  move 0 to #Empl_Col1
  move 0 to #Empl_Col2
  move 0 to #Empl_Col3
  move 0 to #Empl_Col4
  move 0 to #Empl_ColTot
begin-SELECT
A.GP_PAYGROUP ()  On-Break Print=Never Level=1 Before=Group-Heading After=Group-Totals
  let $gp_paygroup = &A.GP_PAYGROUP
A.EMPLID () On-Break Print=Never After=Add-Empl-Cont Level=2
  let $Emplid = &A.EMPLID
D.SOVR_VAL_NUM &D.SOVR_VAL_NUM

  if &D.SOVR_VAL_NUM < 10
     move &D.SOVR_VAL_NUM to $tipo 88
     let $Tipo = edit('0' ||  $Tipo,'99')
  else
     move &D.SOVR_VAL_NUM to $tipo 88
  end-if
SUM(A.CALC_RSLT_VAL) &A.CALC_RSLT_VAL ,
SUM(A.USER_ADJ_VAL) &A.USER_ADJ_VAL

  if &A.USER_ADJ_VAL = 0
     let #Imp =  &A.CALC_RSLT_VAL
  else
     let #Imp =  &A.USER_ADJ_VAL
  end-if
  if $Tipo = '01'
     do Deductions
     let #Imp = #Imp - #Ded
     evaluate #Imp
        when < #Salario_Minimo
           let #Col = 0
           break
        when < #Salario_Minimo3
           let #Col = 1
           break
        when < #Salario_Minimo5
           let #Col = 2
           break
        when < #Salario_Minimo10
           let #Col = 3
           break
        when > #Salario_Minimo10
           let #Col = 4
           break
     end-evaluate
  end-if

  do Find-LevelType-Array
  array-add #Imp to rep_array(#ContFind) rango(#Col)
  array-add #Imp to totales(#ContFind)   rango(#Col)

FROM PS_GP_RSLT_ACUM A, PS_JOB B, PS_GP_PIN C ,PS_GP_ELM_DFN_SOVR D , PS_GP_PIN E
  WHERE A.RSLT_SEG_NUM = (SELECT MAX(AM.RSLT_SEG_NUM) FROM PS_GP_RSLT_ACUM AM
          WHERE A.EMPLID = AM.EMPLID
            AND A.CAL_RUN_ID = AM.CAL_RUN_ID
            AND A.EMPL_RCD = AM.EMPL_RCD
            AND A.GP_PAYGROUP = AM.GP_PAYGROUP
            AND A.CAL_ID = AM.CAL_ID
            AND A.ORIG_CAL_RUN_ID = AM.ORIG_CAL_RUN_ID
            AND A.PIN_NUM = AM.PIN_NUM
            AND A.EMPL_RCD_ACUM = AM.EMPL_RCD_ACUM
            AND A.ACM_FROM_DT >= $BgnDt
            AND A.ACM_THRU_DT <= $EndDt)
     AND A.EMPLID = B.EMPLID
     AND A.EMPL_RCD = B.EMPL_RCD
     AND A.GP_PAYGROUP = B.GP_PAYGROUP
     AND B.EFFDT = (SELECT MAX(B_ED.EFFDT) FROM PS_JOB B_ED
        WHERE B_ED.COMPANY = $Company
          AND B.EMPLID = B_ED.EMPLID
          AND B.EMPL_RCD = B_ED.EMPL_RCD
          AND B.GP_PAYGROUP =  B_ED.GP_PAYGROUP
          AND B_ED.EFFDT <= $EndDt  )
     AND B.EFFSEQ = (SELECT MAX(B_ES.EFFSEQ) FROM PS_JOB B_ES
        WHERE B_ES.COMPANY = $Company
          AND B.EMPLID = B_ES.EMPLID
          AND B.EMPL_RCD = B_ES.EMPL_RCD
          AND B.GP_PAYGROUP = B_ES.GP_PAYGROUP
          AND B.EFFDT = B_ES.EFFDT)
     AND B.COMPANY = $Company
     AND B.GP_PAYGROUP = $Actual_Paygroup
     AND A.ACM_FROM_DT >= $BgnDt
     AND A.ACM_THRU_DT <= $EndDt
     AND A.PIN_NUM = C.PIN_NUM
     AND C.PIN_NM =
     #ifdef  MICROSOFT
       RTRIM(E.PIN_NM) + $suffix
     #end-if
     #ifdef DB2ALL
       RTRIM(E.PIN_NM) || $suffix
     #end-if
     #ifdef INFORMIX
       RTRIM(E.PIN_NM) || $suffix
     #end-if
     #ifdef ORACLE
       RTRIM(E.PIN_NM,' ') || $suffix
     #end-if
     #ifdef SYBASE
       RTRIM(E.PIN_NM) + $suffix
     #end-if
     #ifdef SQLBASE
       @RTRIM(E.PIN_NM) || $suffix
     #end-if
     AND D.BGN_DT = (SELECT MAX(D_ED.BGN_DT) FROM PS_GP_ELM_DFN_SOVR D_ED
        WHERE D.PIN_NUM = D_ED.PIN_NUM
          AND D_ED.BGN_DT <= $EndDt )
     AND D.ENTRY_TYPE_SOVR = 'VR0'
     AND D.PIN_SOVR_NUM =  #pin_var
     AND D.PIN_NUM = E.PIN_NUM
     AND E.PIN_TYPE = 'ER'
     GROUP BY  A.GP_PAYGROUP, A.EMPLID, D.SOVR_VAL_NUM
     ORDER BY  A.GP_PAYGROUP, A.EMPLID, D.SOVR_VAL_NUM
end-select
end-procedure Process-Main


!************************************
!Procedure Deductions
!************************************
begin-procedure Deductions
  move 0 to #Ded
begin-SELECT
DA.GP_PAYGROUP
DA.EMPLID
SUM(DA.CALC_RSLT_VAL) &DA.CALC_RSLT_VAL

  let #Ded = &DA.CALC_RSLT_VAL

FROM PS_GP_RSLT_ACUM DA, PS_JOB DB, PS_GP_PIN DC ,PS_GP_ELM_DFN_SOVR DD , PS_GP_PIN DE
  WHERE  DA.RSLT_SEG_NUM = (SELECT MAX(DAM.RSLT_SEG_NUM) FROM PS_GP_RSLT_ACUM DAM
          WHERE DA.EMPLID = DAM.EMPLID
            AND DA.CAL_RUN_ID = DAM.CAL_RUN_ID
            AND DA.EMPL_RCD = DAM.EMPL_RCD
            AND DA.GP_PAYGROUP = DAM.GP_PAYGROUP
            AND DA.CAL_ID = DAM.CAL_ID
            AND DA.ORIG_CAL_RUN_ID = DAM.ORIG_CAL_RUN_ID
            AND DA.PIN_NUM = DAM.PIN_NUM
            AND DA.EMPL_RCD_ACUM = DAM.EMPL_RCD_ACUM
            AND DA.ACM_FROM_DT >= $BgnDt
            AND DA.ACM_THRU_DT <= $EndDt)
     AND DA.EMPLID = DB.EMPLID
     AND DA.EMPL_RCD = DB.EMPL_RCD
     AND DA.GP_PAYGROUP = DB.GP_PAYGROUP
     AND DB.EFFDT = (SELECT MAX(DB_ED.EFFDT) FROM PS_JOB DB_ED
        WHERE DB_ED.COMPANY = $Company
          AND DB.EMPLID = DB_ED.EMPLID
          AND DB.EMPL_RCD = DB_ED.EMPL_RCD
          AND DB.GP_PAYGROUP =  DB_ED.GP_PAYGROUP
          AND DB_ED.EFFDT <=  $Enddt )
    AND DB.EFFSEQ = (SELECT MAX(DB_ES.EFFSEQ) FROM PS_JOB DB_ES
        WHERE DB_ES.COMPANY =  $Company
          AND DB.EMPLID = DB_ES.EMPLID
          AND DB.EMPL_RCD = DB_ES.EMPL_RCD
          AND DB.GP_PAYGROUP = DB_ES.GP_PAYGROUP
          AND DB.EFFDT = DB_ES.EFFDT)
     AND  DB.COMPANY =  $Company
     AND DA.ACM_FROM_DT >=  $Bgndt
     AND DA.ACM_THRU_DT <=  $Enddt
     AND DA.PIN_NUM = DC.PIN_NUM
     AND DC.PIN_NM =
     #ifdef  MICROSOFT
       RTRIM(DE.PIN_NM) + $suffix
     #end-if
     #ifdef DB2ALL
       RTRIM(DE.PIN_NM) || $suffix
     #end-if
     #ifdef INFORMIX
       RTRIM(DE.PIN_NM) || $suffix
     #end-if
     #ifdef ORACLE
       RTRIM(DE.PIN_NM,' ') || $suffix
     #end-if
     #ifdef SYBASE
       RTRIM(DE.PIN_NM) + $suffix
     #end-if
     #ifdef SQLBASE
       @RTRIM(DE.PIN_NM) || $suffix
     #end-if
     AND DD.BGN_DT = (SELECT MAX(DD_ED.BGN_DT) FROM PS_GP_ELM_DFN_SOVR DD_ED
        WHERE DD.PIN_NUM = DD_ED.PIN_NUM
          AND DD_ED.BGN_DT <= $EndDt )
     AND DD.ENTRY_TYPE_SOVR = 'VR0'
     AND DD.PIN_SOVR_NUM =  #pin_var
     AND DD.PIN_NUM = DE.PIN_NUM
     AND DE.PIN_TYPE = 'DD'
     AND DB.GP_PAYGROUP = $gp_paygroup
     AND DB.EMPLID = $Emplid
     GROUP BY  DA.GP_PAYGROUP, DA.EMPLID
end-select
end-procedure Deductions


!************************************
!Procedure Group-Heading
!************************************
begin-procedure Group-Heading
   print $Head11            (+3,1)
   print $Actual_Paygroup   ( ,+1)
   print ','                (  )
begin-SELECT
PG.DESCR
  let $Desc_Paygroup = &PG.DESCR
FROM PS_GP_PYGRP PG
WHERE PG.GP_PAYGROUP = $Actual_Paygroup
end-select
   print $Desc_Paygroup            (,+1)
   print ' '                       (+1 )
end-procedure Group-Heading

!************************************
!Procedure Group-Totals
!************************************
begin-procedure Group-Totals
  move 0 to #Total_Rango0      !Inicializa Totales Grupo Pago
  move 0 to #Total_Rango1
  move 0 to #Total_Rango2
  move 0 to #Total_Rango3
  move 0 to #Total_Rango4
  move 0 to #Total_RangoTot

  move 0 to #ind
  while #ind <= (#ContLevel - 1)
     get $tipo $Descripcion #rango0 #rango1 #rango2 #rango3 #rango4
     from rep_array(#ind) tipo descripcion rango(0) rango(1) rango(2) rango(3) rango(4)
     put 0 0 0 0 0 into rep_array(#ind) rango(0) rango(1) rango(2) rango(3) rango(4)
     let #rangotot = #rango0 + #rango1 + #rango2 + #rango3 + #rango4
     print $Descripcion  (+1,1)
     print #rango0       ( ,{c1})  edit 9,999,999,999,999.99
     print #rango1       ( ,{c2})  edit 9,999,999,999,999.99
     print #rango2       ( ,{c3})  edit 9,999,999,999,999.99
     print #rango3       ( ,{c4})  edit 9,999,999,999,999.99
     print #rango4       ( ,{c5})  edit 9,999,999,999,999.99
     print #rangotot     ( ,{c6})  edit 9,999,999,999,999.99

     add #rango0 to #Total_Rango0       !Totales Grupo de Pago
     add #rango1 to #Total_Rango1
     add #rango2 to #Total_Rango2
     add #rango3 to #Total_Rango3
     add #rango4 to #Total_Rango4
     add #rangotot to #Total_RangoTot

     add #rango0 to #Total_Comp_Rango0  !Totales Generales Compañía
     add #rango1 to #Total_Comp_Rango1
     add #rango2 to #Total_Comp_Rango2
     add #rango3 to #Total_Comp_Rango3
     add #rango4 to #Total_Comp_Rango4
     add #rangotot to #Total_Comp_RangoTot

     add 1 to #ind
  end-while
  print $Head12                      (+2,1)
  print #Total_Rango0                ( ,{c1})  edit 9,999,999,999,999.99
  print #Total_Rango1                ( ,{c2})  edit 9,999,999,999,999.99
  print #Total_Rango2                ( ,{c3})  edit 9,999,999,999,999.99
  print #Total_Rango3                ( ,{c4})  edit 9,999,999,999,999.99
  print #Total_Rango4                ( ,{c5})  edit 9,999,999,999,999.99
  print #Total_Rango4                ( ,{c5})  edit 9,999,999,999,999.99
  print #Total_RangoTot              ( ,{c6})  edit 9,999,999,999,999.99

  let #Empl_ColTot = #Empl_Col0 + #Empl_Col1 + #Empl_Col2 + #Empl_Col3 + #Empl_Col4
  print $Head13                      (+1,1)
  print #Empl_Col0                   ( ,{c1})  edit 9,999,999,999,999
  print #Empl_Col1                   ( ,{c2})  edit 9,999,999,999,999
  print #Empl_Col2                   ( ,{c3})  edit 9,999,999,999,999
  print #Empl_Col3                   ( ,{c4})  edit 9,999,999,999,999
  print #Empl_Col4                   ( ,{c5})  edit 9,999,999,999,999
  print #Empl_ColTot                 ( ,{c6})  edit 9,999,999,999,999

end-procedure Group-Totals

!************************************
!Procedure Company-Totals
!************************************
begin-procedure Company-Totals
  print $Head14         (+4,1)
  print $Company                     ( ,+1)
  print ', '                         (  )
  print $CompanyName                 (  )
  print ' '                          (+1)

  move 0 to #ind
  while #ind <= (#ContLevel - 1)
     get $Comp_tipo $Comp_Descripcion #Comp_rango0 #Comp_rango1 #Comp_rango2 #Comp_rango3 #Comp_rango4
     from totales(#ind) tipo descripcion rango(0) rango(1) rango(2) rango(3) rango(4)
     let #Comp_RangoTot = #Comp_rango0 + #Comp_rango1 + #Comp_rango2 + #Comp_rango3 + #Comp_rango4
     print $Comp_Descripcion         (+1,1)
     print #Comp_rango0              ( ,{c1})  edit 9,999,999,999,999.99
     print #Comp_rango1              ( ,{c2})  edit 9,999,999,999,999.99
     print #Comp_rango2              ( ,{c3})  edit 9,999,999,999,999.99
     print #Comp_rango3              ( ,{c4})  edit 9,999,999,999,999.99
     print #Comp_rango4              ( ,{c5})  edit 9,999,999,999,999.99
     print #Comp_RangoTot            ( ,{c6})  edit 9,999,999,999,999.99
     add 1 to #ind
  end-while

  print $Head15                      (+2,1)
  print #Total_Comp_Rango0           ( ,{c1})  edit 9,999,999,999,999.99
  print #Total_Comp_Rango1           ( ,{c2})  edit 9,999,999,999,999.99
  print #Total_Comp_Rango2           ( ,{c3})  edit 9,999,999,999,999.99
  print #Total_Comp_Rango3           ( ,{c4})  edit 9,999,999,999,999.99
  print #Total_Comp_Rango4           ( ,{c5})  edit 9,999,999,999,999.99
  print #Total_Comp_RangoTot         ( ,{c6})  edit 9,999,999,999,999.99

  let #Empl_Comp_ColTot = #Empl_Comp_Col0 + #Empl_Comp_Col1 + #Empl_Comp_Col2 + #Empl_Comp_Col3 + #Empl_Comp_Col4
  print $Head13                      (+1,1)
  print #Empl_Comp_Col0              ( ,{c1})  edit 9,999,999,999,999
  print #Empl_Comp_Col1              ( ,{c2})  edit 9,999,999,999,999
  print #Empl_Comp_Col2              ( ,{c3})  edit 9,999,999,999,999
  print #Empl_Comp_Col3              ( ,{c4})  edit 9,999,999,999,999
  print #Empl_Comp_Col4              ( ,{c5})  edit 9,999,999,999,999
  print #Empl_Comp_ColTot            ( ,{c6})  edit 9,999,999,999,999
end-procedure Company-Totals


!************************************
!Procedure Add-Empl-Cont
!************************************
begin-procedure Add-Empl-Cont
   evaluate #Col
      when = 0
         add 1 to #Empl_Col0
         add 1 to #Empl_Comp_Col0
         break
      when = 1
         add 1 to #Empl_Col1
         add 1 to #Empl_Comp_Col1
         break
      when = 2
         add 1 to #Empl_Col2
         add 1 to #Empl_Comp_Col2
         break
      when = 3
         add 1 to #Empl_Col3
         add 1 to #Empl_Comp_Col3
         break
      when = 4
         add 1 to #Empl_Col4
         add 1 to #Empl_Comp_Col4
         break
   end-evaluate
   add 1 to #EmplTotCont
   let #EmplRange = mod(#EmplTotCont,1)
   if #EmplRange = 0
      date-time () hh:mi:ss &TimeEmpl
!      show $emplid noline
!      show '   ' &TimeEmpl noline
!      show '   No. empleados parcial: ' #EmplTotCont
   end-if
end-procedure

!**********************************
!Procedure Report-Translation
!**********************************
begin-Procedure Report-Translation
! Read from GPMXAA01 program IDS
 let $language_cd = $prcs_languaje_cd
 do Init_Report_Translation ('GPMXAA01', $language_cd)
 do Append_Report_Translation ('GPMXAA01')
 do Get_Field_Information ('GPMXAA01', 'REPORT',  $Report,   #DW)
 do Get_Field_Information ('GPMXAA01', 'REPORT_DESCR',  $ReportDescr,   #DW)
 do Get_Field_Information ('GPMXAA01', 'HEAD1',  $Head1,   #DW)
 do Get_Field_Information ('GPMXAA01', 'HEAD2',  $Head2,   #DW)
 do Get_Field_Information ('GPMXAA01', 'HEAD3',  $Head3,   #DW)
 do Get_Field_Information ('GPMXAA01', 'HEAD4',  $Head4,   #DW)
 do Get_Field_Information ('GPMXAA01', 'HEAD5',  $Head5,   #DW)
 do Get_Field_Information ('GPMXAA01', 'HEAD6',  $Head6,   #DW)
 do Get_Field_Information ('GPMXAA01', 'HEAD7',  $Head7,   #DW)
 do Get_Field_Information ('GPMXAA01', 'HEAD8',  $Head8,   #DW)
 do Get_Field_Information ('GPMXAA01', 'HEAD9',  $Head9,   #DW)
 do Get_Field_Information ('GPMXAA01', 'HEAD10',  $Head10,   #DW)
 do Get_Field_Information ('GPMXAA01', 'HEAD11',  $Head11,   #DW)
 do Get_Field_Information ('GPMXAA01', 'HEAD12',  $Head12,   #DW)
 do Get_Field_Information ('GPMXAA01', 'HEAD13',  $Head13,   #DW)
 do Get_Field_Information ('GPMXAA01', 'HEAD14',  $Head14,   #DW)
 do Get_Field_Information ('GPMXAA01', 'HEAD15',  $Head15,   #DW)
 do Get_Field_Information ('GPMXAA01', 'PARAM1',  $param1,   #DW)
 do Get_Field_Information ('GPMXAA01', 'PARAM2',  $param2,   #DW)
End-Procedure Report-Translation

#Include 'getcomex.sqc'  !Get selected fields from Company Table and Mexico Fields
#Include 'reset.sqc'     !Reset printer procedure
#Include 'curdttim.sqc'  !Get-Current-DateTime procedure
#Include 'datetime.sqc'  !Routines for date and time formatting
#Include 'number.sqc'    !Routines to format numbers
#Include 'stdapi.sqc'    !Update Process API
#Include 'datemath.sqc'  !Datemath Routines
#Include 'sqrtrans.sqc'
