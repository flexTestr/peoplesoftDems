!***********************************************************************
!  GPMXIE01:  State Taxes by Location Summary                          *
!***********************************************************************
!***********************************************************************
!                                                                      *
!                                                                      *
!                                                                      *
!                                                                      *
! This software and related documentation are provided under a         *
! license agreement containing restrictions on use and                 *
! disclosure and are protected by intellectual property                *
! laws. Except as expressly permitted in your license agreement        *
! or allowed by law, you may not use, copy, reproduce,                 *
! translate, broadcast, modify, license, transmit, distribute,         *
! exhibit, perform, publish or display any part, in any form or        *
! by any means. Reverse engineering, disassembly, or                   *
! decompilation of this software, unless required by law for           *
! interoperability, is prohibited.                                     *
! The information contained herein is subject to change without        *
! notice and is not warranted to be error-free. If you find any        *
! errors, please report them to us in writing.                         *
!                                                                      *
!                                                                      *
! Copyright (C) 1988, 2020, Oracle and/or its affiliates.              *
! All Rights Reserved.                                                 *
!***********************************************************************
!                                                                      *
!       $Release:  HR92                                                !
!           $Bug:  31344488                                            !
!                                                                      *
!***********************************************************************
#include 'setenv.sqc' !Set environment
#Include 'setup32.sqc'  !printer and page-size initialization

#define d1 1
#define d2 15
#define d3 50
#define d4 75
#define d5 90
#define d6 115
#define d7 145
#define d8 155

begin-report
    move '1' to $ReportDateType
    move '1' to $ReportYear4
    do Init-DateTime
    do Init-Number
    do Stdapi-Init
    do Get-Current-DateTime
    do Select-Parameters
    do Init-Report
    do Stdapi-Term
    do Reset
end-report

begin-procedure Init-Report
    move 'GPMXIE01' to $ReportID
    move 'State Taxes by Location Summary' to $ReportTitle
    display $ReportID
    display $ReportTitle
    show 'Ver. 2020 PUM35.01'
    date-time () hh:mi:ss &timeBegan
    display 'Report Began: ' noline
    display &timeBegan
    do Process-Main
    date-time () hh:mi:ss &timeEnd
    display 'Report End: ' noline
    display &timeEnd
end-procedure

begin-heading 8
  print 'PeopleSoft'                  (1,1) center
  uppercase $ReportTitle
  uppercase $Head1
  print $Head1                        (2,1) center
  
  print $Report                       (+1,1)
  print $ReportID                     (,15)
  
  print $Param1                       (+1,1)
  print $Company1                     (,15)
  print $Desc_company                 (,+2)
  let $pag = edit(#page-count,'9999')
  let $pag = $Head2 || ' ' || $pag
  print $pag                          (0,155)
  print $Param3                       (+1,1)
  print $TState                       (,15)
  print ','                           (,)
  print $Desc_state                   (,+1)
  print $Head3                        (0,155)
  date-time () HH:MM:SS  &ReportTime
  print &ReportTime                   (,+1)
  print $Param4                       (+1,1)
  do Format-DateTime($BgnDt, $BgnDt1,{DEFDATE},'','')
  print $BgnDt1                       (,+2)
  print $Param5                       (,+4)
  do Format-DateTime($EndDt, $EndDt1,{DEFDATE},'','')
  print $EndDt1                       (,+2)
  print $Head4                        (,155)
  print $ReportDate                   (,+1)
  print '='                         (+1,1,175) fill
  uppercase $Head5
  uppercase $Head6
  uppercase $Head7
  print $Head5                      (+1,{d2})
  print $Head6                      (,{d5})
  print $Head7                      (,{d8})
  print '='                         (+1,1,175)fill
  
end-heading


!***************************************
!Procedure Select-Parameters
!***************************************
begin-procedure Select-Parameters
LET $Gp_Batch_Cd = 'PROCESS IE'
begin-select
MEXRNCTL.COMPANY
MEXRNCTL.STATE_FROM
MEXRNCTL.STATE
MEXRNCTL.BALANCE_YEAR
MEXRNCTL.MONTHCD
MEXRNCTL.GPMX_WA_VALUES
MEXRNCTL.GPMX_POS_INPUT

  let $Company   =  &MEXRNCTL.COMPANY
  let $State     = RTRIM(&MEXRNCTL.STATE_FROM,' ')
  let $State_A   = RTRIM(&MEXRNCTL.STATE,' ')
  let #Asofyear  = &MEXRNCTL.BALANCE_YEAR
  let #Asofmonth = &MEXRNCTL.MONTHCD
  let $Wa_Values = LTRIM(RTRIM(&MEXRNCTL.GPMX_WA_VALUES, ' '), ' ')
  let $PosInput  = LTRIM(RTRIM(&MEXRNCTL.GPMX_POS_INPUT, ' '), ' ')

  If rtrim($State,' ') = '' AND rtrim($State_A,' ') = ''
     let $where = 'B.STATE BETWEEN ' || '''' || 'AAA' || '''' || ' AND '  || '''' || 'ZZZ' || ''''
  End-if

  If rtrim($State,' ') = '' AND rtrim($State_A,' ') <> ''
     let $where = 'B.STATE BETWEEN ' || '''' || 'AAA' || '''' || ' AND '  || '''' || $State_a || ''''
  End-if

  If rtrim($State,' ') <> '' AND rtrim($State_A,' ') = ''
     let $where = 'B.STATE BETWEEN ' || '''' || $State || '''' || ' AND '  || '''' || 'ZZZ' || ''''
  End-if

  If rtrim($State,' ') <> '' AND rtrim($State_A,' ') <> ''
     let $where = 'B.STATE BETWEEN ' || '''' || $State || '''' || ' AND '  || '''' || $State_a || ''''
  End-if

  if mod(#AsofYear, 4) = 0
    let #bis = 1
  else
    let #bis = 0
  end-if

  let $AsofYear = edit(#AsofYear,'9999')
  let $AsofMonth = edit(#AsofMonth,'09')

  let $BgnDt = $AsofYear || '-' || $AsofMonth || '-' || '01'

  if  $AsofMonth = '01' OR $AsofMonth = '03' OR $AsofMonth = '05' OR $AsofMonth = '07' OR $AsofMonth = '08' OR $AsofMonth = '10' OR $AsofMonth = '12'
      let $EndDt =  $AsofYear || '-' || $AsofMonth || '-' || '31'
  end-if

  if  $AsofMonth ='02'
      if #bis = 1
         let $EndDt = $AsofYear || '-' || $AsofMonth || '-' || '29'
      else
         let $EndDt = $AsofYear || '-' || $AsofMonth || '-' || '28'
      end-if
  end-if

  if  $AsofMonth = '04' OR $AsofMonth = '06' OR $AsofMonth = '09' OR $AsofMonth = '11'
        let $EndDt =  $AsofYear || '-' || $AsofMonth || '-' || '30'
  end-if
  
  #debug show 'End Date  .- ' $EndDt
  show 'where ' $where

  do Convert-From-DTU-Date($BgnDt,$BgnDt)
  do Convert-From-DTU-Date($EndDt,$EndDt)

FROM  PS_GPMX_RC_STTAXES          MEXRNCTL
WHERE MEXRNCTL.OPRID            = $Prcs_OprID
AND   MEXRNCTL.RUN_CNTL_ID      = $Prcs_Run_Cntl_ID
end-select
show ' '
show '  Company    : ' $Company
show '  State From : ' $State
show '  State To   : ' $State_A
show '  As of Year : ' #Asofyear
show '  As of Month: ' #Asofmonth
show '  Wa Values  : ' $Wa_Values
show '  PosInput   : ' $PosInput
show '  EndDt      : ' $EndDt
show '  BgnDt      : ' $BgnDt
show ' '
end-procedure Select-Parameters

!***************************************************************!
!Function: Srch_Pin
!Description: Seaarch Pin
!***************************************************************!
BEGIN-PROCEDURE Srch_Pin($Pin_Nm, :$Pin_Type, :#Pin_Num)
  LET #Pin_Num     = 0
  LET $Pin_Type    = '' 
BEGIN-SELECT On-Error=SQL-Error
PINM.PIN_NUM
PINM.PIN_TYPE
     
  LET $Pin_Type    = LTRIM(RTRIM(&PINM.PIN_TYPE   , ' '), ' ')
  LET #Pin_Num     = &PINM.PIN_NUM
     
  FROM PS_GP_PIN PINM 
  WHERE PINM.PIN_NM = $Pin_Nm                  
END-SELECT
END-PROCEDURE !Busca_Pin_Num

!************************************
!Procedure Arrays-Init
!************************************
Begin-Procedure Arrays-Init

  Create-Array name = Percepciones Size = 100
  Field=Cod_Ern:Number
  Field=Descr_Ern:Char
  Field=Imp:Number

  Create-Array name = Deducciones Size = 100
  Field=Cod_Ded:Number
  Field=Descr_Ded:Char
  Field=Imp_Ded:Number

End-procedure Arrays-Init


!***************************************
!Procedure Process-Main
!***************************************
Begin-procedure Process-Main
   do Report-Translation
   do Arrays-Init
   do Get-Earns-Ded
   IF $PosInput = 'Y'
     do Positive_Input
   END-IF
end-procedure Process-Main

!***************************************
!Procedure Positive_Input
!***************************************
Begin-procedure Positive_Input
LET #BaseEdo   = 0
LET #CountBase = 0
LET #BaseImpEstTot = 0
LET $Key =  ''
LET $OldKey = ''
SHOW ' '
SHOW 'State Taxes CAMP HGO Base for Company: '  $Company ' from '$BgnDt ' to ' $EndDt
let $fecha_hora = edit(datenow(),'YYYY-MM-DD-HH:MI:SS')
DO Srch_Pin('IMPUESTO EST', $Entry_Type_DD, #Pin_Num_DD)
DO Srch_Pin('IE VR BASE EXCPT', $Entry_Type_VR, #Pin_Num_VR)
DO Del_GP_PI_MNL_SOVR
BEGIN-SELECT DISTINCT
COUNT(DISTINCT B.EMPLID)                                &AR_Count
C.STATE                                                 &AR_STATE
A.CAL_RUN_ID                                            &AR_CAL_RUN_ID
A.GP_PAYGROUP                                           &AR_GP_PAYGROUP
A.CAL_ID                                                &AR_CAL_ID
SUM( A.CALC_RSLT_VAL+ A.CALC_ADJ_VAL)                   &AR_CALC_RSLT_VAL
   
   ADD 1 TO #CountBase

   LET #NoEmpleados   = &AR_Count
   LET $State         = LTRIM(RTRIM(&AR_STATE          , ' '), ' ')
   LET $Gp_Paygroup   = LTRIM(RTRIM(&AR_GP_PAYGROUP    , ' '), ' ')
   LET $Cal_Id        = LTRIM(RTRIM(&AR_CAL_ID         , ' '), ' ')
   LET $Cal_Run_Id    = LTRIM(RTRIM(&AR_CAL_RUN_ID     , ' '), ' ')
   LET #BaseImpEst    = &AR_CALC_RSLT_VAL

   DO Get_DD_Base
   LET #BaseImpEstTot = #BaseImpEsT - #BaseImpEstDD
   SHOW '#BaseImpEstDD: ' #BaseImpEstDD ', #BaseImpEst: ' #BaseImpEsT ', #BaseImpEstTot: ' #BaseImpEstTot
   DO Insert_GP_PI_MNL_DATA
   DO Insert_GP_PI_MNL_SOVR

   FROM PS_GP_RSLT_ERN_DED A
      , PS_JOB             B
      , PS_LOCATION_TBL    C
      , PS_GPMX_ST_TAXES_C D
      , PS_GP_CAL_RUN      E
      , PS_GP_CAL_RUN_DTL  F
WHERE A.EMPLID              = B.EMPLID 
  AND A.EMPL_RCD            = B.EMPL_RCD 
  AND A.GP_PAYGROUP         = B.GP_PAYGROUP 
  AND B.COMPANY           = $Company
  AND A.SLICE_BGN_DT     >= $BgnDt
  AND A.SLICE_END_DT     <= $EndDt
  AND D.ENTRY_TYPE_ELEM   = 'ER0'
  AND B.EFFDT               = (SELECT MAX(B_ED.EFFDT) FROM PS_JOB B_ED 
                                WHERE B.EMPLID    = B_ED.EMPLID 
                                  AND B.EMPL_RCD  = B_ED.EMPL_RCD 
                                  AND B_ED.EFFDT <= A.SLICE_END_DT) 
 AND B.EFFSEQ               = (SELECT MAX(B_ES.EFFSEQ) FROM PS_JOB B_ES 
                                WHERE B.EMPLID    = B_ES.EMPLID 
                                  AND B.EMPL_RCD  = B_ES.EMPL_RCD 
                                  AND B.EFFDT     = B_ES.EFFDT) 
  AND C.LOCATION            = B.LOCATION 
  AND C.EFFDT               = (SELECT MAX(C_ED.EFFDT) FROM PS_LOCATION_TBL C_ED 
                                WHERE C.SETID     = C_ED.SETID 
                                  AND C.LOCATION  = C_ED.LOCATION 
                                  AND C_ED.EFFDT <= B.EFFDT) 
  AND C.SETID               = B.SETID_LOCATION 
  AND D.COUNTRY             = C.COUNTRY 
  AND D.STATE               = C.STATE 
  AND D.EFFDT               = (SELECT MAX(D_ED.EFFDT) FROM PS_GPMX_ST_TAXES_C D_ED 
                                WHERE D.COUNTRY   = D_ED.COUNTRY 
                                  AND D.STATE     = D_ED.STATE 
                                  AND D.PIN_NUM   = D_ED.PIN_NUM 
                                  AND D_ED.EFFDT <= A.SLICE_END_DT) 
  AND D.PIN_NUM             = A.PIN_NUM 
  AND D.STATE              IN ('CAMP','HGO') 
  AND D.COUNTRY             = 'MEX'
  AND A.CAL_RUN_ID          = E.CAL_RUN_ID 
  AND A.CAL_RUN_ID          = F.CAL_RUN_ID 
  AND A.GP_PAYGROUP         = F.GP_PAYGROUP 
  AND A.CAL_ID              = F.CAL_ID 
  AND F.CALC_TYPE           = 'P' 
  AND A.ORIG_CAL_RUN_ID     = A.CAL_RUN_ID 
  AND E.RUN_FINALIZED_IND   = 'N'  
GROUP BY  C.STATE,  A.CAL_RUN_ID,  A.GP_PAYGROUP,  A.CAL_ID
ORDER BY 2, 3, 4, 5 DESC
END-SELECT

end-procedure Positive_Input

!***************************************
!Procedure Get_DD_Base
!***************************************
Begin-procedure Get_DD_Base

LET #BaseImpEstDD = 0

BEGIN-SELECT
SUM( A.CALC_RSLT_VAL+ A.CALC_ADJ_VAL)                   &DD_CALC_RSLT_VAL
   
   LET #BaseImpEstDD  = &DD_CALC_RSLT_VAL

   FROM PS_GP_RSLT_ERN_DED A
      , PS_JOB             B
      , PS_LOCATION_TBL    C
      , PS_GPMX_ST_TAXES_C D
      , PS_GP_CAL_RUN      E
      , PS_GP_CAL_RUN_DTL  F
WHERE A.EMPLID              = B.EMPLID 
  AND A.EMPL_RCD            = B.EMPL_RCD 
  AND A.GP_PAYGROUP         = B.GP_PAYGROUP 
  AND B.COMPANY           = $Company
  AND A.SLICE_BGN_DT     >= $BgnDt
  AND A.SLICE_END_DT     <= $EndDt
  AND D.ENTRY_TYPE_ELEM   = 'DD0'
  AND B.EFFDT               = (SELECT MAX(B_ED.EFFDT) FROM PS_JOB B_ED 
                                WHERE B.EMPLID    = B_ED.EMPLID 
                                  AND B.EMPL_RCD  = B_ED.EMPL_RCD 
                                  AND B_ED.EFFDT <= A.SLICE_END_DT) 
 AND B.EFFSEQ               = (SELECT MAX(B_ES.EFFSEQ) FROM PS_JOB B_ES 
                                WHERE B.EMPLID    = B_ES.EMPLID 
                                  AND B.EMPL_RCD  = B_ES.EMPL_RCD 
                                  AND B.EFFDT     = B_ES.EFFDT) 
  AND C.LOCATION            = B.LOCATION 
  AND C.EFFDT               = (SELECT MAX(C_ED.EFFDT) FROM PS_LOCATION_TBL C_ED 
                                WHERE C.SETID     = C_ED.SETID 
                                  AND C.LOCATION  = C_ED.LOCATION 
                                  AND C_ED.EFFDT <= B.EFFDT) 
  AND C.SETID               = B.SETID_LOCATION 
  AND D.COUNTRY             = C.COUNTRY 
  AND D.STATE               = C.STATE 
  AND D.EFFDT               = (SELECT MAX(D_ED.EFFDT) FROM PS_GPMX_ST_TAXES_C D_ED 
                                WHERE D.COUNTRY   = D_ED.COUNTRY 
                                  AND D.STATE     = D_ED.STATE 
                                  AND D.PIN_NUM   = D_ED.PIN_NUM 
                                  AND D_ED.EFFDT <= A.SLICE_END_DT) 
  AND D.PIN_NUM             = A.PIN_NUM 
  AND D.STATE               = $State
  AND A.CAL_RUN_ID          = $Cal_Run_Id
  AND A.GP_PAYGROUP         = $Gp_Paygroup
  AND A.CAL_ID              = $Cal_Id
  AND D.COUNTRY             = 'MEX'
  AND A.CAL_RUN_ID          = E.CAL_RUN_ID 
  AND A.CAL_RUN_ID          = F.CAL_RUN_ID 
  AND A.GP_PAYGROUP         = F.GP_PAYGROUP 
  AND A.CAL_ID              = F.CAL_ID 
  AND F.CALC_TYPE           = 'P' 
  AND A.ORIG_CAL_RUN_ID     = A.CAL_RUN_ID 
  AND E.RUN_FINALIZED_IND   = 'N'  
END-SELECT

end-procedure Get_DD_Base


!***************************************************************!
! Function   : Insert_GP_PI_MNL_DATA.
! Description:  Insert PI in Record GP_PI_MNL_SOVR.
!***************************************************************!
BEGIN-PROCEDURE Insert_GP_PI_MNL_DATA 
SHOW 'State: ' $State EDIT XXXX ', Gp Paygroup: ' $Gp_Paygroup ', Cal Id: ' $Cal_Id ', Cal Run Id: ' $Cal_Run_Id ', Tax Base: ' #BaseImpEstTot EDIT 999,999,999.99 ', No Employees: ' #NoEmpleados edit 999,999
BEGIN-SQL 
INSERT INTO PS_GP_PI_MNL_DATA
(GP_PAYGROUP           ,
CAL_ID                 ,
EMPLID                 ,
EMPL_RCD               ,
PIN_NUM                ,
INSTANCE               ,
ENTRY_TYPE_ID          ,
PI_ACTION_TYPE         ,
CURRENCY_CD            ,
GP_RATE                ,
ENTRY_TYPE_RATE        ,
PIN_RATE_NUM           ,
GP_UNIT                ,
ENTRY_TYPE_UNIT        ,
PIN_UNIT_NUM           ,
GP_BASE                ,
ENTRY_TYPE_BASE        ,
PIN_BASE_NUM           ,
GP_PCT                 ,
ENTRY_TYPE_PCT         ,
PIN_PCT_NUM            ,
GP_AMT                 ,
ENTRY_TYPE_AMT         ,
PIN_AMT_NUM            ,
BGN_DT                 ,
END_DT                 ,
BUSINESS_UNIT          ,
DESCR                  ,
GP_BATCH_CD            ,
BAL_ADJ                ,
ABS_PRD_BGN_DT         ,
ABS_PRD_END_DT         ,
ABS_BGN_DT             ,
ABS_END_DT             ,
LASTUPDOPRID           , 
LASTUPDDTTM            ) 
SELECT DISTINCT A.GP_PAYGROUP,
A.CAL_ID,
A.EMPLID,
A.EMPL_RCD,
#Pin_Num_DD,
1,
$Entry_Type_DD||'0',
'O',
B.CURRENCY_CD,
0,
'002',
0,
0,
'002',
0,
0,
'002',
0,
0,
'002',
0,
0,
' ',
0,
'',
'',
B.BUSINESS_UNIT,
' ',
'IE CALC',
0,
'',
'',
'',
'',
$Prcs_OprID,
TO_DATE($FECHA_HORA,'YYYY-MM-DD-HH24:MI:SS')
   FROM PS_GP_RSLT_ERN_DED A
      , PS_JOB             B
      , PS_LOCATION_TBL    C
      , PS_GPMX_ST_TAXES_C D
      , PS_GP_CAL_RUN      E
      , PS_GP_CAL_RUN_DTL  F
WHERE A.EMPLID              = B.EMPLID 
  AND A.EMPL_RCD            = B.EMPL_RCD 
  AND A.GP_PAYGROUP         = B.GP_PAYGROUP 
  AND B.COMPANY             = $Company
  AND B.EFFDT               = (SELECT MAX(B_ED.EFFDT) FROM PS_JOB B_ED 
                                WHERE B.EMPLID    = B_ED.EMPLID 
                                  AND B.EMPL_RCD  = B_ED.EMPL_RCD 
                                  AND B_ED.EFFDT <= A.SLICE_END_DT) 
 AND B.EFFSEQ               = (SELECT MAX(B_ES.EFFSEQ) FROM PS_JOB B_ES 
                                WHERE B.EMPLID    = B_ES.EMPLID 
                                  AND B.EMPL_RCD  = B_ES.EMPL_RCD 
                                  AND B.EFFDT     = B_ES.EFFDT) 
  AND C.LOCATION            = B.LOCATION 
  AND C.EFFDT               = (SELECT MAX(C_ED.EFFDT) FROM PS_LOCATION_TBL C_ED 
                                WHERE C.SETID     = C_ED.SETID 
                                  AND C.LOCATION  = C_ED.LOCATION 
                                  AND C_ED.EFFDT <= B.EFFDT) 
  AND C.SETID               = B.SETID_LOCATION 
  AND D.COUNTRY             = C.COUNTRY 
  AND D.STATE               = C.STATE 
  AND D.EFFDT               = (SELECT MAX(D_ED.EFFDT) FROM PS_GPMX_ST_TAXES_C D_ED 
                                WHERE D.COUNTRY   = D_ED.COUNTRY 
                                  AND D.STATE     = D_ED.STATE 
                                  AND D.PIN_NUM   = D_ED.PIN_NUM 
                                  AND D_ED.EFFDT <= A.SLICE_END_DT) 
  AND D.PIN_NUM             = A.PIN_NUM 
  AND D.STATE               = $State
  AND A.GP_PAYGROUP         = $Gp_Paygroup 
  AND A.CAL_ID              = $Cal_Id
  AND A.EMPLID         NOT IN (SELECT H.EMPLID 
                                 FROM PS_GP_PI_MNL_DATA H 
                                WHERE H.GP_PAYGROUP = A.GP_PAYGROUP 
                                  AND H.CAL_ID           = A.CAL_ID 
                                  AND H.EMPLID           = A.EMPLID 
                                  AND H.EMPL_RCD         = A.EMPL_RCD 
                                  AND H.PIN_NUM          = #Pin_Num_DD )
  AND D.COUNTRY             = 'MEX'
  AND A.CAL_RUN_ID          = E.CAL_RUN_ID 
  AND A.CAL_RUN_ID          = F.CAL_RUN_ID 
  AND A.GP_PAYGROUP         = F.GP_PAYGROUP 
  AND A.CAL_ID              = F.CAL_ID 
  AND F.CALC_TYPE           = 'P' 
  AND A.ORIG_CAL_RUN_ID     = A.CAL_RUN_ID 
  AND E.RUN_FINALIZED_IND   = 'N'
  AND A.SLICE_BGN_DT       >= $BgnDt
  AND A.SLICE_END_DT       <= $EndDt
END-SQL
END-PROCEDURE Insert_GP_PI_MNL_DATA

!***************************************************************!
! Function: Delete GP_PI_MNL_SOVR 
! Description:  Delete PI for Var IE VR BASE EXCPT
!***************************************************************!
BEGIN-PROCEDURE Del_GP_PI_MNL_SOVR
BEGIN-SQL
DELETE FROM PS_GP_PI_MNL_SOVR SOVR
WHERE EXISTS( SELECT DT.EMPLID FROM PS_GP_PI_MNL_DATA DT, PS_JOB B
               WHERE SOVR.PIN_NUM      = DT.PIN_NUM
               AND SOVR.EMPLID       = DT.EMPLID 
               AND SOVR.EMPL_RCD     = DT.EMPL_RCD 
               AND SOVR.GP_PAYGROUP  = DT.GP_PAYGROUP
               AND SOVR.CAL_ID       = DT.CAL_ID 
               AND DT.EMPLID         = B.EMPLID 
               AND DT.EMPL_RCD       = B.EMPL_RCD 
               AND DT.GP_PAYGROUP    = B.GP_PAYGROUP 
               AND B.EFFDT           = (SELECT MAX(B_ED.EFFDT) FROM PS_JOB B_ED 
                              WHERE B.EMPLID    = B_ED.EMPLID 
                                AND B.EMPL_RCD  = B_ED.EMPL_RCD 
                                AND B_ED.EFFDT <= $EndDt) 
            AND B.EFFSEQ            = (SELECT MAX(B_ES.EFFSEQ) FROM PS_JOB B_ES 
                              WHERE B.EMPLID    = B_ES.EMPLID 
                                AND B.EMPL_RCD  = B_ES.EMPL_RCD 
                                AND B.EFFDT     = B_ES.EFFDT)
               AND B.COMPANY        = $Company
               AND (DT.BGN_DT       >= $BgnDt 
               OR DT.BGN_DT IS NULL)
               AND (DT.END_DT       <= $EndDt
               OR DT.END_DT IS NULL)
               AND DT.PIN_NUM       = #Pin_Num_DD)
END-SQL
END-PROCEDURE Del_GP_PI_MNL_SOVR




!***************************************************************!
! Function   : Insert_GP_PI_MNL_SOVR.
! Description:  Insert PI in Record GP_PI_MNL_SOVR.
!***************************************************************!
BEGIN-PROCEDURE Insert_GP_PI_MNL_SOVR 

BEGIN-SQL 
INSERT INTO PS_GP_PI_MNL_SOVR  
( GP_PAYGROUP
, CAL_ID
, EMPLID
, EMPL_RCD
, PIN_NUM
, INSTANCE
, PIN_SOVR_NUM
, ENTRY_TYPE_SOVR
, SOVR_VAL_CHAR
, SOVR_VAL_NUM
, SOVR_VAL_DT
, PIN_SOVR_SETID_NUM
, SETID
)
SELECT DISTINCT 
A.GP_PAYGROUP,
A.CAL_ID,
A.EMPLID,
A.EMPL_RCD,
G.PIN_NUM,
G.INSTANCE,
#Pin_Num_VR,
$Entry_Type_VR||'0',
' ',
#BaseImpEstTot,
'',
0,
' '
 FROM PS_GP_RSLT_ERN_DED A
    , PS_JOB B
    , PS_LOCATION_TBL C
    , PS_GPMX_ST_TAXES_C D
    , PS_GP_CAL_RUN E
    , PS_GP_CAL_RUN_DTL F
    , PS_GP_PI_MNL_DATA G
WHERE A.EMPLID            = B.EMPLID 
  AND A.EMPL_RCD          = B.EMPL_RCD 
  AND A.GP_PAYGROUP       = B.GP_PAYGROUP 
  AND A.EMPLID            = G.EMPLID 
  AND A.EMPL_RCD          = G.EMPL_RCD 
  AND A.GP_PAYGROUP       = G.GP_PAYGROUP
  AND A.CAL_ID            = G.CAL_ID
  AND G.PIN_NUM           = #Pin_Num_DD
  AND B.EFFDT             = (SELECT MAX(B_ED.EFFDT) FROM PS_JOB B_ED 
                              WHERE B.EMPLID    = B_ED.EMPLID 
                                AND B.EMPL_RCD  = B_ED.EMPL_RCD 
                                AND B_ED.EFFDT <= A.SLICE_END_DT) 
  AND B.EFFSEQ            = (SELECT MAX(B_ES.EFFSEQ) FROM PS_JOB B_ES 
                              WHERE B.EMPLID    = B_ES.EMPLID 
                                AND B.EMPL_RCD  = B_ES.EMPL_RCD 
                                AND B.EFFDT     = B_ES.EFFDT) 
  AND C.LOCATION          = B.LOCATION 
  AND C.EFFDT             = (SELECT MAX(C_ED.EFFDT) FROM PS_LOCATION_TBL C_ED 
                            WHERE C.SETID       = C_ED.SETID 
                              AND C.LOCATION    = C_ED.LOCATION 
                              AND C_ED.EFFDT   <= B.EFFDT) 
  AND C.SETID             = B.SETID_LOCATION 
  AND D.COUNTRY           = C.COUNTRY 
  AND D.STATE             = C.STATE 
  AND D.EFFDT             = (SELECT MAX(D_ED.EFFDT) FROM PS_GPMX_ST_TAXES_C D_ED 
                              WHERE D.COUNTRY   = D_ED.COUNTRY 
                                AND D.STATE     = D_ED.STATE 
                                AND D.PIN_NUM   = D_ED.PIN_NUM 
                                AND D_ED.EFFDT <= A.SLICE_END_DT) 
  AND D.PIN_NUM           = A.PIN_NUM 
  AND A.CAL_RUN_ID        = E.CAL_RUN_ID 
  AND A.CAL_RUN_ID        = F.CAL_RUN_ID 
  AND A.GP_PAYGROUP       = F.GP_PAYGROUP 
  AND A.CAL_ID            = F.CAL_ID 
  AND F.CALC_TYPE         = 'P' 
  AND A.ORIG_CAL_RUN_ID   = A.CAL_RUN_ID 
  AND E.RUN_FINALIZED_IND = 'N' 
  AND C.STATE             = $State 
  AND A.GP_PAYGROUP       = $Gp_Paygroup 
  AND A.CAL_ID            = $Cal_Id
  AND B.COMPANY           = $Company
  AND A.SLICE_BGN_DT     >= $BgnDt
  AND A.SLICE_END_DT     <= $EndDt
END-SQL
END-PROCEDURE Insert_GP_PI_MNL_SOVR


!***************************************
!Procedure Get-Earns-Ded
!***************************************
begin-procedure Get-Earns-Ded
  let #countern = 0
  let #countded = 0
  let #CountEmpState = 0
  let #CountEmpCia = 0
  let #ban = 0
Begin-Select
A.COMPANY
D.DESCR
B.STATE
C.DESCR           ()  On-Break Print=never Level=6 After=New-State
F.PIN_NUM
G.PIN_NM
G.DESCR
G.PIN_TYPE
SUM(F.CALC_RSLT_VAL)  &F.CALC_RSLT_VAL
SUM(F.CALC_ADJ_VAL)  &F.CALC_ADJ_VAL

 let $Company1 = RTRIM(&A.COMPANY,' ')
 let $Desc_company = RTRIM(&D.DESCR, ' ')
 let $TState = RTRIM(&B.STATE, ' ')
 let $Desc_state = RTRIM(&C.DESCR,' ')
 let #pin_nmp = &F.PIN_NUM
 do Get-Pin-Descr

 if &F.CALC_ADJ_VAL = 0
    let #imp = &F.CALC_RSLT_VAL
 else
    let #imp = &F.CALC_ADJ_VAL
 end-if

   if &G.PIN_TYPE ='ER'
     put &F.PIN_NUM $Desc_ernded #imp into Percepciones(#countern)
     let #countern = #countern + 1
   else
     put &F.PIN_NUM $Desc_ernded #imp into Deducciones(#countded)
     let #countded = #countded + 1
   end-if

  FROM PS_JOB A,  PS_LOCATION_TBL B, PS_STATE_NAMES_TBL C, PS_COMPANY_TBL D,
       PS_GP_RSLT_ERN_DED F, PS_GP_PIN G, PS_GPMX_ST_TAXES_C S
  WHERE A.COMPANY = $Company
     AND A.EFFDT =
        (SELECT MAX(A_ED.EFFDT) FROM PS_JOB A_ED
        WHERE A.EMPLID = A_ED.EMPLID
          AND A.EMPL_RCD = A_ED.EMPL_RCD
          AND A_ED.EFFDT <= $EndDt)
    AND A.EFFSEQ =
        (SELECT MAX(A_ES.EFFSEQ) FROM PS_JOB A_ES
        WHERE A.EMPLID = A_ES.EMPLID
          AND A.EMPL_RCD = A_ES.EMPL_RCD
          AND A.EFFDT = A_ES.EFFDT)
     AND B.SETID = A.SETID_LOCATION
     AND B.LOCATION = A.LOCATION
     AND B.EFFDT = (SELECT MAX(B_ED.EFFDT) FROM PS_LOCATION_TBL B_ED
        WHERE B.SETID = B_ED.SETID
          AND B.LOCATION = B_ED.LOCATION
          AND B_ED.EFFDT <= A.EFFDT)
     AND C.COUNTRY = B.COUNTRY
     AND C.STATE = B.STATE
     AND [$where]
     AND D.COMPANY = A.COMPANY
     AND D.EFFDT = (SELECT MAX(D_ED.EFFDT) FROM PS_COMPANY_TBL D_ED
        WHERE D.COMPANY = D_ED.COMPANY
          AND D_ED.EFFDT <= A.EFFDT)
     AND A.EMPLID = F.EMPLID
     AND A.EMPL_RCD = F.EMPL_RCD
     AND A.GP_PAYGROUP = F.GP_PAYGROUP
     AND F.SLICE_BGN_DT >= $BgnDt
     AND F.SLICE_END_DT <= $EndDt
     AND F.PIN_NUM = G.PIN_NUM
     AND S.COUNTRY = 'MEX'
     AND B.STATE = S.STATE
     AND F.PIN_NUM = S.PIN_NUM
     AND S.EFFDT = (SELECT MAX(S_ED.EFFDT) FROM PS_GPMX_ST_TAXES S_ED
        WHERE S.COUNTRY = S_ED.COUNTRY
          AND S.STATE = S_ED.STATE
          AND S_ED.EFFDT <= $EndDt)
     AND F.RSLT_SEG_NUM = (SELECT MAX(AM.RSLT_SEG_NUM) FROM PS_GP_RSLT_ERN_DED AM
          WHERE F.EMPLID = AM.EMPLID
            AND F.CAL_RUN_ID = AM.CAL_RUN_ID
            AND F.EMPL_RCD = AM.EMPL_RCD
            AND F.GP_PAYGROUP = AM.GP_PAYGROUP
            AND F.CAL_ID = AM.CAL_ID
            AND F.ORIG_CAL_RUN_ID = AM.ORIG_CAL_RUN_ID
            AND F.PIN_NUM = AM.PIN_NUM
            AND F.SLICE_BGN_DT >= $BgnDt
            AND F.SLICE_END_DT <= $EndDt)
    GROUP BY A.COMPANY, D.DESCR, B.STATE, C.DESCR,  F.PIN_NUM, G.PIN_NM, G.DESCR, G.PIN_TYPE
    ORDER BY A.COMPANY, D.DESCR, B.STATE, C.DESCR,  F.PIN_NUM, G.PIN_NM, G.DESCR, G.PIN_TYPE
End-Select
  uppercase $Foot5
  print $Foot5 (+1,{d1}) bold
  print #CountEmpCia (,+2) edit '99,999,999' bold
End-procedure Get-Earns-Ded


!***************************************
!Procedure CountEmpGp
!***************************************
begin-procedure CountEmpGp
  let #CountEmpGpoPay = 0
Begin-Select
COUNT(DISTINCT FF.EMPLID)  &countemp
  let #CountEmpGpoPay =  &countemp
  FROM PS_JOB AA,  PS_LOCATION_TBL BB, PS_STATE_NAMES_TBL CC, PS_COMPANY_TBL DD,
       PS_GP_RSLT_ERN_DED FF, PS_GP_PIN GG, PS_GPMX_ST_TAXES_C SS
  WHERE AA.COMPANY = $Company1
     AND AA.EFFDT =
        (SELECT MAX(AA_ED.EFFDT) FROM PS_JOB AA_ED
        WHERE AA.EMPLID = AA_ED.EMPLID
          AND AA.EMPL_RCD = AA_ED.EMPL_RCD
          AND AA_ED.EFFDT <= $EndDt)
    AND AA.EFFSEQ =
        (SELECT MAX(AA_ES.EFFSEQ) FROM PS_JOB AA_ES
        WHERE AA.EMPLID = AA_ES.EMPLID
          AND AA.EMPL_RCD = AA_ES.EMPL_RCD
          AND AA.EFFDT = AA_ES.EFFDT)
     AND BB.SETID = AA.SETID_LOCATION
     AND BB.LOCATION = AA.LOCATION
     AND BB.EFFDT = (SELECT MAX(BB_ED.EFFDT) FROM PS_LOCATION_TBL BB_ED
        WHERE BB.SETID = BB_ED.SETID
          AND BB.LOCATION = BB_ED.LOCATION
          AND BB_ED.EFFDT <= AA.EFFDT)
     AND CC.COUNTRY = BB.COUNTRY
     AND CC.STATE = BB.STATE
     AND DD.COMPANY = AA.COMPANY
     AND DD.EFFDT = (SELECT MAX(DD_ED.EFFDT) FROM PS_COMPANY_TBL DD_ED
        WHERE DD.COMPANY = DD_ED.COMPANY
          AND DD_ED.EFFDT <= AA.EFFDT)
     AND AA.EMPLID = FF.EMPLID
     AND AA.EMPL_RCD = FF.EMPL_RCD
     AND AA.GP_PAYGROUP = FF.GP_PAYGROUP
     AND FF.SLICE_BGN_DT >= $BgnDt
     AND FF.SLICE_END_DT <= $EndDt
     AND FF.PIN_NUM = GG.PIN_NUM
     AND AA.COMPANY = $Company1
     AND BB.STATE = $Tstate
     AND SS.COUNTRY = 'MEX'
     AND BB.STATE = SS.STATE
     AND FF.PIN_NUM = SS.PIN_NUM
     AND SS.EFFDT = (SELECT MAX(SS_ED.EFFDT) FROM PS_GPMX_ST_TAXES SS_ED
        WHERE SS.COUNTRY = SS_ED.COUNTRY
          AND SS.STATE = SS_ED.STATE
          AND SS_ED.EFFDT <= $EndDt)
End-Select
  let #CountEmpState = #CountEmpState + #CountEmpGpoPay
  let #CountEmpCia = #CountEmpCia +  #CountEmpGpoPay
End-procedure CountEmpGp


!**********************************
!Procedure New-State
!**********************************
begin-Procedure New-State
  let #ind = 0
  let #TotEarnGpo = 0
  let #TotDedGpo = 0

  if #countern > #countded
     let #limite  = #countern
  else
     let #limite  = #countded
  end-if

  if #limite <> 0
  while #ind <= (#limite - 1)
      print ' ' (+1)
      if #ind <= #CountErn
         get #ErnCd $ErnDesc #importe FROM percepciones(#ind)
         if #ErnCd <> 0
            print #ErnCd   (,{d1}) edit '99999999'
            print $ErnDesc (,{d2},40)
            print #importe (,{d3}) edit 999,999,999,999,999.99
            add #importe to #TotEarnGpo
         end-if
      end-if
      if #ind <= #CountDed
         get #DedCd $DedDesc #importe FROM deducciones(#ind)
         if #DedCd <> 0
            print #DedCd   (,{d4}) edit '99999999'
            print $DedDesc (,{d5},25)
            print #importe (,{d6}) edit 999,999,999,999,999.99
            add #importe to #TotDedGpo
         end-if
      end-if
      add 1 to #ind
  end-while
  print $Foot1 (+2,{d1}) bold
  print #TotEarnGpo (,{d3}) edit 999,999,999,999,999.99 bold
  print $Foot2 (0,{d4})  bold
  print #TotDedGpo (,{d6}) edit 999,999,999,999,999.99 bold
  let #Subtot = #TotEarnGpo -  #TotDedGpo
  print #Subtot (0,{d7}) edit 999,999,999,999,999.99 bold
  do CountEmpGp
  print $Foot3     (+1,{d1}) bold
  print #CountEmpGpoPay (,+2) edit '99999' bold
  end-if
  Clear-Array Name=Percepciones
  Clear-Array Name=Deducciones
  let #countern = 0
  let #countded = 0
  do Get-Tax
  print $Foot4 (+1,{d1}) bold
  print #CountEmpState (,+2) edit '99999' bold
  let #CountEmpState = 0
  NEW-PAGE
End-Procedure New-State


!***************************************
!Procedure Get-Tax
!***************************************
begin-procedure Get-Tax
  let #f  = 0
Begin-Select
WA.SEQNUM
WA.PERCENT_RATE
WA.PIN_NUM
N.PIN_NM
SUM(WA.AMT) &suma
SUM(WA.GPMX_BASE) &Amount
   
   Let #Amount = &Amount
   
   if #f = 0
      let $Tfoot6 = $foot6 || ' ' || upper(RTRIM($TState,' ')) || ', ' ||  upper(RTRIM($Desc_state,' ')) || ' ' || $foot7 || ' ' || '%' || to_char(&WA.PERCENT_RATE) || ' ' || $foot8
      print $Tfoot6 (+3,{d1})
        if $Wa_Values = 'Y'
          print #Amount (0,+5) edit 999,999,999,999,999.99 bold
        else
          print #Subtot (0,+5) edit 999,999,999,999,999.99 bold
        End-If              
      if #Amount <> #Subtot
          Let $State_Aux = upper(RTRIM($Desc_state,' '))
          Show 'La base de cálculo del impuesto procesada en la(s) nómina(s) para el estado de '  $State_Aux ' es diferente a la suma de las percepciones y deducciones actualmente parametrizada.'
          show 'Base Procesada en las nominas = ' #Amount
          show 'Base Suma Percepciones y Deduciones = ' #Subtot
      End-If
      let #f = 1
   end-if
   print '%'              (+1,1)
   print &WA.PERCENT_RATE (0,+1,5)
   print &N.PIN_NM        (0,+2,25)
   print &suma            (0,35) edit '999,999,999,999.99'
FROM PS_GPMX_ST_TAX_WA WA, PS_GP_PIN N
WHERE WA.COMPANY =$Company1
AND WA.PERIOD_BEGIN_DT >= $BgnDt
AND WA.PERIOD_END_DT  <= $EndDt
AND WA.STATE = $TState
AND WA.PIN_NUM = N.PIN_NUM
AND WA.RSLT_SEG_NUM = (SELECT MAX(MAW.RSLT_SEG_NUM) FROM PS_GPMX_ST_TAX_WA MAW WHERE WA.EMPLID = MAW.EMPLID
                       AND WA.CAL_RUN_ID = MAW.CAL_RUN_ID
                       AND WA.EMPL_RCD = MAW.EMPL_RCD
                       AND WA.GP_PAYGROUP = MAW.GP_PAYGROUP
                       AND WA.CAL_ID = MAW.CAL_ID
                       AND WA.RSLT_SEG_NUM = MAW.RSLT_SEG_NUM
                       AND WA.ORIG_CAL_RUN_ID = MAW.ORIG_CAL_RUN_ID)
GROUP BY WA.SEQNUM,WA.PERCENT_RATE, WA.PIN_NUM, N.PIN_NM
ORDER BY WA.SEQNUM, WA.PERCENT_RATE, WA.PIN_NUM, N.PIN_NM DESC
End-Select
End-procedure Get-Tax

!***************************************
!Procedure Get-Pin-Descr
!***************************************
begin-procedure  Get-Pin-Descr
  let $pin_descr = ''
  let $pin_nm = ''
begin-select
PD.DESCR
PD.PIN_NM
PD.PIN_CODE
  MOVE &PD.PIN_NM TO $pin_nm
  MOVE &PD.DESCR  TO $pin_descr
  MOVE &PD.PIN_CODE TO $pin_code
from PS_GP_PIN PD
WHERE PD.PIN_NUM = #pin_nmp
end-select
begin-select
PDL.DESCR
  MOVE &PDL.DESCR TO $pin_descr
from PS_GP_PIN_LANG PDL
WHERE PDL.PIN_NUM = #pin_nmp
and   PDL.LANGUAGE_CD = $Curr_language_Cd
end-select
Let $Desc_ernded = RTRIM($pin_nm, ' ') || ' ' || RTRIM($pin_descr, ' ')
end-procedure   Get-Pin-Descr

!**********************************
!Procedure Report-Translation
!**********************************
begin-Procedure Report-Translation
 do Init_Report_Translation ('GPMXIE01', $Curr_language_cd)
 do Append_Report_Translation ('GPMXIE01')
 do Get_Field_Information ('GPMXIE01', 'REPORT', $Report,   #DW)
 do Get_Field_Information ('GPMXIE01', 'HEAD1',  $Head1,   #DW)
 do Get_Field_Information ('GPMXIE01', 'HEAD2',  $Head2,   #DW)
 do Get_Field_Information ('GPMXIE01', 'HEAD3',  $Head3,   #DW)
 do Get_Field_Information ('GPMXIE01', 'HEAD4',  $Head4,   #DW)
 do Get_Field_Information ('GPMXIE01', 'HEAD5',  $Head5,   #DW)
 do Get_Field_Information ('GPMXIE01', 'HEAD6',  $Head6,   #DW)
 do Get_Field_Information ('GPMXIE01', 'HEAD7',  $Head7,   #DW)
 do Get_Field_Information ('GPMXIE01', 'PARAM1',  $Param1,   #DW)
 do Get_Field_Information ('GPMXIE01', 'PARAM2',  $Param2,   #DW)
 do Get_Field_Information ('GPMXIE01', 'PARAM3',  $Param3,   #DW)
 do Get_Field_Information ('GPMXIE01', 'PARAM4',  $Param4,   #DW)
 do Get_Field_Information ('GPMXIE01', 'PARAM5',  $Param5,   #DW)
 do Get_Field_Information ('GPMXIE01', 'FOOT1',  $Foot1,   #DW)
 do Get_Field_Information ('GPMXIE01', 'FOOT2',  $Foot2,   #DW)
 do Get_Field_Information ('GPMXIE01', 'FOOT3',  $Foot3,   #DW)
 do Get_Field_Information ('GPMXIE01', 'FOOT4',  $Foot4,   #DW)
 do Get_Field_Information ('GPMXIE01', 'FOOT5',  $Foot5,   #DW)
 do Get_Field_Information ('GPMXIE01', 'FOOT6',  $Foot6,   #DW)
 do Get_Field_Information ('GPMXIE01', 'FOOT7',  $Foot7,   #DW)
 do Get_Field_Information ('GPMXIE01', 'FOOT8',  $Foot8,   #DW)
End-Procedure Report-Translation

#Include 'reset.sqc'     !Reset printer procedure
#Include 'curdttim.sqc'  !Get-Current-DateTime procedure
#Include 'datetime.sqc'  !Routines for date and time formatting
#Include 'number.sqc'    !Routines to format numbers
#Include 'stdapi.sqc'    !Update Process API
#Include 'datemath.sqc'  !Datemath Routines
#Include 'sqrtrans.sqc'
