!***********************************************************************
!  CTX910RP:   PRINT  RL-1 Supplementary from Tax Extract Table        *
!***********************************************************************
!***********************************************************************
!                                                                      *
!                                                                      *
!                                                                      *
!                                                                      *
! This software and related documentation are provided under a         *
! license agreement containing restrictions on use and                 *
! disclosure and are protected by intellectual property                *
! laws. Except as expressly permitted in your license agreement        *
! or allowed by law, you may not use, copy, reproduce,                 *
! translate, broadcast, modify, license, transmit, distribute,         *
! exhibit, perform, publish or display any part, in any form or        *
! by any means. Reverse engineering, disassembly, or                   *
! decompilation of this software, unless required by law for           *
! interoperability, is prohibited.                                     *
! The information contained herein is subject to change without        *
! notice and is not warranted to be error-free. If you find any        *
! errors, please report them to us in writing.                         *
!                                                                      *
!                                                                      *
! Copyright (C) 1988, 2020, Oracle and/or its affiliates.              *
! All Rights Reserved.                                                 *
!***********************************************************************
!***********************************************************************
!                                                                      *
!       $Release:  HR92                                                !
!           $Bug:  31481206                                            !
!                                                                      *
!***********************************************************************

#include 'setenv.sqc' !Set environment
#include 'setup07.sqc'
#Include 'ctxrnctl.sqc'  !Get-Tax-Reporting-Run-Controls procedure

#DEFINE FORMS/PAGE      3
#DEFINE LASTPRINTLINE   17

begin-setup

!#include 'setupdb.sqc'               ! Database specific setup

DECLARE-LAYOUT DEFAULT
PAPER-SIZE      =(150,150)
FORMFEED        =NO
ORIENTATION     =PORTRAIT
TOP-MARGIN      =0.0
LEFT-MARGIN     =0.08
LINE-HEIGHT     = 6pt
END-DECLARE

#ifdef TAXTEST

#ifndef MVS
#ifndef OS400
  declare-printer DEFAULT-HP
  end-declare
  declare-printer DEFAULT-LP
  init-string=<27>E<27>(10U<27>&l0O<27>&l6D<27>&l0E<27>&l80F<27>(s10.0H
  end-declare
#else
  declare-printer DEFAULT-HP
  end-declare
  declare-printer DEFAULT-LP
  init-string=<39>E<39>(10U<39>&l0O<39>&l6D<39>&l0E<39>&l80F<39>(s10.0H
  end-declare
#endif
#else
  declare-printer DEFAULT-HP
  end-declare
  declare-printer DEFAULT-LP
  init-string=<39>E<39>(10U<39>&l0O<39>&l6D<39>&l0E<39>&l80F<39>(s10.0H
  end-declare
#endif

#endif



#ifndef MVS
#ifndef OS400
  declare-printer DEFAULT-HP
  end-declare
 !              |     !--> Perforation Skip
 !              --> Reset
  declare-printer DEFAULT-LP
  init-string=<27>E<27>&l0L
  end-declare
#else
  declare-printer DEFAULT-HP
  end-declare
  declare-printer DEFAULT-LP
  init-string=<39>E<39>&l0L
  end-declare
#endif
#else
  declare-printer DEFAULT-HP
  end-declare
  declare-printer DEFAULT-LP
  init-string=<39>E<39>&l0L
  end-declare
#endif

  declare-procedure
    before-report = Init-Page-Size
  end-declare



end-setup

!***********************************************************************
begin-procedure Init-Page-Size
!***********************************************************************
! This sends a 'Perforation Skip' command to disable any bottom
! margin and obtain the maximum PCL page.  This is required in
! the printing of the Releve 1 laser form.

#ifndef MVS
#ifndef OS400
 encode '<27>&l0L<27>&l3E' into $perforation_skip
#else
 encode '<39>&l0L<39>&l3E' into $perforation_skip
#endif
#else
 encode '<39>&l0L<39>&l3E' into $perforation_skip
#endif

 print $perforation_skip () code

end-procedure

begin-report

  do Init-Report

  do Process-Main

  if $XMLPublisher = 'Y'
     do Capture-OS
     do Insert-YE-Runcontrol
  end-if

  if $SelectEEs = 'R' or $SelectEEs = 'A' or $SelectEEs = 'C'
    close 1
  end-if

!**** XML Publisher
  if $XMLPublisher = 'Y'
      do XML-Close-Tag('CAN_YearEnd', $closeTag)
      do Write-XML-Line(#Fnum2, $closeTag)
      do Close-XML-File(#FNum2)

      if $RC_CAN_YE.RV1_Copy_Type = '2' and #FNum <> 0
         do Write-XML-Line(#Fnum, $closeTag)
         do Close-XML-File(#FNum)
      end-if
  end-if

#ifdef PRCSSCHD
  do StdAPI-Term
#endif

end-report


begin-procedure Init-Report

  display 'PRINT RL-1'
  display '  '
  do Init-DateTime
  do Init-Number
  #ifdef PRCSSCHD
     do StdAPI-Init
  #endif

  do Initialization

  use-printer-type HPLASERJET
  do Declare-Printer-RV1
  do Set-RV1-Row-Pointers
!**
! do Laser-Print-Alignment


end-procedure

begin-procedure Initialization

  move 'Y' to $FirstRecord
  do Get-Current-DateTime

  do Array-Create

  move '1' to $Year4
  move '-' to $DDelimiter
  move '1' to $MMLZero
  display $AsOfToday
  do Format-DateTime($AsOfToday, $AsOfDateYMD, {DEFYMD},'','')
  move $AsOfDateYMD to $AsOfYear xxxx
  display $AsOfYear

  move $AsOfYear  to #AsOfYear
  subtract 1 from #AsOfYear

  do Get-Can-Tax-Processing-Params  !TX.Balance_Year

  move &TX.Balance_Year to $CalYear 9999
  let $AsOfDate = $CalYear || '1231'
  do Format-DateTime($AsOfDate, $AsOfDate, {DEFCMP}, '', 'native')

  do Initialize-OtherInfo-Array

  let #i = 0
  move 'ENG' to $FootNotes_Lang
  do Array-Setup-FootNotes
  move 'CFR' to $FootNotes_Lang
  do Array-Setup-FootNotes

  move &TX.Balance_Year to #TaxYear
  move &TX.Balance_Year to $TaxYear
  let #CalendarYear = &TX.Balance_Year

   if &TX.Balance_Year <> #AsOfYear and $Prcs_Process_Instance = ''
    display ''
    display 'Current Year is ' noline
    display $AsOfYear
    display 'Tax Reporting Year is ' noline
    display $TaxYear
    display 'Current Year is not one greater than Tax Reporting Year.'
    input $Answer maxlen=1 'Current Year is not one greater than Tax Reporting Year. Do you want to continue? (Y/N)'
    uppercase $Answer

    if $Answer <> 'Y'
      stop
    end-if
  end-if

  if $Prcs_Process_Instance = ''
     do Prompts
  else
      #ifdef PRCSSCHD
           do Select-Canadian-YrEnd-Parameters
           do Convert-Parameters
      #endif
  end-if

  if $SelectEEs = 'R' or $SelectEEs = 'A' or $SelectEEs = 'C'
#ifdef MPE
  let $feq='FILE CTX910R1.DAT; REC=-325,,F,ASCII'
  call system using $feq #status
  open 'CTX910R1.DAT' as 1 for-writing record=325:fixed
#else
  open '{IMPORTPREFIX}CTX910R1{IMPORTSUFFIX}' as 1 for-writing record=325:vary
#endif
  end-if

  move $RC_CAN_YE.RL1_Final_Print to $Slip_Final_Print

!**** XML Publisher
  if $XMLPublisher = 'Y'

     do Get-XMLP-Definitions

     do Format-DateTime($AsOfToday, $out, {DEFYMD},'','')
     do Format-DateTime($out, $IssueDate, {DEFYMD},'','native')

     move 0 to #BatchSize
     move 1 to #BatchId
     let $BurstValueSave = ''

     move 7 to #CheckDigit_Calc_Value     ! as per Quebec guidelines
     let #SWDeveloper_Num = 86400001

     let #FNum  = 0
     let #FStat = 0

     do Clear-Guide-Temp

     let $FName2 = 'CTXRLXM1.xml'
     let $sourceFile2     = $FName2 || '{IMPORTSUFFIX}'
     let $sourceLocation2 = '{IMPORTPREFIX}' || ''
     let $sourceFileName2 = rtrim($sourceLocation2,' ') || rtrim($sourceFile2,' ')
     let #FNum2  = 4
     let #FStat2 = 0
     do Open-XML-File($FName2, #FNum2, #FStat2)
     do XML-Open-Tag('CAN_YearEnd', $openTag2)
     do Write-XML-Line(#Fnum2, $openTag2)

  end-if

end-procedure


begin-procedure Prompts

  display ' '
  display ' '
  display ' '
  display 'Select the type of form to use for your RL-1 printing '
  display ' '

  while $FormType = ''
    input $FormType 'Enter L(Laser Form), or S(Standard Tractor Feed), or enter Q to quit'
    uppercase $FormType
    if INSTR('LSQ',$FormType, 1) = 0
      display ' '
      display '***** Enter L, S, or Q to quit *****'
      display ' '
      move '' to $FormType
    end-if
  end-while

  if $FormType = 'X'
      let $XMLPublisher = 'Y'
  else
      let $XMLPublisher = 'N'
  end-if

  if $FormType = 'Q'
     stop
  end-if

  display ' '
  display ' '
  display ' '
  display 'If this job will print forms to send to the Quebec '
  display 'government, enter 1 and the system will establish the '
  display 'correct sort sequence.'
  display ' '
  display 'If this job will print forms for another use (such as '
  display 'self-mailers, laser forms, employer copy, etc., you '
  display 'may select the sort sequence you prefer.  Enter 2 to '
  display 'see the sort options.  You will be shown three levels '
  display 'of sorting options, one level at a time.'
  display ' '

  while $SortInd = ''
    input $SortInd 'Enter 1(Standard Govt Sequence), or 2(Other Sort Options), or enter Q to quit'
    uppercase $SortInd
    if INSTR('12Q',$SortInd, 1) = 0
      display ' '
      display '***** Enter 1, 2, or Q to quit *****'
      display ' '
      move '' to $SortInd
    end-if
  end-while

  if $SortInd = 'Q'
     stop
  end-if

 if $SortInd = '1'
     let $SortSequence = 'SL.REPORTING_ID ASC, ' ||
       'EE.SLIP_SURNAME ASC , EE.SLIP_FIRST_NAME ASC, EE.EMPLID ASC'

 else
  display ' '
  display ' '
  display ' '
  display 'Select the Primary Sort Sequence for your Releve 1 slips.'
  display 'To sequence by Quebec Identification number, enter 1.'
  display 'To sequence by company, enter 2.'
  display ' '

  while $SortInd1 = ''
    input $SortInd1 '1st Sort field: 1(by QID), or 2(by Company), or enter Q to quit'
    uppercase $SortInd1
    if INSTR('12Q',$SortInd1, 1) = 0
      display ' '
      display '***** Enter 1, 2, or Q to quit *****'
      display ' '
      move '' to $SortInd1
    end-if
  end-while

  if $SortInd1 = 'Q'
     stop
  end-if

  if $SortInd1 = '1'
     let $Sort1 = 'SL.REPORTING_ID ASC'
     let $DispSort1 = 'Quebec ID Number; '
  end-if

  if $SortInd1 = '2'
     let $Sort1 = 'SL.COMPANY ASC'
     let $DispSort1 = 'Company; '
  end-if


  display ' '
  display ' '
  display ' '
  display 'Within the previous sort, select the second sort sequence.'
  display '    To sort by Location Code           - enter 1'
  display '    To sort by Department Code         - enter 2'
  display '    To sort by Mail Drop               - enter 3'
  display '    To sort by Postal Code             - enter 4'
  display '    To sort by none of the above       - enter 5'
  display '    To quit                            - enter Q'
  display ' '

  while $SortInd2 = ''
    input $SortInd2 '2nd Sort Field : 1(by LocCode), 2(by DeptCode), 3(by MailDrop), 4(by PostCode), or 5(None), or Q to quit'
    uppercase $SortInd2
    if INSTR('12345Q',$SortInd2, 1) = 0
      display ' '
      display '***** Enter 1, 2, 3, 4, 5, or Q to quit *****'
      display ' '
      move '' to $SortInd2
    end-if
  end-while

  if $SortInd2 = 'Q'
     stop
  end-if

  if $SortInd2 = '1'
     let $Sort2 = 'EE.LOCATION ASC'
     let $DispSort2 = 'Location; '
  end-if

  if $SortInd2 = '2'
     let $Sort2 = 'EE.DEPTID ASC'
     let $DispSort2 = 'Department; '
  end-if

  if $SortInd2 = '3'
     let $Sort2 = 'EE.MAIL_DROP ASC'
     let $DispSort2 = 'Mail Drop; '
  end-if

  if $SortInd2 = '4'
     let $Sort2 = 'EE.POSTAL ASC'
     let $DispSort2 = 'Postal Code; '
  end-if

  if $SortInd2 = '5'
     let $Sort2 = 'N'
  end-if

  display ' '
  display ' '
  display ' '
  display 'Within the previous sorts, select the third sort sequence.'
  display '   To sort by Employee Name  - enter 1'
  display '   To sort by Employee ID    - enter 2'
  display '   To sort by Employee SIN   - enter 3'
  display '   To quit                   - enter Q'
  display ' '

  while $SortInd3 = ''
    input $SortInd3 '3rd Sort Field : 1(by Empl Name), 2(by Empl ID), 3(by Empl SIN), or Q to quit'
    uppercase $SortInd3
    if INSTR('123Q',$SortInd3, 1) = 0
      display ' '
      display '***** Enter 1, 2, 3, or Q to quit *****'
      display ' '
      move '' to $SortInd3
    end-if
  end-while

  if $SortInd3 = 'Q'
     stop
  end-if

  if $SortInd3 = '1'
     let $Sort3 = 'EE.SLIP_SURNAME ASC, EE.SLIP_FIRST_NAME ASC, EE.EMPLID ASC'
     let $DispSort3 = 'Employee Name; Employee ID'
  end-if

  if $SortInd3 = '2'
     let $Sort3 = 'EE.EMPLID ASC'
     let $DispSort3 = 'Employee ID'
  end-if

  if $SortInd3 = '3'
     let $Sort3 = 'EE.SIN ASC'
     let $DispSort3 = 'Employee SIN'
  end-if

  if $Sort2 = 'N'
     let $SortSequence =  $Sort1 || ', ' ||  $Sort3
     let $DisplaySeq = $DispSort1 || $DispSort3
  else
     let $SortSequence = $Sort1 || ', ' || $Sort2 || ', ' || $Sort3
     let $DisplaySeq = $DispSort1 || $DispSort2 || $DispSort3
  end-if
 end-if

   if $SortInd = '2' and $SortInd1 = '1'
      move 'R' to $SortCode
   else
      if $SortInd = '2' and $SortInd1 = '2'
         move 'C' to $SortCode
      else
         move ' ' to $SortCode
      end-if
   end-if

   if $SortInd <> '1'
      display '  '
      display '  '
      display 'Your sort sequence is ' noline
      display $DisplaySeq
      display '  '
      display '  '
     display 'Is this sequence correct?'
     display 'If the sequence is correct, enter Y'

      while $Response = ''
        input $Response 'Enter Y to continue or enter Q to quit'
        uppercase $Response
        if INSTR('YQ',$Response, 1) = 0
          display ' '
          display '***** Enter Y or Q to quit *****'
          display ' '
          move '' to $Response
        end-if
      end-while

     if $Response = 'Q'
        stop
     end-if
   else
     display 'Standard Government sort selected.'
   end-if

  while $SelectEEs = ''
    display ''
    display 'Regular processing or Select employees?'
    input $SelectEEs 'Enter R or S'
    uppercase $SelectEEs
    if INSTR('RS',$SelectEEs,1) = 0
      display 'Enter R or S'
      move '' to $SelectEEs
    end-if
  end-while

  if $SelectEEs = 'S'
    display 'Enter EmplID or hit ENTER to stop selecting'
    move 'AND EE.EMPLID in (''' to $E.SelectedEEs
    move ' ' to $SelectedEmplID
    while $SelectedEmplID <> ''
      input $SelectedEmplID ' '
      if $SelectedEmplID <> ''
        uppercase $SelectedEmplID
        let $E.SelectedEEs = $E.SelectedEEs || $SelectedEmplID || ''','''
        move 'Y' to $EE_Selected
      end-if
    end-while

    if $EE_Selected <> 'Y'
      display ''
      display 'No employees selected. Program stopped.'
      display ''
      stop
    end-if

    let $E.SelectedEEs = SUBSTR($E.SelectedEEs,1,LENGTH($E.SelectedEEs) - 2)
    let $E.SelectedEEs = $E.SelectedEEs || ')'
  else
    move '' to $E.SelectedEEs
  end-if

end-procedure

begin-procedure Array-Create

  create-array name=OtherInfo size=200 -
    field=OtherInfo_Box_Num:char       -
    field=OtherInfo_Box_Amt:number     -
    field=OtherInfo_Text:char

 !stores footnotes for lang Eng & CFR
  create-array name=FootNotes size=200 -
    field=FN_Lang:char
    field=FN_Box_Num:char              -
    field=FN_Box_Priority:number       -
    field=FN_Text:char

  create-array name=FormNotes size=200 -
    field=Box_Num:char                 -
    field=Box_Priority:number          -
    field=Text:char                    -
    field=Box_Value:char               -
    field=Box_Amount:number

end-procedure


begin-procedure Initialize-OtherInfo-Array

  move 0 to #load
  move 0 to #loadamt
  move ' ' to $loadspace
  move 200 to #Max_OtherInfo

  while #load < #Max_OtherInfo

    put $loadspace #loadamt $loadspace into OtherInfo(#load)
        OtherInfo_Box_Num OtherInfo_Box_Amt OtherInfo_Text

    add 1 to #load

  end-while

end-procedure


begin-procedure Array-Setup-FootNotes

begin-SELECT
FN.BOX
FN.BOX_NOTE_SEQ
FN.BOX_NOTE_TEXT
FN.EFFDT

  let $CodeText = ' '
  let $Box = RTRIM(&FN.Box, ' ')
  let $TAXFORM_NOTES-BOX_NOTE_TEXT = &FN.BOX_NOTE_TEXT

  let $FootNotes_Lang = RTRIM($FootNotes_Lang, ' ')
  if $FootNotes_Lang  = 'CFR'
    do Get-Related-Taxform-Notes
  end-if

  evaluate $Box
    when = 'O01'
      move 'RA' to $CodeText
      break
    when = 'O02'
      move 'RB' to $CodeText
      break
    when = 'O03'
      move 'RC' to $CodeText
      break
    when = 'O04'
      move 'RD' to $CodeText
      break
    when = 'O07'
      move 'RG' to $CodeText
      break
    when = 'O08'
      move 'RH' to $CodeText
      break
    when = 'O09'
      move 'RI' to $CodeText
      break
    when = 'O10'
      move 'RJ' to $CodeText
      break
    when = 'O11'
      move 'RK' to $CodeText
      break
    when = 'O12'
      move 'RL' to $CodeText
      break
    when = 'O13'
      move 'RM' to $CodeText
      break
    when = 'O14'
      move 'RN' to $CodeText
      break
    when = 'O15'
      move 'RO' to $CodeText
      break
    when = 'O16'
      move 'RP' to $CodeText
      break
    when = 'O17'
      move 'RQ' to $CodeText
      break
    when = 'O18'
      move 'RR' to $CodeText
      break
    when = 'O19'
      move 'RS' to $CodeText
      break
    when = 'O20'
      move 'RT' to $CodeText
      break
    when = 'O21'
      move 'RU' to $CodeText
      break
    when = 'O22'
      move 'RV' to $CodeText
      break
    when = 'O23'
      move 'RX' to $CodeText
      break
    when = 'O24'
      move 'CA' to $CodeText
      break
    when = 'O25'
      move 'CB' to $CodeText
      break
    when = 'O26'
      move 'CC' to $CodeText
      break
    when = 'O92'
      find '(' in &FN.Box_Note_Text 0 #loc
      if #loc <> - 1
        extract $FootText from &FN.Box_Note_Text 0 #loc
        let $FootText = 'RB ' || $FootText
      end-if
      break
    when-other
      move $TAXFORM_NOTES-BOX_NOTE_TEXT to $FootText
    end-evaluate

    if $CodeText <> ' '
      move $CodeText to $FootText
    end-if

    PUT $FootNotes_Lang &FN.Box &FN.Box_Note_Seq $FootText
            INTO FOOTNOTES(#I) FN_LANG FN_BOX_NUM FN_BOX_PRIORITY FN_TEXT

    move #i to #max_i
    add 1 to #i
    move ' ' to $CodeText

FROM  PS_TAXFORM_NOTES  FN
WHERE FN.TAXFORM_ID  =  'R'
  AND FN.EFFDT = (SELECT
      MAX(EFFDT)
      FROM PS_TAXFORM_NOTES
      WHERE EFFDT <= $AsOfDate
        AND TAXFORM_ID = 'R')
end-SELECT
end-procedure

begin-procedure Get-Related-Taxform-Notes

begin-SELECT
RFN.BOX_NOTE_TEXT
 let $TAXFORM_NOTES-BOX_NOTE_TEXT = &RFN.BOX_NOTE_TEXT

FROM PS_TAXFORM_NT_LANG RFN
WHERE
   RFN.TAXFORM_ID      = 'R'
   AND RFN.BOX         = &FN.BOX
   AND RFN.EFFDT       = &FN.EFFDT
   AND RFN.LANGUAGE_CD = $FootNotes_Lang
end-SELECT
end-procedure


begin-procedure  Process-Main

begin-SELECT
SL.REPORTING_ID
SL.RELEVE_SLIP_NO
SL.COMPANY
SL.WAGE_LOSS_PLAN
SL.EMPLID
SL.CALENDAR_YEAR
SL.PROVINCE
SL.SEQUENCE_NUMBER
SL.YE_SLIP_PROCESS
SL.CAN_YE_SLIP_SEQ
EE.SIN
EE.EMPLID
EE.SLIP_SURNAME
EE.SLIP_FIRST_NAME
EE.SLIP_INITIAL
EE.ADDRESS1
EE.ADDRESS2
EE.CITY
EE.PROVINCE
EE.COUNTRY
EE.POSTAL
EE.LOCATION
EE.DEPTID
EE.MAIL_DROP

 if $Proc_Amend_Cancel = 'Y'
  do Check-Status
 end-if

 if $StatusOpen = 'Y'

  if $FirstRecord = 'N'
    if $SortInd = '1'

      if &EE.EmplID <> $EmplID or
         &SL.CAN_YE_SLIP_SEQ <> #YE_Slip_Seq or
         &SL.Reporting_ID <> $ReportID
         do Print-RV1-Data
         do Finish-Printing

         if &SL.Reporting_ID <> $ReportID
            if $SelectEEs = 'R' or $SelectEEs = 'A' or $SelectEEs = 'C'
              do Write-Summary-Record
            end-if
         end-if
         do Initialize-Employee-Data
         move 0 to #j
      end-if
    else
      if $SortInd1 = '1'
         if &SL.Reporting_ID <> $ReportID or
            &EE.EmplID <> $EmplID  or
            &SL.CAN_YE_SLIP_SEQ <> #YE_Slip_Seq
            do Print-RV1-Data
            do Finish-Printing
            if &SL.Reporting_ID <> $ReportID
               if $SelectEEs = 'R' or $SelectEEs = 'A' or $SelectEEs = 'C'
                 do Write-Summary-Record
               end-if
            end-if
            do Initialize-Employee-Data
            move 0 to #j
         end-if
      else
         if &SL.Company <> $Company or
            &SL.Reporting_ID <> $ReportID or
            &EE.EmplID <> $EmplID or
            &SL.CAN_YE_SLIP_SEQ <> #YE_Slip_Seq
            do Print-RV1-Data
            do Finish-Printing
            if &SL.Company <> $Company
               if $SelectEEs = 'R'  or $SelectEEs = 'A' or $SelectEEs = 'C'
                 do Write-Summary-Record
               end-if
            end-if
            do Initialize-Employee-Data
            move 0 to #j
         end-if
      end-if
    end-if
  else
    move 'N' to $FirstRecord
  end-if

  if $XMLPublisher = 'Y'

  ! create burst value for this slip
     let $BurstValue = rtrim(&EE.EMPLID, ' ') || $CalYear
     let $BurstValue = $BurstValue || rtrim(&SL.Company, ' ') || 'RL1'
     let $BurstValue = $BurstValue || rtrim(&SL.PROVINCE, ' ')
     let $BurstValue = $BurstValue || rtrim(&SL.WAGE_LOSS_PLAN, ' ')

     do format-number(&SL.SEQUENCE_NUMBER, $RL1SeqNo, '09')
     do format-number(&SL.CAN_YE_SLIP_SEQ, $RL1SlipseqNo, '09')
     let $BurstValue = $BurstValue || $RL1SeqNo || $RL1SlipseqNo
!

     if $RC_CAN_YE.RV1_Copy_Type = '2'

        if $BurstValue <> $BurstValueSave

           if $BurstValueSave <> ''
!              do XML-Close-Tag('RL1_Data',$closeTag)
!              do Write-XML-Line(#Fnum, $closeTag)
              do XML-Close-Tag('CAN_YearEnd', $closeTag)
              do Write-XML-Line(#Fnum, $closeTag)
              do Close-XML-File(#FNum)
           end-if

           move $BurstValue to $BurstValueSave

           let $FName = rtrim($BurstValue,' ') || '.xml'
           let $sourceFile     = $FName || '{IMPORTSUFFIX}'
           let $sourceLocation = '{IMPORTPREFIX}' || ''
           let $sourceFileName = rtrim($sourceLocation,' ') || rtrim($sourceFile,' ')
           let #FNum  = 3
           let #FStat = 0
           do Open-XML-File($FName, #FNum, #FStat)
           do XML-Open-Tag('CAN_YearEnd', $openTag)
           do Write-XML-Line(#Fnum, $openTag)
        end-if
     end-if

     do XML-Open-Tag('RL1_Data', $openTag)

     if $RC_CAN_YE.RV1_Copy_Type = '2' and #FNum <> 0
        do Write-XML-Line(#Fnum, $openTag)
     end-if

     do Get-SS-Consent
     if $UserFlag = 'Y'
        do Write-XML-Line(#Fnum2, $openTag)
     end-if

     if #OPT_BLK.PY_RPT_BATCH_SIZE > 0
        add 1 to #BatchSize
        if #BatchSize > #OPT_BLK.PY_RPT_BATCH_SIZE
           move 0 to #BatchSize
           add 1 to #BatchId
        end-if
     end-if

     do Build-XML-Line('BURST_VALUE', $BurstValue, 'Y', #FNum, #FNum2, $UserFlag)
     do Build-XML-Line('EMPLID', &EE.EMPLID, 'Y', #FNum, #FNum2, $UserFlag)

     do format-number(#BatchId, $RL1Batch, '09999')
     do Build-XML-Line('BATCH_ID', $RL1Batch, 'Y', #FNum, #FNum2, $UserFlag)
  end-if


  do Check-Releve-Start-No
  do Format-Employee-Data
  do Get-Slip-Detail

  if $SelectEEs = 'A' or
     $SelectEEs = 'E'

     do Get-Original-Slip-No
  end-if

  if &SL.Company <> $PriorCompany
      move &SL.Company to $Company
      do Get-Setup-Viewing-Date
      do Get-Company-Data
      move $Company to $PriorCompany
      move 'Y'      to $Compbrk
  end-if

  if $Proc_Amend_Cancel = 'Y' and $RC_CAN_YE.RL1_Final_Print = 'Y'
    do Close-Amend-Cancel-RL1
  end-if

  if $XMLPublisher = 'Y' and $CopyType = '2'

     do Insert-Guide-Data
  end-if

 end-if

FROM   PS_CAN_YE_SLIP  SL,
       PS_CAN_YE_EMPL  EE
WHERE  SL.CALENDAR_YEAR = &TX.Balance_Year
  AND  SL.TAXFORM_ID = 'R'
  AND  SL.PROCESS_FLAG <> 'V'
  AND  EE.CALENDAR_YEAR = &TX.Balance_Year
  AND  EE.EMPLID = SL.EMPLID
#ifdef MVS
  \$E.SelectedEEs\
#else
  [$E.SelectedEEs]             !NULL string if SELECT EEs option not used
#endif
  AND  EE.COMPANY = SL.COMPANY
  AND  EE.SEQUENCE_NUMBER = SL.SEQUENCE_NUMBER
  AND  SL.SEQUENCE_NUMBER = (SELECT MAX(SL1.SEQUENCE_NUMBER)
               FROM PS_CAN_YE_SLIP SL1
               WHERE SL1.COMPANY = SL.COMPANY
                 AND SL1.EMPLID  = SL.EMPLID
                 AND SL1.CALENDAR_YEAR = SL.CALENDAR_YEAR
#ifdef MVS
  \$seq_number\
#else
  [$seq_number]
#endif
#ifdef MVS
  \$ye_slip_process\
#else
  [$ye_slip_process]
#endif
#ifdef MVS
  ORDER BY \$SORTSEQUENCE\
#else
  ORDER by [$SortSequence]
#endif
end-SELECT

   if $EmplID <> ''
     do Print-RV1-Data
     do Finish-Printing

     if $SelectEEs = 'R' or $SelectEEs = 'A' or $SelectEEs = 'C'
       do Write-Summary-Record
     end-if
   end-if

end-procedure

begin-procedure Check-Releve-Start-No

begin-SELECT
RV.RELEVE_SERIAL_BEG
RV.RELEVE_SERIAL_END

  if &RV.Releve_Serial_Beg > 0  and
     &RV.Releve_Serial_End = 0
        display 'If you plan to file by magnetic media, you must run '
        display 'job CTX910RM.SQR first to calculate the Releve Serial '
        display 'numbers to be printed on the paper forms.  If you do  '
        display 'not plan to file by magnetic media, set the Beginning '
        display 'Serial Number to 0 on the PQ_REPTNG_TBL. Then run this job.'
      STOP
  end-if

FROM PS_PQ_REPTNG_TBL  RV
WHERE RV.COMPANY = &SL.COMPANY
  AND EFFDT =
  (SELECT MAX(EFFDT)
     FROM PS_PQ_REPTNG_TBL
     WHERE  COMPANY = RV.COMPANY
       AND  EFFDT  <= $AsOfDate)
end-SELECT

end-procedure


begin-procedure Get-SLIP-Detail

Move 8 to #MaxOtherInfo

begin-SELECT
DE.BOX
DE.CAN_YE_BOX_TEXT
DE.CAN_YE_BOX_AMT
PD.LANG_CD

  let $Box = RTRIM(&DE.Box, ' ')

  let $Preferred_Lang = RTRIM(&PD.LANG_CD, ' ')
  if $Preferred_Lang <> 'ENG'
    move 'CFR' to $Preferred_Lang
  end-if

  if rtrim(&DE.BOX, ' ') = 'CDO'
    let $Code = RTRIM(&DE.CAN_YE_BOX_TEXT, ' ')
    let $Print_Code = 'Y'
  else
    if rtrim(&DE.BOX, ' ') = 'B01' or
       rtrim(&DE.BOX, ' ') = 'B-1'
      move &DE.CAN_YE_BOX_AMT   to #EmpCPPAmt
    end-if
  end-if

 if #CalendarYear < 2012

  EXTRACT $FN_Ind from &DE.Box 2 1

  if $FN_Ind = '' or $FN_Ind = ' '
      do Accumulate-Employee-Data
  else
      if $Box <> 'CDO'
        EXTRACT $Box_Ind from &DE.Box 0 1

        evaluate $Box_Ind
          when = 'O'
            if $Code = 'RZ'
              do Insert-EmplNote-Data
            end-if
            let $Print_Code = 'Y'
              break
          when-other
            do Insert-EmplNote-Data
        end-evaluate

        if rtrim(&DE.Box, ' ') = 'O92' and $Code = 'RB'
          do Insert-EmplNote-Data
        end-if

        if rtrim(&DE.Box, ' ') = 'O82' or rtrim(&DE.Box, ' ') = 'O30'
         do Insert-EmplNote-Data
        end-if

      end-if
  end-if
 else

  EXTRACT $FN_Ind from &DE.Box 1 1

    if $FN_Ind = '' or $FN_Ind = ' '
       do Accumulate-Employee-Data
    else
       if rtrim(&DE.BOX, ' ') <> 'CDO'
         do Process-Other-Info
       end-if
    end-if

 end-if

FROM  PS_CAN_YE_DETAIL   DE,
      PS_PERS_DATA_EFFDT PD
WHERE DE.EMPLID            = &EE.EmplID
  AND DE.TAXFORM_ID        = 'R'
  AND DE.WAGE_LOSS_PLAN    = &SL.Wage_Loss_Plan
  AND DE.CALENDAR_YEAR     = &TX.Balance_Year
  AND DE.COMPANY           = &SL.Company
  AND DE.SEQUENCE_NUMBER   = &SL.Sequence_Number
  AND DE.CAN_YE_SLIP_SEQ   = &SL.Can_YE_Slip_Seq
  AND DE.EMPLID            = PD.EMPLID
  AND PD.EFFDT =
  (SELECT MAX(PD1.EFFDT)
     FROM PS_PERS_DATA_EFFDT PD1
     WHERE PD1.EMPLID = PD.EMPLID
       AND PD1.EFFDT <= $AsOfDate)
ORDER BY DE.BOX ASC
end-SELECT

end-procedure

begin-procedure Insert-EmplNote-Data


  move 0 to #k
  move 50 to #max_k

  while #k <= #max_k
     get $FindBox from FormNotes(#k) Box_Num


     if $FindBox = &DE.Box
        array-add #DE.Can_YE_Box_Amt to FormNotes(#k) Box_Amount
        break
     else
       if $FindBox = ' ' or $FindBox = ''
          put  &DE.Box &DE.Can_YE_Box_Text &DE.Can_YE_Box_Amt
             into FormNotes(#k) Box_Num Box_Value Box_Amount
           let #max_j = #k + 1

          move 0 to #i
          while #i <= #max_i
            do Get-FootNotes
            if $FN_Box = &DE.Box and (rtrim($FN_lang, ' ') = $Preferred_Lang)
               put #FN_Priority $FN_Text
                 into FormNotes(#k) Box_Priority Text
               break
            else
              if $FN_Box = '' or $FN_Box = ' '
                 break
              else
                 add 1 to #i
              end-if
            end-if
          end-while
       else
          add 1 to #k
       end-if
    end-if
  end-while

end-procedure

begin-procedure Get-FootNotes

  get  $FN_lang
       $FN_Box
       #FN_Priority
       $FN_Text
  from FootNotes(#i)
        FN_LANG
        FN_Box_Num
        FN_Box_Priority
        FN_Text

end-procedure

begin-procedure Accumulate-Employee-Data


    let $Box = RTRIM(&DE.Box, ' ')
    let #BoxAmt = &DE.Can_YE_Box_Amt
    let $BoxText = &DE.Can_YE_Box_Text

    evaluate $Box

       when = 'A'
         if #BoxAmt > 0
            add #BoxAmt to #TotIncome
            add #BoxAmt to #EmpIncome
         end-if

       when = 'B'
         if #BoxAmt > 0
            add #BoxAmt to #TotQPPContrib
            add #BoxAmt to #EmpQPPContrib
         end-if

       when = 'C'
         if #BoxAmt > 0
            add #BoxAmt to #TotUIPrem
            add #BoxAmt to #EmpUIPrem
         end-if

       when = 'D'
         if #BoxAmt > 0
            add #BoxAmt to #TotRPPContrib
            add #BoxAmt to #EmpRPPContrib
         end-if

       when = 'E'
         if #BoxAmt > 0
            add #BoxAmt to #TotTaxWithheld
            add #BoxAmt to #EmpTaxWithheld
         end-if

       when = 'F'
         if #BoxAmt > 0
            add #BoxAmt to #TotUnionDues
            add #BoxAmt to #EmpUnionDues
         end-if

       when = 'G'
         if #BoxAmt > 0
           add #BoxAmt to #TotPensionableEarn
           add #BoxAmt to #EmpPensionableEarn
         end-if

       when = 'H'
         if #BoxAmt > 0
            add #BoxAmt to #TotQPIPPrem
            add #BoxAmt to #EmpQPIPPrem
         end-if

       when = 'I'
         if #BoxAmt > 0
             add #BoxAmt to #TotQPIPInsEarn
             add #BoxAmt to #EmpQPIPInsEarn
         end-if

       when = 'J'
         if #BoxAmt > 0
            add #BoxAmt to #TotPrivHealthContrib
            add #BoxAmt to #EmpPrivHealthContrib
         end-if

       when = 'K'
         if #BoxAmt > 0
            add #BoxAmt to #TotTravel
            add #BoxAmt to #EmpTravel
         end-if

       when = 'L'
         if #BoxAmt > 0
            add #BoxAmt to #TotOtherBenefits
            add #BoxAmt to #EmpOtherBenefits
         end-if

       when = 'M'
         if #BoxAmt > 0
            add #BoxAmt to #TotCommissions
            add #BoxAmt to #EmpCommissions
         end-if

       when = 'N'
         if #BoxAmt > 0
            add #BoxAmt to #TotDonations
            add #BoxAmt to #EmpDonations
         end-if

       when = 'O'
         if #BoxAmt > 0
            add #BoxAmt to #TotOtherIncome
            add #BoxAmt to #EmpOtherIncome
         end-if

       when = 'P'
         if #BoxAmt > 0
            add #BoxAmt to #TotMultiEmplAmt
            add #BoxAmt to #EmpMultiEmplAmt
         end-if

       when = 'Q'
         if #BoxAmt > 0
            add #BoxAmt to #TotDeferredPay
            add #BoxAmt to #EmpDeferredPay
         end-if

       when = 'R'
         if #BoxAmt > 0
            add #BoxAmt to #TotIndianIncome
            add #BoxAmt to #EmpIndianIncome
         end-if

       when = 'S'
         if #BoxAmt > 0
            add #BoxAmt to #TotTip
            add #BoxAmt to #EmpTip
         end-if

       when = 'T'
         if #BoxAmt > 0
            add #BoxAmt to #TotTipsAllocated
            add #BoxAmt to #EmpTipsAllocated
         end-if

       when = 'U'
         if #BoxAmt > 0
            add #BoxAmt to #TotPhasedRetire
            add #BoxAmt to #EmpPhasedRetire
         end-if

       when = 'V'
         if #BoxAmt > 0
            add #BoxAmt to #TotRoomBoard
            add #BoxAmt to #EmpRoomBoard
         end-if

       when = 'W'
         if #BoxAmt > 0
            add #BoxAmt to #TotErAuto
            add #BoxAmt to #EmpErAuto
         end-if

      end-evaluate

end-procedure


begin-procedure Process-Other-Info

  let $Box = RTRIM(&DE.Box, ' ')
  let #BoxAmt = &DE.Can_YE_Box_Amt
  let $BoxText = &DE.Can_YE_Box_Text
  move 0 to #srch_idx

  while #srch_idx < #Max_OtherInfo
    get $i_Box from OtherInfo(#srch_idx) OtherInfo_Box_Num

    if rtrim($i_Box, ' ') = ''

      put $Box #BoxAmt $BoxText into OtherInfo(#OtherInfo_i)
          OtherInfo_Box_Num OtherInfo_Box_Amt OtherInfo_Text
      add 1 to #OtherInfo_i
      do Add-Other-Box-Summary
      break
    else
      if rtrim($i_Box, ' ') = $Box
        array-add #BoxAmt to OtherInfo(#srch_idx) OtherInfo_Box_Amt
        do Add-Other-Box-Summary
        break
      end-if
    end-if

   add 1 to #srch_idx
   end-while

end-procedure


begin-procedure Add-Other-Box-Summary

  evaluate $Box
    when = '028'
      if #BoxAmt > 0
        add #BoxAmt to #EmpOtherIncome
        add #BoxAmt to #TotOtherIncome
      end-if

    when = '030'
      if #BoxAmt > 0
        add #BoxAmt to #EmpPatronageAlloc
        add #BoxAmt to #TotPatronageAlloc
      end-if

    when = '032'
      if #BoxAmt > 0
        add #BoxAmt to #EmpPastPension
        add #BoxAmt to #TotPastPension
      end-if

    when = '036'
      if $BoxText <> ''
        let $Box36Text = RTRIM($BoxText, ' ')
      end-if

    when = '034'
      if #BoxAmt > 0
        add #BoxAmt to #EmpPensionAdj
        add #BoxAmt to #TotPensionAdj
      end-if

    when = '040'
      if #BoxAmt > 0
        add #BoxAmt to #EmpRESPIncome
        add #BoxAmt to #TotRESPIncome
      end-if

    when = '042'
      if #BoxAmt > 0
        add #BoxAmt to #EmpRESPAssistPay
        add #BoxAmt to #TotRESPAssistPay
      end-if

    when-other
      break
  end-evaluate

end-procedure


begin-procedure Get-Original-Slip-No

begin-SELECT

OS.RELEVE_SLIP_NO

  move &OS.Releve_Slip_No to $OriginalSlipNo

FROM PS_CAN_YE_SLIP OS
WHERE OS.COMPANY         = &SL.COMPANY
  AND OS.EMPLID          = &EE.EMPLID
  AND OS.CALENDAR_YEAR   = &TX.BALANCE_YEAR
  AND OS.TAXFORM_ID      = 'R'
  AND OS.YE_SLIP_PROCESS <> 'D'
  AND OS.CAN_YE_SLIP_SEQ = &SL.CAN_YE_SLIP_SEQ
  AND OS.SEQUENCE_NUMBER =
      (SELECT MAX(OS1.SEQUENCE_NUMBER)
         FROM PS_CAN_YE_SLIP OS1
         WHERE OS1.COMPANY = OS.COMPANY
           AND OS1.EMPLID  = OS.EMPLID
           AND OS1.CALENDAR_YEAR = OS.CALENDAR_YEAR
           AND OS1.TAXFORM_ID    = OS.TAXFORM_ID
           AND OS1.YE_SLIP_PROCESS <> 'D'
           AND OS1.CAN_YE_SLIP_SEQ = OS.CAN_YE_SLIP_SEQ
           AND OS1.SEQUENCE_NUMBER < &SL.SEQUENCE_NUMBER)

end-SELECT
end-procedure


begin-procedure Calculate-SWDevlpr-Slip-No

   let #SWDevlpr_Add_Value = mod(#SWDeveloper_Num,#CheckDigit_Calc_Value)

   do Format-Number (#SWDeveloper_Num,$SWDeveloper_Num,'09999999')
   do Format-Number (#SWDevlpr_Add_Value,$SWDevlpr_Add_Value,'9')

   let $SWDeveloper_Nbr = $SWDeveloper_Num || $SWDevlpr_Add_Value
   let $RV1SDevlprNo = edit($SWDeveloper_Nbr, 'xxxbxxxbxxx')

   add 1 to #SWDeveloper_Num
   if #SWDeveloper_Num > 86900000
      let #SWDeveloper_Num = 86400001
   end-if

end-procedure


begin-procedure Print-RV1-Data

  add 1 to #RV1Count

  do Format-Number(#Releve_Slip_No,$Releve_Slip_No,'099999999')
  let $RV1SlipNo = edit($Releve_Slip_No, 'xxxbxxxbxxx')


  if $XMLPublisher = 'Y'
     move ' ' to $BoxAmt
     do Build-XML-Line('RV1SlipNo', $RV1SlipNo, 'Y', #FNum, #FNum2, $UserFlag)

     do Calculate-SWDevlpr-Slip-No
     do Build-XML-Line('SerialNo2', $RV1SDevlprNo, 'Y', #FNum, #FNum2, $UserFlag)

  else
   if #CalendarYear < 2013
    print $RV1SlipNo (#Slip1_Row2,67)
    print $RV1SlipNo (#Slip2_Row2,67)
   else
    print $RV1SlipNo (#Slip2_Row2,67)
   end-if
  end-if

     if $SelectEEs = 'S' or $SelectEEs = 'E'
        let #Dup1_Row =  #Slip1_Row3
        let #Dup2_Row =  #Slip2_Row3
        if $XMLPublisher = 'Y'
           move ' ' to $BoxAmt
           do Build-XML-Line('Duplicate', 'DOUBLE', 'Y', #FNum, #FNum2, $UserFlag)
        else
         if #CalendarYear < 2013
          print 'DOUBLE' (#Dup1_Row,34)
          print 'DOUBLE' (#Dup2_Row,34)
         else
          print 'DOUBLE' (#Slip2_Row2,34)
         end-if
        end-if
     end-if


         if #EmpIncome > 0
            do Format-Number (#EmpIncome, $BoxAmt, '99999999.99')
         else
            move ' ' to $BoxAmt
         end-if

         if $XMLPublisher = 'Y'
            do Build-XML-Line('EmpIncome', $BoxAmt, 'Y', #FNum, #FNum2, $UserFlag)
         else
           if #CalendarYear < 2013
            print $BoxAmt (#Slip1_Row10,3)                  ! Box 'A'
            print $BoxAmt (#Slip2_Row10,3)
           else
            print $BoxAmt (#Slip2_Row9,2)                   ! Box 'A'
           end-if
         end-if

         if #EmpQPPContrib > 0
            do Format-Number (#EmpQPPContrib, $BoxAmt, '99999999.00')
         else
            move ' ' to $BoxAmt
         end-if

         if $XMLPublisher = 'Y'
            do Build-XML-Line('EmpQPPContrib', $BoxAmt, 'Y', #FNum, #FNum2, $UserFlag)
         else
           if #CalendarYear < 2013
            print $BoxAmt (#Slip1_Row10,16)                 ! Box 'B'
            print $BoxAmt (#Slip2_Row10,16)
           else
            print $BoxAmt (#Slip2_Row9,15)                  ! Box 'B'
           end-if
         end-if

         if #EmpUIPrem > 0
            do Format-Number (#EmpUIPrem, $BoxAmt, '99999999.00')
         else
            move ' ' to $BoxAmt
         end-if

         if $XMLPublisher = 'Y'
            do Build-XML-Line('EmpUIPrem', $BoxAmt, 'Y', #FNum, #FNum2, $UserFlag)
         else
           if #CalendarYear < 2013
            print $BoxAmt (#Slip1_Row10,29)                 ! Box 'C'
            print $BoxAmt (#Slip2_Row10,29)
           else
            print $BoxAmt (#Slip2_Row9,29)                  ! Box 'C'
           end-if
         end-if

         if #EmpRPPContrib > 0
            do Format-Number (#EmpRPPContrib, $BoxAmt, '99999999.99')
         else
            move ' ' to $BoxAmt
         end-if

         if $XMLPublisher = 'Y'
            do Build-XML-Line('EmpRPPContrib', $BoxAmt, 'Y', #FNum, #FNum2, $UserFlag)
         else
           if #CalendarYear < 2013
            print $BoxAmt (#Slip1_Row10,42)                 ! Box 'D'
            print $BoxAmt (#Slip2_Row10,42)
           else
            print $BoxAmt (#Slip2_Row9,42)                  ! Box 'D'
           end-if
         end-if

         if #EmpTaxWithheld > 0
            do Format-Number (#EmpTaxWithheld, $BoxAmt, '99999999.00')
         else
            move ' ' to $BoxAmt
         end-if

         if $XMLPublisher = 'Y'
            do Build-XML-Line('EmpTaxWithheld', $BoxAmt, 'Y', #FNum, #FNum2, $UserFlag)
         else
           if #CalendarYear < 2013
            print $BoxAmt (#Slip1_Row10,55)                 ! Box 'E'
            print $BoxAmt (#Slip2_Row10,55)
           else
            print $BoxAmt (#Slip2_Row9,55)                  ! Box 'E'
           end-if
         end-if

         if #EmpUnionDues > 0
            do Format-Number (#EmpUnionDues, $BoxAmt, '99999999.99')
         else
            move ' ' to $BoxAmt
         end-if

         if $XMLPublisher = 'Y'
            do Build-XML-Line('EmpUnionDues', $BoxAmt, 'Y', #FNum, #FNum2, $UserFlag)
         else
           if #CalendarYear < 2013
            print $BoxAmt (#Slip1_Row10,69)                 ! Box 'F'
            print $BoxAmt (#Slip2_Row10,69)
           else
            print $BoxAmt (#Slip2_Row9,68)                  ! Box 'F'
           end-if
         end-if

         if #EmpPensionableEarn >= 0
            do Format-Number (#EmpPensionableEarn, $BoxAmt, '99999999.00')
         else
            move ' ' to $BoxAmt
         end-if

         if $XMLPublisher = 'Y'
            do Build-XML-Line('EmpPensionableEarn', $BoxAmt, 'Y', #FNum, #FNum2, $UserFlag)
         else
           if #CalendarYear < 2013
            print $BoxAmt (#Slip1_Row14,3)                 ! Box 'G'
            print $BoxAmt (#Slip2_Row14,3)
           else
            print $BoxAmt (#Slip2_Row13,2)                 ! Box 'G'
           end-if
         end-if

         if #EmpQPIPPrem  > 0
            do Format-Number (#EmpQPIPPrem, $BoxAmt, '99999999.99')
         else
            move ' ' to $BoxAmt
         end-if

         if $XMLPublisher = 'Y'
            do Build-XML-Line('EmpQPIPPrem', $BoxAmt, 'Y', #FNum, #FNum2, $UserFlag)
         else
           if #CalendarYear < 2013
            print $BoxAmt (#Slip1_Row14,16)                ! Box 'H'
            print $BoxAmt (#Slip2_Row14,16)
           else
            print $BoxAmt (#Slip2_Row13,15)                ! Box 'H'
           end-if
         end-if

         if #EmpQPIPInsEarn  >= 0
            do Format-Number (#EmpQPIPInsEarn, $BoxAmt, '99999999.99')
         else
            move ' ' to $BoxAmt
         end-if

         if $XMLPublisher = 'Y'
            do Build-XML-Line('EmpQPIPInsEarn', $BoxAmt, 'Y', #FNum, #FNum2, $UserFlag)
         else
           if #CalendarYear < 2013
            print $BoxAmt (#Slip1_Row14,29)                ! Box 'I'
            print $BoxAmt (#Slip2_Row14,29)
           else
            print $BoxAmt (#Slip2_Row13,29)                ! Box 'I'
           end-if
         end-if

         if #EmpPrivHealthContrib > 0
            do Format-Number (#EmpPrivHealthContrib, $BoxAmt, '99999999.99')
         else
            move ' ' to $BoxAmt
         end-if

         if $XMLPublisher = 'Y'
            do Build-XML-Line('EmpPrivHealthContrib', $BoxAmt, 'Y', #FNum, #FNum2, $UserFlag)
         else
           if #CalendarYear < 2013
            print $BoxAmt (#Slip1_Row14,42)                ! Box 'J'
            print $BoxAmt (#Slip2_Row14,42)
           else
            print $BoxAmt (#Slip2_Row13,42)                ! Box 'J'
           end-if
         end-if

         if #EmpTravel > 0
            do Format-Number (#EmpTravel, $BoxAmt, '99999999.99')
         else
            move ' ' to $BoxAmt
         end-if

         if $XMLPublisher = 'Y'
            do Build-XML-Line('EmpTravel', $BoxAmt, 'Y', #FNum, #FNum2, $UserFlag)
         else
           if #CalendarYear < 2013
            print $BoxAmt (#Slip1_Row14,55)                ! Box 'K'
            print $BoxAmt (#Slip2_Row14,55)
           else
            print $BoxAmt (#Slip2_Row13,55)                ! Box 'K'
           end-if
         end-if

         if #EmpOtherBenefits > 0
            do Format-Number (#EmpOtherBenefits, $BoxAmt, '99999999.99')
         else
            move ' ' to $BoxAmt
         end-if

         if $XMLPublisher = 'Y'
            do Build-XML-Line('EmpOtherBenefits', $BoxAmt, 'Y', #FNum, #FNum2, $UserFlag)
         else
           if #CalendarYear < 2013
            print $BoxAmt (#Slip1_Row14,69)                ! Box 'L'
            print $BoxAmt (#Slip2_Row14,69)
           else
            print $BoxAmt (#Slip2_Row13,68)                ! Box 'L'
           end-if
         end-if

         if #EmpCommissions > 0
            do Format-Number (#EmpCommissions, $BoxAmt, '99999999.99')
         else
            move ' ' to $BoxAmt
         end-if

         if $XMLPublisher = 'Y'
            do Build-XML-Line('EmpCommissions', $BoxAmt, 'Y', #FNum, #FNum2, $UserFlag)
         else
           if #CalendarYear < 2013
            print $BoxAmt (#Slip1_Row18,3)                 ! Box 'M'
            print $BoxAmt (#Slip2_Row18,3)
           else
            print $BoxAmt (#Slip2_Row17,2)                 ! Box 'M'
           end-if
         end-if

         if #EmpDonations > 0
            do Format-Number (#EmpDonations, $BoxAmt, '99999999.99')
         else
            move ' ' to $BoxAmt
         end-if

         if $XMLPublisher = 'Y'
            do Build-XML-Line('EmpDonations', $BoxAmt, 'Y', #FNum, #FNum2, $UserFlag)
         else
           if #CalendarYear < 2013
            print $BoxAmt (#Slip1_Row18,16)                ! Box 'N'
            print $BoxAmt (#Slip2_Row18,16)
           else
            print $BoxAmt (#Slip2_Row17,15)                ! Box 'N'
           end-if
         end-if

         if #EmpOtherIncome > 0
            do Format-Number (#EmpOtherIncome, $BoxAmt, '99999999.99')
         else
            move ' ' to $BoxAmt
         end-if

         if $XMLPublisher = 'Y'
            do Build-XML-Line('EmpOtherIncome', $BoxAmt, 'Y', #FNum, #FNum2, $UserFlag)
         else
           if #CalendarYear < 2013
            print $BoxAmt (#Slip1_Row18,29)                ! Box 'O'
            print $BoxAmt (#Slip2_Row18,29)
           else
            print $BoxAmt (#Slip2_Row17,29)                ! Box 'O'
           end-if
         end-if

         if #EmpMultiEmplAmt > 0
            do Format-Number (#EmpMultiEmplAmt, $BoxAmt, '99999999.99')
         else
            move ' ' to $BoxAmt
         end-if

         if $XMLPublisher = 'Y'
            do Build-XML-Line('EmpMultiEmplAmt', $BoxAmt, 'Y', #FNum, #FNum2, $UserFlag)
         else
           if #CalendarYear < 2013
            print $BoxAmt (#Slip1_Row18,42)                ! Box 'P'
            print $BoxAmt (#Slip2_Row18,42)
           else
            print $BoxAmt (#Slip2_Row17,42)                ! Box 'P'
           end-if
         end-if

         if #EmpDeferredPay > 0
            do Format-Number (#EmpDeferredPay, $BoxAmt, '99999999.99')
         else
            move ' ' to $BoxAmt
         end-if

         if $XMLPublisher = 'Y'
            do Build-XML-Line('EmpDeferredPay', $BoxAmt, 'Y', #FNum, #FNum2, $UserFlag)
         else
           if #CalendarYear < 2013
            print $BoxAmt (#Slip1_Row18,55)                ! Box 'Q'
            print $BoxAmt (#Slip2_Row18,55)
           else
            print $BoxAmt (#Slip2_Row17,55)                ! Box 'Q'
           end-if
         end-if

         if #EmpIndianIncome > 0
            do Format-Number (#EmpIndianIncome, $BoxAmt, '99999999.99')
         else
            move ' ' to $BoxAmt
         end-if

         if $XMLPublisher = 'Y'
            do Build-XML-Line('EmpIndianIncome', $BoxAmt, 'Y', #FNum, #FNum2, $UserFlag)
         else
           if #CalendarYear < 2013
            print $BoxAmt (#Slip1_Row18,69)                ! Box 'R'
            print $BoxAmt (#Slip2_Row18,69)
           else
            print $BoxAmt (#Slip2_Row17,68)                ! Box 'R'
           end-if
         end-if

         if #EmpTip > 0
            do Format-Number (#EmpTip, $BoxAmt, '99999999.99')
         else
            move ' ' to $BoxAmt
         end-if

         if $XMLPublisher = 'Y'
            do Build-XML-Line('EmpTip', $BoxAmt, 'Y', #FNum, #FNum2, $UserFlag)
         else
           if #CalendarYear < 2013
            print $BoxAmt (#Slip1_Row22,3)                 ! Box 'S'
            print $BoxAmt (#Slip2_Row22,3)
           else
            print $BoxAmt (#Slip2_Row21,2)                 ! Box 'S'
           end-if
         end-if

         if #EmpTipsAllocated > 0
            do Format-Number (#EmpTipsAllocated, $BoxAmt, '99999999.99')
         else
            move ' ' to $BoxAmt
         end-if

         if $XMLPublisher = 'Y'
            do Build-XML-Line('EmpTipsAllocated', $BoxAmt, 'Y', #FNum, #FNum2, $UserFlag)
         else
           if #CalendarYear < 2013
            print $BoxAmt (#Slip1_Row22,16)                ! Box 'T'
            print $BoxAmt (#Slip2_Row22,16)
           else
            print $BoxAmt (#Slip2_Row21,15)                ! Box 'T'
           end-if
         end-if

         if #EmpPhasedRetire > 0
            do Format-Number (#EmpPhasedRetire, $BoxAmt, '99999999.99')
         else
            move ' ' to $BoxAmt
         end-if

         if $XMLPublisher = 'Y'
            do Build-XML-Line('EmpPhasedRetire', $BoxAmt, 'Y', #FNum, #FNum2, $UserFlag)
         else
           if #CalendarYear < 2013
            print $BoxAmt (#Slip1_Row22,29)                ! Box 'U'
            print $BoxAmt (#Slip2_Row22,29)
           else
            print $BoxAmt (#Slip2_Row21,29)                ! Box 'U'
           end-if
         end-if

         if #EmpRoomBoard > 0
            do Format-Number (#EmpRoomBoard, $BoxAmt, '99999999.99')
         else
            move ' ' to $BoxAmt
         end-if

         if $XMLPublisher = 'Y'
            do Build-XML-Line('EmpRoomBoard', $BoxAmt, 'Y', #FNum, #FNum2, $UserFlag)
         else
           if #CalendarYear < 2013
            print $BoxAmt (#Slip1_Row22,42)                ! Box 'V'
            print $BoxAmt (#Slip2_Row22,42)
           else
            print $BoxAmt (#Slip2_Row21,42)                ! Box 'V'
           end-if
         end-if

         if #EmpErAuto > 0
            do Format-Number (#EmpErAuto, $BoxAmt, '99999999.99')
         else
            move ' ' to $BoxAmt
         end-if

         if $XMLPublisher = 'Y'
            do Build-XML-Line('EmpErAuto', $BoxAmt, 'Y', #FNum, #FNum2, $UserFlag)
         else
           if #CalendarYear < 2013
            print $BoxAmt (#Slip1_Row22,55)                ! Box 'W'
            print $BoxAmt (#Slip2_Row22,55)
           else
            print $BoxAmt (#Slip2_Row21,55)                ! Box 'W'
           end-if
         end-if

         if $Code <> ''  and $Print_Code = 'Y'          ! Box Code O
            if $XMLPublisher = 'Y'
               move ' ' to $BoxAmt
               do Build-XML-Line('CodeO', $Code, 'Y', #FNum, #FNum2, $UserFlag)
            else
              if #CalendarYear < 2013
               print $Code (#Slip1_Row22,68)
               print $Code (#Slip2_Row22,68)
              else
               print $Code (#Slip2_Row21,68)
              end-if
            end-if
         end-if

         if #CalendarYear >= 2012
            do Print-Other-Info
         end-if

end-procedure


begin-procedure Print-Other-Info

  let #j = 0
  let #OtherInfoCount = 0

  while #j < #OtherInfo_i
    get $OtherInfo_Box_Num #OtherInfo_Box_Amt $OtherInfo_Text from OtherInfo(#j)
        OtherInfo_Box_Num OtherInfo_Box_Amt OtherInfo_Text

    do Set-Other-Info-Row-Pointers

    add 1 to #OtherInfoCount

    if #OtherInfoCount > #MaxOtherInfo     ! more than 1 RL1 slip
      do Finish-Printing
      let #OtherInfoCount = 0
      do Set-Other-Info-Row-Pointers
      add 1 to #OtherInfoCount

      if $XMLPublisher = 'Y'

         do XML-Open-Tag('RL1_Data', $openTag)

         if $RC_CAN_YE.RV1_Copy_Type = '2' and #FNum <> 0
            do Write-XML-Line(#Fnum, $openTag)
         end-if

         if $UserFlag = 'Y'
            do Write-XML-Line(#Fnum2, $openTag)
         end-if

         do Build-XML-Line('BURST_VALUE', $BurstValue, 'Y', #FNum, #FNum2, $UserFlag)
         do Build-XML-Line('EMPLID', &EE.EMPLID, 'Y', #FNum, #FNum2, $UserFlag)
         do Build-XML-Line('BATCH_ID', $RL1Batch, 'Y', #FNum, #FNum2, $UserFlag)

      end-if

    end-if

    if #OtherInfo_Box_Amt > 0
       do Format-Number (#OtherInfo_Box_Amt, $OtherInfo_Box_Amt, '9999999.99')

       evaluate $OtherInfo_Box_Num
         when = 'RA'
         when = 'RB'
         when = 'RC'
         when = 'RD'
         when = 'RG'
         when = 'RH'
         when = 'RI'
         when = 'RJ'
         when = 'RK'
         when = 'RM'
         when = 'RN'
         when = 'RO'
         when = 'RP'
         when = 'RQ'
         when = 'RR'
         when = 'RS'
         when = 'RT'
         when = 'RU'
         when = 'RV'
         when = 'RX'
         when = 'CA'
         when = 'CB'
         when = 'CC'
         when = 'CD'
           let $OtherInfo_Box_Num = 'RZ-' || $OtherInfo_Box_Num
         when-other
           break
       end-evaluate

       if $XMLPublisher = 'Y'
          do Build-XML-Line($OtherBox, $OtherInfo_Box_Num, 'Y', #FNum, #FNum2, $UserFlag)
          do Build-XML-Line($OtherAmt, $OtherInfo_Box_Amt, 'Y', #FNum, #FNum2, $UserFlag)
       else
         if #CalendarYear < 2013
          print $OtherInfo_Box_Num (#LaserRow,#BoxCol)
          print $OtherInfo_Box_Amt (#LaserRow,#AmtCol)
          print $OtherInfo_Box_Num (#LaserC2Row,#BoxCol)
          print $OtherInfo_Box_Amt (#LaserC2Row,#AmtCol)
         else
          do Alter-Printer-RL1-Small-P9
          print $OtherInfo_Box_Num (#C2LaserRow,#C2BoxCol)
          do Alter-Printer-RL1-Reg
          print $OtherInfo_Box_Amt (#C2LaserRow,#C2AmtCol)
         end-if
       end-if
    end-if

    add 1 to #j

  end-while

  do Initialize-OtherInfo-Array

  move 0  to #OtherInfo_i

end-procedure

begin-procedure Set-Other-Info-Row-Pointers

 evaluate #OtherInfoCount
    when = 0
      let #LaserRow    = #Slip1_Row26
      let #LaserC2Row  = #Slip2_Row26
      let #BoxCol      = 1
      let #AmtCol      = 10
      let #C2LaserRow  = #Slip2_Row24
      let #C2BoxCol    = 7
      let #C2AmtCol    = 14
      let $OtherBox    = 'OtherBox1'
      let $OtherAmt    = 'OtherAmt1'
    when = 1
      let #LaserRow    = #Slip1_Row26
      let #LaserC2Row  = #Slip2_Row26
      let #BoxCol      = 21
      let #AmtCol      = 30
      let #C2LaserRow  = #Slip2_Row24
      let #C2BoxCol    = 25
      let #C2AmtCol    = 32
      let $OtherBox    = 'OtherBox2'
      let $OtherAmt    = 'OtherAmt2'
    when = 2
      let #LaserRow    = #Slip1_Row26
      let #LaserC2Row  = #Slip2_Row26
      let #BoxCol      = 40
      let #AmtCol      = 49
      let #C2LaserRow  = #Slip2_Row24
      let #C2BoxCol    = 44
      let #C2AmtCol    = 50
      let $OtherBox    = 'OtherBox3'
      let $OtherAmt    = 'OtherAmt3'
    when = 3
      let #LaserRow    = #Slip1_Row26
      let #LaserC2Row  = #Slip2_Row26
      let #BoxCol      = 60
      let #AmtCol      = 69
      let #C2LaserRow  = #Slip2_Row24
      let #C2BoxCol    = 62
      let #C2AmtCol    = 69
      let $OtherBox    = 'OtherBox4'
      let $OtherAmt    = 'OtherAmt4'
    when = 4
      let #LaserRow    = #Slip1_Row31
      let #LaserC2Row  = #Slip2_Row31
      let #BoxCol      = 1
      let #AmtCol      = 10
      let $OtherBox    = 'OtherBox5'
      let $OtherAmt    = 'OtherAmt5'
    when = 5
      let #LaserRow    = #Slip1_Row31
      let #LaserC2Row  = #Slip2_Row31
      let #BoxCol      = 21
      let #AmtCol      = 30
      let $OtherBox    = 'OtherBox6'
      let $OtherAmt    = 'OtherAmt6'
    when = 6
      let #LaserRow    = #Slip1_Row31
      let #LaserC2Row  = #Slip2_Row31
      let #BoxCol      = 41
      let #AmtCol      = 49
      let $OtherBox    = 'OtherBox7'
      let $OtherAmt    = 'OtherAmt7'
    when = 7
      let #LaserRow    = #Slip1_Row31
      let #LaserC2Row  = #Slip2_Row31
      let #BoxCol      = 61
      let #AmtCol      = 69
      let $OtherBox    = 'OtherBox8'
      let $OtherAmt    = 'OtherAmt8'

  end-evaluate

end-procedure


begin-procedure Format-Employee-Data

  let $SIN = edit(&EE.SIN, 'xxxbbxxxbbxxx')
  let $XML_SIN = edit(&EE.SIN, 'xxxbbbbbxxxbbbbbxxx')

  let $LastName = &EE.SLIP_Surname
  let $LastName = RTRIM($LastName, ' ')
  let $FirstName = &EE.SLIP_First_Name
  let $FirstName = RTRIM($FirstName, ' ')
  if $XMLPublisher <> 'Y'
    let $FirstName = substr($FirstName, 1, 20)
  end-if
  let $MidInitial = &EE.SLIP_Initial
  let $Name = $LastName || ', ' || $FirstName || ' ' || $MidInitial
  do convert-to-char($Name, $Name)
  let $Street1 =  &EE.Address1
  do convert-to-char($Street1, $Street1)
  let $Street2 =  &EE.Address2
  let $Street2 = RTRIM(&EE.Address2, ' ')
  do convert-to-char($Street2, $Street2)

  let $EeAddr = RTRIM(&EE.City, ' ')
  do convert-to-char($EeAddr, $EeAddr)
  let $ProvCd = rtrim(&EE.Province, ' ')
  if $ProvCd = 'ZZ'
     move ' ' to $ProvCd
  end-if
  concat $ProvCd with $EeAddr ,bxx
  if &EE.Country <> 'CAN'
     move &EE.Country to $Country
     concat $Country with $EeAddr bxxxxxxxxxx
  end-if
  let $TempAddr = RTRIM($EeAddr, ' ')

  unstring &EE.POSTAL by ' ' into $ZIP1 $ZIP2
  let $ZIP1 = $ZIP1  || $ZIP2

  if &EE.Country = 'CAN'
     let $ZipValue = edit($ZIP1, 'xxxbxxx')
     let $PrintAddr = $TempAddr || '  ' || $ZipValue
     let $PrintZip = ' '
  else
     let $PrintAddr = $TempAddr
     let $PrintZip = rtrim(&EE.POSTAL, ' ')
  end-if

  let $ReportID =  &SL.Reporting_ID
  let $Releve_Slip_No = &SL.Releve_Slip_No
  let #Releve_Slip_No = &SL.Releve_Slip_No
  do Format-Number(#Releve_Slip_No,$Releve_Slip_No,'099999999')
  let $EmplID =  &EE.EmplID
  let $Slip_Process = &SL.YE_Slip_Process
  let #YE_Slip_Seq = &SL.CAN_YE_SLIP_SEQ

end-procedure

begin-procedure Finish-Printing

  let $PrintSlipNo = edit($OriginalSlipNo, 'xxxbxxxbxxx')

  if ($SelectEEs = 'R' or $SelectEEs = 'S')
    let $Code_du_Releve = 'R'
  end-if

  if ($SelectEEs = 'A' or $SelectEEs = 'E') and $Slip_Process = 'A'
    if $XMLPublisher = 'Y'
       let $CanAmend = 'MODIFI'
       do Build-XML-Line('CanAmend', $CanAmend, 'Y', #FNum, #FNum2, $UserFlag)
       do Build-XML-Line('PrintSlipNo', $PrintSlipNo, 'Y', #FNum, #FNum2, $UserFlag)
    else
      if #CalendarYear < 2013
       print 'MODIFI' (#Slip1_Row5,32)
       print 'MODIFI' (#Slip2_Row5,32)
       print $PrintSlipNo (#Slip1_Row4,55)
       print $PrintSlipNo (#Slip2_Row4,55)
      else
       print 'MODIFIE' (#Slip2_Row4,34)
       print $PrintSlipNo (#Slip2_Row4,55)
      end-if
    end-if
    let $Code_du_Releve = 'A'
  end-if

  if ($SelectEEs = 'C' or $SelectEEs = 'E') and $Slip_Process = 'C'
    if $XMLPublisher = 'Y'
       let $CanAmend = 'ANNUL'
       do Build-XML-Line('CanAmend', $CanAmend, 'Y', #FNum, #FNum2, $UserFlag)
    else
     if #CalendarYear < 2013
      print 'ANNUL' (#Slip1_Row5,32)
      print 'ANNUL' (#Slip2_Row5,32)
     else
      print 'ANNULE' (#Slip2_Row4,34)
     end-if
    end-if
    let $Code_du_Releve = 'D'
  end-if

  if $XMLPublisher = 'Y'
     do Build-XML-Line('Code_du_Releve', $Code_du_Releve, 'Y', #FNum, #FNum2, $UserFlag)
  else
    if #CalendarYear < 2013
     print $Code_du_Releve (#Slip1_Row4,50)
     print $Code_du_Releve (#Slip2_Row4,50)
    else
     print $Code_du_Releve (#Slip2_Row4,50)
    end-if
  end-if

  if $XMLPublisher = 'Y'
     do Build-XML-Line('SIN', $XML_SIN, 'Y', #FNum, #FNum2, $UserFlag)
  else
    if #CalendarYear < 2013
     print $SIN (#Slip1_Row42,48)
     print $SIN (#Slip2_Row42,48)
    else
     print $SIN (#Slip2_Row35,48)
    end-if
  end-if

  if $XMLPublisher = 'Y'
     do Build-XML-Line('EmplID', $EmplID, 'Y', #FNum, #FNum2, $UserFlag)
  else
    if #CalendarYear < 2013
     print $EmplID (#Slip1_Row42,65)
     print $EmplID (#Slip2_Row42,65)
    else
     print $EmplID (#Slip2_Row35,65)
    end-if
  end-if

  if $XMLPublisher = 'Y'
     do Build-XML-Line('Name', $Name, 'Y', #FNum, #FNum2, $UserFlag)
  else
    if #CalendarYear < 2013
     print $Name (#Slip1_Row43,4)
     print $Name (#Slip2_Row43,4)
    else
     do Alter-Printer-RL1-Small-P9
     print $Name (#Slip2_Row44,5)
    end-if
  end-if

  if $XMLPublisher = 'Y'
     do Build-XML-Line('Street1', $Street1, 'Y', #FNum, #FNum2, $UserFlag)
  else
    if #CalendarYear < 2013
     print $Street1 (#Slip1_Row45,4)
     print $Street1 (#Slip2_Row45,4)
    else
     print $Street1 (#Slip2_Row46,5)
    end-if
  end-if

  if $Street2 <> ''
    if $XMLPublisher = 'Y'
       do Build-XML-Line('Street2', $Street2, 'Y', #FNum, #FNum2, $UserFlag)
    else
     if #CalendarYear < 2013
      print $Street2 (#Slip1_Row47,4)
      print $Street2 (#Slip2_Row47,4)
     else
      print $Street2 (#Slip2_Row48,5)
     end-if
    end-if

    if $XMLPublisher = 'Y'
       do Build-XML-Line('PrintAddr', $PrintAddr, 'Y', #FNum, #FNum2, $UserFlag)
    else
      if #CalendarYear < 2013
       print $PrintAddr (#Slip1_Row49,4)
       print $PrintAddr (#Slip2_Row49,4)
      else
       print $PrintAddr (#Slip2_Row50,5)
      end-if
    end-if

    if $XMLPublisher = 'Y'
       do Build-XML-Line('PrintZip', $PrintZip, 'Y', #FNum, #FNum2, $UserFlag)
    else
      if #CalendarYear < 2013
       print $PrintZip (#Slip1_Row51,4)
       print $PrintZip (#Slip2_Row51,4)
      else
       print $PrintZip (#Slip2_Row52,5)
      end-if
    end-if
  else
    if $XMLPublisher = 'Y'
       do Build-XML-Line('Street2', $PrintAddr, 'Y', #FNum, #FNum2, $UserFlag)
    else
      if #CalendarYear < 2013
       print $PrintAddr (#Slip1_Row47,4)
       print $PrintAddr (#Slip2_Row47,4)
      else
       print $PrintAddr (#Slip2_Row48,5)
      end-if
    end-if

    if $XMLPublisher = 'Y'
       do Build-XML-Line('PrintAddr', $PrintZip, 'Y', #FNum, #FNum2, $UserFlag)
    else
      if #CalendarYear < 2013
       print $PrintZip (#Slip1_Row49,4)
       print $PrintZip (#Slip2_Row49,4)
      else
       print $PrintZip (#Slip2_Row50,5)
      end-if
    end-if
  end-if


  do Format-Company-Data

  if $XMLPublisher = 'Y'
     do Build-XML-Line('CompanyName', $CompanyName, 'Y', #FNum, #FNum2, $UserFlag)
  else
   if #CalendarYear < 2013
    print $CompanyName (#Slip1_Row49,46)
    print $CompanyName (#Slip2_Row49,46)
   else
    do Alter-Printer-RL1-Reg
    print $CompanyName (#Slip2_Row41,47)
   end-if
  end-if

  if $XMLPublisher = 'Y'
     do Build-XML-Line('CompStreet1', $CompStreet1, 'Y', #FNum, #FNum2, $UserFlag)
  else
   if #CalendarYear < 2013
    print $CompStreet1 (#Slip1_Row51,46)
    print $CompStreet1 (#Slip2_Row51,46)
   else
    print $CompStreet1 (#Slip2_Row43,47)
   end-if
  end-if

  if $XMLPublisher = 'Y'
     do Build-XML-Line('PrintCompAddr', $PrintCompAddr, 'Y', #FNum, #FNum2, $UserFlag)
  else
   if #CalendarYear < 2013
    print $PrintCompAddr (#Slip1_Row53,46)
    print $PrintCompAddr (#Slip2_Row53,46)
   else
    print $PrintCompAddr (#Slip2_Row45,47)
   end-if
  end-if

!** Print Authorization Number on RL-1 PDF slips only
  if $XMLPublisher = 'Y'
     let $AuthorizationNo = 'FS2001002'
     do Build-XML-Line('AuthorizationNo', $AuthorizationNo, 'Y', #FNum, #FNum2, $UserFlag)
  end-if


  if #max_j > 0
     do Print-Footnotes
  end-if

  if $XMLPublisher = 'Y'

     do XML-Close-Tag('RL1_Data', $closeTag)

     if $UserFlag = 'Y'
        do Write-XML-Line(#Fnum2, $closeTag)
     end-if


     if $RC_CAN_YE.RV1_Copy_Type = '2' and #Fnum <> 0
        do Write-XML-Line(#Fnum, $closeTag)
     end-if

  end-if

  if $FormType = 'L'
     do Insert-FormFeed
  end-if

  new-page

end-procedure

begin-procedure Format-Company-Data
  do convert-to-char($CompanyName, $CompanyName)
  let $CompStreet1 = &CT.Address1
  do convert-to-char($CompStreet1, $CompStreet1)

  let $ErAddr = RTRIM(&CT.City, ' ')
  let $ProvCd = rtrim(&CT.State, ' ')
  concat $ProvCd with $ErAddr ,bxx
  do convert-to-char($ErAddr, $ErAddr)
  if &CT.Country <> 'CAN'
     move &CT.Country to $Country
     concat $Country with $ErAddr bxxxxxxxxxx
  end-if
  let $TempCompAddr = RTRIM($ErAddr, ' ')

  unstring &CT.POSTAL by ' ' into $ErZIP1 $ErZIP2
  let $ErZIP1 = $ErZIP1 || $ErZIP2
  let $ErZipValue = edit($ErZIP1, 'xxxbxxx')
  do convert-to-char($ErZipValue, $ErZipValue)
  let $PrintCompAddr = $TempCompAddr || '  ' || $ErZipValue


end-procedure

begin-procedure Print-Footnotes


  move 'Y' to $MoreFootnotes
  move 0 to #NoteCount

  move 2 to #MaxFootnotes


  while #NoteCount < #MaxFootnotes
     if #NoteCount > #max_j  or $MoreFootnotes = 'N'
        break
     end-if
     move 999 to #HoldPriority
     move 0 to #j

     while #j <= #max_j
        get $CkBox #BoxPriority from FormNotes(#j) Box_Num Box_Priority
        move #BoxPriority to $BoxPriority

        if $CkBox = ' ' or
           $CkBox = ''
           break
        else

          if #BoxPriority < #HoldPriority and
             #BoxPriority <> 0
             let #HoldIdx = #j
             move #BoxPriority to #HoldPriority
          end-if
          add 1 to #j
        end-if
     end-while

      if #HoldPriority <> 999 and
         #HoldPriority <> 0
        put 999 into FormNotes (#HoldIdx) Box_Priority

        get $Note_Box from FormNotes (#HoldIdx) Box_Num
        get $Note_Text from FormNotes (#HoldIdx) Text
        get #Note_Amt from FormNotes (#HoldIdx) Box_Amount
        get $Note_Value from FormNotes (#HoldIdx) Box_Value

        add 1 to #NoteCount
        evaluate #NoteCount
           when = 1
             let #Slip1_FNLine = 34
             let #Slip_FNCol  = 44

           when = 2
             let #Slip1_FNLine = 36
             let #Slip_FNCol  = 44

           when-other
             break

        end-evaluate

        if $FormType = 'L'
          let #Slip2_FNLine = #Slip1_FNLine + 66
        end-if

        let $Note_Text = RTRIM($Note_Text, ' ')

        evaluate $Note_Box

            when = 'D03'
            when = 'D04'
            when = 'N01'
            when = 'N02'
            when = 'N03'

               if $XMLPublisher = 'Y'
                 do Build-XML-Line('Note_Text', $Note_Text, 'Y', #FNum, #FNum2, $UserFlag)
               else
                 print $Note_Text (#Slip1_FNLine,#Slip_FNCol)
                 print $Note_Text (#Slip2_FNLine,#Slip_FNCol)
               end-if

            when-other

               if #Note_Amt <> 0
                  do Format-Number(#Note_Amt, $Note_Amt, '9999999.99')
                  let $Note_Amt = LTRIM($Note_Amt, ' ')
                  let $PrintAmt =  '$' || $Note_Amt || ' ' || $Note_Text

                  if $XMLPublisher = 'Y'
                    do Build-XML-Line('PrintAmt', $PrintAmt, 'Y', #FNum, #FNum2, $UserFlag)
                  else
                    print $PrintAmt (#Slip1_FNLine,#Slip_FNCol)
                    print $PrintAmt (#Slip2_FNLine,#Slip_FNCol)
                  end-if
               else
                  if $Note_Text <> ''
                     let $PrintAmt = $Note_Text

                     if $XMLPublisher = 'Y'
                       do Build-XML-Line('$Note_Text', $Note_Text, 'Y', #FNum, #FNum2, $UserFlag)
                     else
                       print $Note_Text (#Slip1_FNLine,#Slip_FNCol)
                       print $Note_Text (#Slip2_FNLine,#Slip_FNCol)
                     end-if
                  end-if
               end-if

       end-evaluate

      else
         move 'N' to $MoreFootnotes
      end-if

  end-while
  do Initialize-FormNotes-Array

end-procedure



begin-procedure Initialize-FormNotes-Array

  let #i = 0
  let $InitSpace = ' '
  let #InitNumber = 0

  while #i <= #max_i
   put $InitSpace #InitNumber $InitSpace $InitSpace #InitNumber
    into FormNotes(#i) Box_Num Box_Priority Text Box_Value Box_Amount

    add 1 to #i
  end-while

end-procedure


begin-procedure Initialize-Employee-Data

  move 0 to #EmpIncome                ! box A
  move 0 to #EmpQPPContrib            ! box B
  move 0 to #EmpUIPrem                ! box C
  move 0 to #EmpRPPContrib            ! box D
  move 0 to #EmpTaxWithheld           ! box E
  move 0 to #EmpUnionDues             ! box F
  move 0 to #EmpPensionableEarn       ! box G
  move 0 to #EmpQPIPPrem              ! box H
  move 0 to #EmpQPIPInsEarn           ! box I

  move 0 to #EmpPrivHealthContrib     ! box J
  move 0 to #EmpTravel                ! box K
  move 0 to #EmpOtherBenefits         ! box L
  move 0 to #EmpCommissions           ! box M
  move 0 to #EmpDonations             ! box N
  move 0 to #EmpOtherIncome           ! box O
  move 0 to #EmpMultiEmplAmt          ! box P
  move 0 to #EmpDeferredPay           ! box Q
  move 0 to #EmpIndianIncome          ! box R
  move 0 to #EmpTip                   ! box S
  move 0 to #EmpTipsAllocated         ! box T
  move 0 to #EmpPhasedRetire          ! box U
  move 0 to #EmpRoomBoard             ! box V
  move 0 to #EmpErAuto                ! box W
  move 0 to #EmpCPPAmt                ! CPP Amt

  move 'N' to $Print_Code
  if &EE.EmplID <> $EmplID
    move ' ' to $Code                   ! box Code
  end-if

end-procedure

begin-procedure Write-Summary-Record

!Totals Formatting

  do format-number(#CalendarYear, $TaxYear, '9999')
  do format-number(#RV1Count, $TotalSupplementary, '0999999')
  do format-number(#TotIncome, $TotalIncome, '0999999999.99')
  do format-number(#TotQPPContrib, $TotalEeQPPContrib, '09999999.99')
  do format-number(#TotUIPrem, $TotalEeUIPrem, '09999999.99')
  do format-number(#TotRPPContrib, $TotalRegPPContrib, '09999999.99')
  do format-number(#TotTaxWithheld, $TotalTaxWithheld, '0999999999.99')
  do format-number(#TotUnionDues, $TotalUnionDues, '09999999.99')
  do format-number(#TotPensionableEarn, $TotalPensionableEarn, '0999999999.99')
  do format-number(#TotQPIPPrem, $TotQPIPPrem, '09999999.99')
  do format-number(#TotQPIPInsEarn, $TotQPIPInsEarn, '0999999999.99')
  do format-number(#TotPrivHealthContrib, $TotPrivHealthContrib, '09999999.99')
  do format-number(#TotTravel, $TotalTravel, '09999999.99')
  do format-number(#TotOtherBenefits, $TotalOtherBenefits, '09999999.99')
  do format-number(#TotCommissions, $TotalCommissions, '09999999.99')
  do format-number(#TotDonations, $TotalDonations, '09999999.99')
  do format-number(#TotOtherIncome, $TotalOtherIncome, '09999999.99')
  do format-number(#TotMultiEmplAmt, $TotMultiEmplAmt, '09999999.99')
  do format-number(#TotDeferredPay, $TotDeferredPay, '09999999.99')
  do format-number(#TotIndianIncome, $TotIndianIncome, '09999999.99')
  do format-number(#TotTip, $TotTip, '09999999.99')
  do format-number(#TotTipsAllocated, $TotTipsAllocated, '09999999.99')
  do format-number(#TotPhasedRetire, $TotPhasedRetire, '09999999.99')
  do format-number(#TotRoomBoard, $TotalAccom, '09999999.99')
  do format-number(#TotErAuto, $TotalUseErAuto, '09999999.99')

  write 1 from $CompanyName:30          -    ! 1   thru  30  Summary record
               $TaxYear:4                 -    ! 31  thru 34
               $TotalSupplementary:7      -    ! 35  thru 41
               $TotalIncome:13            -    ! 42  thru 54   Box A
               $TotalEeQPPContrib:11      -    ! 55  thru 65   Box B
               $TotalEeUIPrem:11          -    ! 66  thru 76   Box C
               $TotalRegPPContrib:11      -    ! 77  thru 87   Box D
               $TotalTaxWithheld:13       -    ! 88  thru 100  Box E
               $TotalUnionDues:11         -    ! 101 thru 111  Box F
               $TotalPensionableEarn:13   -    ! 112 thru 124  Box G
               $TotQPIPPrem:11            -    ! 125 thru 135  Box H
               $TotQPIPInsEarn:13         -    ! 138 thru 148  Box I
               $TotPrivHealthContrib:11   -    ! 149 thru 159  Box J
               $TotalTravel:11            -    ! 160 thru 170  Box K
               $TotalOtherBenefits:11     -    ! 171 thru 181  Box L
               $TotalCommissions:11       -    ! 182 thru 192  Box M
               $TotalDonations:11         -    ! 193 thru 203  Box N
               $TotalOtherIncome:11       -    ! 204 thru 214  Box O
               $TotMultiEmplAmt:11        -    ! 215 thru 225  Box P
               $TotDeferredPay:11         -    ! 226 thru 236  Box Q
               $TotIndianIncome:11        -    ! 237 thru 247  Box R
               $TotTip:11                 -    ! 248 thru 258  Box S
               $TotTipsAllocated:11       -    ! 259 thru 269  Box T
               $TotPhasedRetire:11        -    ! 270 thru 280  Box U
               $TotalAccom:11             -    ! 281 thru 291  Box V
               $TotalUseErAuto:11         -    ! 292 thru 302  Box W
               $ReportID:16               -    ! 303 thru 318  no box
               $ReportType:1                   ! 319   original/amended


  do Init-Summary-Values

end-procedure


begin-procedure Init-Summary-Values

  move 0 to #RV1Count                 ! box no equiv
  move 0 to #TotIncome                ! box A
  move 0 to #TotQPPContrib            ! box B
  move 0 to #TotUIPrem                ! box C
  move 0 to #TotRPPContrib            ! box D
  move 0 to #TotTaxWithheld           ! box E
  move 0 to #TotUnionDues             ! box F
  move 0 to #TotPensionableEarn       ! box G
  move 0 to #TotQPIPPrem              ! box H
  move 0 to #TotQPIPInsEarn           ! box I
  move 0 to #TotPrivHealthContrib     ! box J
  move 0 to #TotTravel                ! box K
  move 0 to #TotOtherBenefits         ! box L
  move 0 to #TotCommissions           ! box M
  move 0 to #TotDonations             ! box N
  move 0 to #TotOtherIncome           ! box O
  move 0 to #TotMultiEmplAmt          ! box P
  move 0 to #TotDeferredPay           ! box Q
  move 0 to #TotIndianIncome          ! box R
  move 0 to #TotTip                   ! box S
  move 0 to #TotTipsAllocated         ! box T
  move 0 to #TotPhasedRetire          ! box U
  move 0 to #TotRoomBoard             ! box V
  move 0 to #TotErAuto                ! box W

end-procedure

begin-procedure Set-RV1-Row-Pointers

  let #Slip1_Row1 = 1
  let #Slip1_Row2 = 2
  let #Slip1_Row3 = 3
  let #Slip1_Row4 = 4
  let #Slip1_Row5 = 5
  let #Slip1_Row6 = 6
  let #Slip1_Row7 = 7
  let #Slip1_Row8 = 8
  let #Slip1_Row9 = 9
  let #Slip1_Row10 = 10
  let #Slip1_Row11 = 11
  let #Slip1_Row12 = 12
  let #Slip1_Row13 = 13
  let #Slip1_Row14 = 14
  let #Slip1_Row15 = 15
  let #Slip1_Row16 = 16
  let #Slip1_Row17 = 17
  let #Slip1_Row18 = 18
  let #Slip1_Row19 = 19
  let #Slip1_Row20 = 20
  let #Slip1_Row21 = 21
  let #Slip1_Row22 = 22
  let #Slip1_Row23 = 23
  let #Slip1_Row24 = 24
  let #Slip1_Row25 = 25
  let #Slip1_Row26 = 26
  let #Slip1_Row27 = 27
  let #Slip1_Row28 = 28
  let #Slip1_Row29 = 29
  let #Slip1_Row30 = 30
  let #Slip1_Row31 = 31
  let #Slip1_Row32 = 32
  let #Slip1_Row33 = 33
  let #Slip1_Row34 = 34
  let #Slip1_Row35 = 35
  let #Slip1_Row36 = 36
  let #Slip1_Row37 = 37
  let #Slip1_Row39 = 39
  let #Slip1_Row40 = 40
  let #Slip1_Row41 = 41
  let #Slip1_Row42 = 42
  let #Slip1_Row43 = 43
  let #Slip1_Row44 = 44
  let #Slip1_Row45 = 45
  let #Slip1_Row46 = 46
  let #Slip1_Row47 = 47
  let #Slip1_Row48 = 48
  let #Slip1_Row49 = 49
  let #Slip1_Row50 = 50
  let #Slip1_Row51 = 51
  let #Slip1_Row52 = 52
  let #Slip1_Row53 = 53

  let #Slip2_Row1  = #Slip1_Row1 + 66
  let #Slip2_Row2  = #Slip1_Row2 + 66
  let #Slip2_Row3  = #Slip1_Row3 + 66
  let #Slip2_Row4  = #Slip1_Row4 + 66
  let #Slip2_Row5  = #Slip1_Row5 + 66
  let #Slip2_Row6  = #Slip1_Row6 + 66
  let #Slip2_Row7  = #Slip1_Row7 + 66
  let #Slip2_Row8  = #Slip1_Row8 + 66
  let #Slip2_Row9  = #Slip1_Row9 + 66
  let #Slip2_Row10 = #Slip1_Row10 + 66
  let #Slip2_Row11 = #Slip1_Row11 + 66
  let #Slip2_Row12 = #Slip1_Row12 + 66
  let #Slip2_Row13 = #Slip1_Row13 + 66
  let #Slip2_Row14 = #Slip1_Row14 + 66
  let #Slip2_Row15 = #Slip1_Row15 + 66
  let #Slip2_Row16 = #Slip1_Row16 + 66
  let #Slip2_Row17 = #Slip1_Row17 + 66
  let #Slip2_Row18 = #Slip1_Row18 + 66
  let #Slip2_Row19 = #Slip1_Row19 + 66
  let #Slip2_Row20 = #Slip1_Row20 + 66
  let #Slip2_Row21 = #Slip1_Row21 + 66
  let #Slip2_Row22 = #Slip1_Row22 + 66
  let #Slip2_Row23 = #Slip1_Row23 + 66
  let #Slip2_Row24 = #Slip1_Row24 + 66
  let #Slip2_Row25 = #Slip1_Row25 + 66
  let #Slip2_Row26 = #Slip1_Row26 + 66
  let #Slip2_Row27 = #Slip1_Row27 + 66
  let #Slip2_Row28 = #Slip1_Row28 + 66
  let #Slip2_Row29 = #Slip1_Row29 + 66
  let #Slip2_Row30 = #Slip1_Row30 + 66
  let #Slip2_Row31 = #Slip1_Row31 + 66
  let #Slip2_Row32 = #Slip1_Row32 + 66
  let #Slip2_Row33 = #Slip1_Row33 + 66
  let #Slip2_Row34 = #Slip1_Row34 + 66
  let #Slip2_Row35 = #Slip1_Row35 + 66
  let #Slip2_Row36 = #Slip1_Row36 + 66
  let #Slip2_Row37 = #Slip1_Row37 + 66
  let #Slip2_Row39 = #Slip1_Row39 + 66
  let #Slip2_Row40 = #Slip1_Row40 + 66
  let #Slip2_Row41 = #Slip1_Row41 + 66
  let #Slip2_Row42 = #Slip1_Row42 + 66
  let #Slip2_Row43 = #Slip1_Row43 + 66
  let #Slip2_Row44 = #Slip1_Row44 + 66
  let #Slip2_Row45 = #Slip1_Row45 + 66
  let #Slip2_Row46 = #Slip1_Row46 + 66
  let #Slip2_Row47 = #Slip1_Row47 + 66
  let #Slip2_Row48 = #Slip1_Row48 + 66
  let #Slip2_Row49 = #Slip1_Row49 + 66
  let #Slip2_Row50 = #Slip1_Row50 + 66
  let #Slip2_Row51 = #Slip1_Row51 + 66
  let #Slip2_Row52 = #Slip1_Row52 + 66
  let #Slip2_Row53 = #Slip1_Row53 + 66


end-procedure

begin-procedure Insert-FormFeed

#ifndef MVS
#ifndef OS400
 encode '<27>&k2G' into $FormFeed
#else
 encode '<39>&k2G' into $FormFeed
#endif
#else
 encode '<39>&k2G' into $FormFeed
#endif

 print $FormFeed () code
 print ' ' (140,01)

end-procedure


begin-procedure Convert-Parameters

 if $RC_CAN_YE.RV1_Primary_Sort  = '1'
       let $SortSequence = 'SL.REPORTING_ID ASC, ' ||
       'EE.SLIP_SURNAME ASC, EE.SLIP_FIRST_NAME ASC, EE.EMPLID ASC, SL.CAN_YE_SLIP_SEQ ASC'

 else

      if $RC_CAN_YE.RV1_Primary_Sort  = '2'
         let $Sort1 = 'SL.REPORTING_ID ASC'
         let $DispSort1 = 'Quebec ID Number; '
      end-if

      if $RC_CAN_YE.RV1_Primary_Sort  = '3'
         let $Sort1 = 'SL.COMPANY ASC'
         let $DispSort1 = 'Company; '
      end-if

      if $RC_CAN_YE.RV1_Sec_Sort  = '1'
          let $Sort2 = 'EE.LOCATION ASC'
          let $DispSort2 = 'Location; '
      end-if

      if $RC_CAN_YE.RV1_Sec_Sort  = '2'
         let $Sort2 = 'EE.DEPTID ASC'
         let $DispSort2 = 'Department; '
      end-if

      if $RC_CAN_YE.RV1_Sec_Sort  = '3'
         let $Sort2 = 'EE.MAIL_DROP ASC'
         let $DispSort2 = 'Mail Drop; '
      end-if

      if $RC_CAN_YE.RV1_Sec_Sort  = '4'
          let $Sort2 = 'EE.POSTAL ASC'
          let $DispSort2 = 'Postal Code; '
      end-if

      if $RC_CAN_YE.RV1_Sec_Sort  = '5'
          let $Sort2 = 'N'
      end-if

      if $RC_CAN_YE.RV1_Third_Sort  = '1'
          let $Sort3 = 'EE.SLIP_SURNAME ASC, EE.SLIP_FIRST_NAME ASC, EE.EMPLID ASC, SL.CAN_YE_SLIP_SEQ ASC'
          let $DispSort3 = 'Employee Name; Employee ID'
      end-if

      if $RC_CAN_YE.RV1_Third_Sort  = '2'
         let $Sort3 = 'EE.EMPLID ASC, SL.CAN_YE_SLIP_SEQ ASC'
         let $DispSort3 = 'Employee ID'
      end-if

      if $RC_CAN_YE.RV1_Third_Sort  = '3'
         let $Sort3 = 'EE.SIN ASC, SL.CAN_YE_SLIP_SEQ ASC'
         let $DispSort3 = 'Employee SIN'
      end-if

  if $Sort2 = 'N'
     let $SortSequence =  $Sort1 || ', ' ||  $Sort3
     let $DisplaySeq = $DispSort1 || $DispSort3
  else
     let $SortSequence = $Sort1 || ', ' || $Sort2 || ', ' || $Sort3
     let $DisplaySeq = $DispSort1 || $DispSort2 || $DispSort3
  end-if

end-if

    if $RC_CAN_YE.RV1_Primary_Sort  = '2'
          move 'R' to $SortCode
   else
      if $RC_CAN_YE.RV1_Primary_Sort  = '3'
         move 'C' to $SortCode
      else
         move ' ' to $SortCode
      end-if
   end-if

let $FormType = $RC_CAN_YE.RV1_Form_Type

if $FormType = 'X'
    let $XMLPublisher = 'Y'
else
    let $XMLPublisher = 'N'
end-if

let $CopyType = $RC_CAN_YE.RV1_Copy_Type
let $SelectEEs = $RC_CAN_YE.RL1_Processing_Flg

  if $SelectEEs = 'S' or $SelectEEs = 'E'
    do Read-EEs
  end-if

  let $ye_slip_process = ' '
  let $seq_number      = ' '

  evaluate $SelectEEs
  when = 'A'                    ! Amended slips
    let $ye_slip_process = ' AND SL.YE_SLIP_PROCESS = ''A'' '
    let $seq_number      = ')'
    let $ReportType      = 'A'
    break
  when = 'C'                    ! Cancelled slips
    let $ye_slip_process = ' AND SL.YE_SLIP_PROCESS = ''C'' '
    let $seq_number      = ')'
    let $ReportType      = 'C'
    break
  when = 'E'
    let $ye_slip_process = ' AND SL.YE_SLIP_PROCESS IN (''A'', ''C'') '
    let $seq_number      = ' AND SL1.TAXFORM_ID  = SL.TAXFORM_ID AND SL1.YE_SLIP_PROCESS = SL.YE_SLIP_PROCESS) '
    break
  when = 'R'
  when = 'S'
    let $ye_slip_process = ' AND SL.YE_SLIP_PROCESS = ''O'' '
    let $seq_number      = ' AND SL1.YE_SLIP_PROCESS = SL.YE_SLIP_PROCESS) '
    let $ReportType      = 'O'
    break
  end-evaluate

  if $SelectEEs = 'A' or $SelectEEs = 'C'
    let $Proc_Amend_Cancel = 'Y'
  else
    let $StatusOpen = 'Y'
  end-if


end-procedure

!***********************************************************************
begin-procedure Declare-Printer-RV1
!***********************************************************************

  alter-printer
    symbol-set    = 10U
    font          = 3
    point-size    = 12


end-procedure

begin-procedure Alter-Printer-RL1-Small-P9

  alter-printer
    symbol-set    = 10U
    font          = 4
    point-size    = 9

end-procedure

begin-procedure Alter-Printer-RL1-Reg

  alter-printer
    symbol-set    = 10U
    font          = 3
    point-size    = 12

end-procedure

begin-procedure Read-EEs

move 'AND EE.EMPLID in (''' to $E.SelectedEEs
move ' ' to $SelectedEmplID

begin-SELECT
CT.EMPLID

  move &CT.EMPLID     to $SelectedEmplID

  let $E.SelectedEEs = $E.SelectedEEs || $SelectedEmplID || ''','''

  move 'Y' to $EE_Selected

FROM  PS_RC_CTX910RP CT
WHERE CT.OPRID         = $Prcs_OprID
  AND CT.RUN_CNTL_ID   = $Prcs_Run_Cntl_ID
end-SELECT


let $E.SelectedEEs = SUBSTR($E.SelectedEEs,1,LENGTH($E.SelectedEEs) - 2)
let $E.SelectedEEs = $E.SelectedEEs || ')'

end-procedure


begin-procedure Check-Status

  move 'N' to $StatusOpen

begin-SELECT
CS.EMPLID

  move 'Y' to $StatusOpen

FROM PS_CAN_AMEND_RL1_S CS
WHERE CS.COMPANY         = &SL.COMPANY
  AND CS.EMPLID          = &SL.EMPLID
  AND CS.CALENDAR_YEAR   = &SL.CALENDAR_YEAR
  AND CS.SEQUENCE_NUMBER = &SL.SEQUENCE_NUMBER
  AND CS.WAGE_LOSS_PLAN  = &SL.WAGE_LOSS_PLAN
  AND CS.PROVINCE        = &SL.PROVINCE
  AND CS.CAN_YE_SLIP_SEQ = &SL.CAN_YE_SLIP_SEQ
  AND CS.AMEND_STATUS    = 'O'

end-SELECT

end-procedure


begin-procedure Close-Amend-Cancel-RL1

begin-SQL

  UPDATE PS_CAN_AMEND_RL1_S
  SET AMEND_STATUS       = 'C'
  WHERE COMPANY          = &SL.COMPANY
    AND EMPLID           = &SL.EMPLID
    AND CALENDAR_YEAR    = &SL.CALENDAR_YEAR
    AND SEQUENCE_NUMBER  = &SL.SEQUENCE_NUMBER
    AND WAGE_LOSS_PLAN   = &SL.WAGE_LOSS_PLAN
    AND PROVINCE         = &SL.PROVINCE
    AND CAN_YE_SLIP_SEQ  = &SL.CAN_YE_SLIP_SEQ

end-SQL

end-procedure


begin-procedure Get-XMLP-Definitions

  do Get-XML-URLID
  do Get-Report-Definitions

  move 'N'  to $RC_CAN_YE.Py_Ye_Rpt_Burst
  move 'N'  to $Py_Ye_Rpt_Burst
  if $OPT_SS.PY_YE_RPT_SS = 'Y'
     move $OPT_SS.REPORT_DEFN_ID to $Report_Defn_ID

!     do Check-Burst-Value
!     move $Py_Ye_Rpt_Burst  to $RC_CAN_YE.Py_Ye_Rpt_Burst

!     if $Py_Ye_Rpt_Burst <> 'Y'
!        move 'N' to $OPT_SS.PY_YE_RPT_SS
!        display 'No burst fieldname given for self-service report definition ' noline
!        display $OPT_SS.REPORT_DEFN_ID
!     end-if
  end-if

  move 'N'  to $Py_Ye_Rpt_Burst

  if #OPT_BLK.PY_RPT_BATCH_SIZE <> 0
     move $OPT_BLK.REPORT_DEFN_ID to $Report_Defn_ID
     do Check-Burst-Value

     if $Py_Ye_Rpt_Burst <> 'Y'
        display 'No burst fieldname given for batched report definition ' noline
        display $OPT_BLK.REPORT_DEFN_ID
     end-if
  end-if

end-procedure

begin-procedure Get-XML-URLID

! fetch XML data files URLID

  move ' '  to $OPT_XML.PY_YE_XMLURLID

begin-SELECT
OPT_XML.PY_YE_XMLURLID

  move &OPT_XML.PY_YE_XMLURLID      to $OPT_XML.PY_YE_XMLURLID

  exit-SELECT

FROM PS_PY_YE_OPT_TAX OPT_XML
WHERE OPT_XML.PY_TAX_YEAR     = &TX.BALANCE_YEAR
  AND OPT_XML.TAXFORM_ID      = 'R'
end-SELECT

  if $OPT_XML.PY_YE_XMLURLID = ' '
     display 'XML data file URLID is missing.'
  end-if

end-procedure

begin-procedure Get-Report-Definitions

! fetch definitions for self-service

  move 'N'  to $OPT_SS.PY_YE_RPT_SS
  move ' '  to $OPT_SS.REPORT_DEFN_ID
  move ' '  to $OPT_SS.TMPLDEFN_ID

begin-SELECT
OPT_SS.PY_YE_RPT_SS
OPT_SS.REPORT_DEFN_ID
OPT_SS.TMPLDEFN_ID
OPT_SS.PY_RPT_BATCH_SIZE

  move &OPT_SS.PY_YE_RPT_SS      to $OPT_SS.PY_YE_RPT_SS
  move &OPT_SS.REPORT_DEFN_ID    to $OPT_SS.REPORT_DEFN_ID
  move &OPT_SS.TMPLDEFN_ID       to $OPT_SS.TMPLDEFN_ID
!  move &OPT_SS.PY_RPT_BATCH_SIZE to #OPT_SS.PY_RPT_BATCH_SIZE

  exit-SELECT

FROM PS_PY_YE_OPT_RPT OPT_SS
WHERE OPT_SS.PY_TAX_YEAR     = &TX.BALANCE_YEAR
  AND OPT_SS.TAXFORM_ID      = 'R'
  AND OPT_SS.PY_W2_COPY_TYPE = $RC_CAN_YE.RV1_Copy_Type
  AND OPT_SS.PY_YE_RPT_SS    = 'Y'
end-SELECT

! fetch definitions for bulk report

  move ' '  to $OPT_BLK.REPORT_DEFN_ID
  move ' '  to $OPT_BLK.TMPLDEFN_ID
  move 0    to #OPT_BLK.PY_RPT_BATCH_SIZE

begin-SELECT
OPT_BLK.REPORT_DEFN_ID
OPT_BLK.TMPLDEFN_ID
OPT_BLK.PY_RPT_BATCH_SIZE

  move &OPT_BLK.REPORT_DEFN_ID    to $OPT_BLK.REPORT_DEFN_ID
  move &OPT_BLK.TMPLDEFN_ID       to $OPT_BLK.TMPLDEFN_ID
  move &OPT_BLK.PY_RPT_BATCH_SIZE to #OPT_BLK.PY_RPT_BATCH_SIZE

  exit-SELECT

FROM PS_PY_YE_OPT_RPT OPT_BLK
WHERE OPT_BLK.PY_TAX_YEAR     = &TX.BALANCE_YEAR
  AND OPT_BLK.TAXFORM_ID      = 'R'
  AND OPT_BLK.PY_W2_COPY_TYPE = $RC_CAN_YE.RV1_Copy_Type
  AND OPT_BLK.PY_YE_RPT_SS    <> 'Y'
end-SELECT

end-procedure

begin-procedure Check-Burst-Value
  let $Py_Ye_Rpt_Burst = 'N'
begin-SELECT
burst.BURST_FIELDNAME

  if rtrim(&burst.BURST_FIELDNAME, ' ') = ''
     let $Py_Ye_Rpt_Burst = 'N'
  else
     let $Py_Ye_Rpt_Burst = 'Y'
  end-if

  exit-SELECT

from PSXPRPTDEFN burst
WHERE burst.REPORT_DEFN_ID = $Report_Defn_ID
end-SELECT
end-procedure

begin-procedure Get-SS-Consent
  let $Consent = 'N'
begin-SELECT
CONS.YE_CONSENT_CURRENT

  move &CONS.YE_CONSENT_CURRENT to $Consent

FROM PS_PY_YE_CONS_MRQ CONS
WHERE CONS.EMPLID = &EE.EMPLID
  AND CONS.LASTUPDDTTM =
      (SELECT MAX(CONS1.LASTUPDDTTM)
       FROM PS_PY_YE_CONS_MRQ CONS1
       WHERE CONS1.EMPLID = CONS.EMPLID)
end-SELECT

  if $Consent = 'C' and $RC_CAN_YE.RV1_Copy_Type = '2'
     let $UserFlag = 'N'
  else
     let $UserFlag = 'Y'
  end-if
end-procedure


begin-procedure Clear-Guide-Temp
  let $err-statement = 'Clear-Guide-Temp, DELETE PS_PY_SS_RL_TMP'
begin-SQL on-error=Sql-Statement-Error
DELETE FROM PS_PY_SS_RL_TMP
WHERE PY_YE_UPD_ACTION <> 'F'
end-SQL
end-procedure

begin-procedure Insert-Guide-Data

  let $updateAction = 'I'
  let $NotifyFlag = 'N'
  do Get-Guide-Seqnum

  let $err-statement = 'Insert-Guide-Data, INSERT PS_PY_SS_RL_TMP'
begin-SQL on-error=Sql-Statement-Error
INSERT INTO PS_PY_SS_RL_TMP
(OPRID
,RUN_CNTL_ID
,PRCSINSTANCE
,PRCSNAME
,JOBINSTANCE
,EMPLID
,PY_TAX_YEAR
,COMPANY
,TAXFORM_ID
,SEQUENCE_NUMBER
,WAGE_LOSS_PLAN
,PROVINCE
,PY_YE_FORM_SEQNUM
,YE_FORM_ISSUE_DT
,YE_FORM_VIEWING_DT
,ATTACHSYSFILENAME
,ATTACHUSERFILE
,PY_YE_FILEURLID
,YE_SLIP_PROCESS
,PY_YE_REPORT_URL
,PY_YE_BURST_ID
,PROCESS_INSTANCE
,REPORT_DEFN_ID
,BURST_FIELDNAME
,PY_YE_NOTIFY_FLG
,PY_SLIP_FINAL_PRT
,PY_YE_UPD_ACTION)
Values
($Prcs_OprID
,$Prcs_Run_Cntl_ID
,#prcs_process_instance
,'CTX910RP'
,#prcs_job_instance
,&SL.EMPLID
,&SL.CALENDAR_YEAR
,&SL.COMPANY
,'R'                               ! taxform id
,&SL.SEQUENCE_NUMBER
,&SL.WAGE_LOSS_PLAN
,&SL.PROVINCE
,&SL.CAN_YE_SLIP_SEQ               ! form sequence number
,$IssueDate
,$ViewingDate1
,$FName                            ! attachsysfilename
,$FName                            ! attachuserfile
,$OPT_XML.PY_YE_XMLURLID           ! urlid
,&SL.YE_SLIP_PROCESS
,' '                               ! report url
,$BurstValue
,0
,$OPT_SS.REPORT_DEFN_ID
,'BURST_VALUE'
,$NotifyFlag
,$Slip_Final_Print
,$updateAction)
end-SQL

end-procedure

begin-procedure Insert-YE-Runcontrol


  let $err-statement = 'Insert-YE-Runcontrol, INSERT PS_PY_RL_SLIP_RCTL'
begin-SQL on-error=Sql-Statement-Error
INSERT INTO PS_PY_RL_SLIP_RCTL
(OPRID
,RUN_CNTL_ID
,PRCSINSTANCE
,PRCSNAME
,JOBINSTANCE
,REPORT_DEFN_ID
,TMPLDEFN_ID
,PY_YE_SOURCEFILE
,PY_YE_SOURCELOC
,PY_YE_SRC_FILENAME
,PY_YE_OSPLATFORM
,FORM_ID
,RL1_FINAL_PRINT
,RL2_FINAL_PRINT
,RV1_COPY_TYPE
,PY_YE_RPT_BURST
,PY_YE_RPT_SS
,PY_RPT_DEFN_ID_SS
,PY_RPT_TMPL_ID_SS
,TAXFORM_ID
,PY_YE_SOURCEFILE2
,PY_YE_SRC_FILENAM2
,PY_SLIP_FINAL_PRT
,PY_YE_FILE_COUNT
,PY_YE_XMLURLID
,PY_TAX_YEAR
)
Values
($Prcs_OprID
,$Prcs_Run_Cntl_ID
,#prcs_process_instance
,'CTX910RP'
,#prcs_job_instance
,$OPT_BLK.REPORT_DEFN_ID
,$OPT_BLK.TMPLDEFN_ID
,$sourceFile2
,$sourceLocation2
,$sourceFileName2
,$yeOS
,'XMLP'
,$RC_CAN_YE.RL1_Final_Print
,$RC_CAN_YE.RL2_Final_Print
,$RC_CAN_YE.RV1_Copy_Type
,$RC_CAN_YE.Py_Ye_Rpt_Burst
,$OPT_SS.PY_YE_RPT_SS
,$OPT_SS.REPORT_DEFN_ID
,$OPT_SS.TMPLDEFN_ID
,'R'
,$sourceFile2
,$sourceFileName2
,$Slip_Final_Print
,0
,$OPT_XML.PY_YE_XMLURLID
,&SL.CALENDAR_YEAR
)
end-SQL
end-procedure

begin-procedure Get-Setup-Viewing-Date
  let $yeopt.YE_FORM_VIEWING_DT = ''
  let $yeopt.PY_YE_SS_NOTE_DT   = ''

  let $Year4 = '1'
  do Format-DateTime('20491231', $ViewingDate, {DEFCMP},'','native')

begin-SELECT
yeopt.YE_FORM_VIEWING_DT
yeopt.PY_YE_SS_NOTE_DT

  let $yeopt.YE_FORM_VIEWING_DT = &yeopt.YE_FORM_VIEWING_DT
  let $yeopt.PY_YE_SS_NOTE_DT   = &yeopt.PY_YE_SS_NOTE_DT

  if $yeopt.YE_FORM_VIEWING_DT <> '' and
     $yeopt.YE_FORM_VIEWING_DT >= $IssueDate

     let $ViewingDate = $yeopt.YE_FORM_VIEWING_DT

  else
     if $yeopt.YE_FORM_VIEWING_DT <> '' and
        $yeopt.YE_FORM_VIEWING_DT < $IssueDate

        let $ViewingDate = $IssueDate

     end-if
  end-if

from PS_PY_YEOPT_TX_CAN yeopt
WHERE yeopt.COMPANY     = &SL.COMPANY
  AND yeopt.PY_TAX_YEAR = &SL.CALENDAR_YEAR
  AND yeopt.TAXFORM_ID  = 'R'
end-SELECT
end-procedure

begin-procedure Get-Guide-Seqnum
  let #gde.PY_YE_FORM_SEQNUM =  0
  let $ViewingDate1 = $ViewingDate

begin-SELECT
gde.PY_YE_FORM_SEQNUM
gde.YE_FORM_VIEWING_DT
gde.PY_YE_NOTIFY_FLG

  let #gde.PY_YE_FORM_SEQNUM = &gde.PY_YE_FORM_SEQNUM
  let $NotifyFlag = &gde.PY_YE_NOTIFY_FLG
  let $ViewingDate1 = &gde.YE_FORM_VIEWING_DT
  let $updateAction = 'C'

  exit-SELECT

from PS_PY_SS_RL_GDE gde
WHERE gde.EMPLID = &SL.EMPLID
  AND gde.PY_TAX_YEAR = &SL.CALENDAR_YEAR
  AND gde.COMPANY = &SL.COMPANY
  AND gde.TAXFORM_ID = 'R'
  AND gde.SEQUENCE_NUMBER = &SL.SEQUENCE_NUMBER
  AND gde.WAGE_LOSS_PLAN   = &SL.WAGE_LOSS_PLAN
  AND gde.PROVINCE    = &SL.PROVINCE
ORDER BY gde.PY_YE_FORM_SEQNUM DESC
end-SELECT

  if $updateAction <> 'C'
     let #gde.PY_YE_FORM_SEQNUM = #gde.PY_YE_FORM_SEQNUM + 1
  end-if

! It is possible that an Original is run more than once. Therefore,
! the forms must not be viewable until released.
  if $updateAction = 'C' and $NotifyFlag = 'N'

     let $ViewingDate1 = $ViewingDate
  end-if

end-procedure


begin-procedure Report-Translation
! this procedure is required by sqrtrans.sqc
end-procedure


!***********************************************************************
! The following procedure is FOR TESTING LASER PRINTING ALIGNMENT only *
!***********************************************************************
! begin-procedure Laser-Print-Alignment
!
!   let $RV1SlipNo = '999  999  999'
!
!   print $RV1SlipNo (#Slip1_Row1, 62)
!   print $RV1SlipNo (#Slip2_Row1, 62)
!   print $RV1SlipNo (#Slip3_Row1, 62)
!
!   move 99999999.99 to #TestAmt
!   do Format-Number (#TestAmt, $BoxAmt, '99999999.99')
!
!   print $BoxAmt (#Slip1_Row7,3)
!   print $BoxAmt (#Slip2_Row7,3)
!   print $BoxAmt (#Slip3_Row7,3)
!
!   print $BoxAmt (#Slip1_Row7,15)
!   print $BoxAmt (#Slip2_Row7,15)
!   print $BoxAmt (#Slip3_Row7,15)
!
!   print $BoxAmt (#Slip1_Row7,27)
!   print $BoxAmt (#Slip2_Row7,27)
!   print $BoxAmt (#Slip3_Row7,27)
!
!   print $BoxAmt (#Slip1_Row7,39)
!   print $BoxAmt (#Slip2_Row7,39)
!   print $BoxAmt (#Slip3_Row7,39)
!
!   print $BoxAmt (#Slip1_Row7,52)
!   print $BoxAmt (#Slip2_Row7,52)
!   print $BoxAmt (#Slip3_Row7,52)
!
!   print $BoxAmt (#Slip1_Row7,64)
!   print $BoxAmt (#Slip2_Row7,64)
!   print $BoxAmt (#Slip3_Row7,64)
!
!   print $BoxAmt (#Slip1_Row11,3)
!   print $BoxAmt (#Slip2_Row11,3)
!   print $BoxAmt (#Slip3_Row11,3)
!
!   print $BoxAmt (#Slip1_Row11,15)
!   print $BoxAmt (#Slip2_Row11,15)
!   print $BoxAmt (#Slip3_Row11,15)
!
!   print $BoxAmt (#Slip1_Row11,27)
!   print $BoxAmt (#Slip2_Row11,27)
!   print $BoxAmt (#Slip3_Row11,27)
!
!   print $BoxAmt (#Slip1_Row11,39)
!   print $BoxAmt (#Slip2_Row11,39)
!   print $BoxAmt (#Slip3_Row11,39)
!
!   print $BoxAmt (#Slip1_Row11,52)
!   print $BoxAmt (#Slip2_Row11,52)
!   print $BoxAmt (#Slip3_Row11,52)
!
!   print $BoxAmt (#Slip1_Row11,64)
!   print $BoxAmt (#Slip2_Row11,64)
!   print $BoxAmt (#Slip3_Row11,64)
!
!   print $BoxAmt (#Slip1_Row15,3)
!   print $BoxAmt (#Slip2_Row15,3)
!   print $BoxAmt (#Slip3_Row15,3)
!
!   print $BoxAmt (#Slip1_Row15,15)
!   print $BoxAmt (#Slip2_Row15,15)
!   print $BoxAmt (#Slip3_Row15,15)
!
!   print $BoxAmt (#Slip1_Row15,27)
!   print $BoxAmt (#Slip2_Row15,27)
!   print $BoxAmt (#Slip3_Row15,27)
!
!   print $BoxAmt (#Slip1_Row15,39)
!   print $BoxAmt (#Slip2_Row15,39)
!   print $BoxAmt (#Slip3_Row15,39)
!
!   print $BoxAmt (#Slip1_Row15,52)
!   print $BoxAmt (#Slip2_Row15,52)
!   print $BoxAmt (#Slip3_Row15,52)
!
!   print $BoxAmt (#Slip1_Row15,64)
!   print $BoxAmt (#Slip2_Row15,64)
!   print $BoxAmt (#Slip3_Row15,64)
!
!   print $BoxAmt (#Slip1_Row19,3)
!   print $BoxAmt (#Slip2_Row19,3)
!   print $BoxAmt (#Slip3_Row19,3)
!
!   print $BoxAmt (#Slip1_Row19,15)
!   print $BoxAmt (#Slip2_Row19,15)
!   print $BoxAmt (#Slip3_Row19,15)
!
!   print $BoxAmt (#Slip1_Row19,27)
!   print $BoxAmt (#Slip2_Row19,27)
!   print $BoxAmt (#Slip3_Row19,27)
!
!   print 'XX' (#Slip1_Row19,39)
!   print 'XX' (#Slip2_Row19,39)
!   print 'XX' (#Slip3_Row19,39)
!
!   print '123  456  789' (#Slip1_Row25,46)
!   print '123  456  789' (#Slip2_Row25,46)
!   print '123  456  789' (#Slip3_Row25,46)
!
!   print '777777' (#Slip1_Row25,63)
!   print '777777' (#Slip2_Row25,63)
!   print '777777' (#Slip3_Row25,63)
!
!   print 'Namexxxxxxxxxxxxxxxxxxxxx' (#Slip1_Row24,2)
!   print 'Namexxxxxxxxxxxxxxxxxxxxx' (#Slip2_Row24,2)
!   print 'Namexxxxxxxxxxxxxxxxxxxxx' (#Slip3_Row24,2)
!
!   print 'Street1xxxxxxxxxxxxxxxxxx' (#Slip1_Row26,2)
!   print 'Street1xxxxxxxxxxxxxxxxxx' (#Slip2_Row26,2)
!   print 'Street1xxxxxxxxxxxxxxxxxx' (#Slip3_Row26,2)
!
!   print 'Street2xxxxxxxxxxxxxxxxxx' (#Slip1_Row28,2)
!   print 'Street2xxxxxxxxxxxxxxxxxx' (#Slip2_Row28,2)
!   print 'Street2xxxxxxxxxxxxxxxxxx' (#Slip3_Row28,2)
!
!   print 'PrintAddrxxxxxxxxxxxxxxxx' (#Slip1_Row30,2)
!   print 'PrintAddrxxxxxxxxxxxxxxxx' (#Slip2_Row30,2)
!   print 'PrintAddrxxxxxxxxxxxxxxxx' (#Slip3_Row30,2)
!
!   print 'PrintZipxxxxxxxxxxxxxxxxx' (#Slip1_Row32,2)
!   print 'PrintZipxxxxxxxxxxxxxxxxx' (#Slip2_Row32,2)
!   print 'PrintZipxxxxxxxxxxxxxxxxx' (#Slip3_Row32,2)
!
!   print 'CompanyNamexxxxxxxxxxxxxx' (#Slip1_Row29,44)
!   print 'CompanyNamexxxxxxxxxxxxxx' (#Slip2_Row29,44)
!   print 'CompanyNamexxxxxxxxxxxxxx' (#Slip3_Row29,44)
!
!   print 'CompStreet1xxxxxxxxxxxxxx' (#Slip1_Row31,44)
!   print 'CompStreet1xxxxxxxxxxxxxx' (#Slip2_Row31,44)
!   print 'CompStreet1xxxxxxxxxxxxxx' (#Slip3_Row31,44)
!
!   print 'PrintCompAddrxxxxxxxxxxxx' (#Slip1_Row33,44)
!   print 'PrintCompAddrxxxxxxxxxxxx' (#Slip2_Row33,44)
!   print 'PrintCompAddrxxxxxxxxxxxx' (#Slip3_Row33,44)
!
!   let #Slip1_FNLine = 17
!   let #Slip2_FNLine = #Slip1_FNLine + 44
!   let #Slip3_FNLine = #Slip2_FNLine + 44
!
!   let #Slip_FNCol   = 44
!   print 'Footnote1xxxxxxxxxxxxxxxx' (#Slip1_FNLine,#Slip_FNCol)
!   print 'Footnote1xxxxxxxxxxxxxxxx' (#Slip2_FNLine,#Slip_FNCol)
!   print 'Footnote1xxxxxxxxxxxxxxxx' (#Slip3_FNLine,#Slip_FNCol)
!
!   let #Slip1_FNLine = 19
!   let #Slip2_FNLine = #Slip1_FNLine + 44
!   let #Slip3_FNLine = #Slip2_FNLine + 44
!   print 'Footnote2xxxxxxxxxxxxxxxx' (#Slip1_FNLine,#Slip_FNCol)
!   print 'Footnote2xxxxxxxxxxxxxxxx' (#Slip2_FNLine,#Slip_FNCol)
!   print 'Footnote2xxxxxxxxxxxxxxxx' (#Slip3_FNLine,#Slip_FNCol)
!
!   let #Slip1_FNLine = 21
!   let #Slip2_FNLine = #Slip1_FNLine + 44
!   let #Slip3_FNLine = #Slip2_FNLine + 44
!
!   print 'Footnote3xxxxxxxxxxxxxxxx' (#Slip1_FNLine,#Slip_FNCol)
!   print 'Footnote3xxxxxxxxxxxxxxxx' (#Slip2_FNLine,#Slip_FNCol)
!   print 'Footnote3xxxxxxxxxxxxxxxx' (#Slip3_FNLine,#Slip_FNCol)
!
!   let #Dup1_Row =  #Slip1_Row1
!   let #Dup2_Row =  #Slip2_Row1
!   let #Dup3_Row =  #Slip3_Row1
!   print 'DUPLICATE' (#Dup1_Row,35)
!   print 'DUPLICATE' (#Dup2_Row,35)
!   print 'DUPLICATE' (#Dup3_Row,35)
!
!   print 'AMENDED' (#Slip1_Row3,35)
!   print 'AMENDED' (#Slip2_Row3,35)
!   print 'AMENDED' (#Slip3_Row3,35)
!
!   if $FormType = 'S'
!      print '  ' (24,5)
!   end-if
!
!   if $FormType = 'L'
!      do Insert-FormFeed
!  end-if
!
!  new-page
!end-procedure

begin-procedure Capture-OS
  let $yeOS = ' '

  #ifdef NT
     let $yeOS = 'NT'
  #end-if

  #ifdef MVS
     let $yeOS = 'MVS'
  #end-if

  #ifdef UNIX
     let $yeOS = 'UNIX'
  #end-if
end-procedure

!----------------------------------
begin-procedure Sql-Statement-Error
!----------------------------------
  show $err-statement
  show ' Error : ' $sql-error
end-procedure

!*****************************************************************

#Include 'curdttim.sqc'  !Get-Current-DateTime procedure
#Include 'getcodta.sqc'  !Get-Company-Data procedure
#Include 'datetime.sqc'  !Routines for date and time formatting
#Include 'number.sqc'    !Routines to format numbers
#Include 'getdptnm.sqc'  !Get-Department-Name
#Include 'sqrtrans.sqc'  !Translate SQR strings to given language
#Include 'getrplng.sqc'  !Get the report language
#Include 'getmonnm.sqc'  !Get Month Name
#ifdef PRCSSCHD
#Include 'ctxrctl1.sqc'  !Get-Can-Tax YE Report Parameters
#Include 'stdapi.sqc'    !Update Process API
#Include 'txrnctl1.sqc'  !Process Scheduler Run Controls
#Include 'pyxmlbld.sqc'  !Routines for XML formatting
#endif

