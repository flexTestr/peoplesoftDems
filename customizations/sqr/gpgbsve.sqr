!***********************************************************************
!*  GPGBSVE.SQR- Starters,leavers  Validation and Exception Report
!***********************************************************************
!***********************************************************************
!                                                                      *
!                                                                      *
!                                                                      *
! This software and related documentation are provided under a         *
! license agreement containing restrictions on use and                 *
! disclosure and are protected by intellectual property                *
! laws. Except as expressly permitted in your license agreement        *
! or allowed by law, you may not use, copy, reproduce,                 *
! translate, broadcast, modify, license, transmit, distribute,         *
! exhibit, perform, publish or display any part, in any form or        *
! by any means. Reverse engineering, disassembly, or                   *
! decompilation of this software, unless required by law for           *
! interoperability, is prohibited.                                     *
! The information contained herein is subject to change without        *
! notice and is not warranted to be error-free. If you find any        *
! errors, please report them to us in writing.                         *
!                                                                      *
!                                                                      *
! Copyright (C) 1988, 2013, Oracle and/or its affiliates.              *
! All Rights Reserved.                                                 *
!***********************************************************************
!                                                                      *
!          $Date:  2013/01/18:04:06:27                                 *
!       $Release:  HR92                                                *
!      $Revision:  101                                                 *
!
!***********************************************************************
! Date        Modification
! ----        ------------
! 01/2005     New Starters/Leavers views integrated
!
!***********************************************************************

!* Constants definitions

#define PageLength              177
#define Pos5                    7
#define Pos1                    12
#define Pos2                    26
#define Pos3                    35
#define Pos4                    83
#define Pos6                    73
#define Pos7                    65
#DEFINE POS88                   88
#define Pos8                    134
#define Pos9                    0
#define Pos10                   0
#define Pos11                   0
#define Pos12                   0
#define Pos13                   0
#define Pos14                   0
#define Pos15                   0
#define Pos16                   0
#define Pos17                   0
#define Pos18                   0
#define Pos19                   0
#define Row1                    9
#define Row2                    10
#define Row3                    11
#define Header-Size             9

#define Debug_Indent_String  '    '   !* The Indent Amount for the Auto Debug.


!***********************************************************************
!* Include Files

#include 'setenv.sqc'     ! Set environment
#include 'setup32.sqc'    ! Printer and page-size initialisation

!***********************************************************************
Begin-Report



    let $ReportID    = 'GPGBSVE'
    let $ReportTitle = 'STARTERS / LEAVERS VALIDATION AND EXCEPTIONS REPORT'
    show $ReportID ' - ' $ReportTitle
    LET #malemonths    = -779
    LET #femalemonths = -719

    !* Initialising Processes and Variables
    do Init-Datetime
    do Define-Prcs-Vars
    do Get-Current-Datetime

    let $Time_Begin = edit(datenow(), 'DD/MM/YYYY HH24:MI:SS')

    do Get-Run-Control-Parms
    do Read-Run-Control($Prcs_OprID, $Prcs_Run_Cntl_ID, $RC_Cal_Run_ID, $RC_Paygroup)
    do Init-Variables
    do Title-Page
    ALTER-REPORT
        HEADING     = 'MAIN'
        HEADING-SIZE    = {Header-Size}
        FOOTING     = 'MAIN'

    do Main-Fonts
    do Build-Dynamic-SQL($RC_Cal_Run_ID, $RC_Paygroup, $Employee_Selection, $Employee_Selection_B, $From_Date, $Thru_Date)

    do Build-Starters-SQL($From_Date, $Thru_Date, $Where_Starters)
    do Build-Leavers-SQL($From_Date, $Thru_Date, $Where_Leavers)

        Let $Where_Starters-Leavers = ' AND (' || $Where_Starters || ' OR' || $Where_Leavers || ')'
        Let $Where_Starters         = ' AND' || $Where_Starters
        Let $Where_Leavers          = ' AND' || $Where_Leavers

    do Hire-Rehire-Employees($Employee_Selection, $From_Date, $Thru_Date)
    DO Terminated($Employee_Selection, $From_Date, $Thru_Date)
    do Incomplete-Address($Employee_Selection, $Thru_Date, $Where_Starters-Leavers)
    do Missing-Address($Employee_Selection, $Thru_Date, $Where_Starters-Leavers)
    do Check-Bank-Accounts($Employee_Selection, $Thru_Date, $Where_Starters-Leavers)
    do Check-Bank-Accounts_NPD($Employee_Selection, $Thru_Date, $Where_Starters-Leavers)
    do NI-Check($Employee_Selection, $Thru_Date, $Where_Starters-Leavers)
    do Nid-Prefix-GBR($Employee_Selection, $Thru_Date, $Where_Starters-Leavers)
    do Duplicate-NI($Employee_Selection, $Thru_Date, $Where_Starters-Leavers)
    do Tax-Check($Employee_Selection, $Thru_Date, $Where_Starters-Leavers)
    do Old-Person-NI($Employee_Selection, $Thru_Date, $Where_Starters-Leavers)
    do Check-Leavers-Court-Orders($Employee_Selection, $From_Date, $Thru_Date)
    do Check-Leavers-Tax-Credits($Employee_Selection, $From_Date, $Thru_Date)
    do Leavers-with-active-loans($Employee_Selection,$From_Date, $Thru_Date)
    ! do Leavers-with-active-share-save($Employee_Selection,$From_Date, $Thru_Date)
    ! do Leavers-with-active-share-buy($Employee_Selection,$From_Date, $Thru_Date)

    do Update-Prcs-Run-Status
    do End-Of-Report

    #debuga do Remove-Procedure-Indent
End-Report


!***********************************************************************
Begin-Procedure Init-Variables
    !* Initialising Global Variables.

    #debuga do Show-Procedure-Name('Init-Variables')

    Let #Columns    = 7
    Let $Header1    = 'Empl ID'
    Let $Header2    = 'Empl Rcd'
    Let $Header3    = 'Name'
    Let $Header4    = 'Details'
    Let $Header6    = 'Company'


    #debuga do Remove-Procedure-Indent
End-Procedure Init-Variables


!***********************************************************************
Begin-Procedure Read-Run-Control($OprID, $Run_ID, :$Cal_Run_ID, :$Paygroup)
     let $Cal_Run_ID = ''
     let $Paygroup = ''

Begin-Select On-Error=Error-SQL
A.CAL_RUN_ID
A.GP_PAYGROUP

    let $Cal_Run_ID = rtrim(&A.CAL_RUN_ID, ' ')
    let $Paygroup = rtrim(&A.GP_PAYGROUP, ' ')
    let $_Param1 = 'Calendar Run ID: ' || $Cal_Run_ID
    let $_Param2 = 'Pay Group: ' || $Paygroup
    let $_Operator = 'Oper ID: ' || $OprID
    let $_PNL_CAL_RUN_ID = $Cal_Run_ID
    let $_PNL_PAYGROUP = $Paygroup

FROM  PS_GPGB_RNCTL_VE A
WHERE A.OPRID = $OprID
AND   A.RUN_CNTL_ID = $Run_ID
End-Select

    #debuga do Remove-Procedure-Indent
End-Procedure Read-Run-Control




!***********************************************************************
Begin-Procedure Report-Heading($Heading, $Description)
    NEXT-LISTING NEED=5
    print ' '                                   (+1, {Pos1})
    ALTER-PRINTER font=5 POINT-SIZE=10
    print $Heading                              (+1, {Pos1}) bold
    do Main-Fonts
    print $Description                          (+1, {Pos2}) wrap 157 3
    print ' '                                   (+1, {Pos1})
    #debuga do Remove-Procedure-Indent
End-Procedure Report-Heading


!*****************************************************************************************************
!* Finding employees that don't have a postcode listed for their  address and missing address details*
!*****************************************************************************************************

Begin-Procedure Missing-Address($Employee_Selection, $EffDt, $Where_Other)
do Report-Heading('', 'Address for the following employees are  missing ,Review is required ')
    
    let #Count = 0


Begin-Select On-Error=Error-SQL
A.EMPLID                                    (+1, {Pos1})
AA.NAME_PSFORMAT                            (0,  {Pos3})
B.COMPANY                                  (0,  {Pos6})
B.EMPL_RCD                             (0,  {Pos2})

    add 1 to #Count
    
            let $Missing = 'Missing Address'
            print $Missing                          (0, {Pos4})
FROM  PS_PERS_DATA_EFFDT A
,PS_PERSON_NAME AA
,PS_JOB B
,PS_GP_PYGRP E
,PS_GP_PYENT F
 WHERE
A.EMPLID= AA.EMPLID
AND A.EMPLID =B.EMPLID
AND A.EFFDT = (SELECT MAX(A2.EFFDT) FROM PS_PERS_DATA_EFFDT A2
                 WHERE  A2.EMPLID = A.EMPLID
                 AND    A2.EFFDT <= $EffDt)

AND B.EFFDT = (SELECT MAX(B2.EFFDT) FROM PS_JOB B2
                 WHERE  B2.EMPLID = B.EMPLID
AND B2.EMPL_RCD = B.EMPL_RCD
                 AND    B2.EFFDT <= $EffDt)
AND B.EFFSEQ =(SELECT MAX(B3.EFFSEQ) FROM PS_JOB B3
                 WHERE  B3.EMPLID = B.EMPLID
 AND B3.EMPL_RCD = B.EMPL_RCD
                 AND    B3.EFFDT = B.EFFDT)
AND   EXISTS (SELECT 'X' FROM PS_GP_PYE_PRC_STAT GRP
              WHERE  GRP.EMPLID = A.EMPLID
    [$Employee_Selection]
)
    [$Where_Other]

AND B.GP_PAYGROUP = E.GP_PAYGROUP
AND F.PAY_ENTITY = E.PAY_ENTITY
AND F.COUNTRY = 'GBR'
AND NOT EXISTS (SELECT 'X' FROM PS_PERSON_ADDRESS A1
                WHERE A1.EMPLID = A.EMPLID)
ORDER BY   B.COMPANY, A.EMPLID,AA.NAME_PSFORMAT
End-Select

    if not #Count
        print '*** None Found ***' (+1, {Pos1})
    end-if

    #debuga do Remove-Procedure-Indent
End-Procedure Missing-Address

Begin-Procedure Incomplete-Address($Employee_Selection, $EffDt, $Where_Other)
    do Report-Heading('Missing Address Information', 'Address for the following employees maybe  incomplete,Review is required ')
    let #Count = 0
Begin-Select On-Error=Error-SQL
A.EMPLID              &AEMPLID                             
A1.ADDRESS_TYPE
A1.POSTAL
A1.CITY
A1.ADDRESS1
A1.ADDRESS2
A1.ADDRESS3
AA.NAME_PSFORMAT           &aaname                  
B.COMPANY             &bcmpny                      
B.EMPL_RCD          &bempl_rcd                   

               if isblank(&A1.POSTAL) or isblank(&A1.ADDRESS1) or isblank(&A1.ADDRESS2) or isblank(&A1.ADDRESS3)
           let $aemplid= &AEMPLID
           MOVE &bempl_rcd TO $bempl_rcd
           let $aaname = &aaname
           let $bcmpny = &bcmpny
            let $Missing = ' Incomplete '||&A1.ADDRESS_TYPE||' Address'
              print $aemplid        (+1, {Pos1}) 
              print $aaname          (0,  {Pos3})
              print $bcmpny          (0,  {Pos6})  
              print $bempl_rcd      (0,{Pos2}) 
              print $Missing                        (0, {Pos4})  
             add 1 to #Count
      end-if
FROM  PS_PERS_DATA_EFFDT A
,PS_PERSON_NAME AA
,PS_PERSON_ADDRESS A1
,PS_JOB B
,PS_GP_PYGRP E
,PS_GP_PYENT F
 WHERE
A.EMPLID= AA.EMPLID
AND A.EMPLID =A1.EMPLID
AND A.EMPLID =B.EMPLID
AND A.EFFDT = (SELECT MAX(A2.EFFDT) FROM PS_PERS_DATA_EFFDT A2
                 WHERE  A2.EMPLID = A.EMPLID
                 AND    A2.EFFDT <= $EffDt)
AND B.EFFDT = (SELECT MAX(B2.EFFDT) FROM PS_JOB B2
                 WHERE  B2.EMPLID = B.EMPLID
AND B2.EMPL_RCD = B.EMPL_RCD
                 AND    B2.EFFDT <= $EffDt)
AND B.EFFSEQ =(SELECT MAX(B3.EFFSEQ) FROM PS_JOB B3
                 WHERE  B3.EMPLID = B.EMPLID
 AND B3.EMPL_RCD = B.EMPL_RCD
                 AND    B3.EFFDT = B.EFFDT)
AND   EXISTS (SELECT 'X' FROM PS_GP_PYE_PRC_STAT GRP
              WHERE  GRP.EMPLID = A.EMPLID
    [$Employee_Selection]
)
     [$Where_Other]

AND B.GP_PAYGROUP = E.GP_PAYGROUP
AND F.PAY_ENTITY = E.PAY_ENTITY
AND F.COUNTRY = 'GBR'
ORDER BY   B.COMPANY, A.EMPLID,AA.NAME_PSFORMAT
End-Select

     if not #Count
        print '*** None Found ***' (+1, {Pos1})
    end-if

    #debuga do Remove-Procedure-Indent
End-Procedure Incomplete-Address



!***********************************************************************
Begin-Procedure Get-Pension-Split($EmplID, #Empl_Rcd, $Employee_Selection,
                $Accumulators, $EffDt, :$Split)
    !* Working out the Actual Split of Pension for the employee.  This
    !* allows us to show some more information on the report.
    let $Split = ''

Begin-Select On-Error=SQL-Error
C.PIN_NM,
C.DESCR,
SUM(B.CALC_RSLT_VAL)  &TOTAL
    let $Total = '£' || edit(&TOTAL, '888,880.00')
    let $Output = &C.PIN_NM || ' - ' || &C.DESCR || ': ' || $Total
    if isblank($Split)
        let $Split = $Output
    else
        let $Split = $Split || ',  ' || $Output
    end-if

FROM  PS_GP_RSLT_ACUM B,
      PS_GP_PIN C
WHERE B.EMPLID = $EmplID
AND   B.EMPL_RCD = #Empl_Rcd
AND   C.PIN_NUM = B.PIN_NUM
AND   C.PIN_NM IN [$Accumulators]
AND   EXISTS (SELECT 'X' FROM PS_GP_PYE_PRC_STAT GRP
              WHERE  GRP.EMPLID = B.EMPLID
              AND    GRP.CAL_RUN_ID = B.CAL_RUN_ID
              AND    GRP.EMPL_RCD = B.EMPL_RCD
              AND    GRP.GP_PAYGROUP = B.GP_PAYGROUP
              [$Employee_Selection])
AND   B.CAL_RUN_ID = B.CAL_ID   !* This removes Retro Calculations
AND   B.ACM_FROM_DT <= $EffDt
AND   B.ACM_THRU_DT >= $EffDt
GROUP BY C.PIN_NM, C.DESCR
HAVING SUM(B.CALC_RSLT_VAL) <> 0
ORDER BY C.PIN_NM, C.DESCR
End-Select

    #debuga do Remove-Procedure-Indent
End-Procedure Get-Pension-Split


!***********************************************************************
!* Looking for any employees that are terminated ***********************
!***********************************************************************

 Begin-Procedure Terminated($Employee_Selection, $From_Date, $Thru_Date)

    let $Description = 'Employees with Termination in the current period. '
    do Report-Heading('Terminated Employees in the current period', $Description)
    let #Count = 0

Begin-Select On-Error=SQL-Error
B.EMPLID                                        (+1, {Pos1})
B.EMPL_RCD                                      (0,  {Pos2})
AA.NAME                                         (0,  {Pos3})
B.EFFDT      &TERMINATION_DT
C.COMPANY                                       (0, {Pos6})

    do Get-Tax-Details(&B.EMPLID, &B.EMPL_RCD, $Thru_Date, $NI_Category, $Tax_Code, $Tax_Basis)

    add 1 to #Count
    let $Output = 'Termination Date: ' || &TERMINATION_DT || ',  NI Category: ' || $NI_Category || ',  Tax Basis: ' || $Tax_Basis
    let $Output = $Output || ',  Tax Code: ' || $Tax_Code

    print $Output   (0, {Pos4})

FROM    PS_GPGB_LEAVER_VW B,
        PS_PERSON_NAME AA,
        PS_JOB C

WHERE B.EFFDT = (SELECT MAX(B2.EFFDT) FROM PS_GPGB_LEAVER_VW B2
                 WHERE  B2.EMPLID = B.EMPLID
                 AND    B2.EMPL_RCD = B.EMPL_RCD
                 AND    B2.EFFDT >= $From_Date
                 AND    B2.EFFDT <= $Thru_Date)
AND   B.EFFSEQ = (SELECT MAX(B3.EFFSEQ) FROM PS_GPGB_LEAVER_VW B3
                  WHERE  B3.EMPLID = B.EMPLID
                  AND    B3.EMPL_RCD = B.EMPL_RCD
                  AND    B3.EFFDT = B.EFFDT)
AND   B.EMPLID = AA.EMPLID
AND   B.EMPLID = C.EMPLID
AND   B.EMPL_RCD = C.EMPL_RCD
AND   C.EFFDT = (SELECT MAX(C2.EFFDT) FROM PS_JOB C2
                 WHERE  C2.EMPLID = C.EMPLID
                 AND    C2.EMPL_RCD = C.EMPL_RCD
                 AND    C2.EFFDT <= B.EFFDT)             ! Leaver view Effdt is Job Effdt-1
AND   C.EFFSEQ = B.EFFSEQ
AND   EXISTS (SELECT 'X' FROM PS_GP_PYE_PRC_STAT GRP
              WHERE  GRP.EMPLID = B.EMPLID
              AND    GRP.EMPL_RCD = B.EMPL_RCD
              [$Employee_Selection])

ORDER BY  C.COMPANY, B.EMPLID, AA.NAME, B.EMPL_RCD
End-Select

    if not #Count
        print '*** None Found ***' (+1, {Pos1})
    end-if

    #debuga do Remove-Procedure-Indent
End-Procedure Terminated


!***********************************************************************************
! These are employees that have a hire or a re-hire record in the current period.***
!***********************************************************************************

Begin-Procedure Hire-Rehire-Employees($Employee_Selection, $From_Date, $Thru_Date)
    do Report-Heading('Hired/Rehired Employees','The following employees have a Rehire/Hire row in the current period.  This is a warning only')
    let #Count = 0

Begin-Select On-Error=SQL-Error
A.EMPLID                                      (+1, {Pos1})
A.EMPL_RCD                                    (0,  {Pos2})
AA.NAME                                       (0,  {Pos3})
A.EFFDT
B.COMPANY                                     (0, {Pos6})

    add 1 to #Count
    let $Output = 'Rehire  Or Hire Date ' || &A.EFFDT
    print $Output                            (0, {Pos4})

FROM PS_GPGB_STARTER_VW A,
     PS_PERSON_NAME AA,
     PS_JOB B
WHERE A.EFFDT = (SELECT MAX(A2.EFFDT) FROM PS_GPGB_STARTER_VW A2
                 WHERE  A2.EMPLID = A.EMPLID
                 AND    A2.EMPL_RCD = A.EMPL_RCD
                 AND    A2.EFFDT >= $From_Date
                 AND    A2.EFFDT <= $Thru_Date)
AND   A.EMPLID = AA.EMPLID
AND   A.EMPLID = B.EMPLID
AND   A.EMPL_RCD = B.EMPL_RCD
AND   A.EFFDT =  B.EFFDT
AND   A.EFFSEQ = B.EFFSEQ
AND   EXISTS (SELECT 'X' FROM PS_GP_PYE_PRC_STAT GRP
              WHERE  GRP.EMPLID = A.EMPLID
              AND    GRP.EMPL_RCD = A.EMPL_RCD
              [$Employee_Selection])

ORDER BY  B.COMPANY,  A.EMPLID, AA.NAME, A.EMPL_RCD
End-Select

    if not #Count
        print '*** None Found ***' (+1, {Pos1})
    end-if

    #debuga do Remove-Procedure-Indent
End-Procedure Hire-Rehire-Employees


!************************************************************************
!* Check-Leavers-Court-Orders                                           *
!*      Checks for leavers who have court orders outstanding            *
!*      at their termination date                                       *
!************************************************************************

Begin-Procedure Check-Leavers-Court-Orders($Employee_Selection, $From_Date, $Thru_Date)

    let $Detail = 'The following employees are leavers who have outstanding court orders at their'
    let $Detail = $Detail || ' termination date.'
    do Report-Heading('Leavers Court Orders check completed', $Detail)
    let #Count = 0

Begin-Select On-Error=SQL-Error
A.EMPLID                &EMPLID
A.EMPL_RCD              &EMPL_RCD
A.EFFDT                 &TERM_DT
AA.NAME                 &NAME
B.COMPANY               &COMPANY
C.GPGB_CO_LEG           &CO_REGION
C.GPGB_CO_ORDER         &CO_TYPE
C.GPGB_CO_ORD_REF       &CO_REF
C.STOP_DATE             &STOP_DATE
C.END_DATE              &END_DATE

   

        add 1 to #Count
        if rtrim(&STOP_DATE,' ') = ''
                let $END_DATE = '( End Date not specified )'
        else
                let $END_DATE = &STOP_DATE
        end-if

        let $Output = 'Employee terminated on ' || &TERM_DT || ', but is paying ' || &CO_REGION || '-' || &CO_TYPE
        let $Output = $Output || ' Ref: ' || &CO_REF || ' until ' || $END_DATE

        print &EMPLID           (+1, {Pos1})
        print &EMPL_RCD         (0, {Pos2})
         print &NAME           (0, {Pos3})
        print &COMPANY          (0, {Pos6})
        print $Output           (0, {Pos4})


FROM    PS_GPGB_LEAVER_VW A,
        PS_PERSON_NAME AA,
        PS_JOB B,
        PS_GPGB_EE_CO_DTL C

WHERE   A.EFFDT = (SELECT MAX(A2.EFFDT) FROM PS_GPGB_LEAVER_VW A2
                WHERE  A2.EMPLID   = A.EMPLID
                AND    A2.EMPL_RCD = A.EMPL_RCD
                AND    A2.EFFDT   >= $From_Date
                AND    A2.EFFDT   <= $Thru_Date)
AND     A.EFFSEQ = (SELECT MAX(A3.EFFSEQ) FROM PS_GPGB_LEAVER_VW A3
                 WHERE  A3.EMPLID   = A.EMPLID
                 AND    A3.EMPL_RCD = A.EMPL_RCD
                 AND    A3.EFFDT    = A.EFFDT)
AND     A.EMPLID   = AA.EMPLID
AND     A.EMPLID   = B.EMPLID
AND     A.EMPL_RCD = B.EMPL_RCD
AND     B.EFFDT = (SELECT MAX(B2.EFFDT) FROM PS_JOB B2
                 WHERE  B2.EMPLID   = B.EMPLID
                 AND    B2.EMPL_RCD = B.EMPL_RCD
                 AND    B2.EFFDT   <= A.EFFDT)             ! Leaver view Effdt is Job Effdt-1
AND     B.EFFSEQ = A.EFFSEQ
AND     A.EMPLID      = C.EMPLID
AND     C.ACTIVE_FLAG = 'A'
AND     EXISTS (SELECT 'X' FROM PS_GP_PYE_PRC_STAT GRP
                WHERE  GRP.EMPLID   = A.EMPLID
                AND    GRP.EMPL_RCD = A.EMPL_RCD
                [$Employee_Selection])

ORDER BY B.COMPANY, A.EMPLID, AA.NAME, A.EMPL_RCD
End-Select

    if not #Count
        print '*** None Found ***' (+1, {Pos1})
    end-if

    #debuga do Remove-Procedure-Indent
End-Procedure Check-Leavers-Court-Orders

!***********************************************************************************************************
!* Check-Leavers-Tax-Credits Checks for leavers who have Tax Credits outstanding at their termination date *                                      *
!***********************************************************************************************************

Begin-Procedure Check-Leavers-Tax-Credits($Employee_Selection, $From_Date, $Thru_Date)

    let $Detail = 'The following employees are leavers who are still receiving tax credits after their'
    let $Detail = $Detail || ' termination date.'
    do Report-Heading('Leavers Tax Credits check completed', $Detail)
    let #Count = 0

Begin-Select On-Error=SQL-Error
A.EMPLID                &EMPLID
A.EMPL_RCD              &EMPL_RCD
A.EFFDT                 &TERM_DT
AA.NAME                 &NAME
B.COMPANY               &COMPANY
C.GPGB_TXCRD_RT         &TAX_CREDIT
C.END_DATE              &END_DATE
C.STOP_DATE             &STOP_DATE
C.GPGB_TXCRD_RT         &GPGB_TXCRD_RT
C.START_DATE            &START_DATE

        let $Value = '£' || edit(&TAX_CREDIT, '888,880.00')
        add 1 to #Count

        if rtrim(&STOP_DATE,' ') = ''
                let $END_DATE = &END_DATE
        else
                let $END_DATE = &STOP_DATE
        end-if

        let $Output = 'Employee finished on ' || &TERM_DT || ', but is receiving a Tax Credit of ' || $Value || ' until ' || $END_DATE

        print &EMPLID           (+1, {Pos1})
        print &EMPL_RCD         (0, {Pos2})
        print &NAME             (0, {Pos3})
        print &COMPANY          (0, {Pos6})
        PRINT &GPGB_TXCRD_RT    (0, {Pos5})
        print $Output           (0, {Pos4})

FROM    PS_GPGB_LEAVER_VW A,
        PS_PERSON_NAME AA,
        PS_JOB B,
        PS_GPGB_EE_TAXCRD C

WHERE   A.EFFDT = (SELECT MAX(A2.EFFDT) FROM PS_GPGB_LEAVER_VW A2
                WHERE  A2.EMPLID   = A.EMPLID
                AND    A2.EMPL_RCD = A.EMPL_RCD
                AND    A2.EFFDT   >= $From_Date
                AND    A2.EFFDT   <= $Thru_Date)
AND     A.EFFSEQ = (SELECT MAX(A3.EFFSEQ) FROM PS_GPGB_LEAVER_VW A3
                 WHERE  A3.EMPLID   = A.EMPLID
                 AND    A3.EMPL_RCD = A.EMPL_RCD
                 AND    A3.EFFDT    = A.EFFDT)
AND     A.EMPLID   = AA.EMPLID
AND     A.EMPLID   = B.EMPLID
AND     A.EMPL_RCD = B.EMPL_RCD
AND     B.EFFDT = (SELECT MAX(B2.EFFDT) FROM PS_JOB B2
                 WHERE  B2.EMPLID   = B.EMPLID
                 AND    B2.EMPL_RCD = B.EMPL_RCD
                 AND    B2.EFFDT   <= A.EFFDT)             ! Leaver view Effdt is Job Effdt-1
AND     B.EFFSEQ   = A.EFFSEQ
AND     A.EMPLID   = C.EMPLID
AND     A.EMPL_RCD = C.EMPL_RCD
AND    (C.END_DATE > B.TERMINATION_DT OR C.END_DATE IS NULL)

AND     EXISTS (SELECT 'X' FROM PS_GP_PYE_PRC_STAT GRP
                WHERE  GRP.EMPLID   = A.EMPLID
                AND    GRP.EMPL_RCD = A.EMPL_RCD
                [$Employee_Selection])

ORDER BY B.COMPANY, A.EMPLID, AA.NAME, A.EMPL_RCD
End-Select

    if not #Count
        print '*** None Found ***' (+1, {Pos1})
    end-if

    #debuga do Remove-Procedure-Indent
End-Procedure Check-Leavers-Tax-Credits

!***********************************************************************************************************
!* Leavers-with-active-loans Check employees who have outstanding Loans at their Termination               *
!***********************************************************************************************************

Begin-Procedure Leavers-with-active-loans($Employee_Selection,$From_Date, $Thru_Date)

do Report-Heading('Leavers-with-active-loans','The following employees are Leavers-with-active-loans')
   let #Count = 0

Begin-Select On-Error=SQL-Error
A.EMPLID
A.EMPL_RCD
A.EFFDT            &TERM_DT
AA.NAME
B.COMPANY          &COMPANY
L.GPGB_LOAN_ID
L.GPGB_AMT_BAL

   add 1 to #Count

   let $Value = '£' || edit(&L.GPGB_AMT_BAL, '888,888,880.00')

   let $Output = 'Employee finished on ' || &TERM_DT || ', while Loan Id: ' || rtrim(&L.GPGB_LOAN_ID, ' ') || ' has an outstanding balance of ' || $Value

        print &A.EMPLID           (+1, {Pos1})
        print &A.EMPL_RCD         (0, {Pos2})
        print &AA.NAME            (0, {Pos3})
        print &COMPANY            (0, {Pos6})
        print $Output             (0, {Pos4})


FROM PS_GPGB_LEAVER_VW A,
     PS_PERSON_NAME AA,
     PS_JOB B,
     PS_GPGB_LOAN_WA L

WHERE A.EFFDT = (SELECT MAX(A2.EFFDT) FROM PS_GPGB_LEAVER_VW A2
                WHERE  A2.EMPLID   = A.EMPLID
                AND    A2.EMPL_RCD = A.EMPL_RCD
                AND    A2.EFFDT   >= $From_Date
                AND    A2.EFFDT   <= $Thru_Date)
AND   A.EFFSEQ = (SELECT MAX(A3.EFFSEQ) FROM PS_GPGB_LEAVER_VW A3
                 WHERE A3.EMPLID   = A.EMPLID
                 AND   A3.EMPL_RCD = A.EMPL_RCD
                 AND   A3.EFFDT    = A.EFFDT)
AND  A.EMPLID     = AA.EMPLID
AND  A.EMPLID   = B.EMPLID
AND  A.EMPL_RCD = B.EMPL_RCD
AND  B.EFFDT    = (SELECT MAX(B2.EFFDT) FROM PS_JOB B2
                 WHERE  B2.EMPLID   = B.EMPLID
                 AND    B2.EMPL_RCD = B.EMPL_RCD
                 AND    B2.EFFDT   <= A.EFFDT)             ! Leaver view Effdt is Job Effdt-1
AND  B.EFFSEQ   = A.EFFSEQ
AND  A.EMPLID     = L.EMPLID
AND  A.EMPL_RCD   = L.EMPL_RCD
AND  L.SEG_END_DT = (SELECT MAX(L2.SEG_END_DT) FROM PS_GPGB_LOAN_WA L2
                     WHERE  L2.EMPLID      = L.EMPLID
                     AND    L2.EMPL_RCD    = L.EMPL_RCD
                     AND    L2.SEG_END_DT >= $From_Date
                     AND    L2.SEG_END_DT <= $Thru_Date)
AND  L.GPGB_AMT_BAL>0
AND   EXISTS (SELECT 'X' FROM PS_GP_PYE_PRC_STAT GRP
              WHERE  GRP.EMPLID   = A.EMPLID
              AND    GRP.EMPL_RCD = A.EMPL_RCD
              [$Employee_Selection])

ORDER BY B.COMPANY, A.EMPLID, AA.NAME, A.EMPL_RCD

End-Select

   if not #Count
       print '*** None Found ***' (+1, {Pos1})
   end-if

   #debuga do Remove-Procedure-Indent
End-Procedure Leavers-with-active-loans


!*************************************************************
! Leavers with active share-save

Begin-Procedure Leavers-with-active-share-save($Employee_Selection,$From_Date, $Thru_Date)
   !* Description of Procedure

   do Report-Heading('Leavers with active share-save','The following employees are Leavers with active share-save')
   let #Count = 0

   if not #Count
       print '*** None Found ***' (+1, {Pos1})
   end-if


End-Procedure Leavers-with-active-share-save

!*************************************************************
! Leavers with active share-save

Begin-Procedure Leavers-with-active-share-buy($Employee_Selection,$From_Date, $Thru_Date)
   !* Description of Procedure


   do Report-Heading('Leavers-with-active-share-buy','The following employees are Leavers-with-active-share-buy')
   let #Count = 0

   if not #Count
       print '*** None Found ***' (+1, {Pos1})
   end-if


End-Procedure Leavers-with-active-share-buy


!***********************************************************************
Begin-Procedure Get-Accumulator-Amount($EmplID, #Empl_Rcd, $Employee_Selection,
                $EffDt, $Accumulator, :#Amount)
!***********************************************************************

    let #Amount = 0

Begin-Select On-Error=SQL-Error
SUM(B.CALC_RSLT_VAL)  &TOTAL
    let #Amount = &TOTAL
FROM  PS_GP_RSLT_ACUM B,
      PS_GP_PIN C
WHERE B.EMPLID = $EmplID
AND   B.EMPL_RCD = #Empl_Rcd
AND   C.PIN_NUM = B.PIN_NUM
AND   C.PIN_NM  = $Accumulator
AND   EXISTS (SELECT 'X' FROM PS_GP_PYE_PRC_STAT GRP
              WHERE  GRP.EMPLID = B.EMPLID
              AND    GRP.CAL_RUN_ID = B.CAL_RUN_ID
              AND    GRP.EMPL_RCD = B.EMPL_RCD
              AND    GRP.GP_PAYGROUP = B.GP_PAYGROUP
              [$Employee_Selection])
AND   B.CAL_RUN_ID = B.CAL_ID
AND   B.ACM_FROM_DT <= $EffDt
AND   (   B.ACM_THRU_DT >= $EffDt
       OR B.ACM_THRU_DT IS NULL
      )
End-Select

    #debuga do Remove-Procedure-Indent
End-Procedure Get-Accumulator-Amount


!***********************************************************************
Begin-Procedure Get-Variable-Amount($EmplID, #Empl_Rcd, $From_Date, $Thru_Date,
        $Variable, :#Amount)
!***********************************************************************

    let #Amount = 0

Begin-Select On-Error=SQL-Error
A.SOVR_VAL_NUM

    let #Amount = &A.SOVR_VAL_NUM

FROM  PS_GP_PYE_SOVR A,
      PS_GP_PIN B
WHERE A.EMPLID = $EmplID
AND   A.EMPL_RCD = #Empl_Rcd
AND   B.PIN_NUM = A.PIN_NUM
AND   B.PIN_NM = $Variable
AND   A.BGN_DT <= $Thru_Date
AND   (   A.END_DT IS NULL
       OR A.END_DT >= $From_Date
      )
End-Select

    #debuga do Remove-Procedure-Indent
End-Procedure Get-Variable-Amount


!***********************************************************************
Begin-Procedure ERROR-SQL
    #debuga do Show-Procedure-Name('ERROR-SQL')

    show 'SQL Error -> ' $SQL-ERROR
    stop

    #debuga do Remove-Procedure-Indent
End-Procedure ERROR-SQL

!***********************************************************************
Begin-Procedure Show-Procedure-Name($Procedure_Name)
    do Get-Current-DateTime
    LET $_Debug_Indent = $_Debug_Indent  || {Debug_Indent_String}
End-Procedure Show-Procedure-Name


!***********************************************************************
Begin-Procedure Remove-Procedure-Indent LOCAL
End-Procedure Remove-Procedure-Indent


!******************************************************************
!***    Common SQC Modules Copied In For Program Usage
!******************************************************************
#include 'curdttim.sqc'   ! Get-Current-DateTime procedure
#include 'datetime.sqc'   ! Routines for date and time formatting
#include 'datemath.sqc'   ! Routines for date manipulation
#include 'prcsapi.sqc'    ! Update Process Request API
#include 'prcsdef.sqc'    ! Update Process Request variable declaration
#include 'gpgblout.sqc'   ! Report Layout Template
#include 'gpgbave.sqc'    ! GP UK - Payroll Exception shared procedures


!*********************************************
!* END OF REPORT******************************
!*********************************************
