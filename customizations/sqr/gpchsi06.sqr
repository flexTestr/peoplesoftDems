!***********************************************************************
!  GPCHSI06.SQR: Social Insurance Company Setup.                       *
!***********************************************************************
!                                                                      *
!                                                                      *
!                                                                      *
! This software and related documentation are provided under a         *
! license agreement containing restrictions on use and                 *
! disclosure and are protected by intellectual property                *
! laws. Except as expressly permitted in your license agreement        *
! or allowed by law, you may not use, copy, reproduce,                 *
! translate, broadcast, modify, license, transmit, distribute,         *
! exhibit, perform, publish or display any part, in any form or        *
! by any means. Reverse engineering, disassembly, or                   *
! decompilation of this software, unless required by law for           *
! interoperability, is prohibited.                                     *
! The information contained herein is subject to change without        *
! notice and is not warranted to be error-free. If you find any        *
! errors, please report them to us in writing.                         *
!                                                                      *
!
! Copyright (C) 1988, 2014, Oracle and/or its affiliates.              *
! All Rights Reserved.                                                 *
!----------------------------------------------------------------------
!
!          $Date:  2014/04/10:03:39:29                                 !
!       $Release:  HR92                                                !
!      $Revision:  102                                                 !
!                                                                      *
!***********************************************************************

#include 'setenv.sqc'    ! set Default environment
#include 'gpchut10.sqc'  ! setenv override for Swiss Default.
#include 'gpchut12.sqc'  ! Substitution Variables Defined.
#include 'setup31.sqc'

#define col0     10
#define col1     22   ! UV
#define col1_1   32   ! CLASS
#define colh_2   42   ! NBU
#define col1_2   38   ! NBU
#define colh_3   52   ! BU
#define col1_3   47   ! BU
#define colh_4   56
#define col2     53   ! AG
#define col3     70
#define col3_1   90
#define col4     110
!FMB #define colh_5   116
#define colh_5   112
#define col5     16
! 09/2001
#define col1b    20
#define col2b    50
#define col3b    80
#define col4b1  107
#define col4b   110


!************************************************************************

begin-Program
  do Init-DateTime
  do Init-Number
  do Get-Current-DateTime
  do Init-Report
  do Process-Main
  do Reset
  do Stdapi-Term
  do Get-Log
end-Program

!************************************************************************
begin-heading 12
   #include 'gpchut08.sqc'

   do Get-Company-Address($Ctl_Company,$Last_Date,$language_cd,$ADDLINE1,$ADDLINE2,$ADDLINE3,$ADDLINE31,
                          $ADDLINE4,$ADDLINE5,$ADDLINE6,$COMPANY_DESCR,$COMPANY_DESCRSHORT,
                          $CITY,$STATE,$POSTAL,$CpBusn_Phone,$CpFax_Phone,$CpOtr_Phone)
   print $TITLE2                                  (+1)          center
   print $COMPANY                                 (+2,{col0})   bold
   print $ADDLINE1                                (0, +2)


   print $COMPANY_DESCR                           (+1,{col0})
!FMB   print $PAY_CALC_NO                             (0,{col3_1})
!FMB   print $AHV                                     (0,{colh_5})

   print $ADDLINE2                                (+1,{col0})
   print $HD_FROMDT                               (0,{col3_1})
   print $Fromdt                                  (0,{colh_5})

   print $ADDLINE3                                (+1,{col0})
   print $HD_TODATE                               (0,{col3_1})
   print $Todate                                  (0,{colh_5})

   print $ADDLINE31                               (+1,{col0})
   print '    %'                                  (0,{col2})
   print $DATE                                    (0,{col3_1})
   print $Print_AsOfDate                          (0,{colh_5})

   print '_'                                      (+1,{col0},120) FILL

end-heading

!************************************************************************

begin-procedure Init-Report

  do Stdapi-Init

  if $prcs_process_instance = ''
     do ask-input
  else
     do Get-Report-Parameters                !in Gpchut06.sqc
     do Get-Values
  end-if

  move 'GPCHSI06' to $ReportID
  do Init_Report_Translation ($ReportID, $language_cd)
  do Append_Report_Translation ('GPCHGLOB')
  do Gpce_Init_Report_Translation ($ReportID, $language_cd)
  do Gpce_Append_Report_Translation ('GPCHGLOB', $language_cd)
  do Report-Translation
  let $ReportTitle = $TITLE1
  #debug show ' Report ID : ' $ReportID  ' ReportTitle : '  $ReportTitle

end-procedure Init-Report

!************************************************************************
begin-procedure Ask-Input

input $Start_Date     'Begin Date       : '
input $Last_Date      'End Date         : '
input $Ctl_Company    'Company          : ' type=char      maxlen=3

end-procedure Ask-Input

!*************************************************************************
begin-procedure Get-Values
    let $language_cd = $PRCS_LANGUAGE_CD
    let $Ctl_Company = RTRIM(&GPCH_RUN_CNTL.COMPANY ,' ')
    let $Start_Date  = &GPCH_RUN_CNTL.GPCH_RC_FROM_DT
    let $Last_Date   = &GPCH_RUN_CNTL.GPCH_BK_GEN_DT
    do Format-DateTime ($Start_Date,$Fromdt, {DEFDATE},'','')
    do Format-DateTime ($Last_Date ,$Todate, {DEFDATE},'','')
end-procedure Get-Values
!****************************************************************************
Begin-Procedure Report-Translation


  do Get_Field_Information ('GPCHSI06','ACC_INS_PROV',  $ACC_INS_PROV, #CW)
  do Get_Field_Information ('GPCHSI06','AHV_SUS',       $AHV_SUS,      #CW)
  do Get_Field_Information ('GPCHSI06','COMPANY',       $COMPANY,      #CW)
  do Get_Field_Information ('GPCHSI06','CUST_ID',       $CUST_ID,      #CW)
  do Get_Field_Information ('GPCHSI06','EMPEE_CONT',    $EMPEE_CONT ,  #CW)
  do Get_Field_Information ('GPCHSI06','EMPLER_AMT',    $EMPLER_AMT,   #CW)
  do Get_Field_Information ('GPCHSI06','MEMB_NO',       $MEMB_NO   ,   #CW)
  do Get_Field_Information ('GPCHSI06','PAY_CALC_NO',   $PAY_CALC_NO,  #CW)
  do Get_Field_Information ('GPCHSI06','ST_AHV',        $ST_AHV,       #CW)
  do Get_Field_Information ('GPCHSI06','ST_RET_MAN',    $ST_RET_MAN,   #CW)
  do Get_Field_Information ('GPCHSI06','ST_RET_WMAN',   $ST_RET_WMAN,  #CW)
  do Get_Field_Information ('GPCHSI06','TITLE1',        $TITLE1 ,      #CW)
  do Get_Field_Information ('GPCHSI06','TITLE2',        $TITLE2,       #CW)
  do Get_Field_Information ('GPCHSI06','DATE',          $DATE,         #CW)
  do Get_Field_Information ('GPCHSI06','MAX_SAL',       $MAX_SAL,      #CW)
  do Get_Field_Information ('GPCHSI06','PROVIDER',      $PROVIDER,     #CW)
  do Get_Field_Information ('GPCHSI06','CLASS',         $CLASS,        #CW)


  ! 09/2001
  do Get_Field_Information ('GPCHSI06','AHV_ADM_COST',  $TXT_ADM_COST,     #CW)
  do Get_Field_Information ('GPCHSI06','SH_SFUND',      $TXT_SH_SFUND,     #CW)
  do Get_Field_Information ('GPCHSI06','CANTON',        $TXT_CANTON,       #CW)
  do Get_Field_Information ('GPCHSI06','FAK_CONTRIB',   $TXT_FAK_CONTRIB,  #CW)
  do Get_Field_Information ('GPCHSI06','FAK_CALC_RT',   $TXT_FAK_CALC_RT,  #CW)
  do Get_Field_Information ('GPCHSI06','FAK_COMP_RT',   $TXT_FAK_COMP_RT,  #CW)
  do Get_Field_Information ('GPCHSI06','FAK_PROC_FEE',  $TXT_FAK_PROC_FEE, #CW)
  do Get_Field_Information ('GPCHSI06','FAK_QS_REFUND', $TXT_FAK_QS_REFUND,#CW)

  !11/2001
  do Get_Field_Information ('GPCHGLOB', 'HD_FROMDT',       $HD_FROMDT , #CW)
  do Get_Field_Information ('GPCHGLOB', 'HD_TODATE',       $HD_TODATE , #CW)
  do Get_Field_Information ('GPCHGLOB', 'LG_BDTTIME',      $LG_BDTTIME,      #CW)
  do Get_Field_Information ('GPCHGLOB', 'LG_DBNAME',       $LG_DBNAME,       #CW)
  do Get_Field_Information ('GPCHGLOB', 'LG_DBTYPE',       $LG_DBTYPE,       #CW)
  do Get_Field_Information ('GPCHGLOB', 'LG_EDTTIME',      $LG_EDTTIME,      #CW)
  do Get_Field_Information ('GPCHGLOB', 'LG_OPERID',       $LG_OPERID,       #CW)
  do Get_Field_Information ('GPCHGLOB', 'LG_OPERSYS',      $LG_OPERSYS,      #CW)
  do Get_Field_Information ('GPCHGLOB', 'LG_OUTDESTFOR',   $LG_OUTDESTFOR,   #CW)
  do Get_Field_Information ('GPCHGLOB', 'LG_OUTDESTTYPE',  $LG_OUTDESTTYPE,  #CW)
  do Get_Field_Information ('GPCHGLOB', 'LG_PRCINSTNUM',   $LG_PRCINSTNUM,   #CW)
  do Get_Field_Information ('GPCHGLOB', 'LG_PRCNM',        $LG_PRCNM,        #CW)
  do Get_Field_Information ('GPCHGLOB', 'LG_PRCTYPE',      $LG_PRCTYPE,      #CW)
  do Get_Field_Information ('GPCHGLOB', 'LG_REPLNG',       $LG_REPLNG,       #CW)
  do Get_Field_Information ('GPCHGLOB', 'LG_REPNM',        $LG_REPNM,        #CW)
  do Get_Field_Information ('GPCHGLOB', 'LG_RUNCTLID',     $LG_RUNCTLID,     #CW)
  do Get_Field_Information ('GPCHGLOB', 'LG_RUNLOC',       $LG_RUNLOC,       #CW)
  do Get_Field_Information ('GPCHGLOB', 'LG_RUNSTAT',      $LG_RUNSTAT,      #CW)
  do Get_Field_Information ('GPCHGLOB', 'LG_TOTDURA',      $LG_TOTDURA,      #CW)
  do Get_Field_Information ('GPCHGLOB', 'LG_RUNCTLPA',  $LG_RUNCTLPA,  #CW)
  do Get_Field_Information ('GPCHGLOB', 'LG_VALUE',     $LG_VALUE,     #CW)
  do Get_Field_Information ('GPCHGLOB', 'LG_REPLOG',    $LG_REPLOG,    #CW)
  do Get_Field_Information ('GPCHGLOB', 'LG_LOGITEM',   $LG_LOGITEM,   #CW)
  do Get_Field_Information ('GPCHSI06', 'MU_FUND',         $MU_FUND   ,      #CW)
  do Get_Field_Information ('GPCHSI06', 'BASE_AHV_WAGE',   $BASE_AHV_WAGE  , #CW)

!09/2007
  do Get_Field_Information ('GPCHSI06','MIN_SAL',       $MIN_SAL,      #CW)
  do Get_Field_Information ('GPCHSI06','H_MEN',       $H_MEN,      #CW)
  do Get_Field_Information ('GPCHSI06','H_WOMEN',       $H_WOMEN,      #CW)
  do Get_Field_Information ('GPCHSI06','CODE',       $CODE,      #CW)
  do Get_Field_Information ('GPCHSI06','BVG_LPP',       $BVG_LPP,      #CW)
  do Get_Field_Information ('GPCHSI06','BVG_KOORD',       $BVG_KOORD,      #CW)
  do Get_Field_Information ('GPCHSI06','KTG_AMC',       $KTG_AMC,      #CW)
  do Get_Field_Information ('GPCHSI06','UVGZ_LAAC',       $UVGZ_LAAC,      #CW)


end-Procedure Report-Translation

!****************************************************************************

begin-procedure Process-Main
#DEBUG show '-> Process-Main  '

        do Load-All-Effdt

        do Report-All-Effdt

        do Delete-Temp-Table

#DEBUG show '<- Process-Main  '
end-procedure Process-Main

!****************************************************************************
begin-procedure New-Date
#DEBUG show '-> New-Date  '

  new-page

#DEBUG show '<- New-Date  '
end-procedure

!****************************************************************************
begin-procedure Report-All-Effdt
#DEBUG show '-> Report-All-Effdt '

Begin-SELECT
TMP.EFFDT       &Tmp_AsOfDate

        Let $Tmp_AsOfDate = &Tmp_AsOfDate
        do Format-DateTime(&Tmp_AsOfDate,$Print_AsOfDate, {DEFDATE},'','')

        do Get-UID
        do Get-AHV
        do Get-Mutter
        do Get-UV
        do Get-FAK
        do Get-BVG
        do Get-KTG
        do Get-UVGZ
        do New-Date

FROM PS_GPCHSI061_TMP TMP
WHERE TMP.PROCESS_INSTANCE = #prcs_process_instance
AND   TMP.OPRID            = $prcs_oprid
AND   TMP.RUN_CNTL_ID      = $prcs_run_cntl_id
ORDER BY  TMP.EFFDT

end-SELECT

#DEBUG show '<- Report-All-Effdt '
end-procedure Report-All-Effdt

!****************************************************************************
begin-procedure Get-UID
#DEBUG show '<- Get-UID  '
Begin-Select
CUID.GPCH_AL_UID               
CUID.GPCH_AL_UID_BFS   

!define Labels
 let $L_TradeRegister = 'Handelsregister'
 let $L_UID_BFS       = 'Unternehmensidentifikation'

!assign Values
 let $TradeRegister   = &CUID.GPCH_AL_UID
 let $UID_BFS         = &CUID.GPCH_AL_UID_BFS

 print   $L_TradeRegister                   (+1,{col1})
 print   $TradeRegister                     (0,{col2})
 print   $L_UID_BFS                         (+1,{col1})
 print   $UID_BFS                           (0,{col2})


FROM PS_GPCH_SI_COMPANY CUID
WHERE CUID.COMPANY = $Ctl_Company
AND   CUID.EFFDT = (SELECT MAX(CUID2.EFFDT) FROM PS_GPCH_SI_COMPANY CUID2
                                     WHERE CUID2.EFFDT <= $Tmp_AsOfDate)
End-select

#DEBUG show '<- Get-UID  '
end-procedure Get-UID

!****************************************************************************
begin-procedure Get-AHV
#DEBUG show '-> Get-AHV ' $Tmp_AsOfDate

   do Get_Fak_Tax_Data('NA',$Ctl_Company,$Tmp_AsOfDate,
                       $Fak_Language,$Employer_Fak_Nbr,#Employer_Fak_Pct,
                       $Tax_Language,$Employer_StaxNr,#Staxb_PCTR,
                       #Employer_SI_Pct,$AHV,$SI_AHV_PROVCD,
                       $KK,#COM_AHV_ADM_COST,#SI_TOT_AHV_PC,
                       #SI_TOT_ALV1PC,#SI_TOT_ALV2PC)

begin-select
SI.EFFDT                                        &AHV_DATE
SI.GPCH_SI_EE_AHV_PCT                           &AHV_AN
SI.GPCH_SI_TOT_AHV_PC - SI.GPCH_SI_EE_AHV_PCT   &AHV_AG
SI.GPCH_SI_WORK_AGE                             &AHV_Beginn
SI.GPCH_SI_PENSAGE_MN                           &AHV_RENTE_M
SI.GPCH_SI_PENSAGE_WM                           &AHV_RENTE_F
SI.GPCH_SI_EXEMPT_AHV                           &AHV_FREIGRENZE
SI.GPCH_SI_EE_ALV1_PC                           &ALV1_AN
SI.GPCH_SI_TOT_ALV1PC - SI.GPCH_SI_EE_ALV1_PC   &ALV1_AG
SI.GPCH_SI_LIM_ALV1                             &ALV1_lohn
SI.GPCH_SI_EE_ALV2_PC                           &ALV2_AN
SI.GPCH_SI_TOT_ALV2PC - SI.GPCH_SI_EE_ALV2_PC   &ALV2_AG
SI.GPCH_SI_LIM_ALV2                             &ALV2_lohn
SI.GPCH_SI_LIM_UV                               &NBU_Hoechstlohn
SI.GPCH_SI_EE_SH_PCT                            &SH_SFUND_AN
SI.GPCH_SI_TOT_SH_PCT - SI.GPCH_SI_EE_SH_PCT    &SH_SFUND_AG

 do Format-Number(&AHV_AN,$AHV_AN,'B99.B999')
 do Format-Number(&AHV_Beginn,$AHV_Beginn,'B999999999999999')
 do Format-Number(&AHV_AG,$AHV_AG,'B99.9999')
 do Format-Number(&AHV_RENTE_M,$AHV_RENTE_M,'B999999999999999')
 do Format-Number(&AHV_RENTE_F,$AHV_RENTE_F,'B999999999999999')
 do Format-Number(&AHV_FREIGRENZE,$AHV_FREIGRENZE,'B,999,999,999.99')
 do Format-Number(#COM_AHV_ADM_COST,$AHV_ADM_COST,'B99.B999')

 do Format-Number(&ALV1_AN,$ALV1_AN,'B99.9999')
 let #YR_ALV1_lohn = &ALV1_lohn * 12
 do Format-Number(#YR_ALV1_lohn,$ALV1_lohn,'B,999,999,999.99')
 do Format-Number(&ALV1_AG,$ALV1_AG,'B99.9999')

 do Format-Number(&ALV2_AN,$ALV2_AN,'B99.9999')
 let #YR_ALV2_lohn = &ALV2_lohn * 12
 do Format-Number(#YR_ALV2_lohn,$ALV2_lohn,'B,999,999,999.99')
 do Format-Number(&ALV2_AG,$ALV2_AG,'B99.9999')

 do Format-Number(&SH_SFUND_AN,$SH_SFUND_AN,'B99.9999')                 ! 09/2001
 do Format-Number(&SH_SFUND_AG,$SH_SFUND_AG,'B99.9999')                 ! 09/2001


 let #YR_NBU_Hoechstlohn =  &NBU_Hoechstlohn * 12
 do Format-Number(#YR_NBU_Hoechstlohn,$NBU_Hoechstlohn,'B,999,999,999.99')

 let $SPRV_TYPE = '7'
 let $SPRV_CD   = '#AHV'
 let $SPRV_CANTON = ' '
 let $SPRV_ALL = ' '

 do Get-MBR-ID

 print   'AHV'                                  (+1,{col0})
 print $PAY_CALC_NO                             (0,{col3_1})
 !print $AHV                                (0,{colh_5}) 
 print $MBR_ID                                  (0,{colh_5}) 

 print   $EMPEE_CONT                            (+1,{col1})
 print   $AHV_AN                                (0,{col2})
 print   $ST_AHV                                (0,{col3})
 print   $AHV_Beginn                            (0,{col4})
 print   $EMPLER_AMT                            (+1,{col1})
 print   $AHV_AG                                (0,{col2})
 print   $ST_RET_MAN                            (0,{col3})
 print   $AHV_RENTE_M                           (0,{col4})
 print   $TXT_ADM_COST                          (+1,{col1})           ! 09/2001
 print   $AHV_ADM_COST                          (0,{col2})            ! 09/2001
 print   $ST_RET_WMAN                           (0,{col3})            ! 09/2001
 print   $AHV_RENTE_F                           (0,{col4})
 print   $AHV_SUS                               (+1,{col3})
 print   $AHV_FREIGRENZE                        (0,{col4})
 print '_'                                      (+1,{col0},120) FILL


 print   'ALV'                                 (+2,{col0})
 print   $EMPEE_CONT                            (0,{col1})
 print   $ALV1_AN                               (0,{col2})
 print   $MAX_SAL                               (0,{col3})
 print   $ALV1_lohn                             (0,{col4})
 print   $EMPLER_AMT                            (+1,{col1})
 print   $ALV1_AG                               (0,{col2})
 print '_'                                      (+1,{col0},120) FILL


 print   'ALVZ'                                 (+2,{col0})
 print   $EMPEE_CONT                            (0,{col1})
 print   $ALV2_AN                               (0,{col2})
 print   $MAX_SAL                               (0,{col3})
 print   $ALV2_lohn                             (0,{col4})
 print   $EMPLER_AMT                            (+1,{col1})
 print   $ALV2_AG                               (0,{col2})
 print '_'                                      (+1,{col0},120) FILL


 ! Canton SH - Solidarity Fund     09/2001
 print   $TXT_SH_SFUND                           (+2,{col0})
 print   $EMPEE_CONT                            (+1,{col1})
 print   $SH_SFUND_AN                           (0,{col2})
 print   $MAX_SAL                               (0,{col3})
 print   $ALV1_lohn                             (0,{col4})
 print   $EMPLER_AMT                            (+1,{col1})
 print   $SH_SFUND_AG                           (0,{col2})
 print '_'                                      (+1,{col0},120) FILL


from PS_GPCH_SI_SYS SI
WHERE  SI.EFFDT  = (SELECT MAX(Z1.EFFDT) FROM PS_GPCH_SI_SYS Z1
                                     WHERE Z1.EFFDT <= $Tmp_AsOfDate)

end-select

#DEBUG show '<- Get-AHV '
end-procedure Get-AHV

!****************************************************************************
begin-procedure Get-Mutter
#DEBUG show '<- Get-Mutter  '
Begin-Select
MU.GPCH_SI_MI_EE_PCT                            &MU_EE
MU.GPCH_SI_MI_TTL_PCT - MU.GPCH_SI_MI_EE_PCT    &MU_ER

 do Format-Number(&MU_EE,$MU_EE,'B99.B999')
 do Format-Number(&MU_ER,$MU_ER,'B99.B999')

 print   $MU_FUND                               (+2,{col0})
 print   $EMPEE_CONT                            (+1,{col1})
 print   $MU_EE                                 (0,{col2})
 print   $BASE_AHV_WAGE                         (0,{col3})
 print   $EMPLER_AMT                            (+1,{col1})
 print   $MU_ER                                 (0,{col2})
 print '_'                                      (+1,{col0},120) FILL



FROM PS_GPCH_SI_MI_SCL MU
WHERE MU.GPCH_TX_CANTON = 'GE'
AND   MU.EFFDT = (SELECT MAX(M2.EFFDT) FROM PS_GPCH_SI_MI_SCL M2
                                     WHERE M2.EFFDT <= $Tmp_AsOfDate)
End-select

#DEBUG show '<- Get-Mutter  '
end-procedure Get-Mutter

!****************************************************************************
begin-procedure Get-UV
#DEBUG  show '-> Get-UV '  $Tmp_AsOfDate '  ,  '   $Ctl_Company
 print   'UV'                             (+2,{col0})
 print   $ACC_INS_PROV                    (0,{col1})
 print   $MAX_SAL                         (0,{col3})
 print   $NBU_Hoechstlohn                 (0,{col4})
 print   $PROVIDER                        (+2,{col1})
 print   $CLASS                           (0,{col1_1})
 print   'NBU%'                           (0,{colh_2})
 print   'BU%'                            (0,{colh_3})
! print   $CUST_ID                         (0,{col3_1})
 print   ' '                              (+1,{col1_2})

 let $UV_Num = ' '

BEGIN-SELECT
B.EFFDT
B.GPCH_SI_PROV_CD        &Provider  !() on-break print=never skiplines=1
B.GPCH_SI_COV_UV         &Class
B.GPCH_SI_TOT_NBU_PC     &NBU_PER
B.GPCH_SI_BU_PCT         &BU_PER
A.GPCH_SI_UV_MBR_ID      &UV_NUMBER

          do Format-Number(&NBU_PER,$NBU_PER,'B99.9999')
          do Format-Number(&BU_PER,$BU_PER,'B99.9999')

          print &Provider    (+1,{col1})
          print &Class       (0,{col1_1})
          print $NBU_PER     (0,{col1_2})
          print $BU_PER      (0,{col1_3})

          if  &UV_NUMBER <> $UV_Num
          print   $CUST_ID   (0,{col3_1})
! FMB          print &UV_NUMBER   (0,{colh_4})
          print &UV_NUMBER   (0,{colh_5})
          end-if

          let $UV_Num = &UV_NUMBER

FROM PS_GPCH_SI_ACC_INS A, PS_GPCH_SI_UV_PCT B
WHERE A.GPCH_TX_UV_PROV_CD = B.GPCH_SI_PROV_CD
AND   A.EFFDT = (SELECT MAX(A1.EFFDT) FROM PS_GPCH_SI_ACC_INS A1
                                    WHERE A1.EFFDT <= $Tmp_AsOfDate
                                    AND   A1.GPCH_TX_UV_PROV_CD = A.GPCH_TX_UV_PROV_CD
                                    AND   A1.COMPANY = A.COMPANY)
AND   B.EFFDT = (SELECT MAX(B1.EFFDT) FROM PS_GPCH_SI_UV_PCT B1
                                    WHERE B1.EFFDT <= $Tmp_AsOfDate
                                    AND B1.GPCH_SI_PROV_CD = B.GPCH_SI_PROV_CD
                                    AND B1.GPCH_SI_COV_UV = B.GPCH_SI_COV_UV)
AND   A.COMPANY= $Ctl_Company
ORDER BY B.GPCH_SI_PROV_CD,B.EFFDT,B.GPCH_SI_COV_UV

END-SELECT


 print '_'                                      (+1,{col0},120) FILL

#DEBUG show '<- Get-UV '
end-procedure Get-UV

! 09/2001
!****************************************************************************
begin-procedure Get-FAK
#DEBUG show '-> Get-FAK '  $Tmp_AsOfDate  '  ,  '   $Ctl_Company

 print   $TXT_CANTON                          (+2,{col0})
 print   $TXT_FAK_CONTRIB                     (0,{col1b})
! print   $TXT_FAK_CONTRIB                     (0,{col2b})
! print   $TXT_FAK_PROC_FEE                    (0,{col3b})
! print   '(‰)'                                                            (0,103)
! print   $TXT_FAK_QS_REFUND                   (0,{col4b1})
 print   $TXT_FAK_PROC_FEE                    (0,{col2b})
 print   '(‰)'                                                            (0,73)
 print   $TXT_FAK_QS_REFUND                   (0,{col3b})
 print   $CUST_ID                             (0,{col4b1})

 print   $TXT_FAK_COMP_RT                     (+1,{col1b})
! print   $TXT_FAK_CALC_RT                     ( 0,{col2b})

 print   ' '                                  (+1,{col1b})


BEGIN-SELECT
C.GPCH_TX_CANTON         &CANTON
C.GPCH_TX_STAXB_PCT      &OFFIC_RT

          Let $Canton = &CANTON
          do Format-Number(&OFFIC_RT,$OFFIC_RT,'B99.9999')

          do Get-FAK_Company


 let $SPRV_TYPE = '5'
 let $SPRV_CD   = ' '
 let $SPRV_CANTON = $Canton
 let $SPRV_ALL = 'X'

        do Get-MBR-ID

 IF $MBR_ID = ' '
  let $SPRV_TYPE = '5'
  let $SPRV_CD   = ' '
  let $SPRV_CANTON = ' '
  let $SPRV_ALL = 'Y'

        do Get-MBR-ID

 End-If

          print $Canton        (+1,{col0})
          print $OFFIC_RT      (0,{col3b})
          print $COMP_RT       (0,{col1b})
!          print $PROC_FEE      (0,{col3b})
!          print $QS_REFUND     (0,{col2b})
          print $PROC_FEE      (0,{col2b})
          print $MBR_ID       (0,{col4b})


FROM PS_GPCH_TX_FAK_PCT C
WHERE C.EFFDT = (SELECT MAX(C1.EFFDT) FROM PS_GPCH_TX_FAK_PCT C1
                                      WHERE C1.EFFDT <= $Tmp_AsOfDate)
ORDER BY C.GPCH_TX_CANTON

END-SELECT


! print '_'                                      (+1,{col0},120) FILL

#DEBUG show '<- Get-FAK '
end-procedure Get-FAK

! 09/2001
!****************************************************************************
begin-procedure Get-FAK_Company
#DEBUG show '-> Get-FAK-Company '  $Tmp_AsOfDate  '  ,  '   $Ctl_Company

Let $COMP_RT = ''
Let $PROC_FEE = ''
Let $QS_REFUND = ''

BEGIN-SELECT
D.GPCH_SI_PCT            &COMP_RT
D.GPCH_TX_FAK_PCT        &PROC_FEE
D.GPCH_RP_AMOUNT1        &QS_REFUND

          do Format-Number(&COMP_RT,$COMP_RT,'B99.9999')
          do Format-Number(&PROC_FEE,$PROC_FEE,'B99.9999')
          do Format-Number(&QS_REFUND,$QS_REFUND,'B99.9999')

FROM  PS_GPCH_TX_FAK_STX D
WHERE D.GPCH_TX_CANTON = $Canton
AND   D.EFFDT = (SELECT MAX(D1.EFFDT) FROM PS_GPCH_TX_FAK_STX D1
                                    WHERE D1.EFFDT <= $Tmp_AsOfDate
                                    AND D1.GPCH_TX_CANTON = $Canton)
AND   D.COMPANY= $Ctl_Company

END-SELECT

#DEBUG show '<- Get-FAK-Company '
end-procedure Get-FAK-Company

!****************************************************************************
begin-procedure Get-MBR-ID
#DEBUG show '<- Get-MBR-ID  '

 show '$SPRV_TYPE = ' $SPRV_TYPE
 show '$SPRV_CD = ' $SPRV_CD
 show '$SPRV_CANTON = ' $SPRV_CANTON
 show '$SPRV_ALL = ' $SPRV_ALL
 show '$Tmp_AsOfDate = ' $Tmp_AsOfDate
 show '$Ctl_Company = ' $Ctl_Company

 Let $MBR_ID = ' '

Begin-Select
MBR.GPCH_SI_UV_MBR_ID     &MBR_ID

 Let $MBR_ID =  &MBR_ID

FROM PS_GPCH_SI_ACC_INS MBR
WHERE MBR.COMPANY = $Ctl_Company
AND   MBR.EFFDT = (SELECT MAX(MBR2.EFFDT) FROM PS_GPCH_SI_ACC_INS MBR2
                                     WHERE MBR2.EFFDT <= $Tmp_AsOfDate)
AND   MBR.GPCH_SI_PROV_TYPE = $SPRV_TYPE
AND  ( 
       ( MBR.GPCH_SI_PROV_TYPE = '5' AND 
       ( MBR.GPCH_TX_CANTON = $SPRV_CANTON or MBR.GPCH_ALL_CANTONFLG = $SPRV_ALL ) )
    OR ( MBR.GPCH_SI_PROV_TYPE <> '5' AND  MBR.GPCH_TX_UV_PROV_CD = $SPRV_CD ) )
End-select

 show '$MBR_ID = ' $MBR_ID

#DEBUG show '<- Get-MBR-ID  '
end-procedure Get-MBR-ID

!****************************************************************************
begin-procedure Get-BVG
#DEBUG show '<- Get-BVG  '

Begin-Select
BP.GPCH_AL_PROVIDER
CM.GPCH_SI_UV_MBR_ID
CM.GPCH_SI_MBR_SUBID
CM.GPCH_TX_UV_PROV_CD
BP.DESCR
BP.GROUP_LIST_ID

! do Get_BVG_Codebeschreibung -- To Do

! Define Labels
 let $BVG_Contract = 'BVG-Vertragsnummer'
 let $BVG_Provider = 'BVG-Versicherer'
 let $BVG_BUKR     = 'Buchungskreis'
 let $BVG_CodeHeader = 'BVG-Codebeschreibung'

! Define Values
 let $BVG_CUST_ID      = &BP.GPCH_AL_PROVIDER
 let $BVG_MBR_ID       = &CM.GPCH_SI_UV_MBR_ID
 let $BVG_MBR_Contract = &CM.GPCH_SI_MBR_SUBID
 let $BVG_PROV_CD      = &CM.GPCH_TX_UV_PROV_CD
 let $BVG_Descr        = &BP.DESCR
 let $BVG_Group        = &BP.GROUP_LIST_ID

 print   $BVG_LPP                           (+2,{col0})
 print   $BVG_CUST_ID                       (0,{col3_1})
 print   $BVG_MBR_ID                        (0,{colh_5})
 print   $BVG_Contract                      (+1,{col1})
 print   $BVG_MBR_Contract                  (0,{col2})
 print   $BVG_Provider                      (+1,{col1})
 print   $BVG_Descr                         (0,{col2})
 print   $BVG_BUKR                          (+1,{col1})
 print   $BVG_Group                         (0,{col2})
 print   $BVG_CodeHeader                    (+2,{col1})
 
 do Get_BVG_Codebeschreibung

 print '_'                                  (+1,{col0},120) FILL

FROM PS_GPCH_SI_PROVIDR BP, PS_GPCH_SI_ACC_INS CM
 where CM.COMPANY = $Ctl_Company
   and CM.GPCH_SI_PROV_TYPE = '6' 
   AND BP.GPCH_SI_PROV_TYPE = '6'
   and CM.GPCH_TX_UV_PROV_CD = BP.GPCH_SI_PROV_CD 
   and CM.EFFDT = (select max(CM1.EFFDT) from PS_GPCH_SI_COMPANY CM1 where CM.COMPANY = CM1.COMPANY and CM1.EFFDT <= $Tmp_AsOfDate )
   and BP.EFFDT = (select max(BP1.EFFDT) from PS_GPCH_SI_PROVIDR BP1 where BP1.GPCH_SI_PROV_TYPE = '6' and BP.GPCH_SI_PROV_CD = BP1.GPCH_SI_PROV_CD and BP1.EFFDT <= $Tmp_AsOfDate)

End-select

#DEBUG show '<- Get-BVG  '
end-procedure Get-BVG

!****************************************************************************
begin-procedure Get_BVG_Codebeschreibung
#DEBUG show '<- Get_BVG_Codebeschreibung  '

Begin-Select
PKS.GPCH_PK_INS_STATUS
PKS.DESCR

! Define Labels

! Define Values
 let $BVGCodeDescr  = &PKS.DESCR
 let $BVGCode       = &PKS.GPCH_PK_INS_STATUS

 print   $BVGCodeDescr                (+1,{col1})
 print   $BVGCode                     (0,{col2})

FROM PS_GPCH_PK_STATUS PKS
 where PKS.GPCH_SI_PROV_CD   = $BVG_PROV_CD
   and PKS.GPCH_SI_PROV_TYPE = '6' 
   and PKS.EFFDT = (select max(BP.EFFDT) from PS_GPCH_SI_PROVIDR BP where BP.GPCH_SI_PROV_TYPE = '6' and BP.GPCH_SI_PROV_CD = PKS.GPCH_SI_PROV_CD and BP.EFFDT <= $Tmp_AsOfDate)
 order by PKS.GPCH_PK_INS_STATUS

End-select

#DEBUG show '<- Get_BVG_Codebeschreibung  '
end-procedure Get_BVG_Codebeschreibung

!****************************************************************************
begin-procedure Get-KTG
#DEBUG show '<- Get-KTG  '
 print   $KTG_AMC                            (+3,{col0})
 print   $H_MEN                              (0,{col1_3})
 print   $H_WOMEN                            (0,{colh_4})

 Let $GRP_FIRST = ' '

Begin-Select

AD2.GPCH_SI_ALT_ID       &GRP
AD2.GPCH_RATE_M * S.GPCH_PREM_AN_M_PCT / 100               &EE_PCT_M
AD2.GPCH_RATE_F * S.GPCH_PREM_AN_F_PCT / 100               &EE_PCT_F
AD2.GPCH_RATE_M * (100 - S.GPCH_PREM_AN_M_PCT) / 100       &ER_PCT_M
AD2.GPCH_RATE_F * (100 - S.GPCH_PREM_AN_F_PCT) / 100       &ER_PCT_F
AD2.GPCH_LOW_SALARY      &MIN
AD2.GPCH_HIGH_SALARY     &MAX
AD1.GPCH_SI_PROV_CD      &KTG_PRV_CD

 do Format-Number(&EE_PCT_M,$EE_PCT_M,'B99.9999')
 do Format-Number(&EE_PCT_F,$EE_PCT_F,'B99.9999')
 do Format-Number(&ER_PCT_M,$ER_PCT_M,'B99.9999')
 do Format-Number(&ER_PCT_F,$ER_PCT_F,'B99.9999')
 do Format-Number(&MIN,$MIN,'B,999,999,999.99')
 do Format-Number(&MAX,$MAX,'B,999,999,999.99')

 let $SPRV_TYPE = '3'
 let $SPRV_CD   = RTRIM(&KTG_PRV_CD,'')
 let $SPRV_CANTON = ' '
 let $SPRV_ALL = ' '

 do Get-MBR-ID

 print   $CODE                            (+1,{col0})
 print   &GRP   
                             (0,{col1})
 IF $GRP_FIRST = ' '
   print   $CUST_ID                            (0,{col3_1})
   print   $MBR_ID                             (0,{colh_5})
 End-If


 print   $EMPEE_CONT                         (+1,{col1})
 print   $EE_PCT_M                           (0,{col1_3})
 print   $EE_PCT_F                           (0,{colh_4})
 print   $MIN_SAL                            (0,{col3})
 print   $MIN                                (0,{col4})
 print   $EMPLER_AMT                         (+1,{col1})
 print   $ER_PCT_M                           (0,{col1_3})
 print   $ER_PCT_F                           (0,{colh_4})
 print   $MAX_SAL                            (0,{col3})
 print   $MAX                                (0,{col4})

  Let $GRP_FIRST = &GRP


FROM PS_GPCH_SI_ADDTNL2 AD2, PS_GPCH_SI_SPLIT S, PS_GPCH_SI_ADDTNL1 AD1
WHERE AD2.COMPANY = $Ctl_Company
AND   AD2.GPCH_SI_PROV_TYPE = '3'
AND   AD2.EFFDT = AD1.EFFDT
AND   AD2.COMPANY = AD1.COMPANY
AND   AD2.GPCH_SI_PROV_TYPE = AD1.GPCH_SI_PROV_TYPE
AND   AD1.EFFDT = ( select max(AD11.EFFDT) from PS_GPCH_SI_ADDTNL1 AD11
 WHERE AD1.COMPANY = AD11.COMPANY
   AND  AD1.GPCH_SI_PROV_TYPE = AD11.GPCH_SI_PROV_TYPE 
   AND  AD11.EFFDT <= $Tmp_AsOfDate)
AND AD2.COMPANY = S.COMPANY
AND AD2.GPCH_SI_PROV_TYPE = S.GPCH_SI_PROV_TYPE
AND AD2.EFFDT = S.EFFDT
AND AD2.GPCH_SI_TYPE_ID = S.GPCH_SI_TYPE_ID
AND AD2.GPCH_SI_ALT_ID = S.GPCH_SI_ALT_ID
! AND S.GRADE = '211'
order by AD2.GPCH_SI_ALT_ID
End-select

 print '_'                                      (+1,{col0},120) FILL

#DEBUG show '<- Get-KTG  '
end-procedure Get-KTG

!****************************************************************************
begin-procedure Get-UVGZ
#DEBUG show '<- Get-UVGZ  '

 print   $UVGZ_LAAC                          (+3,{col0})
 print   $H_MEN                              (0,{col1_3})
 print   $H_WOMEN                            (0,{colh_4})

 let $GRP_FIRST = ' '

Begin-Select

AD2U.GPCH_SI_ALT_ID       &GRPU
AD2U.GPCH_RATE_M * SU.GPCH_PREM_AN_M_PCT / 100               &EE_PCT_MU
AD2U.GPCH_RATE_F * SU.GPCH_PREM_AN_F_PCT / 100               &EE_PCT_FU
AD2U.GPCH_RATE_M * (100 - SU.GPCH_PREM_AN_M_PCT) / 100       &ER_PCT_MU
AD2U.GPCH_RATE_F * (100 - SU.GPCH_PREM_AN_F_PCT) / 100       &ER_PCT_FU
AD2U.GPCH_LOW_SALARY      &MINU
AD2U.GPCH_HIGH_SALARY     &MAXU
AD1U.GPCH_SI_PROV_CD      &UVGZ_PRV_CD

 do Format-Number(&EE_PCT_MU,$EE_PCT_MU,'B99.9999')
 do Format-Number(&EE_PCT_FU,$EE_PCT_FU,'B99.9999')
 do Format-Number(&ER_PCT_MU,$ER_PCT_MU,'B99.9999')
 do Format-Number(&ER_PCT_FU,$ER_PCT_FU,'B99.9999')
 do Format-Number(&MINU,$MINU,'B,999,999,999.99')
 do Format-Number(&MAXU,$MAXU,'B,999,999,999.99')

 let $SPRV_TYPE = '4'
 let $SPRV_CD   = RTRIM(&UVGZ_PRV_CD,'')
 let $SPRV_CANTON = ' '
 let $SPRV_ALL = ' '

 do Get-MBR-ID


 print   $CODE                            (+1,{col0})
 print   &GRPU                               (0,{col1})
 
 IF $GRP_FIRST = ' ' 
   print   $CUST_ID                            (0,{col3_1})
   print   $MBR_ID                             (0,{colh_5})
 End-IF
 
 print   $EMPEE_CONT                         (+1,{col1})
 print   $EE_PCT_MU                          (0,{col1_3})
 print   $EE_PCT_FU                          (0,{colh_4})
 print   $MIN_SAL                            (0,{col3})
 print   $MINU                               (0,{col4})
 print   $EMPLER_AMT                         (+1,{col1})
 print   $ER_PCT_MU                          (0,{col1_3})
 print   $ER_PCT_FU                          (0,{colh_4})
 print   $MAX_SAL                            (0,{col3})
 print   $MAXU                               (0,{col4})

 let $GRP_FIRST = &GRPU


FROM PS_GPCH_SI_ADDTNL2 AD2U, PS_GPCH_SI_ADDTNL1 AD1U, PS_GPCH_SI_SPLIT SU
WHERE AD2U.COMPANY = $Ctl_Company
AND   AD2U.GPCH_SI_PROV_TYPE = '4'
AND   AD2U.EFFDT = AD1U.EFFDT
AND   AD2U.COMPANY = AD1U.COMPANY
AND   AD2U.GPCH_SI_PROV_TYPE = AD1U.GPCH_SI_PROV_TYPE
AND   AD1U.EFFDT = ( select max(AD11U.EFFDT) from PS_GPCH_SI_ADDTNL1 AD11U
 WHERE AD1U.COMPANY = AD11U.COMPANY
   AND  AD1U.GPCH_SI_PROV_TYPE = AD11U.GPCH_SI_PROV_TYPE 
   AND  AD11U.EFFDT <= $Tmp_AsOfDate)
AND AD2U.COMPANY = SU.COMPANY
AND AD2U.GPCH_SI_PROV_TYPE = SU.GPCH_SI_PROV_TYPE
AND AD2U.EFFDT = SU.EFFDT
AND AD2U.GPCH_SI_TYPE_ID = SU.GPCH_SI_TYPE_ID
AND AD2U.GPCH_SI_ALT_ID = SU.GPCH_SI_ALT_ID
! AND SU.GRADE = '135'
order by AD2U.GPCH_SI_ALT_ID
End-select

 print '_'                                      (+1,{col0},120) FILL

#DEBUG show '<- Get-UVGZ  '
end-procedure Get-UVGZ

! 09/2001
!**************************************************************
begin-procedure Load-All-Effdt
! Load the EFFDT of all the tables that are included in the
! selected period
!**************************************************************
#debug show '-> Load-All-Effdt'

! Load the Date From as the First Effective Date to Report on
let $WW.EFFDT = $Start_Date
Let $ORIG_TBL = '00'
do Insert-Temp-Table

Let $Table    = 'PS_GPCH_SI_SYS WW'
Let $ORIG_TBL = '01'
Let $Where_Company = ''
do Get-Table-Effdt

Let $Table = 'PS_GPCH_SI_COMPANY WW'
Let $ORIG_TBL = '02'
Let $Where_Company = ' AND WW.COMPANY = ''' || $Ctl_Company || ''' '
do Get-Table-Effdt

Let $Table = 'PS_GPCH_SI_ACC_INS WW'
Let $ORIG_TBL = '03'
Let $Where_Company = ' AND WW.COMPANY = ''' || $Ctl_Company || ''' '
do Get-Table-Effdt

Let $Table = 'PS_GPCH_SI_UV_PCT WW'
Let $ORIG_TBL = '04'
Let $Where_Company = ''
do Get-Table-Effdt

Let $Table = 'PS_GPCH_TX_FAK_PCT WW'
Let $ORIG_TBL = '05'
Let $Where_Company = ''
do Get-Table-Effdt

Let $Table = 'PS_GPCH_TX_FAK_STX WW'
Let $ORIG_TBL = '06'
Let $Where_Company = ' AND WW.COMPANY = ''' || $Ctl_Company || ''' '
do Get-Table-Effdt

Let $Table = 'PS_GPCH_SI_MI_SCL WW'
Let $ORIG_TBL = '07'
Let $Where_Company = ''
do Get-Table-Effdt

#debug show '<- Load-All-Effdt'
end-procedure  Load-All-Effdt

!**************************************************************
begin-procedure Get-Table-Effdt
#debug show '-> Get-Table-Effdt'

begin-SELECT

WW.EFFDT      &WW.EFFDT

        Let $WW.EFFDT = &WW.EFFDT
        do Insert-Temp-Table

FROM [$Table]
WHERE WW.EFFDT >= $Start_Date
AND   WW.EFFDT <= $Last_Date
[$Where_Company]

end-SELECT

#debug show '<- Get-Table-Effdt'
end-procedure Get-Table-Effdt

!**************************************************************
begin-procedure Insert-Temp-Table
#debug show '-> Insert-Temp-Table'

Begin-SQL ON-ERROR = Error-SQL

INSERT INTO PS_GPCHSI061_TMP(PROCESS_INSTANCE,OPRID,RUN_CNTL_ID,EFFDT,GPCH_CHG_ORIG)
VALUES(#prcs_process_instance,$prcs_oprid,$prcs_run_cntl_id,$WW.EFFDT,$ORIG_TBL)

end-SQL

#debug show '<- Insert-Temp-Table'
end-procedure Insert-Temp-Table

!**************************************************************
begin-procedure Delete-Temp-Table
#debug show '-> Delete-Temp-Table'

Begin-SQL ON-ERROR = Error-SQL

DELETE FROM PS_GPCHSI061_TMP
WHERE PROCESS_INSTANCE = #prcs_process_instance
AND   OPRID            = $prcs_oprid
AND   RUN_CNTL_ID      = $prcs_run_cntl_id

end-SQL

#debug show '<- Delete-Temp-Table'
end-procedure Delete-Temp-Table

!**************************************************************
begin-procedure Error-SQL
#debug show '-> Error-SQL'

        ! try to see if Duplicate key message can be found..... would prevent
        !       to do a select before the Insert
        ! handle error for delete as well


#debug show '<- Error-SQL'
end-procedure Error-SQL

!**************************************************************
Begin-procedure Get_English_Strings($ReportID)
#Debug show 'Get_English_Strings -> '  $ReportID

evaluate $ReportID
          when = 'GPCHSI06'
               do Get_Eng_GPCHSI06
               break
          when = 'GPCHGLOB'
               do Get_Eng_GPCHGLOB
               break
          when-other
               break
 end-evaluate
#Debug show 'Get_English_Strings <- ' #_str_cnt
End-procedure Get_English_Strings
!****************************************************************************
Begin-procedure Get_German_Strings($ReportID)
#Debug show 'Get_German_Strings -> '  $ReportID

evaluate $ReportID
          when = 'GPCHSI06'
               do Get_Ger_GPCHSI06
               break
          when = 'GPCHGLOB'
               do Get_Ger_GPCHGLOB
               break
          when-other
               break
 end-evaluate

#Debug show 'Get_German_Strings <- ' #_str_cnt
End-procedure Get_German_Strings
!****************************************************************************
Begin-procedure Get_Italian_Strings($ReportID)
#Debug show 'Get_Italian_Strings -> '  $ReportID

 evaluate $ReportID
          when = 'GPCHSI06'
               do Get_Ita_GPCHSI06
               break
          when = 'GPCHGLOB'
               do Get_Ita_GPCHGLOB
               break
          when-other
               break
 end-evaluate

#Debug show 'Get_Italian_Strings <- ' #_str_cnt
End-procedure Get_Italian_Strings
!****************************************************************************
Begin-procedure Get_French_Strings($ReportID)
#Debug show 'Get_French_Strings -> '  $ReportID

evaluate $ReportID
          when = 'GPCHSI06'
               do Get_Fra_GPCHSI06
               break
          when = 'GPCHGLOB'
               do Get_Fra_GPCHGLOB
               break
          when-other
               break
 end-evaluate
#Debug show 'Get_French_Strings <- ' #_str_cnt
End-procedure Get_French_Strings
!****************************************************************************
#include 'gpchut01.sqc'    !Get Strings Values
#include 'gpchut04.sqc'    !Get Various Addresses
#include 'gpchut06.sqc'    !Get Run Control Values and Sort Order Parameters
#include 'gpchut07.sqc'    !Get Log Output Selection
#include 'gpchut11.sqc'    !get Tax and Fak Values
#include 'gpchsi6s.sqc'    !Get Strings Values for GPCHSI06
#include 'gpchglbs.sqc'    !Get Strings Values for GPCHGLOB


#include 'readxlat.sqc'    !routine to get values from xlattbl
#include 'stdapi.sqc'      !routines to update run status
#include 'reset.sqc'       !reset printer procedure
#include 'curdttim.sqc'    !get-current-datetime procedure
#include 'datetime.sqc'    !routines for date and time formatting
#include 'datemath.sqc'  !function for date-calculation
#include 'validdt.sqc'     !validate date routine
#include 'number.sqc'      !routines to format numbers
#include 'sqrtrans.sqc'  !sqr strings table procedures